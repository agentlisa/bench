{
  "Title": "Gas Limitation Can Disable Smart Contract Wallets",
  "Content": "\n\n\nThe `CurrencyLibrary` is used to handle ERC-20 tokens and native currency transfers in UniswapX. The [native transfer](https://github.com/Uniswap/UniswapX/blob/7c5e359fc476f3e55497a8cd6f405f67af2c1dcf/src/lib/CurrencyLibrary.sol#L51) is performed with a low-level call without calldata and a hard-coded gas limit of 6900. \n\n\nGas limits on calls are considered a bad practice for two main reasons: \n\n\n       • Gas costs have changed over time (e.g., as per [EIP-1884](https://eips.ethereum.org/EIPS/eip-1884)).  \n       • EVM side-chains and L2s can have different gas costs. This has previously caused funds to           become stuck as transfers run out of gas and revert. \n\n\nBoth arguments could lead to unreliable functionality over time and across chains. A smart contract wallet can have logic in its `receive` or `fallback` function which exceeds the 6900 gas limit, thereby disabling that wallet to use the service for swaps where the native currency is involved. Furthermore, the protocol injects fees into the orders that are sent to the fee recipient, including ETH. Hence, a fee vault or a multi-sig wallet as a fee recipient can be equally affected by this problem. \n\n\nNote that the concern of reentrancy should be tackled at the entry point level, which was not identified as a risk for this scope. Further, for the concern regarding the swapper being able to spend the filler's gas maliciously, please see [M-01](#m1). \n\n\nConsider removing the gas limitation to guarantee the functionality of smart contract wallets when engaging with UniswapX. \n\n\n\n\n\n***Update:** Resolved in [pull request #189](https://github.com/Uniswap/UniswapX/pull/189) at commit [0cdf97e](https://github.com/Uniswap/UniswapX/commit/0cdf97efb147f22ff2131c949aaa49de48eefae0).*\n\n\n",
  "Impact": "MEDIUM",
  "Source": "",
  "Code": [
    {
      "filename": "src/lib/CurrencyLibrary.sol",
      "content": "// SPDX-License-Identifier: GPL-2.0-or-later\npragma solidity ^0.8.0;\n\nimport {ERC20} from \"solmate/src/tokens/ERC20.sol\";\nimport {IPermit2} from \"permit2/src/interfaces/IPermit2.sol\";\nimport {SafeCast} from \"openzeppelin-contracts/utils/math/SafeCast.sol\";\nimport {SafeTransferLib} from \"solmate/src/utils/SafeTransferLib.sol\";\n\naddress constant NATIVE = 0x0000000000000000000000000000000000000000;\nuint256 constant TRANSFER_NATIVE_GAS_LIMIT = 6900;\n\n/// @title CurrencyLibrary\n/// @dev This library allows for transferring native ETH and ERC20s via direct filler OR fill contract.\nlibrary CurrencyLibrary {\n    using SafeTransferLib for ERC20;\n\n    /// @notice Thrown when a native transfer fails\n    error NativeTransferFailed();\n\n    /// @notice Get the balance of a currency for addr\n    /// @param currency The currency to get the balance of\n    /// @param addr The address to get the balance of\n    /// @return balance The balance of the currency for addr\n    function balanceOf(address currency, address addr) internal view returns (uint256 balance) {\n        if (isNative(currency)) {\n            balance = addr.balance;\n        } else {\n            balance = ERC20(currency).balanceOf(addr);\n        }\n    }\n\n    /// @notice Transfer currency from the caller to recipient\n    /// @dev for native outputs we will already have the currency in local balance\n    /// @param currency The currency to transfer\n    /// @param recipient The recipient of the currency\n    /// @param amount The amount of currency to transfer\n    function transferFill(address currency, address recipient, uint256 amount) internal {\n        if (isNative(currency)) {\n            // we will have received native assets directly so can directly transfer\n            transferNative(recipient, amount);\n        } else {\n            // else the caller must have approved the token for the fill\n            ERC20(currency).safeTransferFrom(msg.sender, recipient, amount);\n        }\n    }\n\n    /// @notice Transfer native currency to recipient\n    /// @param recipient The recipient of the currency\n    /// @param amount The amount of currency to transfer\n    function transferNative(address recipient, uint256 amount) internal {\n        (bool success,) = recipient.call{value: amount, gas: TRANSFER_NATIVE_GAS_LIMIT}(\"\");\n        if (!success) revert NativeTransferFailed();\n    }\n\n    /// @notice returns true if currency is native\n    /// @param currency The currency to check\n    /// @return true if currency is native\n    function isNative(address currency) internal pure returns (bool) {\n        return currency == NATIVE;\n    }\n}"
    }
  ]
}