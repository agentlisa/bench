{
  "Title": "[M-13] Interactions with AMMs do not use deadlines for operations",
  "Content": "\nFrom a judge's comment in a previous [contest](https://code4rena.com/reports/2022-06-canto-v2/#m-01-stableswap---deadline-do-not-work):\n\n```md\nBecause Front-running is a key aspect of AMM design, deadline is a useful tool to ensure that your tx cannot be “saved for later”.\n\nDue to the removal of the check, it may be more profitable for a miner to deny the transaction from being mined until the transaction incurs the maximum amount of slippage.\n```\n\nMost of the functions that interact with AMM pools do not have a deadline parameter, but specifically the one shown below is passing `block.timestamp` to a pool, which means that whenever the miner decides to include the txn in a block, it will be valid at that time, since `block.timestamp` will *be* the current timestamp.\n\n### Proof of Concept\n\n```solidity\nFile: /paraspace-core/contracts/protocol/tokenization/NTokenUniswapV3.sol   #1\n\n51       function _decreaseLiquidity(\n52           address user,\n53           uint256 tokenId,\n54           uint128 liquidityDecrease,\n55           uint256 amount0Min,\n56           uint256 amount1Min,\n57           bool receiveEthAsWeth\n58       ) internal returns (uint256 amount0, uint256 amount1) {\n59           if (liquidityDecrease > 0) {\n60               // amount0Min and amount1Min are price slippage checks\n61               // if the amount received after burning is not greater than these minimums, transaction will fail\n62               INonfungiblePositionManager.DecreaseLiquidityParams\n63                   memory params = INonfungiblePositionManager\n64                       .DecreaseLiquidityParams({\n65                           tokenId: tokenId,\n66                           liquidity: liquidityDecrease,\n67                           amount0Min: amount0Min,\n68                           amount1Min: amount1Min,\n69 @>                        deadline: block.timestamp\n70                       });\n71   \n72               INonfungiblePositionManager(_underlyingAsset).decreaseLiquidity(\n73                   params\n74               );\n75           }\n76:  \n```\n\n<https://github.com/code-423n4/2022-11-paraspace/blob/c6820a279c64a299a783955749fdc977de8f0449/paraspace-core/contracts/protocol/tokenization/NTokenUniswapV3.sol#L51-L76>\n\nA malicious miner can hold the transaction, which may be being done in order to free up capital to ensure that there are funds available to do operations to prevent a liquidation. It is highly likely that a liquidation is more profitable for a miner to mine, with its associated follow-on transactions, than to allow the decrease of liquidity. A miner can also just hold it until maximum slippage is incurred, as the judge stated.\n\n### Recommended Mitigation Steps\n\nAdd deadline arguments to all functions that interact with AMMs, and pass it along to AMM calls.\n\n\n\n***\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-11-paraspace-contest",
  "Code": [
    {
      "filename": "paraspace-core/contracts/protocol/tokenization/NTokenUniswapV3.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\npragma solidity 0.8.10;\n\nimport {IERC20} from \"../../dependencies/openzeppelin/contracts/IERC20.sol\";\nimport {IERC721} from \"../../dependencies/openzeppelin/contracts/IERC721.sol\";\nimport {IERC1155} from \"../../dependencies/openzeppelin/contracts/IERC1155.sol\";\nimport {IERC721Metadata} from \"../../dependencies/openzeppelin/contracts/IERC721Metadata.sol\";\nimport {Address} from \"../../dependencies/openzeppelin/contracts/Address.sol\";\nimport {SafeERC20} from \"../../dependencies/openzeppelin/contracts/SafeERC20.sol\";\nimport {SafeCast} from \"../../dependencies/openzeppelin/contracts/SafeCast.sol\";\nimport {Errors} from \"../libraries/helpers/Errors.sol\";\nimport {WadRayMath} from \"../libraries/math/WadRayMath.sol\";\nimport {IPool} from \"../../interfaces/IPool.sol\";\nimport {NToken} from \"./NToken.sol\";\nimport {DataTypes} from \"../libraries/types/DataTypes.sol\";\nimport {INonfungiblePositionManager} from \"../../dependencies/uniswap/INonfungiblePositionManager.sol\";\nimport {IWETH} from \"../../misc/interfaces/IWETH.sol\";\nimport {XTokenType} from \"../../interfaces/IXTokenType.sol\";\nimport {INTokenUniswapV3} from \"../../interfaces/INTokenUniswapV3.sol\";\n\n/**\n * @title UniswapV3 NToken\n *\n * @notice Implementation of the interest bearing token for the ParaSpace protocol\n */\ncontract NTokenUniswapV3 is NToken, INTokenUniswapV3 {\n    using SafeERC20 for IERC20;\n\n    /**\n     * @dev Constructor.\n     * @param pool The address of the Pool contract\n     */\n    constructor(IPool pool) NToken(pool, true) {\n        _ERC721Data.balanceLimit = 30;\n    }\n\n    function getXTokenType() external pure override returns (XTokenType) {\n        return XTokenType.NTokenUniswapV3;\n    }\n\n    /**\n     * @notice A function that decreases the current liquidity.\n     * @param tokenId The id of the erc721 token\n     * @param liquidityDecrease The amount of liquidity to remove of LP\n     * @param amount0Min The minimum amount to remove of token0\n     * @param amount1Min The minimum amount to remove of token1\n     * @param receiveEthAsWeth If convert weth to ETH\n     * @return amount0 The amount received back in token0\n     * @return amount1 The amount returned back in token1\n     */\n    function _decreaseLiquidity(\n        address user,\n        uint256 tokenId,\n        uint128 liquidityDecrease,\n        uint256 amount0Min,\n        uint256 amount1Min,\n        bool receiveEthAsWeth\n    ) internal returns (uint256 amount0, uint256 amount1) {\n        if (liquidityDecrease > 0) {\n            // amount0Min and amount1Min are price slippage checks\n            // if the amount received after burning is not greater than these minimums, transaction will fail\n            INonfungiblePositionManager.DecreaseLiquidityParams\n                memory params = INonfungiblePositionManager\n                    .DecreaseLiquidityParams({\n                        tokenId: tokenId,\n                        liquidity: liquidityDecrease,\n                        amount0Min: amount0Min,\n                        amount1Min: amount1Min,\n                        deadline: block.timestamp\n                    });\n\n            INonfungiblePositionManager(_underlyingAsset).decreaseLiquidity(\n                params\n            );\n        }\n\n        (\n            ,\n            ,\n            address token0,\n            address token1,\n            ,\n            ,\n            ,\n            ,\n            ,\n            ,\n            ,\n\n        ) = INonfungiblePositionManager(_underlyingAsset).positions(tokenId);\n\n        address weth = _addressesProvider.getWETH();\n        receiveEthAsWeth = (receiveEthAsWeth &&\n            (token0 == weth || token1 == weth));\n\n        INonfungiblePositionManager.CollectParams\n            memory collectParams = INonfungiblePositionManager.CollectParams({\n                tokenId: tokenId,\n                recipient: receiveEthAsWeth ? address(this) : user,\n                amount0Max: type(uint128).max,\n                amount1Max: type(uint128).max\n            });\n\n        (amount0, amount1) = INonfungiblePositionManager(_underlyingAsset)\n            .collect(collectParams);\n\n        if (receiveEthAsWeth) {\n            uint256 balanceWeth = IERC20(weth).balanceOf(address(this));\n            if (balanceWeth > 0) {\n                IWETH(weth).withdraw(balanceWeth);\n                _safeTransferETH(user, balanceWeth);\n            }\n\n            address pairToken = (token0 == weth) ? token1 : token0;\n            uint256 balanceToken = IERC20(pairToken).balanceOf(address(this));\n            if (balanceToken > 0) {\n                IERC20(pairToken).safeTransfer(user, balanceToken);\n            }\n        }\n    }\n\n    /// @inheritdoc INTokenUniswapV3\n    function decreaseUniswapV3Liquidity(\n        address user,\n        uint256 tokenId,\n        uint128 liquidityDecrease,\n        uint256 amount0Min,\n        uint256 amount1Min,\n        bool receiveEthAsWeth\n    ) external onlyPool nonReentrant {\n        require(user == ownerOf(tokenId), Errors.NOT_THE_OWNER);\n\n        // interact with Uniswap V3\n        _decreaseLiquidity(\n            user,\n            tokenId,\n            liquidityDecrease,\n            amount0Min,\n            amount1Min,\n            receiveEthAsWeth\n        );\n    }\n\n    function _safeTransferETH(address to, uint256 value) internal {\n        (bool success, ) = to.call{value: value}(new bytes(0));\n        require(success, \"ETH_TRANSFER_FAILED\");\n    }\n\n    receive() external payable {}\n}"
    }
  ]
}