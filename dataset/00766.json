{
  "Title": "M-1: `JUSDBankStorage::getTRate()`,`JUSDBankStorage::accrueRate()` are calculated differently, and the data calculation is biased, Causes the `JUSDBank` contract funciton result to be incorrect",
  "Content": "# Issue M-1: `JUSDBankStorage::getTRate()`,`JUSDBankStorage::accrueRate()` are calculated differently, and the data calculation is biased, Causes the `JUSDBank` contract funciton result to be incorrect \n\nSource: https://github.com/sherlock-audit/2023-12-jojo-exchange-update-judging/issues/1 \n\n## Found by \nFastTiger, T1MOH, bitsurfer, dany.armstrong90, detectiveking, joicygiore, rvierdiiev\n## Summary\n```js\n    function accrueRate() public {\n        uint256 currentTimestamp = block.timestamp;\n        if (currentTimestamp == lastUpdateTimestamp) {\n            return;\n        }\n        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);\n@>         tRate = tRate.decimalMul((timeDifference * borrowFeeRate) / Types.SECONDS_PER_YEAR + 1e18);\n        lastUpdateTimestamp = currentTimestamp;\n    }\n\n    function getTRate() public view returns (uint256) {\n        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);\n@>        return tRate + (borrowFeeRate * timeDifference) / Types.SECONDS_PER_YEAR;\n    }\n```\n`JUSDBankStorage::getTRate()`,`JUSDBankStorage::accrueRate()` are calculated differently, and the data calculation is biased, resulting in the JUSDBank contract not being executed correctly\n## Vulnerability Detail\nThe wrong result causes the funciton calculation results of `JUSDBank::_isAccountSafe()`, `JUSDBank::flashLoan()`, `JUSDBank::_handleBadDebt`, etc. to be biased,and all functions that call the relevant function will be biased\n## Impact\nCauses the `JUSDBank` contract funciton result to be incorrect\n## Code Snippet\n\nhttps://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/JUSDBankStorage.sol#L53-L67\n\n## Tool used\n\nManual Review\n## POC\nPlease add the test code to `JUSDViewTest.t.sol` for execution\n```js\n    function testTRateDeviation() public {\n        console.log(block.timestamp);\n        console.log(jusdBank.lastUpdateTimestamp());\n        vm.warp(block.timestamp + 18_356 days);\n        jusdBank.accrueRate();\n        console.log(\"tRate value than 2e18:\", jusdBank.tRate());\n        // block.timestamp for every 1 increment\n        vm.warp(block.timestamp + 1);\n        uint256 getTRateNum = jusdBank.getTRate();\n        jusdBank.accrueRate();\n        uint256 tRateNum = jusdBank.tRate();\n        console.log(\"block.timestamp for every 1 increment, deviation:\", tRateNum - getTRateNum);\n        // block.timestamp for every 1 days increment\n        vm.warp(block.timestamp + 1 days);\n        getTRateNum = jusdBank.getTRate();\n        jusdBank.accrueRate();\n        tRateNum = jusdBank.tRate();\n        console.log(\"block.timestamp for every 1 days increment, deviation:\", tRateNum - getTRateNum);\n    }\n```\n## Recommendation\nUse the same calculation formula:\n```diff\n    function accrueRate() public {\n        uint256 currentTimestamp = block.timestamp;\n        if (currentTimestamp == lastUpdateTimestamp) {\n            return;\n        }\n        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);\n         tRate = tRate.decimalMul((timeDifference * borrowFeeRate) / Types.SECONDS_PER_YEAR + 1e18);\n        lastUpdateTimestamp = currentTimestamp;\n    }\n\n    function getTRate() public view returns (uint256) {\n        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);\n-       return tRate + (borrowFeeRate * timeDifference) / Types.SECONDS_PER_YEAR;\n+       return  tRate.decimalMul((timeDifference * borrowFeeRate) / Types.SECONDS_PER_YEAR + 1e18);\n    }\n```\n\n\n\n\n## Discussion\n\n**sherlock-admin2**\n\n1 comment(s) were left on this issue during the judging contest.\n\n**takarez** commented:\n>  valid because { This is also valid and a dupp of 016}\n\n\n\n**JoscelynFarr**\n\nAfter internal discussion, we decide to accrue rate in the view which is `getTRate()` function\n\n**JoscelynFarr**\n\nFixed PR: https://github.com/JOJOexchange/smart-contract-EVM/commit/4b591c9fb0a232f784919905752c8e68d32b39ff\n\n**IAm0x52**\n\nFix looks good. Math has been updated to use full precision\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/136",
  "Code": [
    {
      "filename": "smart-contract-EVM/src/JUSDBankStorage.sol",
      "content": "/*\n    Copyright 2022 JOJO Exchange\n    SPDX-License-Identifier: BUSL-1.1\n*/\n\npragma solidity ^0.8.20;\n\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"@openzeppelin/contracts/security/ReentrancyGuard.sol\";\nimport \"./libraries/FlashLoanReentrancyGuard.sol\";\nimport \"./libraries/SignedDecimalMath.sol\";\nimport \"./libraries/Types.sol\";\n\nabstract contract JUSDBankStorage is Ownable, ReentrancyGuard, FlashLoanReentrancyGuard {\n    using SignedDecimalMath for uint256;\n\n    // reserves amount\n    uint256 public reservesNum;\n    // max reserves amount\n    uint256 public maxReservesNum;\n    // max borrow JUSD amount per account\n    uint256 public maxPerAccountBorrowAmount;\n    // max total borrow JUSD amount\n    uint256 public maxTotalBorrowAmount;\n    // t0 total borrow JUSD amount\n    uint256 public t0TotalBorrowAmount;\n    // borrow fee rate\n    uint256 public borrowFeeRate;\n    // t0Rate\n    uint256 public tRate;\n    // update timestamp\n    uint256 public lastUpdateTimestamp;\n\n    bool public isLiquidatorWhitelistOpen;\n    // insurance account\n    address public insurance;\n    // JUSD address\n    address public JUSD;\n    // primary address\n    address public primaryAsset;\n\n    address public JOJODealer;\n    // reserves's list\n    address[] public reservesList;\n    mapping(address => bool) isLiquidatorWhiteList;\n    // reserve token address ==> reserve info\n    mapping(address => Types.ReserveInfo) public reserveInfo;\n    // reserve token address ==> user info\n    mapping(address => Types.UserInfo) public userInfo;\n    // client -> operator -> bool\n    mapping(address => mapping(address => bool)) public operatorRegistry;\n\n    function accrueRate() public {\n        uint256 currentTimestamp = block.timestamp;\n        if (currentTimestamp == lastUpdateTimestamp) {\n            return;\n        }\n        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);\n        tRate = tRate.decimalMul((timeDifference * borrowFeeRate) / Types.SECONDS_PER_YEAR + 1e18);\n        lastUpdateTimestamp = currentTimestamp;\n    }\n\n    function getTRate() public view returns (uint256) {\n        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);\n        return tRate + (borrowFeeRate * timeDifference) / Types.SECONDS_PER_YEAR;\n    }\n}"
    }
  ]
}