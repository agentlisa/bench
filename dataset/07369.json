{
  "Title": "[M-06] Only one GroupBuy can ever use USDT or similar tokens with front-running approval protections",
  "Content": "# Lines of code\n\nhttps://github.com/code-423n4/2022-12-tessera/blob/f37a11407da2af844bbfe868e1422e3665a5f8e4/src/seaport/targets/SeaportLister.sol#L29-L46\n\n\n# Vulnerability details\n\nCalling `approve()` without first calling `approve(0)` if the current approval is non-zero will revert with some tokens, such as Tether (USDT). While Tether is known to do this, it applies to other tokens as well, which are trying to protect against [this attack vector](https://docs.google.com/document/d/1YLPtQxZu1UAvO9cZ1O2RPXBbT0mooh4DYKjA_jp-RLM/edit).\n\n## Impact\nOnly the first listing will start with the conduit's approval at zero and will be able to change it to the maximum. Thereafter, every attempt to approve that same token will revert, causing any order using this lister to revert, including a re-listing at a lower price, which the protocol allows for.\n\n## Proof of Concept\nThe seaport conduit address is set in the constructor as an immutable variable, so it's not possible to change it once the issue is hit:\n```solidity\n// File: src/seaport/targets/SeaportLister.sol : SeaportLister.constructor()   #1\n\n19        constructor(address _conduit) {\n20 @>         conduit = _conduit;\n21:       }\n```\nhttps://github.com/code-423n4/2022-12-tessera/blob/f37a11407da2af844bbfe868e1422e3665a5f8e4/src/seaport/targets/SeaportLister.sol#L19-L21\n\nThe approval is set to the maximum without check whether it's already the maximum:\n```solidity\n// File: src/seaport/targets/SeaportLister.sol : SeaportLister.validateListing()   #2\n\n29                for (uint256 i; i < ordersLength; ++i) {\n30                    uint256 offerLength = _orders[i].parameters.offer.length;\n31                    for (uint256 j; j < offerLength; ++j) {\n32                        OfferItem memory offer = _orders[i].parameters.offer[j];\n33                        address token = offer.token;\n34                        ItemType itemType = offer.itemType;\n35                        if (itemType == ItemType.ERC721)\n36                            IERC721(token).setApprovalForAll(conduit, true);\n37                        if (itemType == ItemType.ERC1155)\n38                            IERC1155(token).setApprovalForAll(conduit, true);\n39                        if (itemType == ItemType.ERC20)\n40 @>                         IERC20(token).approve(conduit, type(uint256).max);\n41                    }\n42                }\n43            }\n44            // Validates the order on-chain so no signature is required to fill it\n45            assert(ConsiderationInterface(_consideration).validate(_orders));\n46:       }\n```\nhttps://github.com/code-423n4/2022-12-tessera/blob/f37a11407da2af844bbfe868e1422e3665a5f8e4/src/seaport/targets/SeaportLister.sol#L29-L46\n\nThe README states: `The Tessera Protocol is designed around the concept of Hyperstructures, which are crypto protocols that can run for free and forever, without maintenance, interruption or intermediaries`, and having to deploy a new `SeaportLister` in order to have a fresh conduit that also needs to be deployed, is not `without maintenance or interruption`.\n\n## Tools Used\nCode inspection\n\n## Recommended Mitigation Steps\nAlways reset the approval to zero before changing it to the maximum\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-12-tessera-versus-contest",
  "Code": [
    {
      "filename": "src/seaport/targets/SeaportLister.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.13;\n\nimport {IERC20} from \"../../interfaces/IERC20.sol\";\nimport {IERC721} from \"../../interfaces/IERC721.sol\";\nimport {IERC1155} from \"../../interfaces/IERC1155.sol\";\n\nimport {ConsiderationInterface} from \"seaport/interfaces/ConsiderationInterface.sol\";\nimport {ItemType, OfferItem} from \"seaport/lib/ConsiderationStructs.sol\";\nimport {ISeaportLister, Order, OrderComponents} from \"../interfaces/ISeaportLister.sol\";\n\n/// @title SeaportLister\n/// @author Tessera\n/// @notice Target contract for executing the listing and delisting of orders on Seaport\ncontract SeaportLister is ISeaportLister {\n    /// @notice Address of the conduit that is approved to spend the items\n    address public immutable conduit;\n\n    constructor(address _conduit) {\n        conduit = _conduit;\n    }\n\n    /// @notice Approves the conduit to list the offer items\n    /// @param _consideration Address of the Consideration contract (Seaport)\n    /// @param _orders List of orders being validated\n    function validateListing(address _consideration, Order[] memory _orders) external {\n        uint256 ordersLength = _orders.length;\n        unchecked {\n            for (uint256 i; i < ordersLength; ++i) {\n                uint256 offerLength = _orders[i].parameters.offer.length;\n                for (uint256 j; j < offerLength; ++j) {\n                    OfferItem memory offer = _orders[i].parameters.offer[j];\n                    address token = offer.token;\n                    ItemType itemType = offer.itemType;\n                    if (itemType == ItemType.ERC721)\n                        IERC721(token).setApprovalForAll(conduit, true);\n                    if (itemType == ItemType.ERC1155)\n                        IERC1155(token).setApprovalForAll(conduit, true);\n                    if (itemType == ItemType.ERC20)\n                        IERC20(token).approve(conduit, type(uint256).max);\n                }\n            }\n        }\n        // Validates the order on-chain so no signature is required to fill it\n        assert(ConsiderationInterface(_consideration).validate(_orders));\n    }\n\n    /// @notice Cancels the listing of all offer items\n    /// @param _consideration Address of the Consideration contract (Seaport)\n    /// @param _orders List of orders being canceled\n    function cancelListing(address _consideration, OrderComponents[] memory _orders) external {\n        assert(ConsiderationInterface(_consideration).cancel(_orders));\n    }\n}"
    }
  ]
}