{
  "Title": "[3] State variables can be packed into fewer storage slots",
  "Content": "If variables occupying the same slot are both written the same function or by the constructor, avoids a separate Gsset (**20000 gas**). Reads of the variables can also be cheaper\n\n*There are 2 instances of this issue:*\n```solidity\nFile: protocol/contracts/tokenomics/VestedEscrow.sol   #1\n\n/// @audit Variable ordering with 8 slots instead of the current 9:\n/// @audit  uint256(32):totalTime, uint256(32):initialLockedSupply, uint256(32):unallocatedSupply, mapping(32):initialLocked, mapping(32):totalClaimed, mapping(32):holdingContract, address(20):admin, bool(1):initializedSupply, address(20):fundAdmin\n34:       address public admin;\n```\nhttps://github.com/code-423n4/2022-05-backd/blob/2a5664d35cde5b036074edef3c1369b984d10010/protocol/contracts/tokenomics/VestedEscrow.sol#L34\n\n```solidity\nFile: protocol/contracts/tokenomics/KeeperGauge.sol   #2\n\n/// @audit Variable ordering with 5 slots instead of the current 6:\n/// @audit  mapping(32):keeperRecords, mapping(32):perPeriodTotalFees, uint256(32):epoch, mapping(32):perPeriodTotalInflation, uint48(6):lastUpdated, bool(1):killed\n27:       mapping(address => KeeperRecord) public keeperRecords;\n```\nhttps://github.com/code-423n4/2022-05-backd/blob/2a5664d35cde5b036074edef3c1369b984d10010/protocol/contracts/tokenomics/KeeperGauge.sol#L27\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/reports/2022-05-backd",
  "Code": [
    {
      "filename": "protocol/contracts/tokenomics/VestedEscrow.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.10;\n\n/*\nRewrite of Convex Finance's Vested Escrow\nfound at https://github.com/convex-eth/platform/blob/main/contracts/contracts/VestedEscrow.sol\nChanges:\n- remove safe math (default from Solidity >=0.8)\n- remove claim and stake logic\n- remove safeTransferFrom logic and add support for \"airdropped\" reward token\n*/\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport \"@openzeppelin/contracts/utils/math/Math.sol\";\nimport \"@openzeppelin/contracts/security/ReentrancyGuard.sol\";\n\nimport \"../../interfaces/tokenomics/IVestedEscrow.sol\";\n\nimport \"../../libraries/Errors.sol\";\nimport \"../../libraries/UncheckedMath.sol\";\n\ncontract EscrowTokenHolder {\n    constructor(address rewardToken_) {\n        IERC20(rewardToken_).approve(msg.sender, type(uint256).max);\n    }\n}\n\ncontract VestedEscrow is IVestedEscrow, ReentrancyGuard {\n    using UncheckedMath for uint256;\n    using SafeERC20 for IERC20;\n\n    IERC20 public immutable rewardToken;\n    address public admin;\n    address public fundAdmin;\n\n    uint256 public immutable startTime;\n    uint256 public immutable endTime;\n    uint256 public totalTime;\n    uint256 public initialLockedSupply;\n    uint256 public unallocatedSupply;\n    bool public initializedSupply;\n\n    mapping(address => uint256) public initialLocked;\n    mapping(address => uint256) public totalClaimed;\n    mapping(address => address) public holdingContract;\n\n    event Fund(address indexed recipient, uint256 reward);\n    event Claim(address indexed user, uint256 amount);\n\n    constructor(\n        address rewardToken_,\n        uint256 starttime_,\n        uint256 endtime_,\n        address fundAdmin_\n    ) {\n        require(starttime_ >= block.timestamp, \"start must be future\");\n        require(endtime_ > starttime_, \"end must be greater\");\n\n        rewardToken = IERC20(rewardToken_);\n        startTime = starttime_;\n        endTime = endtime_;\n        totalTime = endtime_ - starttime_;\n        admin = msg.sender;\n        fundAdmin = fundAdmin_;\n    }\n\n    function setAdmin(address _admin) external override {\n        require(_admin != address(0), Error.ZERO_ADDRESS_NOT_ALLOWED);\n        require(msg.sender == admin, Error.UNAUTHORIZED_ACCESS);\n        admin = _admin;\n    }\n\n    function setFundAdmin(address _fundadmin) external override {\n        require(_fundadmin != address(0), Error.ZERO_ADDRESS_NOT_ALLOWED);\n        require(msg.sender == admin, Error.UNAUTHORIZED_ACCESS);\n        fundAdmin = _fundadmin;\n    }\n\n    function initializeUnallocatedSupply() external override returns (bool) {\n        require(msg.sender == admin, Error.UNAUTHORIZED_ACCESS);\n        require(!initializedSupply, \"Supply already initialized once\");\n        unallocatedSupply = rewardToken.balanceOf(address(this));\n        require(unallocatedSupply > 0, \"No reward tokens in contract\");\n        initializedSupply = true;\n        return true;\n    }\n\n    function fund(FundingAmount[] calldata amounts) external override nonReentrant returns (bool) {\n        require(msg.sender == fundAdmin || msg.sender == admin, Error.UNAUTHORIZED_ACCESS);\n        require(initializedSupply, \"Supply must be initialized\");\n\n        uint256 totalAmount;\n        for (uint256 i; i < amounts.length; i = i.uncheckedInc()) {\n            uint256 amount = amounts[i].amount;\n            address recipient_ = amounts[i].recipient;\n            address holdingAddress = holdingContract[recipient_];\n            if (holdingAddress == address(0)) {\n                holdingAddress = address(new EscrowTokenHolder(address(rewardToken)));\n                holdingContract[recipient_] = holdingAddress;\n            }\n            rewardToken.safeTransfer(holdingAddress, amount);\n            initialLocked[recipient_] = initialLocked[recipient_] + amount;\n            totalAmount = totalAmount + amount;\n            emit Fund(recipient_, amount);\n        }\n\n        initialLockedSupply = initialLockedSupply + totalAmount;\n        unallocatedSupply = unallocatedSupply - totalAmount;\n        return true;\n    }\n\n    function claim() external virtual override {\n        _claimUntil(msg.sender, block.timestamp);\n    }\n\n    function vestedSupply() external view override returns (uint256) {\n        return _totalVested();\n    }\n\n    function lockedSupply() external view override returns (uint256) {\n        return initialLockedSupply - _totalVested();\n    }\n\n    function vestedOf(address _recipient) external view virtual override returns (uint256) {\n        return _totalVestedOf(_recipient, block.timestamp);\n    }\n\n    function balanceOf(address _recipient) external view virtual override returns (uint256) {\n        return _balanceOf(_recipient, block.timestamp);\n    }\n\n    function lockedOf(address _recipient) external view virtual override returns (uint256) {\n        uint256 vested = _totalVestedOf(_recipient, block.timestamp);\n        return initialLocked[_recipient] - vested;\n    }\n\n    function claim(address _recipient) public virtual override nonReentrant {\n        _claimUntil(_recipient, block.timestamp);\n    }\n\n    function _claimUntil(address _recipient, uint256 _time) internal {\n        uint256 claimable = _balanceOf(msg.sender, _time);\n        if (claimable == 0) return;\n        totalClaimed[msg.sender] = totalClaimed[msg.sender] + claimable;\n        rewardToken.safeTransferFrom(holdingContract[msg.sender], _recipient, claimable);\n\n        emit Claim(msg.sender, claimable);\n    }\n\n    function _computeVestedAmount(uint256 locked, uint256 _time) internal view returns (uint256) {\n        if (_time < startTime) {\n            return 0;\n        }\n        uint256 elapsed = _time - startTime;\n        return Math.min((locked * elapsed) / totalTime, locked);\n    }\n\n    function _totalVestedOf(address _recipient, uint256 _time) internal view returns (uint256) {\n        return _computeVestedAmount(initialLocked[_recipient], _time);\n    }\n\n    function _totalVested() internal view returns (uint256) {\n        return _computeVestedAmount(initialLockedSupply, block.timestamp);\n    }\n\n    function _balanceOf(address _recipient, uint256 _time) internal view returns (uint256) {\n        uint256 vested = _totalVestedOf(_recipient, _time);\n        return vested - totalClaimed[_recipient];\n    }\n}"
    },
    {
      "filename": "protocol/contracts/tokenomics/KeeperGauge.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\npragma solidity 0.8.10;\n\nimport \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\n\nimport \"../../interfaces/IController.sol\";\nimport \"../../interfaces/tokenomics/IKeeperGauge.sol\";\n\nimport \"../../libraries/ScaledMath.sol\";\nimport \"../../libraries/Errors.sol\";\nimport \"../../libraries/AddressProviderHelpers.sol\";\nimport \"../../libraries/UncheckedMath.sol\";\n\nimport \"../access/Authorization.sol\";\n\ncontract KeeperGauge is IKeeperGauge, Authorization {\n    using AddressProviderHelpers for IAddressProvider;\n    using ScaledMath for uint256;\n    using UncheckedMath for uint256;\n\n    struct KeeperRecord {\n        mapping(uint256 => uint256) feesInPeriod;\n        uint256 nextEpochToClaim;\n        bool firstEpochSet;\n    }\n\n    mapping(address => KeeperRecord) public keeperRecords;\n    mapping(uint256 => uint256) public perPeriodTotalFees;\n\n    IController public immutable controller;\n    address public immutable pool;\n    uint256 public epoch;\n\n    uint48 public lastUpdated;\n    mapping(uint256 => uint256) public perPeriodTotalInflation;\n\n    bool public override killed;\n\n    modifier onlyInflationManager() {\n        require(msg.sender == address(controller.inflationManager()), Error.UNAUTHORIZED_ACCESS);\n        _;\n    }\n\n    constructor(IController _controller, address _pool)\n        Authorization(_controller.addressProvider().getRoleManager())\n    {\n        controller = _controller;\n        pool = _pool;\n        lastUpdated = uint48(block.timestamp);\n    }\n\n    /**\n     * @notice Shut down the gauge.\n     * @dev Accrued inflation can still be claimed from the gauge after shutdown.\n     * @return `true` if successful.\n     */\n    function kill() external override onlyInflationManager returns (bool) {\n        poolCheckpoint();\n        epoch++;\n        killed = true;\n        return true;\n    }\n\n    /**\n     * @notice Report fees generated by a keeper (this is the basis for inflation accrual).\n     * @dev lpTokenAddress is included for forward compatibility with a single gauge solution.\n     * @param beneficiary Address of the keeper who earned the fees.\n     * @param amount Amount of fees (in lp tokens) earned.\n     * @param lpTokenAddress Address of the lpToken in which the fees are paid.\n     * @return `true` if successful.\n     */\n    function reportFees(\n        address beneficiary,\n        uint256 amount,\n        address lpTokenAddress\n    ) external override returns (bool) {\n        lpTokenAddress; // silencing compiler warning\n        require(\n            IController(controller).addressProvider().isWhiteListedFeeHandler(msg.sender),\n            Error.ADDRESS_NOT_WHITELISTED\n        );\n        require(!killed, Error.CONTRACT_PAUSED);\n        if (!keeperRecords[beneficiary].firstEpochSet) {\n            keeperRecords[beneficiary].firstEpochSet = true;\n            keeperRecords[beneficiary].nextEpochToClaim = epoch;\n        }\n        keeperRecords[beneficiary].feesInPeriod[epoch] += amount;\n        perPeriodTotalFees[epoch] += amount;\n        return true;\n    }\n\n    /**\n     * @notice Advance the inflation accrual epoch.\n     * @return `true` if successful.\n     */\n    function advanceEpoch() external virtual override onlyInflationManager returns (bool) {\n        poolCheckpoint();\n        epoch++;\n        return true;\n    }\n\n    function claimRewards(address beneficiary) external override returns (uint256) {\n        return claimRewards(beneficiary, epoch);\n    }\n\n    function claimableRewards(address beneficiary) external view override returns (uint256) {\n        return _calcTotalClaimable(beneficiary, keeperRecords[beneficiary].nextEpochToClaim, epoch);\n    }\n\n    function poolCheckpoint() public override returns (bool) {\n        if (killed) return false;\n        uint256 timeElapsed = block.timestamp - uint256(lastUpdated);\n        uint256 currentRate = IController(controller).inflationManager().getKeeperRateForPool(pool);\n        perPeriodTotalInflation[epoch] += currentRate * timeElapsed;\n        lastUpdated = uint48(block.timestamp);\n        return true;\n    }\n\n    /**\n     * @notice Claim rewards with an epoch up to which they should be claimed specified.\n     * @param beneficiary Address to claim rewards for.\n     * @param endEpoch Epoch up to which to claim rewards.\n     * @return The amount of rewards claimed.\n     */\n    function claimRewards(address beneficiary, uint256 endEpoch) public override returns (uint256) {\n        require(\n            msg.sender == beneficiary || _roleManager().hasRole(Roles.GAUGE_ZAP, msg.sender),\n            Error.UNAUTHORIZED_ACCESS\n        );\n        if (endEpoch > epoch) {\n            endEpoch = epoch;\n        }\n\n        uint256 totalClaimable = _calcTotalClaimable(\n            beneficiary,\n            keeperRecords[beneficiary].nextEpochToClaim,\n            endEpoch\n        );\n        keeperRecords[beneficiary].nextEpochToClaim = endEpoch;\n        require(totalClaimable > 0, Error.ZERO_TRANSFER_NOT_ALLOWED);\n        _mintRewards(beneficiary, totalClaimable);\n\n        return totalClaimable;\n    }\n\n    function _mintRewards(address beneficiary, uint256 amount) internal returns (bool) {\n        IController(controller).inflationManager().mintRewards(beneficiary, amount);\n        return true;\n    }\n\n    function _calcTotalClaimable(\n        address beneficiary,\n        uint256 startEpoch,\n        uint256 endEpoch\n    ) internal view returns (uint256) {\n        uint256 totalClaimable;\n        for (uint256 i = startEpoch; i < endEpoch; i = i.uncheckedInc()) {\n            totalClaimable += (\n                keeperRecords[beneficiary].feesInPeriod[i].scaledDiv(perPeriodTotalFees[i])\n            ).scaledMul(perPeriodTotalInflation[i]);\n        }\n        return totalClaimable;\n    }\n}"
    }
  ]
}