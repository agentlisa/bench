{
  "Title": "[M-03] hard to clear balance",
  "Content": "_Submitted by jonah1005_\n\nThe contract does not allow users to transfer by share. Therefore, It is hard for users to clear out all the shares.\nThere will be users using this token with Metamask and it is likely the `pricePerShare` would increase after the user sends transactions. I consider this is a medium-risk issue.\n\n#### Proof of Concept\n[WrappedIbbtc.sol#L110-L118](https://github.com/code-423n4/2021-10-badgerdao/blob/main/contracts/WrappedIbbtc.sol#L110-L118)\n\n#### Recommended Mitigation Steps\nA new `transferShares` beside the original `transfer()` would build a better UX. sushi's bento box would be a good ref [BentoBox.sol](https://github.com/sushiswap/bentobox/blob/master/contracts/BentoBox.sol)\n\n**[dapp-whisperer (BadgerDAO) confirmed](https://github.com/code-423n4/2021-10-badgerdao-findings/issues/24)**\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2021-10-badgerdao-ibbtc-wrapper-contest",
  "Code": [
    {
      "filename": "contracts/WrappedIbbtc.sol",
      "content": "//SPDX-License-Identifier: Unlicense\npragma solidity ^0.6.12;\n\nimport \"../deps/@openzeppelin/contracts-upgradeable/proxy/Initializable.sol\";\nimport \"../deps/@openzeppelin/contracts-upgradeable/token/ERC20/ERC20Upgradeable.sol\";\nimport \"./ICoreOracle.sol\";\n\n/*\n    Wrapped Interest-Bearing Bitcoin (Non-Ethereum mainnet variant)\n*/\ncontract WrappedIbbtc is Initializable, ERC20Upgradeable {\n    address public governance;\n    address public pendingGovernance;\n    ERC20Upgradeable public ibbtc; \n\n    ICoreOracle public oracle;\n\n    event SetOracle(address oracle);\n    event SetPendingGovernance(address pendingGovernance);\n    event AcceptPendingGovernance(address pendingGovernance);\n\n    /// ===== Modifiers =====\n    modifier onlyPendingGovernance() {\n        require(msg.sender == pendingGovernance, \"onlyPendingGovernance\");\n        _;\n    }\n\n    modifier onlyGovernance() {\n        require(msg.sender == governance, \"onlyGovernance\");\n        _;\n    }\n\n    modifier onlyOracle() {\n        require(msg.sender == address(oracle), \"onlyOracle\");\n        _;\n    }\n\n    function initialize(address _governance, address _ibbtc, address _oracle) public initializer {\n        __ERC20_init(\"Wrapped Interest-Bearing Bitcoin\", \"wibBTC\");\n        governance = _governance;\n        oracle = ICoreOracle(_oracle);\n        ibbtc = ERC20Upgradeable(_ibbtc);\n\n        emit SetOracle(_oracle);\n    }\n\n    /// ===== Permissioned: Governance =====\n    function setPendingGovernance(address _pendingGovernance) external onlyGovernance {\n        pendingGovernance = _pendingGovernance;\n        emit SetPendingGovernance(pendingGovernance);\n    }\n\n    function setOracle(address _oracle) external onlyGovernance {\n        oracle = ICoreOracle(_oracle);\n        emit SetOracle(_oracle);\n    }\n\n    /// ===== Permissioned: Pending Governance =====\n    function acceptPendingGovernance() external onlyPendingGovernance {\n        governance = pendingGovernance;\n        emit AcceptPendingGovernance(pendingGovernance);\n    }\n\n    /// ===== Permissionless Calls =====\n\n    /// @dev Deposit ibBTC to mint wibBTC shares\n    function mint(uint256 _shares) external {\n        require(ibbtc.transferFrom(_msgSender(), address(this), _shares));\n        _mint(_msgSender(), _shares);\n    }\n\n    /// @dev Redeem wibBTC for ibBTC. Denominated in shares.\n    function burn(uint256 _shares) external {\n        _burn(_msgSender(), _shares);\n        require(ibbtc.transfer(_msgSender(), _shares));\n    }\n\n    /// ===== Transfer Overrides =====\n    /**\n     * @dev See {IERC20-transferFrom}.\n     *\n     * Emits an {Approval} event indicating the updated allowance. This is not\n     * required by the EIP. See the note at the beginning of {ERC20};\n     *\n     * Requirements:\n     * - `sender` and `recipient` cannot be the zero address.\n     * - `sender` must have a balance of at least `amount`.\n     * - the caller must have allowance for ``sender``'s tokens of at least\n     * `amount`.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {\n        /// @dev the _balances mapping represents the underlying ibBTC shares (\"non-rebased balances\")\n        /// @dev the naming confusion is due to maintaining original ERC20 code as much as possible\n\n        uint256 amountInShares = balanceToShares(amount);\n\n        _transfer(sender, recipient, amountInShares);\n        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(amountInShares, \"ERC20: transfer amount exceeds allowance\"));\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-transfer}.\n     *\n     * Requirements:\n     *\n     * - `recipient` cannot be the zero address.\n     * - the caller must have a balance of at least `amount`.\n     */\n    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {\n        /// @dev the _balances mapping represents the underlying ibBTC shares (\"non-rebased balances\")\n        /// @dev the naming confusion is due to maintaining original ERC20 code as much as possible\n\n        uint256 amountInShares = balanceToShares(amount);\n\n        _transfer(_msgSender(), recipient, amountInShares);\n        return true;\n    }\n\n    /// ===== View Methods =====\n\n    /// @dev Current pricePerShare read live from oracle\n    function pricePerShare() public view virtual returns (uint256) {\n        return oracle.pricePerShare();\n    }\n\n    /// @dev Wrapped ibBTC shares of account\n    function sharesOf(address account) public view returns (uint256) {\n        return _balances[account];\n    }\n\n    /// @dev Current account shares * pricePerShare\n    function balanceOf(address account) public view override returns (uint256) {\n        return sharesOf(account).mul(pricePerShare()).div(1e18);\n    }\n\n    /// @dev Total wrapped ibBTC shares\n    function totalShares() public view returns (uint256) {\n        return _totalSupply;\n    }\n\n    /// @dev Current total shares * pricePerShare\n    function totalSupply() public view override returns (uint256) {\n        return totalShares().mul(pricePerShare()).div(1e18);\n    }\n\n    function balanceToShares(uint256 balance) public view returns (uint256) {\n        return balance.mul(1e18).div(pricePerShare());\n    }\n\n    function sharesToBalance(uint256 shares) public view returns (uint256) {\n        return shares.mul(pricePerShare()).div(1e18);\n    }\n}"
    }
  ]
}