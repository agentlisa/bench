{
  "Title": "[07] `OptimismMintableERC20Factory.createOptimismMintableERC20` FUNCTION CAN BE CALLED FOR MULTIPLE TIMES FOR SAME `_remoteToken`-`_name`-`_symbol` COMBINATION",
  "Content": "The `OptimismMintableERC20Factory.createOptimismMintableERC20` function can be called multiple times for the same `_remoteToken`-`_name`-`_symbol` combination. This means that multiple instances of the `OptimismMintableERC20` contract can be created for the same `_remoteToken`-`_name`-`_symbol` combination, which can then be used to trick and phishing attack users. To be more secured, please consider updating the `OptimismMintableERC20Factory.createOptimismMintableERC20` function to revert when an instance of the `OptimismMintableERC20` contract has already been created for the same `_remoteToken`-`_name`-`_symbol` combination.\n\nhttps://github.com/ethereum-optimism/optimism/blob/365367e6716d65ba45b583d72d9b030fbb5c0e47/packages/contracts-bedrock/contracts/universal/OptimismMintableERC20Factory.sol#L87-L109\n```solidity\n    function createOptimismMintableERC20(\n        address _remoteToken,\n        string memory _name,\n        string memory _symbol\n    ) public returns (address) {\n        require(\n            _remoteToken != address(0),\n            \"OptimismMintableERC20Factory: must provide remote token address\"\n        );\n\n        address localToken = address(\n            new OptimismMintableERC20(BRIDGE, _remoteToken, _name, _symbol)\n        );\n\n        // Emit the old event too for legacy support.\n        emit StandardL2TokenCreated(_remoteToken, localToken);\n\n        // Emit the updated event. The arguments here differ from the legacy event, but\n        // are consistent with the ordering used in StandardBridge events.\n        emit OptimismMintableERC20Created(localToken, _remoteToken, msg.sender);\n\n        return localToken;\n    }\n```\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2023-05-base",
  "Code": [
    {
      "filename": "packages/contracts-bedrock/contracts/universal/OptimismMintableERC20Factory.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.15;\n\n/* Contract Imports */\nimport { OptimismMintableERC20 } from \"../universal/OptimismMintableERC20.sol\";\nimport { Semver } from \"./Semver.sol\";\n\n/**\n * @custom:proxied\n * @custom:predeployed 0x4200000000000000000000000000000000000012\n * @title OptimismMintableERC20Factory\n * @notice OptimismMintableERC20Factory is a factory contract that generates OptimismMintableERC20\n *         contracts on the network it's deployed to. Simplifies the deployment process for users\n *         who may be less familiar with deploying smart contracts. Designed to be backwards\n *         compatible with the older StandardL2ERC20Factory contract.\n */\ncontract OptimismMintableERC20Factory is Semver {\n    /**\n     * @notice Address of the StandardBridge on this chain.\n     */\n    address public immutable BRIDGE;\n\n    /**\n     * @custom:legacy\n     * @notice Emitted whenever a new OptimismMintableERC20 is created. Legacy version of the newer\n     *         OptimismMintableERC20Created event. We recommend relying on that event instead.\n     *\n     * @param remoteToken Address of the token on the remote chain.\n     * @param localToken  Address of the created token on the local chain.\n     */\n    event StandardL2TokenCreated(address indexed remoteToken, address indexed localToken);\n\n    /**\n     * @notice Emitted whenever a new OptimismMintableERC20 is created.\n     *\n     * @param localToken  Address of the created token on the local chain.\n     * @param remoteToken Address of the corresponding token on the remote chain.\n     * @param deployer    Address of the account that deployed the token.\n     */\n    event OptimismMintableERC20Created(\n        address indexed localToken,\n        address indexed remoteToken,\n        address deployer\n    );\n\n    /**\n     * @custom:semver 1.1.0\n     *\n     * @notice The semver MUST be bumped any time that there is a change in\n     *         the OptimismMintableERC20 token contract since this contract\n     *         is responsible for deploying OptimismMintableERC20 contracts.\n     *\n     * @param _bridge Address of the StandardBridge on this chain.\n     */\n    constructor(address _bridge) Semver(1, 1, 0) {\n        BRIDGE = _bridge;\n    }\n\n    /**\n     * @custom:legacy\n     * @notice Creates an instance of the OptimismMintableERC20 contract. Legacy version of the\n     *         newer createOptimismMintableERC20 function, which has a more intuitive name.\n     *\n     * @param _remoteToken Address of the token on the remote chain.\n     * @param _name        ERC20 name.\n     * @param _symbol      ERC20 symbol.\n     *\n     * @return Address of the newly created token.\n     */\n    function createStandardL2Token(\n        address _remoteToken,\n        string memory _name,\n        string memory _symbol\n    ) external returns (address) {\n        return createOptimismMintableERC20(_remoteToken, _name, _symbol);\n    }\n\n    /**\n     * @notice Creates an instance of the OptimismMintableERC20 contract.\n     *\n     * @param _remoteToken Address of the token on the remote chain.\n     * @param _name        ERC20 name.\n     * @param _symbol      ERC20 symbol.\n     *\n     * @return Address of the newly created token.\n     */\n    function createOptimismMintableERC20(\n        address _remoteToken,\n        string memory _name,\n        string memory _symbol\n    ) public returns (address) {\n        require(\n            _remoteToken != address(0),\n            \"OptimismMintableERC20Factory: must provide remote token address\"\n        );\n\n        address localToken = address(\n            new OptimismMintableERC20(BRIDGE, _remoteToken, _name, _symbol)\n        );\n\n        // Emit the old event too for legacy support.\n        emit StandardL2TokenCreated(_remoteToken, localToken);\n\n        // Emit the updated event. The arguments here differ from the legacy event, but\n        // are consistent with the ordering used in StandardBridge events.\n        emit OptimismMintableERC20Created(localToken, _remoteToken, msg.sender);\n\n        return localToken;\n    }\n}"
    }
  ]
}