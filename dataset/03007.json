{
  "Title": "M-2: Price oracle could get a stale price",
  "Content": "# Issue M-2: Price oracle could get a stale price \n\nSource: https://github.com/sherlock-audit/2022-09-notional-judging/issues/133 \n\n## Found by \nrajatbeladiya, Lambda, Chom, Waze, GimelSec, ak1, GalloDaSballo\n\n## Summary\n\n`_calculateAnswer()` will get `baseAnswer` from Chainlink oracle. But it doesn't check round id and timestamp, leading to it may get a stale price from Chainlink oracle.\n\n## Vulnerability Detail\n\nIn wstETHChainlinkOracle.sol, it check `baseAnswer` > 0, but it doesn't check for the stale price by `updateAt` and `roundId`.\n\n## Impact\n\nPrice oracle could get a stale price without checking `roundId`.\n\n## Code Snippet\n\nhttps://github.com/sherlock-audit/2022-09-notional/blob/main/leveraged-vaults/contracts/trading/oracles/wstETHChainlinkOracle.sol#L26-L44\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\nCheck `answer`, `updateAt` and `roundId` when getting price:\n\n```\n        (uint80 roundId, int256 answer, , uint256 updatedAt, uint80 answeredInRound) = oracle.latestRoundData();\n\n        require(updatedAt > 0, \"Round is not complete\");\n        require(answer >= 0, \"Malfunction\");\n        require(answeredInRound >= roundID, \"Stale price\");\n```\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/2",
  "Code": [
    {
      "filename": "leveraged-vaults/contracts/trading/oracles/wstETHChainlinkOracle.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-only\npragma solidity 0.8.15;\n\nimport {AggregatorV2V3Interface} from \"../../../interfaces/chainlink/AggregatorV2V3Interface.sol\";\nimport {IWstETH} from \"../../../interfaces/IWstETH.sol\";\nimport {TypeConvert} from \"../../global/TypeConvert.sol\";\n\ncontract WstETHChainlinkOracle is AggregatorV2V3Interface {\n    using TypeConvert for uint256;\n\n    uint8 public override constant decimals = 18;\n    uint256 public override constant version = 1;\n\n    string public override description;\n    AggregatorV2V3Interface public immutable baseOracle;\n    int256 public immutable baseDecimals;\n    IWstETH public immutable wstETH;\n\n    constructor(AggregatorV2V3Interface baseOracle_, IWstETH wstETH_) {\n        baseOracle = baseOracle_;\n        wstETH = wstETH_;\n        description = \"wstETH Chainlink Oracle\";\n        baseDecimals = int256(10**baseOracle_.decimals());\n    }\n\n    function _calculateAnswer() internal view returns (\n        uint80 roundId,\n        int256 answer,\n        uint256 startedAt,\n        uint256 updatedAt,\n        uint80 answeredInRound\n    ) {\n        int256 baseAnswer;\n        (\n            roundId,\n            baseAnswer,\n            startedAt,\n            updatedAt,\n            answeredInRound\n        ) = baseOracle.latestRoundData();\n        require(baseAnswer > 0, \"Chainlink Rate Error\");\n\n        answer = baseAnswer * wstETH.stEthPerToken().toInt() / baseDecimals;\n    }\n\n    function latestRoundData() external view override returns (\n        uint80 roundId,\n        int256 answer,\n        uint256 startedAt,\n        uint256 updatedAt,\n        uint80 answeredInRound\n    ) {\n        return _calculateAnswer();\n    }\n\n    function latestAnswer() external view override returns (int256 answer) {\n        (/* */, answer, /* */, /* */, /* */) = _calculateAnswer();\n    }\n\n    function latestTimestamp() external view override returns (uint256 updatedAt) {\n        (/* */, /* */, /* */, updatedAt, /* */) = _calculateAnswer();\n    }\n\n    function latestRound() external view override returns (uint256 roundId) {\n        (roundId, /* */, /* */, /* */, /* */) = _calculateAnswer();\n    }\n\n    function getRoundData(uint80 _roundId) external view override returns (\n        uint80 roundId,\n        int256 answer,\n        uint256 startedAt,\n        uint256 updatedAt,\n        uint80 answeredInRound\n    ) {\n        revert();\n    }\n\n    function getAnswer(uint256 roundId) external view override returns (int256) { revert(); }\n    function getTimestamp(uint256 roundId) external view override returns (uint256) { revert(); }\n}"
    }
  ]
}