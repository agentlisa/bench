{
  "Title": "[M-07] NTokenMoonBirds Reserve Pool Cannot Receive Airdrops",
  "Content": "\nThe NTokenMoonBirds Reserve Pool only allows Moonbird NFT to be sent to the pool contract as shown in Line 70 below.\n\n<https://github.com/code-423n4/2022-11-paraspace/blob/c6820a279c64a299a783955749fdc977de8f0449/paraspace-core/contracts/protocol/tokenization/NTokenMoonBirds.sol#L63>\n\n```solidity\nFile: NTokenMoonBirds.sol\n63:     function onERC721Received(\n64:         address operator,\n65:         address from,\n66:         uint256 id,\n67:         bytes memory\n68:     ) external virtual override returns (bytes4) {\n69:         // only accept MoonBird tokens\n70:         require(msg.sender == _underlyingAsset, Errors.OPERATION_NOT_SUPPORTED);\n71: \n72:         // if the operator is the pool, this means that the pool is transferring the token to this contract\n73:         // which can happen during a normal supplyERC721 pool tx\n74:         if (operator == address(POOL)) {\n75:             return this.onERC721Received.selector;\n76:         }\n77: \n78:         // supply the received token to the pool and set it as collateral\n79:         DataTypes.ERC721SupplyParams[]\n80:             memory tokenData = new DataTypes.ERC721SupplyParams[](1);\n81: \n82:         tokenData[0] = DataTypes.ERC721SupplyParams({\n83:             tokenId: id,\n84:             useAsCollateral: true\n85:         });\n86: \n87:         POOL.supplyERC721FromNToken(_underlyingAsset, tokenData, from);\n88: \n89:         return this.onERC721Received.selector;\n90:     }\n```\n\nNote that the NTokenMoonBirds Reserve Pool is the holder/owner of all Moonbird NFTs within ParaSpace. If there is any airdrop for Moonbird NFT, the airdrop will be sent to the NTokenMoonBirds Reserve Pool.\n\nHowever, due to the validation in Line 70, the NTokenMoonBirds Reserve Pool will not be able to receive any airdrop (e.g. Moonbirds Oddities NFT) other than the Moonbird NFT. In summary, if any NFT other than Moonbird NFT is sent to the pool, it will revert.\n\nFor instance, Moonbirds Oddities NFT has been airdropped to Moonbird NFT nested stakers in the past. With the nesting staking feature, more airdrops are expected in the future. In this case, any users who collateralized their nested Moonbird NFT within ParaSpace will lose their opportunities to claim the airdrop.\n\n### Impact\n\nLoss of assets for the user. Users will not be able to receive any airdropped assets for their nested Moonbird NFT.\n\n### Recommended Mitigation Steps\n\nUpdate the contract to allow airdrops to be received by the NTokenMoonBirds Reserve Pool.\n\n```diff\nfunction onERC721Received(\n\taddress operator,\n\taddress from,\n\tuint256 id,\n\tbytes memory\n) external virtual override returns (bytes4) {\n-\t// only accept MoonBird tokens\n-\trequire(msg.sender == _underlyingAsset, Errors.OPERATION_NOT_SUPPORTED);\n\n\t// if the operator is the pool, this means that the pool is transferring the token to this contract\n\t// which can happen during a normal supplyERC721 pool tx\n\tif (operator == address(POOL)) {\n\t\treturn this.onERC721Received.selector;\n\t}\n\n+\tif msg.sender == _underlyingAsset(\n        // supply the received token to the pool and set it as collateral\n        DataTypes.ERC721SupplyParams[]\n            memory tokenData = new DataTypes.ERC721SupplyParams[](1);\n\n        tokenData[0] = DataTypes.ERC721SupplyParams({\n            tokenId: id,\n            useAsCollateral: true\n        });\n+\t)\n\n\tPOOL.supplyERC721FromNToken(_underlyingAsset, tokenData, from);\n\n\treturn this.onERC721Received.selector;\n}\n```\n\n**[LSDan (judge) decreased severity to Medium](https://github.com/code-423n4/2022-11-paraspace-findings/issues/286)**\n\n\n\n***\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-11-paraspace-contest",
  "Code": [
    {
      "filename": "paraspace-core/contracts/protocol/tokenization/NTokenMoonBirds.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\npragma solidity 0.8.10;\n\nimport {IERC20} from \"../../dependencies/openzeppelin/contracts/IERC20.sol\";\nimport {IERC721} from \"../../dependencies/openzeppelin/contracts/IERC721.sol\";\nimport {IERC1155} from \"../../dependencies/openzeppelin/contracts/IERC1155.sol\";\nimport {IERC721Metadata} from \"../../dependencies/openzeppelin/contracts/IERC721Metadata.sol\";\nimport {Address} from \"../../dependencies/openzeppelin/contracts/Address.sol\";\nimport {GPv2SafeERC20} from \"../../dependencies/gnosis/contracts/GPv2SafeERC20.sol\";\nimport {SafeCast} from \"../../dependencies/openzeppelin/contracts/SafeCast.sol\";\nimport {IMoonBirdBase} from \"../../dependencies/erc721-collections/IMoonBird.sol\";\nimport {IMoonBird} from \"../../dependencies/erc721-collections/IMoonBird.sol\";\nimport {Errors} from \"../libraries/helpers/Errors.sol\";\nimport {WadRayMath} from \"../libraries/math/WadRayMath.sol\";\nimport {IPool} from \"../../interfaces/IPool.sol\";\nimport {NToken} from \"./NToken.sol\";\nimport {IRewardController} from \"../../interfaces/IRewardController.sol\";\nimport {IncentivizedERC20} from \"./base/IncentivizedERC20.sol\";\nimport {DataTypes} from \"../libraries/types/DataTypes.sol\";\nimport {XTokenType} from \"../../interfaces/IXTokenType.sol\";\n\n/**\n * @title MoonBird NToken\n *\n * @notice Implementation of the interest bearing token for the ParaSpace protocol\n */\ncontract NTokenMoonBirds is NToken, IMoonBirdBase {\n    /**\n     * @dev Constructor.\n     * @param pool The address of the Pool contract\n     */\n    constructor(IPool pool) NToken(pool, false) {\n        // Intentionally left blank\n    }\n\n    function getXTokenType() external pure override returns (XTokenType) {\n        return XTokenType.NTokenMoonBirds;\n    }\n\n    function burn(\n        address from,\n        address receiverOfUnderlying,\n        uint256[] calldata tokenIds\n    ) external virtual override onlyPool nonReentrant returns (uint64, uint64) {\n        (\n            uint64 oldCollateralizedBalance,\n            uint64 newCollateralizedBalance\n        ) = _burnMultiple(from, tokenIds);\n\n        if (receiverOfUnderlying != address(this)) {\n            for (uint256 index = 0; index < tokenIds.length; index++) {\n                IMoonBird(_underlyingAsset).safeTransferWhileNesting(\n                    address(this),\n                    receiverOfUnderlying,\n                    tokenIds[index]\n                );\n            }\n        }\n\n        return (oldCollateralizedBalance, newCollateralizedBalance);\n    }\n\n    function onERC721Received(\n        address operator,\n        address from,\n        uint256 id,\n        bytes memory\n    ) external virtual override returns (bytes4) {\n        // only accept MoonBird tokens\n        require(msg.sender == _underlyingAsset, Errors.OPERATION_NOT_SUPPORTED);\n\n        // if the operator is the pool, this means that the pool is transferring the token to this contract\n        // which can happen during a normal supplyERC721 pool tx\n        if (operator == address(POOL)) {\n            return this.onERC721Received.selector;\n        }\n\n        // supply the received token to the pool and set it as collateral\n        DataTypes.ERC721SupplyParams[]\n            memory tokenData = new DataTypes.ERC721SupplyParams[](1);\n\n        tokenData[0] = DataTypes.ERC721SupplyParams({\n            tokenId: id,\n            useAsCollateral: true\n        });\n\n        POOL.supplyERC721FromNToken(_underlyingAsset, tokenData, from);\n\n        return this.onERC721Received.selector;\n    }\n\n    /**\n        @dev an additional function that is custom to MoonBirds reserve.\n        This function allows NToken holders to toggle on/off the nesting the status for the underlying tokens\n    */\n    function toggleNesting(uint256[] calldata tokenIds) external {\n        for (uint256 index = 0; index < tokenIds.length; index++) {\n            require(\n                _isApprovedOrOwner(_msgSender(), tokenIds[index]),\n                \"ERC721: transfer caller is not owner nor approved\"\n            );\n        }\n\n        IMoonBird(_underlyingAsset).toggleNesting(tokenIds);\n    }\n\n    /**\n        @dev an additional function that is custom to MoonBirds reserve.\n        This function allows NToken holders to get nesting the state for the underlying tokens\n    */\n    function nestingPeriod(uint256 tokenId)\n        external\n        view\n        returns (\n            bool nesting,\n            uint256 current,\n            uint256 total\n        )\n    {\n        return IMoonBird(_underlyingAsset).nestingPeriod(tokenId);\n    }\n\n    /**\n        @dev an additional function that is custom to MoonBirds reserve.\n        This function check if nesting is open for the underlying tokens\n    */\n    function nestingOpen() external returns (bool) {\n        return IMoonBird(_underlyingAsset).nestingOpen();\n    }\n}"
    }
  ]
}