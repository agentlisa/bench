{
  "Title": "[M-11] addFeedFor should check if inverse feed already exists",
  "Content": "# Lines of code\n\nhttps://github.com/jbx-protocol/juice-contracts-v2-code4rena/blob/828bf2f3e719873daa08081cfa0d0a6deaa5ace5/contracts/JBPrices.sol#L109-L122\n\n\n# Vulnerability details\n\n## Impact\nPotentially inconsistent currency conversions\n\n## Proof of Concept\naddFeedFor requires that a price feed for the _currency _base doesn't exist when adding a new price feed but doesn't check if the inverse already exists. This means that two different oracles (potentially with different prices) could be used for _currency -> _base vs. _base -> _currency. Different prices would lead to inconsistent between conversion ratios depending on the direction of the conversion\n\n## Tools Used\n\n## Recommended Mitigation Steps\nChange L115 to:\nif (feedFor[_currency][_base] != IJBPriceFeed(address(0)) || feedFor[_base][_currency] != IJBPriceFeed(address(0))) revert PRICE_FEED_ALREADY_EXISTS()\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-07-juicebox-v2-contest",
  "Code": [
    {
      "filename": "contracts/JBPrices.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/access/Ownable.sol';\nimport '@paulrberg/contracts/math/PRBMath.sol';\nimport './interfaces/IJBPrices.sol';\n\n/** \n  @notice \n  Manages and normalizes price feeds.\n\n  @dev\n  Adheres to -\n  IJBPrices: General interface for the methods in this contract that interact with the blockchain's state according to the protocol's rules.\n\n  @dev\n  Inherits from -\n  Ownable: Includes convenience functionality for checking a message sender's permissions before executing certain transactions.\n*/\ncontract JBPrices is IJBPrices, Ownable {\n  //*********************************************************************//\n  // --------------------------- custom errors ------------------------- //\n  //*********************************************************************//\n  error PRICE_FEED_ALREADY_EXISTS();\n  error PRICE_FEED_NOT_FOUND();\n\n  //*********************************************************************//\n  // --------------------- public stored properties -------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    The available price feeds.\n\n    @dev\n    The feed returns the number of `_currency` units that can be converted to 1 `_base` unit.\n\n    _currency The currency units the feed's resulting price is in terms of.\n    _base The base currency unit being priced by the feed.\n  */\n  mapping(uint256 => mapping(uint256 => IJBPriceFeed)) public override feedFor;\n\n  //*********************************************************************//\n  // ------------------------- external views -------------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice\n    Gets the number of `_currency` units that can be converted to 1 `_base` unit.\n\n    @param _currency The currency units the resulting price is in terms of.\n    @param _base The base currency unit being priced.\n    @param _decimals The number of decimals the returned fixed point price should include.\n    \n    @return The price of the currency in terms of the base, as a fixed point number with the specified number of decimals.\n  */\n  function priceFor(\n    uint256 _currency,\n    uint256 _base,\n    uint256 _decimals\n  ) external view override returns (uint256) {\n    // If the currency is the base, return 1 since they are priced the same. Include the desired number of decimals.\n    if (_currency == _base) return 10**_decimals;\n\n    // Get a reference to the feed.\n    IJBPriceFeed _feed = feedFor[_currency][_base];\n\n    // If it exists, return the price.\n    if (_feed != IJBPriceFeed(address(0))) return _feed.currentPrice(_decimals);\n\n    // Get the inverse feed.\n    _feed = feedFor[_base][_currency];\n\n    // If it exists, return the inverse price.\n    if (_feed != IJBPriceFeed(address(0)))\n      return PRBMath.mulDiv(10**_decimals, 10**_decimals, _feed.currentPrice(_decimals));\n\n    // No price feed available, revert.\n    revert PRICE_FEED_NOT_FOUND();\n  }\n\n  //*********************************************************************//\n  // ---------------------------- constructor -------------------------- //\n  //*********************************************************************//\n\n  /** \n    @param _owner The address that will own the contract.\n  */\n  constructor(address _owner) {\n    // Transfer the ownership.\n    transferOwnership(_owner);\n  }\n\n  //*********************************************************************//\n  // ---------------------- external transactions ---------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    Add a price feed for a currency in terms of the provided base currency.\n\n    @dev\n    Current feeds can't be modified.\n\n    @param _currency The currency units the feed's resulting price is in terms of.\n    @param _base The base currency unit being priced by the feed.\n    @param _feed The price feed being added.\n  */\n  function addFeedFor(\n    uint256 _currency,\n    uint256 _base,\n    IJBPriceFeed _feed\n  ) external override onlyOwner {\n    // There can't already be a feed for the specified currency.\n    if (feedFor[_currency][_base] != IJBPriceFeed(address(0))) revert PRICE_FEED_ALREADY_EXISTS();\n\n    // Store the feed.\n    feedFor[_currency][_base] = _feed;\n\n    emit AddFeed(_currency, _base, _feed);\n  }\n}"
    }
  ]
}