{
  "Title": "[G-02]  Using `calldata` instead of `memory` for read-only arguments in `external` functions saves gas",
  "Content": "\nWhen a function with a `memory` array is called externally, the `abi.decode()` step has to use a for-loop to copy each index of the `calldata` to the `memory` index. **Each iteration of this for-loop costs at least 60 gas** (i.e. `60 * <mem_array>.length`). Using `calldata` directly, obliviates the need for such a loop in the contract code and runtime execution. Note that even if an interface defines a function as having `memory` arguments, it's still valid for implementation contracs to use `calldata` arguments instead. \n\nIf the array is passed to an `internal` function which passes the array to another internal function where the array is modified and therefore `memory` is used in the `external` call, it's still more gass-efficient to use `calldata` when the `external` function uses modifiers, since the modifiers may prevent the internal functions from being called. Structs have the same overhead as an array of length one\n\nNote that I've also flagged instances where the function is `public` but can be marked as `external` since it's not called by the contract, and cases where a constructor is involved\n\n*There are 3 instances of this issue:*\n```solidity\nFile: contracts/proxies/LooksRareProxy.sol\n\n50        function execute(\n51            BasicOrder[] calldata orders,\n52            bytes[] calldata ordersExtraData,\n53            bytes memory,\n54            address recipient,\n55            bool isAtomic,\n56            uint256,\n57:           address\n\n```\nhttps://github.com/code-423n4/2022-11-looksrare/blob/e3b2c053f722b0ca2dce3a3eb06f64859b8b7a6f/contracts/proxies/LooksRareProxy.sol#L50-L57\n\n```solidity\nFile: contracts/TokenReceiver.sol\n\n14        function onERC1155Received(\n15            address,\n16            address,\n17            uint256,\n18            uint256,\n19            bytes memory\n20:       ) external virtual returns (bytes4) {\n\n24        function onERC1155BatchReceived(\n25            address,\n26            address,\n27            uint256[] memory,\n28            uint256[] memory,\n29            bytes memory\n30:       ) external virtual returns (bytes4) {\n\n```\nhttps://github.com/code-423n4/2022-11-looksrare/blob/e3b2c053f722b0ca2dce3a3eb06f64859b8b7a6f/contracts/TokenReceiver.sol#L14-L20\n\n\n```diff\ndiff --git a/contracts/TokenReceiver.sol b/contracts/TokenReceiver.sol\nindex a82e815..759d4ac 100644\n--- a/contracts/TokenReceiver.sol\n+++ b/contracts/TokenReceiver.sol\n@@ -16,7 +16,7 @@ abstract contract TokenReceiver {\n         address,\n         uint256,\n         uint256,\n-        bytes memory\n+        bytes calldata\n     ) external virtual returns (bytes4) {\n         return this.onERC1155Received.selector;\n     }\n@@ -24,9 +24,9 @@ abstract contract TokenReceiver {\n     function onERC1155BatchReceived(\n         address,\n         address,\n-        uint256[] memory,\n-        uint256[] memory,\n-        bytes memory\n+        uint256[] calldata,\n+        uint256[] calldata,\n+        bytes calldata\n     ) external virtual returns (bytes4) {\n         return this.onERC1155BatchReceived.selector;\n     }\ndiff --git a/contracts/proxies/LooksRareProxy.sol b/contracts/proxies/LooksRareProxy.sol\nindex cbce118..1ebe936 100644\n--- a/contracts/proxies/LooksRareProxy.sol\n+++ b/contracts/proxies/LooksRareProxy.sol\n@@ -50,7 +50,7 @@ contract LooksRareProxy is IProxy, TokenRescuer, TokenTransferrer, SignatureChec\n     function execute(\n         BasicOrder[] calldata orders,\n         bytes[] calldata ordersExtraData,\n-        bytes memory,\n+        bytes calldata,\n         address recipient,\n         bool isAtomic,\n         uint256,\n```\n\n```diff\ndiff --git a/tmp/gas_before b/tmp/gas_after\nindex a3222f9..94f3986 100644\n--- a/tmp/gas_before\n+++ b/tmp/gas_after\n@@ -17 +17 @@\n-â”‚ 2504484                                                        â”† 12343           â”†        â”†        â”†        â”†         â”‚\n+â”‚ 2420786                                                        â”† 11925           â”†        â”†        â”†        â”†         â”‚\n@@ -27 +27 @@\n-â”‚ execute                                                        â”† 1370            â”† 434636 â”† 429633 â”† 960082 â”† 67      â”‚\n+â”‚ execute                                                        â”† 1370            â”† 434530 â”† 429633 â”† 960082 â”† 67      â”‚\n@@ -31 +31 @@\n-â”‚ onERC1155Received                                              â”† 1631            â”† 1631   â”† 1631   â”† 1631   â”† 4       â”‚\n+â”‚ onERC1155Received                                              â”† 1305            â”† 1305   â”† 1305   â”† 1305   â”† 4       â”‚\n@@ -56 +56 @@\n-â”‚ 1402373                                                    â”† 6944            â”†        â”†        â”†        â”†         â”‚\n+â”‚ 1321684                                                    â”† 6541            â”†        â”†        â”†        â”†         â”‚\n@@ -62 +62 @@\n-â”‚ execute                                                    â”† 268230          â”† 373671 â”† 361477 â”† 503500 â”† 4       â”‚\n+â”‚ execute                                                    â”† 268230          â”† 373543 â”† 361349 â”† 503244 â”† 4       â”‚\n@@ -71 +71 @@\n-â”‚ 1683078                                                      â”† 8635            â”†        â”†        â”†        â”†         â”‚\n+â”‚ 1699292                                                      â”† 8716            â”†        â”†        â”†        â”†         â”‚\n@@ -75 +75 @@\n-â”‚ execute                                                      â”† 1962            â”† 260929 â”† 279156 â”† 506876 â”† 23      â”‚\n+â”‚ execute                                                      â”† 1640            â”† 260616 â”† 278900 â”† 506620 â”† 23      â”‚\n@@ -230 +230 @@\n-â”‚ 13089423                                                                    â”† 64155           â”†     â”†        â”†     â”†         â”‚\n+â”‚ 12924886                                                                    â”† 63334           â”†     â”†        â”†     â”†         â”‚\n@@ -288 +288 @@\n-â”‚ mint                                                    â”† 26410           â”† 28497 â”† 26410  â”† 31628 â”† 5       â”‚\n+â”‚ mint                                                    â”† 26410           â”† 28366 â”† 26410  â”† 31302 â”† 5       â”‚\n@@ -346,0 +347 @@\n+Overall gas change: -1174338 (-24.848%)\n```\n\n\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/reports/2022-11-looksrare",
  "Code": [
    {
      "filename": "contracts/proxies/LooksRareProxy.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.17;\n\nimport {SignatureChecker} from \"../SignatureChecker.sol\";\nimport {IERC721} from \"../../contracts/interfaces/IERC721.sol\";\nimport {IERC1155} from \"../../contracts/interfaces/IERC1155.sol\";\nimport {ILooksRareExchange} from \"@looksrare/contracts-exchange-v1/contracts/interfaces/ILooksRareExchange.sol\";\nimport {OrderTypes} from \"@looksrare/contracts-exchange-v1/contracts/libraries/OrderTypes.sol\";\n\nimport {CollectionType} from \"../libraries/OrderEnums.sol\";\nimport {BasicOrder, FeeData} from \"../libraries/OrderStructs.sol\";\nimport {IProxy} from \"../interfaces/IProxy.sol\";\nimport {TokenRescuer} from \"../TokenRescuer.sol\";\nimport {TokenTransferrer} from \"../TokenTransferrer.sol\";\n\n/**\n * @title LooksRareProxy\n * @notice This contract allows NFT sweepers to batch buy NFTs from LooksRare\n *         by passing high-level structs + low-level bytes as calldata.\n * @author LooksRare protocol team (ðŸ‘€,ðŸ’Ž)\n */\ncontract LooksRareProxy is IProxy, TokenRescuer, TokenTransferrer, SignatureChecker {\n    struct OrderExtraData {\n        uint256 makerAskPrice; // Maker ask price, which is not necessarily equal to the taker bid price\n        uint256 minPercentageToAsk; // The maker's minimum % to receive from the sale\n        uint256 nonce; // The maker's nonce\n        address strategy; // LooksRare execution strategy\n    }\n\n    ILooksRareExchange public immutable marketplace;\n    address public immutable aggregator;\n\n    /**\n     * @param _marketplace LooksRareExchange's address\n     * @param _aggregator LooksRareAggregator's address\n     */\n    constructor(address _marketplace, address _aggregator) {\n        marketplace = ILooksRareExchange(_marketplace);\n        aggregator = _aggregator;\n    }\n\n    /**\n     * @notice Execute LooksRare NFT sweeps in a single transaction\n     * @dev extraData, feeBp and feeRecipient are not used\n     * @param orders Orders to be executed by LooksRare\n     * @param ordersExtraData Extra data for each order\n     * @param recipient The address to receive the purchased NFTs\n     * @param isAtomic Flag to enable atomic trades (all or nothing) or partial trades\n     */\n    function execute(\n        BasicOrder[] calldata orders,\n        bytes[] calldata ordersExtraData,\n        bytes memory,\n        address recipient,\n        bool isAtomic,\n        uint256,\n        address\n    ) external payable override {\n        if (address(this) != aggregator) revert InvalidCaller();\n\n        uint256 ordersLength = orders.length;\n        if (ordersLength == 0 || ordersLength != ordersExtraData.length) revert InvalidOrderLength();\n\n        for (uint256 i; i < ordersLength; ) {\n            BasicOrder memory order = orders[i];\n\n            OrderExtraData memory orderExtraData = abi.decode(ordersExtraData[i], (OrderExtraData));\n\n            OrderTypes.MakerOrder memory makerAsk;\n            {\n                makerAsk.isOrderAsk = true;\n                makerAsk.signer = order.signer;\n                makerAsk.collection = order.collection;\n                makerAsk.tokenId = order.tokenIds[0];\n                makerAsk.price = orderExtraData.makerAskPrice;\n                makerAsk.amount = order.amounts[0];\n                makerAsk.strategy = orderExtraData.strategy;\n                makerAsk.nonce = orderExtraData.nonce;\n                makerAsk.minPercentageToAsk = orderExtraData.minPercentageToAsk;\n                makerAsk.currency = order.currency;\n                makerAsk.startTime = order.startTime;\n                makerAsk.endTime = order.endTime;\n\n                (bytes32 r, bytes32 s, uint8 v) = _splitSignature(order.signature);\n                makerAsk.v = v;\n                makerAsk.r = r;\n                makerAsk.s = s;\n            }\n\n            OrderTypes.TakerOrder memory takerBid;\n            {\n                takerBid.isOrderAsk = false;\n                takerBid.taker = address(this);\n                takerBid.price = order.price;\n                takerBid.tokenId = makerAsk.tokenId;\n                takerBid.minPercentageToAsk = makerAsk.minPercentageToAsk;\n            }\n\n            _executeSingleOrder(takerBid, makerAsk, recipient, order.collectionType, isAtomic);\n\n            unchecked {\n                ++i;\n            }\n        }\n    }\n\n    function _executeSingleOrder(\n        OrderTypes.TakerOrder memory takerBid,\n        OrderTypes.MakerOrder memory makerAsk,\n        address recipient,\n        CollectionType collectionType,\n        bool isAtomic\n    ) private {\n        if (isAtomic) {\n            marketplace.matchAskWithTakerBidUsingETHAndWETH{value: takerBid.price}(takerBid, makerAsk);\n            _transferTokenToRecipient(\n                collectionType,\n                recipient,\n                makerAsk.collection,\n                makerAsk.tokenId,\n                makerAsk.amount\n            );\n        } else {\n            try marketplace.matchAskWithTakerBidUsingETHAndWETH{value: takerBid.price}(takerBid, makerAsk) {\n                _transferTokenToRecipient(\n                    collectionType,\n                    recipient,\n                    makerAsk.collection,\n                    makerAsk.tokenId,\n                    makerAsk.amount\n                );\n            } catch {}\n        }\n    }\n}"
    },
    {
      "filename": "contracts/TokenReceiver.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.17;\n\nabstract contract TokenReceiver {\n    function onERC721Received(\n        address,\n        address,\n        uint256,\n        bytes calldata\n    ) external pure returns (bytes4) {\n        return this.onERC721Received.selector;\n    }\n\n    function onERC1155Received(\n        address,\n        address,\n        uint256,\n        uint256,\n        bytes memory\n    ) external virtual returns (bytes4) {\n        return this.onERC1155Received.selector;\n    }\n\n    function onERC1155BatchReceived(\n        address,\n        address,\n        uint256[] memory,\n        uint256[] memory,\n        bytes memory\n    ) external virtual returns (bytes4) {\n        return this.onERC1155BatchReceived.selector;\n    }\n}"
    }
  ]
}