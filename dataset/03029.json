{
  "Title": "H-5: `UniV2LPOracle` will malfunction if token0 or token1's `decimals != 18`",
  "Content": "# Issue H-5: `UniV2LPOracle` will malfunction if token0 or token1's `decimals != 18` \nSource: https://github.com/sherlock-audit/2022-08-sentiment-judging/tree/main/026-H \n## Found by \nLambda, WATCHPUG, 0x52, hyh\n\n## Summary\n\nWhen one of the LP token's underlying tokens `decimals` is not 18, the price of the LP token calculated by `UniV2LPOracle` will be wrong. \n\n## Vulnerability Detail\n\n`UniV2LPOracle` is an implementation of Alpha Homora v2's Fair Uniswap's LP Token Pricing Formula:\n\n> The Formula ... of combining fair asset prices and fair asset reserves:\n\n> $$\nP = 2\\cdot \\frac{\\sqrt{r_0 \\cdot r_1} \\cdot \\sqrt{p_0\\cdot p_1}}{totalSupply},\n$$\n\n> where $r_i$ is the asset ii's pool balance and $p_i$ is the asset $i$'s fair price.\n\nHowever, the current implementation wrongful assumes $r_0$ and $r_1$ are always in 18 decimals.\n\nhttps://github.com/sentimentxyz/oracle/blob/59b26a3d8c295208437aad36c470386c9729a4bc/src/uniswap/UniV2LPOracle.sol#L39-L50\n\n```solidity\nfunction getPrice(address pair) external view returns (uint) {\n    (uint r0, uint r1,) = IUniswapV2Pair(pair).getReserves();\n\n    // 2 * sqrt(r0 * r1 * p0 * p1) / totalSupply\n    return FixedPointMathLib.sqrt(\n        r0\n        .mulWadDown(r1)\n        .mulWadDown(oracle.getPrice(IUniswapV2Pair(pair).token0()))\n        .mulWadDown(oracle.getPrice(IUniswapV2Pair(pair).token1()))\n    )\n    .mulDivDown(2e27, IUniswapV2Pair(pair).totalSupply());\n}\n```\n\nhttps://github.com/transmissions11/solmate/blob/main/src/utils/FixedPointMathLib.sol\n\n```solidity\nuint256 internal constant WAD = 1e18; // The scalar of ETH and most ERC20s.\n\nfunction mulWadDown(uint256 x, uint256 y) internal pure returns (uint256) {\n    return mulDivDown(x, y, WAD); // Equivalent to (x * y) / WAD rounded down.\n}\n```\n\nhttps://github.com/transmissions11/solmate/blob/main/src/utils/FixedPointMathLib.sol\n\n```solidity\nfunction mulDivDown(\n    uint256 x,\n    uint256 y,\n    uint256 denominator\n) internal pure returns (uint256 z) {\n    assembly {\n        // Store x * y in z for now.\n        z := mul(x, y)\n\n        // Equivalent to require(denominator != 0 && (x == 0 || (x * y) / x == y))\n        if iszero(and(iszero(iszero(denominator)), or(iszero(x), eq(div(z, x), y)))) {\n            revert(0, 0)\n        }\n\n        // Divide z by the denominator.\n        z := div(z, denominator)\n    }\n}\n```\n\n## Impact\n\nWhen the decimals of one or both tokens in the pair is not 18, the price will be way off.\n\n## Code Snippet\n\nWe've created a test script to demonstrate `UniV2LPOracle` is malfunctioning with USDC/WETH, in which USDC's decimals is 6 instead of 18.\n\n```solidity\n// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport \"forge-std/console.sol\";\nimport {Test} from \"forge-std/Test.sol\";\n\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {UniV2LpOracle} from \"../uniswap/UniV2LPOracle.sol\";\n\nimport {OracleFacade} from \"../core/OracleFacade.sol\";\nimport {ChainlinkOracle} from \"../chainlink/ChainlinkOracle.sol\";\nimport {WETHOracle} from \"../weth/WETHOracle.sol\";\nimport {AggregatorV3Interface} from \"../chainlink/AggregatorV3Interface.sol\";\nimport \"forge-std/console.sol\";\n\ncontract UniV2LPOracleTest is Test {\n    address constant usdc = address(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48);\n    address constant weth = address(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);\n\n    address constant pair = address(0xB4e16d0168e52d35CaCD2c6185b44281Ec28C9Dc);\n\n    address constant ethUsdFeed =\n        address(0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419);\n    address constant usdcUsdFeed =\n        address(0x8fFfFfd4AfB6115b954Bd326cbe7B4BA576818f6);\n\n    ChainlinkOracle chainlinkOracle;\n    WETHOracle wETHOracle;\n    UniV2LpOracle uniV2LpOracle;\n    OracleFacade oracleFacade;\n\n    function setUp() public {\n        // core oracle\n        oracleFacade = new OracleFacade();\n\n        chainlinkOracle = new ChainlinkOracle(\n            AggregatorV3Interface(ethUsdFeed)\n        );\n        chainlinkOracle.setFeed(usdc, AggregatorV3Interface(usdcUsdFeed));\n\n        WETHOracle wethOracle = new WETHOracle();\n        oracleFacade.setOracle(weth, wethOracle);\n        oracleFacade.setOracle(usdc, chainlinkOracle);\n\n        uniV2LpOracle = new UniV2LpOracle(oracleFacade);\n    }\n\n    function testUniV2Price() public {\n        console.log(uniV2LpOracle.getPrice(pair));\n    }\n}\n\n```\n\n#### `reserves` and `totalSupply` of UniswapV2 USDC/ETH Pair `0xB4e16d0168e52d35CaCD2c6185b44281Ec28C9Dc` on Mainnet at the time of writing\n\nResult of `getReserves()`\n```js\n_reserve0   uint112 :  45456843739761\n_reserve1   uint112 :  28342500764440756425363\n_blockTimestampLast   uint32 :  1663233599\n```\n\n`totalSupply()`: `550760227054391377`\n\n#### Expected and actual result\n\n```js\n// real time price\n28342500764440756425363 * 2   / 550760227054391377 = 102921\n\n// normalized r0 and r1 âœ…\n102728696484607347546879 / 1e18 = 102728\n\n// current result\n102728772378134052 / 1e18 = 0.10272877237813405\n```\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\nConsider normalizing r0 and r1 to 18 decimals before using them in the formula.\n\n## Sentiment Team\nFixed as recommended. PRs [here](https://github.com/sentimentxyz/oracle/pull/35) and [here](https://github.com/sentimentxyz/oracle/pull/41).\n\n## Lead Senior Watson\nConfirmed fix. \n\n",
  "Impact": "HIGH",
  "Source": "https://app.sherlock.xyz/audits/contests/1",
  "Code": [
    {
      "filename": "src/uniswap/UniV2LPOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {IUniswapV2Pair} from \"./IUniswapV2Pair.sol\";\nimport {FixedPointMathLib} from \"solmate/utils/FixedPointMathLib.sol\";\n/**\n    @title Uniswap v2 LP Oracle\n    @notice Price oracle for uniswap v2 LP Tokens\n*/\ncontract UniV2LpOracle is IOracle {\n    using FixedPointMathLib for uint;\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice Oracle Facade\n    IOracle public immutable oracle;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _oracle Address of Oracle Facade\n    */\n    constructor(IOracle _oracle) {\n        oracle = _oracle;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @dev Adapted from https://blog.alphaventuredao.io/fair-lp-token-pricing\n    /// @inheritdoc IOracle\n    function getPrice(address pair) external view returns (uint) {\n        (uint r0, uint r1,) = IUniswapV2Pair(pair).getReserves();\n\n        // 2 * sqrt(r0 * r1 * p0 * p1) / totalSupply\n        return FixedPointMathLib.sqrt(\n            r0\n            .mulWadDown(r1)\n            .mulWadDown(oracle.getPrice(IUniswapV2Pair(pair).token0()))\n            .mulWadDown(oracle.getPrice(IUniswapV2Pair(pair).token1()))\n        )\n        .mulDivDown(2e27, IUniswapV2Pair(pair).totalSupply());\n    }\n}"
    }
  ]
}