{
  "Title": "[G-06] A modifier used only once and not being inherited should be inlined to save gas",
  "Content": "\nhttps://github.com/code-423n4/2022-12-forgeries/blob/fc271cf20c05ce857d967728edfb368c58881d85/src/ownable/OwnableUpgradeable.sol#L44-L49\n\n```solidity\nFile: /src/ownable/OwnableUpgradeable.sol\n44:    modifier onlyPendingOwner() {\n45:        if (msg.sender != _pendingOwner) {\n46:            revert ONLY_PENDING_OWNER();\n47:        }\n48:        _;\n49:    }\n```\n\nThe above modifer is only used in the following:\n\nhttps://github.com/code-423n4/2022-12-forgeries/blob/fc271cf20c05ce857d967728edfb368c58881d85/src/ownable/OwnableUpgradeable.sol#L119-L125\n\n```solidity\nFile: /src/ownable/OwnableUpgradeable.sol\n119:    function acceptOwnership() public onlyPendingOwner {\n120:        emit OwnerUpdated(_owner, msg.sender);\n\n122:        _owner = _pendingOwner;\n\n124:        delete _pendingOwner;\n125:    }\n```\n\n```diff\ndiff --git a/src/ownable/OwnableUpgradeable.sol b/src/ownable/OwnableUpgradeable.sol\nindex bfc7eef..d27530c 100644\n--- a/src/ownable/OwnableUpgradeable.sol\n+++ b/src/ownable/OwnableUpgradeable.sol\n@@ -116,7 +116,10 @@ abstract contract OwnableUpgradeable is IOwnableUpgradeable, Initializable {\n     }\n\n     /// @notice Accepts an ownership transfer\n-    function acceptOwnership() public onlyPendingOwner {\n+    function acceptOwnership() public{\n+      if (msg.sender != _pendingOwner) {\n+            revert ONLY_PENDING_OWNER();\n+        }\n         emit OwnerUpdated(_owner, msg.sender);\n\n         _owner = _pendingOwner;\n\n\t\t  delete _pendingOwner;     \n```\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/contests/2022-12-forgeries-contest",
  "Code": [
    {
      "filename": "src/ownable/OwnableUpgradeable.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.16;\n\nimport {IOwnableUpgradeable} from \"./IOwnableUpgradeable.sol\";\nimport {Initializable} from \"@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol\";\n\n/// @title Ownable\n/// @author Rohan Kulkarni / Iain Nash\n/// @notice Modified from OpenZeppelin Contracts v4.7.3 (access/OwnableUpgradeable.sol)\n/// - Uses custom errors declared in IOwnable\n/// - Adds optional two-step ownership transfer (`safeTransferOwnership` + `acceptOwnership`)\nabstract contract OwnableUpgradeable is IOwnableUpgradeable, Initializable {\n    ///                                                          ///\n    ///                            STORAGE                       ///\n    ///                                                          ///\n\n    /// @dev The address of the owner\n    address internal _owner;\n\n    /// @dev The address of the pending owner\n    address internal _pendingOwner;\n\n    /// @dev Modifier to check if the address argument is the zero/burn address\n    modifier notZeroAddress(address check) {\n        if (check == address(0)) {\n            revert OWNER_CANNOT_BE_ZERO_ADDRESS();\n        }\n        _;\n    }\n\n    ///                                                          ///\n    ///                           MODIFIERS                      ///\n    ///                                                          ///\n\n    /// @dev Ensures the caller is the owner\n    modifier onlyOwner() {\n        if (msg.sender != _owner) {\n            revert ONLY_OWNER();\n        }\n        _;\n    }\n\n    /// @dev Ensures the caller is the pending owner\n    modifier onlyPendingOwner() {\n        if (msg.sender != _pendingOwner) {\n            revert ONLY_PENDING_OWNER();\n        }\n        _;\n    }\n\n    ///                                                          ///\n    ///                           FUNCTIONS                      ///\n    ///                                                          ///\n\n    /// @dev Initializes contract ownership\n    /// @param _initialOwner The initial owner address\n    function __Ownable_init(address _initialOwner)\n        internal\n        notZeroAddress(_initialOwner)\n        onlyInitializing\n    {\n        _owner = _initialOwner;\n\n        emit OwnerUpdated(address(0), _initialOwner);\n    }\n\n    /// @notice The address of the owner\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /// @notice The address of the pending owner\n    function pendingOwner() public view returns (address) {\n        return _pendingOwner;\n    }\n\n    /// @notice Forces an ownership transfer from the last owner\n    /// @param _newOwner The new owner address\n    function transferOwnership(address _newOwner)\n        public\n        notZeroAddress(_newOwner)\n        onlyOwner\n    {\n        _transferOwnership(_newOwner);\n    }\n\n    /// @notice Forces an ownership transfer from any sender\n    /// @param _newOwner New owner to transfer contract to\n    /// @dev Ensure is called only from trusted internal code, no access control checks.\n    function _transferOwnership(address _newOwner) internal {\n        emit OwnerUpdated(_owner, _newOwner);\n\n        _owner = _newOwner;\n\n        if (_pendingOwner != address(0)) {\n            delete _pendingOwner;\n        }\n    }\n\n    /// @notice Initiates a two-step ownership transfer\n    /// @param _newOwner The new owner address\n    function safeTransferOwnership(address _newOwner)\n        public\n        notZeroAddress(_newOwner)\n        onlyOwner\n    {\n        _pendingOwner = _newOwner;\n\n        emit OwnerPending(_owner, _newOwner);\n    }\n\n    /// @notice Resign ownership of contract\n    /// @dev only callably by the owner, dangerous call.\n    function resignOwnership() public onlyOwner {\n        _transferOwnership(address(0));\n    }\n\n    /// @notice Accepts an ownership transfer\n    function acceptOwnership() public onlyPendingOwner {\n        emit OwnerUpdated(_owner, msg.sender);\n\n        _owner = _pendingOwner;\n\n        delete _pendingOwner;\n    }\n\n    /// @notice Cancels a pending ownership transfer\n    function cancelOwnershipTransfer() public onlyOwner {\n        emit OwnerCanceled(_owner, _pendingOwner);\n\n        delete _pendingOwner;\n    }\n}"
    },
    {
      "filename": "src/ownable/OwnableUpgradeable.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.16;\n\nimport {IOwnableUpgradeable} from \"./IOwnableUpgradeable.sol\";\nimport {Initializable} from \"@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol\";\n\n/// @title Ownable\n/// @author Rohan Kulkarni / Iain Nash\n/// @notice Modified from OpenZeppelin Contracts v4.7.3 (access/OwnableUpgradeable.sol)\n/// - Uses custom errors declared in IOwnable\n/// - Adds optional two-step ownership transfer (`safeTransferOwnership` + `acceptOwnership`)\nabstract contract OwnableUpgradeable is IOwnableUpgradeable, Initializable {\n    ///                                                          ///\n    ///                            STORAGE                       ///\n    ///                                                          ///\n\n    /// @dev The address of the owner\n    address internal _owner;\n\n    /// @dev The address of the pending owner\n    address internal _pendingOwner;\n\n    /// @dev Modifier to check if the address argument is the zero/burn address\n    modifier notZeroAddress(address check) {\n        if (check == address(0)) {\n            revert OWNER_CANNOT_BE_ZERO_ADDRESS();\n        }\n        _;\n    }\n\n    ///                                                          ///\n    ///                           MODIFIERS                      ///\n    ///                                                          ///\n\n    /// @dev Ensures the caller is the owner\n    modifier onlyOwner() {\n        if (msg.sender != _owner) {\n            revert ONLY_OWNER();\n        }\n        _;\n    }\n\n    /// @dev Ensures the caller is the pending owner\n    modifier onlyPendingOwner() {\n        if (msg.sender != _pendingOwner) {\n            revert ONLY_PENDING_OWNER();\n        }\n        _;\n    }\n\n    ///                                                          ///\n    ///                           FUNCTIONS                      ///\n    ///                                                          ///\n\n    /// @dev Initializes contract ownership\n    /// @param _initialOwner The initial owner address\n    function __Ownable_init(address _initialOwner)\n        internal\n        notZeroAddress(_initialOwner)\n        onlyInitializing\n    {\n        _owner = _initialOwner;\n\n        emit OwnerUpdated(address(0), _initialOwner);\n    }\n\n    /// @notice The address of the owner\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /// @notice The address of the pending owner\n    function pendingOwner() public view returns (address) {\n        return _pendingOwner;\n    }\n\n    /// @notice Forces an ownership transfer from the last owner\n    /// @param _newOwner The new owner address\n    function transferOwnership(address _newOwner)\n        public\n        notZeroAddress(_newOwner)\n        onlyOwner\n    {\n        _transferOwnership(_newOwner);\n    }\n\n    /// @notice Forces an ownership transfer from any sender\n    /// @param _newOwner New owner to transfer contract to\n    /// @dev Ensure is called only from trusted internal code, no access control checks.\n    function _transferOwnership(address _newOwner) internal {\n        emit OwnerUpdated(_owner, _newOwner);\n\n        _owner = _newOwner;\n\n        if (_pendingOwner != address(0)) {\n            delete _pendingOwner;\n        }\n    }\n\n    /// @notice Initiates a two-step ownership transfer\n    /// @param _newOwner The new owner address\n    function safeTransferOwnership(address _newOwner)\n        public\n        notZeroAddress(_newOwner)\n        onlyOwner\n    {\n        _pendingOwner = _newOwner;\n\n        emit OwnerPending(_owner, _newOwner);\n    }\n\n    /// @notice Resign ownership of contract\n    /// @dev only callably by the owner, dangerous call.\n    function resignOwnership() public onlyOwner {\n        _transferOwnership(address(0));\n    }\n\n    /// @notice Accepts an ownership transfer\n    function acceptOwnership() public onlyPendingOwner {\n        emit OwnerUpdated(_owner, msg.sender);\n\n        _owner = _pendingOwner;\n\n        delete _pendingOwner;\n    }\n\n    /// @notice Cancels a pending ownership transfer\n    function cancelOwnershipTransfer() public onlyOwner {\n        emit OwnerCanceled(_owner, _pendingOwner);\n\n        delete _pendingOwner;\n    }\n}"
    }
  ]
}