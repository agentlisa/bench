{
  "Title": "Branchless optimization",
  "Content": "The `sqrt` function in `MathLib` and [related comment](https://github.com/BeanstalkFarms/Wells/blob/e5441fc78f0fd4b77a898812d0fd22cb43a0af55/src/libraries/LibMath.sol#L129-L136) should be updated to reflect changes in Solmate's `FixedPointMathLib` which now includes the [branchless optimization](https://github.com/transmissions11/solmate/blob/1b3adf677e7e383cc684b5d5bd441da86bf4bf1c/src/utils/FixedPointMathLib.sol#L220-L225) `z := sub(z, lt(div(x, z), z))`.\n\n**Beanstalk:** Fixed in commit [46b24ac](https://github.com/BeanstalkFarms/Basin/commit/46b24aca0c65793604e3c24a07debfe163c5322a).\n\n**Cyfrin:** Acknowledged.\n\n\\clearpage",
  "Impact": "GAS",
  "Source": "",
  "Code": [
    {
      "filename": "src/libraries/LibMath.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.17;\n\n/**\n * @title Lib Math contains math operations\n */\nimport {SafeMath} from \"oz/utils/math/SafeMath.sol\";\n\nlibrary LibMath {\n    error PRBMath_MulDiv_Overflow(uint x, uint y, uint denominator);\n\n    /**\n     * @param a numerator\n     * @param b denominator\n     * @dev Division, round to nearest integer (AKA round-half-up).\n     *\n     * Skip explicit checks for division by zero as Solidity will natively revert.\n     *\n     * Implementation:\n     * https://github.com/cryptoticket/openzeppelin-solidity/blob/04e62a7a1ece4832bee411ca5de024d2ce0b15e6/contracts/math/RoundedDivMath.sol#L31\n     */\n    function roundedDiv(uint a, uint b) internal pure returns (uint) {\n        uint halfB = (b % 2 == 0) ? (b / 2) : (b / 2 + 1);\n        return (a % b >= halfB) ? (a / b + 1) : (a / b);\n    }\n\n    /**\n     * @notice Computes the `n`th root of a number `a` using the Newton--Raphson method.\n     * @param a The number to compute the root of\n     * @param n The root to compute\n     * @return root The `n`th root of `a`\n     * @dev TODO: more testing - https://ethereum.stackexchange.com/questions\n     * /38468/calculate-the-nth-root-of-an-arbitrary-uint-using-solidity\n     * https://en.wikipedia.org/wiki/Nth_root_algorithm\n     * This is used in {ConstantProduct.Sol}, where the number of tokens are\n     * restricted to 16. even roots are much cheaper to compute than uneven,\n     * thus we recursively call sqrt().\n     *\n     */\n    function nthRoot(uint a, uint n) internal pure returns (uint root) {\n        assert(n > 1);\n        if (a == 0) return 0;\n        if (n % 2 == 0) {\n            if (n == 2) return sqrt(a); // shortcut for square root\n            if (n == 4) return sqrt(sqrt(a));\n            if (n == 8) return sqrt(sqrt(sqrt(a)));\n            if (n == 16) return sqrt(sqrt(sqrt(sqrt(a))));\n        }\n        // The scale factor is a crude way to turn everything into integer calcs.\n        // Actually do ((10 ^ n) * x) ^ (1/n)\n        uint a0 = (10 ** n) * a;\n\n        uint xNew = 10;\n        uint x;\n        while (xNew != x) {\n            x = xNew;\n            uint t0 = x ** (n - 1);\n            if (x * t0 > a0) {\n                xNew = x - (x - a0 / t0) / n;\n            } else {\n                xNew = x + (a0 / t0 - x) / n;\n            }\n        }\n\n        root = (xNew + 5) / 10;\n    }\n\n    /**\n     * @notice computes the square root of a given number\n     * @param a The number to compute the square root of\n     * @return z The square root of x\n     * @dev\n     * This function is based on the Babylonian method of computing square roots\n     * https://en.wikipedia.org/wiki/Methods_of_computing_square_roots#Babylonian_method\n     * Implementation from: https://github.com/Gaussian-Process/solidity-sqrt/blob/main/src/FixedPointMathLib.sol\n     * based on https://github.com/transmissions11/solmate/blob/main/src/utils/FixedPointMathLib.sol\n     */\n\n    function sqrt(uint a) internal pure returns (uint z) {\n        assembly {\n            // This segment is to get a reasonable initial estimate for the Babylonian method.\n            // If the initial estimate is bad, the number of correct bits increases ~linearly\n            // each iteration instead of ~quadratically.\n            // The idea is to get z*z*y within a small factor of x.\n            // More iterations here gets y in a tighter range. Currently, we will have\n            // y in [256, 256*2^16). We ensure y>= 256 so that the relative difference\n            // between y and y+1 is small. If x < 256 this is not possible, but those cases\n            // are easy enough to verify exhaustively.\n            z := 181 // The 'correct' value is 1, but this saves a multiply later\n            let y := a\n            // Note that we check y>= 2^(k + 8) but shift right by k bits each branch,\n            // this is to ensure that if x >= 256, then y >= 256.\n            if iszero(lt(y, 0x10000000000000000000000000000000000)) {\n                y := shr(128, y)\n                z := shl(64, z)\n            }\n            if iszero(lt(y, 0x1000000000000000000)) {\n                y := shr(64, y)\n                z := shl(32, z)\n            }\n            if iszero(lt(y, 0x10000000000)) {\n                y := shr(32, y)\n                z := shl(16, z)\n            }\n            if iszero(lt(y, 0x1000000)) {\n                y := shr(16, y)\n                z := shl(8, z)\n            }\n            // Now, z*z*y <= x < z*z*(y+1), and y <= 2^(16+8),\n            // and either y >= 256, or x < 256.\n            // Correctness can be checked exhaustively for x < 256, so we assume y >= 256.\n            // Then z*sqrt(y) is within sqrt(257)/sqrt(256) of x, or about 20bps.\n\n            // The estimate sqrt(x) = (181/1024) * (x+1) is off by a factor of ~2.83 both when x=1\n            // and when x = 256 or 1/256. In the worst case, this needs seven Babylonian iterations.\n            z := shr(18, mul(z, add(y, 65536))) // A multiply is saved from the initial z := 181\n\n            // Run the Babylonian method seven times. This should be enough given initial estimate.\n            // Possibly with a quadratic/cubic polynomial above we could get 4-6.\n            z := shr(1, add(z, div(a, z)))\n            z := shr(1, add(z, div(a, z)))\n            z := shr(1, add(z, div(a, z)))\n            z := shr(1, add(z, div(a, z)))\n            z := shr(1, add(z, div(a, z)))\n            z := shr(1, add(z, div(a, z)))\n            z := shr(1, add(z, div(a, z)))\n\n            // See https://en.wikipedia.org/wiki/Integer_square_root#Using_only_integer_division.\n            // If x+1 is a perfect square, the Babylonian method cycles between\n            // floor(sqrt(x)) and ceil(sqrt(x)). This check ensures we return floor.\n            // The solmate implementation assigns zRoundDown := div(x, z) first, but\n            // since this case is rare, we choose to save gas on the assignment and\n            // repeat division in the rare case.\n            // If you don't care whether floor or ceil is returned, you can skip this.\n            if lt(div(a, z), z) { z := div(a, z) }\n        }\n    }\n}"
    }
  ]
}