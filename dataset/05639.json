{
  "Title": "[G-02] Refactor external/internal function to avoid unnecessary SLOAD",
  "Content": "\nThe function below read storage slots that are previously read in the function that invokes it. We can refactor the external/internal functions to pass cached storage variables as stack variables and avoid the extra storage reads that would otherwise take place in the internal functions.\n\n*There is 1 instance for this issue.*\n\nThe `_updatePolicy()` internal function read storage slots that were read in the `updatePolicy` function that invokes it.\n\nhttps://github.com/code-423n4/2023-10-brahma/blob/main/contracts/src/core/registries/PolicyRegistry.sol#L42-#L66\n\n```solidity\nfile: contracts/src/core/registries/PolicyRegistry.sol\n\n35:    function updatePolicy(address account, bytes32 policyCommit) external {\n36:        if (policyCommit == bytes32(0)) {\n37:            revert PolicyCommitInvalid();\n38:        }\n39:\n40:        WalletRegistry walletRegistry = WalletRegistry(AddressProviderService._getRegistry(_WALLET_REGISTRY_HASH));\n41:\n42:        bytes32 currentCommit = commitments[account];  //@audit 1st SLOAD\n43:\n.\n.\n.\n58:        _updatePolicy(account, policyCommit);\n59:    }\n.\n.\n.\n66:    function _updatePolicy(address account, bytes32 policyCommit) internal {\n67:        emit UpdatedPolicyCommit(account, policyCommit, commitments[account]);  //@audit 2nd SLOAD\n68:        commitments[account] = policyCommit;\n69:    }\n```\n\nThe `_updatePolicy()` function above reads storage for the value of `commitments[account]`, thereby, incurring an `SLOAD` and also has to re-evaluate the mapping to get the value. However, this storage value `commitments[account]` was also read in the `updatePolicy` function, which invokes `_updatePolicy()`. We could refactor the code such that we replace the storage read and re-evaluation of the mapping `commitments[account]` in the `_updatePolicy` with a cheaper stack read. The diff below shows how the code could be refactored:\n\n```diff\ndiff --git a/contracts/src/core/registries/PolicyRegistry.sol b/contracts/src/core/registries/Policy\nindex 559d5ca..b897fe6 100644                                                                       \n--- a/contracts/src/core/registries/PolicyRegistry.sol                                              \n+++ b/contracts/src/core/registries/PolicyRegistry.sol                                              \n@@ -55,7 +55,7 @@ contract PolicyRegistry is AddressProviderService {                               \n             revert UnauthorizedPolicyUpdate();                                                     \n         }                                                                                          \n         // solhint-enable no-empty-blocks                                                          \n-        _updatePolicy(account, policyCommit);                                                      \n+        _updatePolicy(account, policyCommit, currentCommit);                                       \n     }                                                                                              \n                                                                                                    \n     /**                                                                                            \n@@ -63,8 +63,8 @@ contract PolicyRegistry is AddressProviderService {                               \n      * @param account address of account to set policy commit for                                  \n      * @param policyCommit policy commit hash to set                                               \n      */                                                                                            \n-    function _updatePolicy(address account, bytes32 policyCommit) internal {                       \n-        emit UpdatedPolicyCommit(account, policyCommit, commitments[account]);                     \n+    function _updatePolicy(address account, bytes32 policyCommit, bytes32 currentCommit) internal {\n+        emit UpdatedPolicyCommit(account, policyCommit, currentCommit);                            \n         commitments[account] = policyCommit;                                                       \n     }                                                                                              \n }                                                                                                  \n```\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/reports/2023-10-brahma",
  "Code": [
    {
      "filename": "contracts/src/core/registries/PolicyRegistry.sol",
      "content": "/// SPDX-License-Identifier: BUSL-1.1\n\n/// Copyright (C) 2023 Brahma.fi\n\npragma solidity 0.8.19;\n\nimport {AddressProviderService} from \"src/core/AddressProviderService.sol\";\nimport {WalletRegistry} from \"src/core/registries/WalletRegistry.sol\";\n\n/**\n * @title PolicyRegistry\n * @author Brahma.fi\n * @notice Registry for policy commits for wallets and sub accounts\n */\ncontract PolicyRegistry is AddressProviderService {\n    error PolicyCommitInvalid();\n    error UnauthorizedPolicyUpdate();\n\n    event UpdatedPolicyCommit(address indexed account, bytes32 policyCommit, bytes32 oldPolicyCommit);\n\n    /// @notice account addresses mapped to their policy commits\n    mapping(address account => bytes32 policyCommit) public commitments;\n\n    constructor(address _addressProvider) AddressProviderService(_addressProvider) {}\n\n    /**\n     * @notice Enables setting policy commits for accounts\n     * @param account address of account to set policy commit for\n     * @param policyCommit policy commit hash to set\n     * @dev policyCommit for an account can be set by:\n     *  1. by safe deployer, if the account is uninitialized\n     *  2. by the registered wallet, if the account is a subAccount\n     *  3. by the account itself, if account is a registered wallet\n     */\n    function updatePolicy(address account, bytes32 policyCommit) external {\n        if (policyCommit == bytes32(0)) {\n            revert PolicyCommitInvalid();\n        }\n\n        WalletRegistry walletRegistry = WalletRegistry(AddressProviderService._getRegistry(_WALLET_REGISTRY_HASH));\n\n        bytes32 currentCommit = commitments[account];\n\n        // solhint-disable no-empty-blocks\n        if (\n            currentCommit == bytes32(0)\n                && msg.sender == AddressProviderService._getAuthorizedAddress(_SAFE_DEPLOYER_HASH)\n        ) {\n            // In case invoker is safe  deployer\n        } else if (walletRegistry.isOwner(msg.sender, account)) {\n            //In case invoker is updating on behalf of sub account\n        } else if (msg.sender == account && walletRegistry.isWallet(account)) {\n            // In case invoker is a registered wallet\n        } else {\n            revert UnauthorizedPolicyUpdate();\n        }\n        // solhint-enable no-empty-blocks\n        _updatePolicy(account, policyCommit);\n    }\n\n    /**\n     * @notice Internal function to update policy commit for an account\n     * @param account address of account to set policy commit for\n     * @param policyCommit policy commit hash to set\n     */\n    function _updatePolicy(address account, bytes32 policyCommit) internal {\n        emit UpdatedPolicyCommit(account, policyCommit, commitments[account]);\n        commitments[account] = policyCommit;\n    }\n}"
    }
  ]
}