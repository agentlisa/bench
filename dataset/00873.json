{
  "Title": "Abstract Contracts Allow Direct Modification of State Variables",
  "Content": "In order to prevent re-entrant calls, the [`_sendEntered` and `_receiveEntered` state variables](https://github.com/ava-labs/teleporter/blob/253b833518aa7a6448650388cc288bd76d8470a7/contracts/src/Teleporter/ReentrancyGuards.sol#L21-L22) in the abstract `ReentrancyGuards` contract track whether a call has been made to a \"sender\" or \"receiver\" function. Since these variables use `internal` visibility within an `abstract` contract, a child contract would be able to directly modify these values which could unintentionally allow a re-entrant call.\n\n\nConsider using `private` visibility for all non-constant and non-immutable state variables in `abstract` contracts as [OpenZeppelin's `ReentrancyGuard` contract](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v5.0.0/contracts/utils/ReentrancyGuard.sol#L37) does. If child contracts should be able to update values for the state variables, consider creating `internal` functions for updating these variables which emit appropriate events and verify that the desired conditions are met.\n\n\n***Update:** Resolved in [pull request #106](https://github.com/ava-labs/teleporter/pull/106) at commit [43d4c728](https://github.com/ava-labs/teleporter/commit/43d4c7286c42e70eaeffe33807cc512cc4ea2c2f).*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/src/Teleporter/ReentrancyGuards.sol",
      "content": "// (c) 2023, Ava Labs, Inc. All rights reserved.\n// See the file LICENSE for licensing terms.\n\n// SPDX-License-Identifier: Ecosystem\n\npragma solidity 0.8.18;\n\n/**\n * @dev Abstract contract that helps implement reentrancy guards between functions for sending and receiving.\n *\n * Consecutive calls for sending functions should work together, same for receive functions, but recursive calls\n * should be detected as a reentrancy and revert.\n *\n * Calls between send and receive functions should also be allowed, but not in the case it ends up being a recursive\n * send or receive call. For example the following should fail: send -> receive -> send.\n */\nabstract contract ReentrancyGuards {\n    // Send and Receive reentrancy guards\n    uint256 internal constant _NOT_ENTERED = 1;\n    uint256 internal constant _ENTERED = 2;\n    uint256 internal _sendEntered;\n    uint256 internal _receiveEntered;\n\n    // Errors\n    error ReceiverReentrancy();\n    error SenderReentrancy();\n\n    // senderNonReentrant modifier makes sure we can not reenter between sender calls.\n    // This modifier should be used for messenger sender functions that have external calls and do not want to allow\n    // recursive calls with other sender functions.\n    modifier senderNonReentrant() {\n        if (_sendEntered != _NOT_ENTERED) {\n            revert SenderReentrancy();\n        }\n        _sendEntered = _ENTERED;\n        _;\n        _sendEntered = _NOT_ENTERED;\n    }\n\n    // receiverNonReentrant modifier makes sure we can not reenter between receiver calls.\n    // This modifier should be used for messenger receiver functions that have external calls and do not want to allow\n    // recursive calls with other receiver functions.\n    modifier receiverNonReentrant() {\n        if (_receiveEntered != _NOT_ENTERED) {\n            revert ReceiverReentrancy();\n        }\n        _receiveEntered = _ENTERED;\n        _;\n        _receiveEntered = _NOT_ENTERED;\n    }\n\n    constructor() {\n        _sendEntered = _NOT_ENTERED;\n        _receiveEntered = _NOT_ENTERED;\n    }\n}"
    },
    {
      "filename": "contracts/utils/ReentrancyGuard.sol",
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (utils/ReentrancyGuard.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev Contract module that helps prevent reentrant calls to a function.\n *\n * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier\n * available, which can be applied to functions to make sure there are no nested\n * (reentrant) calls to them.\n *\n * Note that because there is a single `nonReentrant` guard, functions marked as\n * `nonReentrant` may not call one another. This can be worked around by making\n * those functions `private`, and then adding `external` `nonReentrant` entry\n * points to them.\n *\n * TIP: If you would like to learn more about reentrancy and alternative ways\n * to protect against it, check out our blog post\n * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].\n */\nabstract contract ReentrancyGuard {\n    // Booleans are more expensive than uint256 or any type that takes up a full\n    // word because each write operation emits an extra SLOAD to first read the\n    // slot's contents, replace the bits taken up by the boolean, and then write\n    // back. This is the compiler's defense against contract upgrades and\n    // pointer aliasing, and it cannot be disabled.\n\n    // The values being non-zero value makes deployment a bit more expensive,\n    // but in exchange the refund on every call to nonReentrant will be lower in\n    // amount. Since refunds are capped to a percentage of the total\n    // transaction's gas, it is best to keep them low in cases like this one, to\n    // increase the likelihood of the full refund coming into effect.\n    uint256 private constant NOT_ENTERED = 1;\n    uint256 private constant ENTERED = 2;\n\n    uint256 private _status;\n\n    /**\n     * @dev Unauthorized reentrant call.\n     */\n    error ReentrancyGuardReentrantCall();\n\n    constructor() {\n        _status = NOT_ENTERED;\n    }\n\n    /**\n     * @dev Prevents a contract from calling itself, directly or indirectly.\n     * Calling a `nonReentrant` function from another `nonReentrant`\n     * function is not supported. It is possible to prevent this from happening\n     * by making the `nonReentrant` function external, and making it call a\n     * `private` function that does the actual work.\n     */\n    modifier nonReentrant() {\n        _nonReentrantBefore();\n        _;\n        _nonReentrantAfter();\n    }\n\n    function _nonReentrantBefore() private {\n        // On the first call to nonReentrant, _status will be NOT_ENTERED\n        if (_status == ENTERED) {\n            revert ReentrancyGuardReentrantCall();\n        }\n\n        // Any calls to nonReentrant after this point will fail\n        _status = ENTERED;\n    }\n\n    function _nonReentrantAfter() private {\n        // By storing the original value once again, a refund is triggered (see\n        // https://eips.ethereum.org/EIPS/eip-2200)\n        _status = NOT_ENTERED;\n    }\n\n    /**\n     * @dev Returns true if the reentrancy guard is currently set to \"entered\", which indicates there is a\n     * `nonReentrant` function in the call stack.\n     */\n    function _reentrancyGuardEntered() internal view returns (bool) {\n        return _status == ENTERED;\n    }\n}"
    }
  ]
}