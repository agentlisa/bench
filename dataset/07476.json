{
  "Title": "[M-02] Must approve 0 first",
  "Content": "# Lines of code\n\nhttps://github.com/code-423n4/2022-12-tigris/blob/main/contracts/Lock.sol#L117\n\n\n# Vulnerability details\n\n## Impact\nSome tokens (like USDT) do not work when changing the allowance from an existing non-zero allowance value.They must first be approved by zero and then the actual allowance must be approved.\n\n\n## Proof of Concept\nhttps://github.com/code-423n4/2022-12-tigris/blob/main/contracts/Lock.sol#L117\n\n```solidity\n    function claimGovFees() public {\n        address[] memory assets = bondNFT.getAssets();\n\n        for (uint i=0; i < assets.length; i++) {\n            uint balanceBefore = IERC20(assets[i]).balanceOf(address(this));\n            IGovNFT(govNFT).claim(assets[i]);\n            uint balanceAfter = IERC20(assets[i]).balanceOf(address(this));\n            IERC20(assets[i]).approve(address(bondNFT), type(uint256).max);// @audit this could fail always with some tokens, \n            bondNFT.distribute(assets[i], balanceAfter - balanceBefore);\n        }\n    }\n```\n\n## Tools Used\nmanual revision\n\n## Recommended Mitigation Steps\n\n\n\nAdd an approve(0) before approving;\n```\n    function claimGovFees() public {\n        address[] memory assets = bondNFT.getAssets();\n\n        for (uint i=0; i < assets.length; i++) {\n            uint balanceBefore = IERC20(assets[i]).balanceOf(address(this));\n            IGovNFT(govNFT).claim(assets[i]);\n            uint balanceAfter = IERC20(assets[i]).balanceOf(address(this));\n            IERC20(assets[i]).approve(address(bondNFT), 0);\n            IERC20(assets[i]).approve(address(bondNFT), type(uint256).max);\n            bondNFT.distribute(assets[i], balanceAfter - balanceBefore);\n        }\n  }\n```\n    \n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-12-tigris-trade-contest",
  "Code": [
    {
      "filename": "contracts/Lock.sol",
      "content": "// SPDX-License-Identifier: UNLICENSED\npragma solidity ^0.8.0;\n\nimport \"hardhat/console.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"./interfaces/IBondNFT.sol\";\nimport \"./interfaces/IGovNFT.sol\";\n\ncontract Lock is Ownable{\n\n    uint public constant minPeriod = 7;\n    uint public constant maxPeriod = 365;\n\n    IBondNFT public immutable bondNFT;\n    IGovNFT public immutable govNFT;\n\n    mapping(address => bool) public allowedAssets;\n    mapping(address => uint) public totalLocked;\n\n    constructor(\n        address _bondNFTAddress,\n        address _govNFT\n    ) {\n        bondNFT = IBondNFT(_bondNFTAddress);\n        govNFT = IGovNFT(_govNFT);\n    }\n\n    /**\n     * @notice Claim pending rewards from a bond\n     * @param _id Bond NFT id\n     * @return address claimed tigAsset address\n     */\n    function claim(\n        uint256 _id\n    ) public returns (address) {\n        claimGovFees();\n        (uint _amount, address _tigAsset) = bondNFT.claim(_id, msg.sender);\n        IERC20(_tigAsset).transfer(msg.sender, _amount);\n        return _tigAsset;\n    }\n\n    /**\n     * @notice Claim pending rewards left over from a bond transfer\n     * @param _tigAsset token address being claimed\n     */\n    function claimDebt(\n        address _tigAsset\n    ) external {\n        claimGovFees();\n        uint amount = bondNFT.claimDebt(msg.sender, _tigAsset);\n        IERC20(_tigAsset).transfer(msg.sender, amount);\n    }\n\n    /**\n     * @notice Lock up tokens to create a bond\n     * @param _asset tigAsset being locked\n     * @param _amount tigAsset amount\n     * @param _period number of days to be locked for\n     */\n    function lock(\n        address _asset,\n        uint _amount,\n        uint _period\n    ) public {\n        require(_period <= maxPeriod, \"MAX PERIOD\");\n        require(_period >= minPeriod, \"MIN PERIOD\");\n        require(allowedAssets[_asset], \"!asset\");\n\n        claimGovFees();\n\n        IERC20(_asset).transferFrom(msg.sender, address(this), _amount);\n        totalLocked[_asset] += _amount;\n        \n        bondNFT.createLock( _asset, _amount, _period, msg.sender);\n    }\n\n    /**\n     * @notice Reset the lock time and extend the period and/or token amount\n     * @param _id Bond id being extended\n     * @param _amount tigAsset amount being added\n     * @param _period number of days being added\n     */\n    function extendLock(\n        uint _id,\n        uint _amount,\n        uint _period\n    ) public {\n        address _asset = claim(_id);\n        IERC20(_asset).transferFrom(msg.sender, address(this), _amount);\n        bondNFT.extendLock(_id, _asset, _amount, _period, msg.sender);\n    }\n\n    /**\n     * @notice Release the bond once it's expired\n     * @param _id Bond id being released\n     */\n    function release(\n        uint _id\n    ) public {\n        claimGovFees();\n        (uint amount, uint lockAmount, address asset, address _owner) = bondNFT.release(_id, msg.sender);\n        totalLocked[asset] -= lockAmount;\n        IERC20(asset).transfer(_owner, amount);\n    }\n\n    /**\n     * @notice Claim rewards from gov nfts and distribute them to bonds\n     */\n    function claimGovFees() public {\n        address[] memory assets = bondNFT.getAssets();\n\n        for (uint i=0; i < assets.length; i++) {\n            uint balanceBefore = IERC20(assets[i]).balanceOf(address(this));\n            IGovNFT(govNFT).claim(assets[i]);\n            uint balanceAfter = IERC20(assets[i]).balanceOf(address(this));\n            IERC20(assets[i]).approve(address(bondNFT), type(uint256).max);\n            bondNFT.distribute(assets[i], balanceAfter - balanceBefore);\n        }\n    }\n\n    /**\n     * @notice Whitelist an asset\n     * @param _tigAsset tigAsset token address\n     * @param _isAllowed set tigAsset as allowed\n     */\n    function editAsset(\n        address _tigAsset,\n        bool _isAllowed\n    ) external onlyOwner() {\n        allowedAssets[_tigAsset] = _isAllowed;\n    }\n\n    /**\n     * @notice Owner can retreive Gov NFTs\n     * @param _ids array of gov nft ids\n     */\n    function sendNFTs(\n        uint[] memory _ids\n    ) external onlyOwner() {\n        govNFT.safeTransferMany(msg.sender, _ids);\n    }\n}"
    }
  ]
}