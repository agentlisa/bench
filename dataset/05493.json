{
  "Title": "[G-02] We can optimize the function `updateLRTConfig()`",
  "Content": "\nhttps://github.com/code-423n4/2023-11-kelp/blob/f751d7594051c0766c7ecd1e68daeb0661e43ee3/src/utils/LRTConfigRoleChecker.sol#L47-L51\n```solidity\nFile: /src/utils/LRTConfigRoleChecker.sol\n47:    function updateLRTConfig(address lrtConfigAddr) external virtual onlyLRTAdmin {\n48:        UtilLib.checkNonZeroAddress(lrtConfigAddr);\n49:        lrtConfig = ILRTConfig(lrtConfigAddr);\n50:        emit UpdatedLRTConfig(lrtConfigAddr);\n51:    }\n```\n\nThe above function uses the modifier `onlyLRTAdmin` which has the following implementation:\n```solidity\n    modifier onlyLRTAdmin() {\n        bytes32 DEFAULT_ADMIN_ROLE = 0x00;\n        if (!IAccessControl(address(lrtConfig)).hasRole(DEFAULT_ADMIN_ROLE, msg.sender)) {\n            revert ILRTConfig.CallerNotLRTConfigAdmin();\n        }\n        _;\n    }\n```\n\nThe modifier reads a state variable `lrtConfig` and also makes an external call to `hasRole()`.\n\nIf the check does not pass we have a revert. Using this modifier in our function means we run the check first and revert if the check does not pass.\n\nIf the check passes, our function has one more check `UtilLib.checkNonZeroAddress` which basically checks that our address is not zero. This is way cheaper compared to the check inside the modifier\n\nIf this check does not pass, it means we would also revert, now suppose the first check(modifier) passes but the non zero fails, the gas consumed inside the modifier doing the state read and external call would all be wasted.\n\nWe can do this check more efficiently by prioritizing cheaper checks first but for this to work, we would need to inline our modifier as shown below:\n\n```diff\ndiff --git a/src/utils/LRTConfigRoleChecker.sol b/src/utils/LRTConfigRoleChecker.sol\nindex 991d0e9..1b9917f 100644\n--- a/src/utils/LRTConfigRoleChecker.sol\n+++ b/src/utils/LRTConfigRoleChecker.sol\n@@ -44,8 +44,12 @@ abstract contract LRTConfigRoleChecker {\n     /// @notice Updates the LRT config contract\n     /// @dev only callable by LRT admin\n     /// @param lrtConfigAddr the new LRT config contract Address\n-    function updateLRTConfig(address lrtConfigAddr) external virtual onlyLRTAdmin {\n+    function updateLRTConfig(address lrtConfigAddr) external virtual  {\n         UtilLib.checkNonZeroAddress(lrtConfigAddr);\n+        bytes32 DEFAULT_ADMIN_ROLE = 0x00;\n+        if (!IAccessControl(address(lrtConfig)).hasRole(DEFAULT_ADMIN_ROLE, msg.sender)) {\n+            revert ILRTConfig.CallerNotLRTConfigAdmin();\n+        }\n         lrtConfig = ILRTConfig(lrtConfigAddr);\n         emit UpdatedLRTConfig(lrtConfigAddr);\n     }\n     \n```\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/reports/2023-11-kelp",
  "Code": [
    {
      "filename": "src/utils/LRTConfigRoleChecker.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\npragma solidity 0.8.21;\n\nimport { UtilLib } from \"./UtilLib.sol\";\nimport { LRTConstants } from \"./LRTConstants.sol\";\n\nimport { ILRTConfig } from \"../interfaces/ILRTConfig.sol\";\n\nimport { IAccessControl } from \"@openzeppelin/contracts/access/IAccessControl.sol\";\n\n/// @title LRTConfigRoleChecker - LRT Config Role Checker Contract\n/// @notice Handles LRT config role checks\nabstract contract LRTConfigRoleChecker {\n    ILRTConfig public lrtConfig;\n\n    // events\n    event UpdatedLRTConfig(address indexed lrtConfig);\n\n    // modifiers\n    modifier onlyLRTManager() {\n        if (!IAccessControl(address(lrtConfig)).hasRole(LRTConstants.MANAGER, msg.sender)) {\n            revert ILRTConfig.CallerNotLRTConfigManager();\n        }\n        _;\n    }\n\n    modifier onlyLRTAdmin() {\n        bytes32 DEFAULT_ADMIN_ROLE = 0x00;\n        if (!IAccessControl(address(lrtConfig)).hasRole(DEFAULT_ADMIN_ROLE, msg.sender)) {\n            revert ILRTConfig.CallerNotLRTConfigAdmin();\n        }\n        _;\n    }\n\n    modifier onlySupportedAsset(address asset) {\n        if (!lrtConfig.isSupportedAsset(asset)) {\n            revert ILRTConfig.AssetNotSupported();\n        }\n        _;\n    }\n\n    // setters\n\n    /// @notice Updates the LRT config contract\n    /// @dev only callable by LRT admin\n    /// @param lrtConfigAddr the new LRT config contract Address\n    function updateLRTConfig(address lrtConfigAddr) external virtual onlyLRTAdmin {\n        UtilLib.checkNonZeroAddress(lrtConfigAddr);\n        lrtConfig = ILRTConfig(lrtConfigAddr);\n        emit UpdatedLRTConfig(lrtConfigAddr);\n    }\n}"
    }
  ]
}