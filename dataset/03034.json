{
  "Title": "H-2: `ChainlinkOracle.sol#getPrice()` The price will be wrong when the token's USD price feed's `decimals != 8`",
  "Content": "# Issue H-2: `ChainlinkOracle.sol#getPrice()` The price will be wrong when the token's USD price feed's `decimals != 8` \nSource: https://github.com/sherlock-audit/2022-08-sentiment-judging/tree/main/019-H \n## Found by \nLambda, csanuragjain, CRYP70, WATCHPUG, berndartmueller, pashov, IllIllI, Bahurum, sorrynotsorry\n\n## Summary\n\n`ChainlinkOracle` assumes and inexplicitly requires the token's USD feed's decimals to be 8. However, there are certain token's USD feed has a different `decimals`.\n\n## Vulnerability Detail\n\nIn the current implementation, it assumes $tokenFeedDecimals = ethFeedDecimals$ (`feed[token].decimals()` must equals `ethUsdPriceFeed.decimals() = 8`).\n\nHowever, there are tokens with USD price feed's decimals != 8 (E.g: [`AMPL / USD` feed decimals = 18](https://etherscan.io/address/0xe20CA8D7546932360e37E9D72c1a47334af57706#readContract))\n\nWhen the token's USD feed's decimals != 8, `ChainlinkOracle.sol#getPrice()` will return an incorrect price in ETH.\n\nThe correct calculation formula should be:\n\n$$\n\\frac{ answer_{token} \\cdot 10^{ethFeedDecimals} }{ answer_{eth} \\cdot 10^{tokenFeedDecimals} } \\cdot 10^{18}\n$$\n\n### PoC\n\nGiven:\n\n-   1.0 AMPL worth 1.14 USD, `feed[ampl].decimals() == 18`, `answer_ampl = 1140608758261546000` [Source: `feed[ampl]`](https://etherscan.io/address/0xe20CA8D7546932360e37E9D72c1a47334af57706#readContract)\n\n-   1.0 ETH worth 1588.11 USD, `ethUsdPriceFeed.decimals() == 8`, `answer_eth = 158811562094` [Source: `ethUsdPriceFeed`](https://etherscan.io/address/0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419#readContract)\n\n`chainlinkOracle.getPrice(AMPL)` will return ~7.18m (eth):\n\n$$\n\\frac{ answer_{token} \\cdot 10^{18} }{ answer_{eth} } = \\frac{ 1140608758261546000 \\cdot 10^{18} }{ 158811562094 } = 7182151873718260663354494\n$$\n\n## Impact\n\nWhen the price feed with `decimals != 18` is set, the attacker can deposit a small amount of the asset and drain all the funds from the protocol.\n\n## Code Snippet\n\nhttps://github.com/sentimentxyz/protocol/blob/4e45871e4540df0f189f6c89deb8d34f24930120/src/core/RiskEngine.sol#L178-L188\n\n```solidity\nfunction _valueInWei(address token, uint amt)\n    internal\n    view\n    returns (uint)\n{\n    return oracle.getPrice(token)\n    .mulDivDown(\n        amt,\n        10 ** ((token == address(0)) ? 18 : IERC20(token).decimals())\n    );\n}\n```\n\nhttps://github.com/sentimentxyz/oracle/blob/59b26a3d8c295208437aad36c470386c9729a4bc/src/chainlink/ChainlinkOracle.sol#L47-L59\n\n```solidity\n/// @inheritdoc IOracle\n/// @dev feed[token].latestRoundData should return price scaled by 8 decimals\nfunction getPrice(address token) external view virtual returns (uint) {\n    (, int answer,,,) =\n        feed[token].latestRoundData();\n\n    if (answer < 0)\n        revert Errors.NegativePrice(token, address(feed[token]));\n\n    return (\n        (uint(answer)*1e18)/getEthPrice()\n    );\n}\n```\n\n\nhttps://github.com/sentimentxyz/oracle/blob/59b26a3d8c295208437aad36c470386c9729a4bc/src/chainlink/ChainlinkOracle.sol#L65-L73\n\n```solidity\nfunction getEthPrice() internal view returns (uint) {\n    (, int answer,,,) =\n        ethUsdPriceFeed.latestRoundData();\n\n    if (answer < 0)\n        revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n    return uint(answer);\n}\n```\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\nConsider adding a check for `feed.decimals()` to make sure `feed`'s decimals = `8`:\n\n```solidity\nconstructor(AggregatorV3Interface _ethUsdPriceFeed) Ownable(msg.sender) {\n    require(_ethUsdPriceFeed.decimals() == 8, \"...\");\n    ethUsdPriceFeed = _ethUsdPriceFeed;\n}\n```\n\n```solidity\nfunction setFeed(\n    address token,\n    AggregatorV3Interface _feed\n) external adminOnly {\n    require(_feed.decimals() == 8, \"...\");\n    feed[token] = _feed;\n    emit UpdateFeed(token, address(_feed));\n}\n```\n## Sentiment Team\nOur chainlink oracles use token/usd feed for all the assets we support via chainlink, all the token/usd chainlink feeds have 8 decimals and hence will not lead to any issue with the final price calculation. PR [here](https://github.com/sentimentxyz/oracle/pull/37).\n\n## Lead Senior Watson\nConfirmed fix. \n\n",
  "Impact": "HIGH",
  "Source": "https://app.sherlock.xyz/audits/contests/1",
  "Code": [
    {
      "filename": "src/core/RiskEngine.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport {Errors} from \"../utils/Errors.sol\";\nimport {Ownable} from \"../utils/Ownable.sol\";\nimport {IOracle} from \"oracle/core/IOracle.sol\";\nimport {IERC20} from \"../interface/tokens/IERC20.sol\";\nimport {ILToken} from \"../interface/tokens/ILToken.sol\";\nimport {IAccount} from \"../interface/core/IAccount.sol\";\nimport {IRegistry} from \"../interface/core/IRegistry.sol\";\nimport {IRiskEngine} from \"../interface/core/IRiskEngine.sol\";\nimport {IAccountManager} from \"../interface/core/IAccountManager.sol\";\nimport {FixedPointMathLib} from \"solmate/utils/FixedPointMathLib.sol\";\n\n/**\n    @title Risk Engine\n    @notice Risk engine is a sentiment utility contract used by the protocol to\n    analyze the health factor of a given account.\n*/\ncontract RiskEngine is Ownable, IRiskEngine {\n    using FixedPointMathLib for uint;\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice Registry\n    IRegistry public immutable registry;\n\n    /// @notice Oracle Facade\n    IOracle public oracle;\n\n    /// @notice Account Manager\n    IAccountManager public accountManager;\n\n    /// @notice Balance:Borrow, Default = 1.2\n    uint public constant balanceToBorrowThreshold = 1.2e18;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _registry Address of registry contract\n    */\n    constructor(IRegistry _registry) {\n        initOwnable(msg.sender);\n        registry = _registry;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                             EXTERNAL FUNCTIONS                             */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice Initializes external dependencies\n    function initDep() external adminOnly {\n        oracle = IOracle(registry.getAddress('ORACLE'));\n        accountManager = IAccountManager(registry.getAddress('ACCOUNT_MANAGER'));\n    }\n\n    /**\n        @notice Utility function to determine if an account can borrow a\n        specified amount of a token\n            isBorrowAllowed = (currentAccountBalance + borrowValue) /\n                (currentAccountBorrows + borrowValue) > balanceToBorrowThreshold\n        @param account Address of account\n        @param token Address of token\n        @param amt Amount of token to borrow\n        @return isBorrowAllowed Returns whether a borrow is allowed or not\n    */\n    function isBorrowAllowed(\n        address account,\n        address token,\n        uint amt\n    )\n        external\n        view\n        returns (bool)\n    {\n        uint borrowValue = _valueInWei(token, amt);\n        return _isAccountHealthy(\n            _getBalance(account) + borrowValue,\n            _getBorrows(account) + borrowValue\n        );\n    }\n\n    /**\n        @notice Utility function to determine if an account can withdraw a\n        specified amount of a token\n            isWithdrawAllowed = (currentAccountBalance - withdrawValue) /\n                currentAccountBorrows > balanceToBorrowThreshold\n        @param account Address of account\n        @param token Address of token\n        @param amt Amount of token to withdraw\n        @return isWithdrawAllowed Returns whether a withdraw is allowed or not\n    */\n    function isWithdrawAllowed(\n        address account,\n        address token,\n        uint amt\n    )\n        external\n        view\n        returns (bool)\n    {\n        if (IAccount(account).hasNoDebt()) return true;\n        return _isAccountHealthy(\n            _getBalance(account) - _valueInWei(token, amt),\n            _getBorrows(account)\n        );\n    }\n\n    /**\n        @notice Utility function to determine if an account is healthy or not\n            isAccountHealthy = currentAccountBalance / currentAccountBorrows >\n                balanceToBorrowThreshold\n         @param account Address of account\n        @return isAccountHealthy Returns whether an account is healthy or not.\n    */\n    function isAccountHealthy(address account) external view returns (bool) {\n        return _isAccountHealthy(\n            _getBalance(account),\n            _getBorrows(account)\n        );\n    }\n\n    /**\n        @notice Returns total account Balance\n        @param account Address of account\n        @return balance Total account balance\n    */\n    function getBalance(address account) external view returns (uint) {\n        return _getBalance(account);\n    }\n\n    /**\n        @notice Returns total account Borrows\n        @param account Address of account\n        @return borrows Total account borrows\n    */\n    function getBorrows(address account) external view returns (uint) {\n        return _getBorrows(account);\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                             Internal Functions                             */\n    /* -------------------------------------------------------------------------- */\n\n    function _getBalance(address account) internal view returns (uint) {\n        address[] memory assets = IAccount(account).getAssets();\n        uint assetsLen = assets.length;\n        uint totalBalance;\n        for(uint i; i < assetsLen; ++i) {\n            totalBalance += _valueInWei(\n                assets[i],\n                IERC20(assets[i]).balanceOf(account)\n            );\n        }\n        return totalBalance + account.balance;\n    }\n\n    function _getBorrows(address account) internal view returns (uint) {\n        if (IAccount(account).hasNoDebt()) return 0;\n        address[] memory borrows = IAccount(account).getBorrows();\n        uint borrowsLen = borrows.length;\n        uint totalBorrows;\n        for(uint i; i < borrowsLen; ++i) {\n            address LTokenAddr = registry.LTokenFor(borrows[i]);\n            totalBorrows += _valueInWei(\n                borrows[i],\n                ILToken(LTokenAddr).getBorrowBalance(account)\n            );\n        }\n        return totalBorrows;\n    }\n\n    function _valueInWei(address token, uint amt)\n        internal\n        view\n        returns (uint)\n    {\n        return oracle.getPrice(token)\n        .mulDivDown(\n            amt,\n            10 ** ((token == address(0)) ? 18 : IERC20(token).decimals())\n        );\n    }\n\n    function _isAccountHealthy(uint accountBalance, uint accountBorrows)\n        internal\n        pure\n        returns (bool)\n    {\n        return (accountBorrows == 0) ? true :\n            (accountBalance.divWadDown(accountBorrows) > balanceToBorrowThreshold);\n    }\n}"
    },
    {
      "filename": "src/chainlink/ChainlinkOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {Ownable} from \"../utils/Ownable.sol\";\nimport {Errors} from \"../utils/Errors.sol\";\nimport {AggregatorV3Interface} from \"./AggregatorV3Interface.sol\";\n\n/**\n    @title Chain Link Oracle\n    @notice Oracle to fetch price using chainlink oracles\n*/\ncontract ChainlinkOracle is Ownable, IOracle {\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice ETH USD Chainlink price feed\n    AggregatorV3Interface immutable ethUsdPriceFeed;\n\n    /// @notice Mapping of token to token/usd chainlink price feed\n    mapping(address => AggregatorV3Interface) public feed;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                   EVENTS                                   */\n    /* -------------------------------------------------------------------------- */\n\n    event UpdateFeed(address indexed token, address indexed feed);\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _ethUsdPriceFeed ETH USD Chainlink price feed\n    */\n    constructor(AggregatorV3Interface _ethUsdPriceFeed) Ownable(msg.sender) {\n        ethUsdPriceFeed = _ethUsdPriceFeed;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @inheritdoc IOracle\n    /// @dev feed[token].latestRoundData should return price scaled by 8 decimals\n    function getPrice(address token) external view virtual returns (uint) {\n        (, int answer,,,) =\n            feed[token].latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(token, address(feed[token]));\n\n        return (\n            (uint(answer)*1e18)/getEthPrice()\n        );\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                             INTERNAL FUNCTIONS                             */\n    /* -------------------------------------------------------------------------- */\n\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,,,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                               ADMIN FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    function setFeed(\n        address token,\n        AggregatorV3Interface _feed\n    ) external adminOnly {\n        feed[token] = _feed;\n        emit UpdateFeed(token, address(_feed));\n    }\n}"
    },
    {
      "filename": "src/chainlink/ChainlinkOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {Ownable} from \"../utils/Ownable.sol\";\nimport {Errors} from \"../utils/Errors.sol\";\nimport {AggregatorV3Interface} from \"./AggregatorV3Interface.sol\";\n\n/**\n    @title Chain Link Oracle\n    @notice Oracle to fetch price using chainlink oracles\n*/\ncontract ChainlinkOracle is Ownable, IOracle {\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice ETH USD Chainlink price feed\n    AggregatorV3Interface immutable ethUsdPriceFeed;\n\n    /// @notice Mapping of token to token/usd chainlink price feed\n    mapping(address => AggregatorV3Interface) public feed;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                   EVENTS                                   */\n    /* -------------------------------------------------------------------------- */\n\n    event UpdateFeed(address indexed token, address indexed feed);\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _ethUsdPriceFeed ETH USD Chainlink price feed\n    */\n    constructor(AggregatorV3Interface _ethUsdPriceFeed) Ownable(msg.sender) {\n        ethUsdPriceFeed = _ethUsdPriceFeed;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @inheritdoc IOracle\n    /// @dev feed[token].latestRoundData should return price scaled by 8 decimals\n    function getPrice(address token) external view virtual returns (uint) {\n        (, int answer,,,) =\n            feed[token].latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(token, address(feed[token]));\n\n        return (\n            (uint(answer)*1e18)/getEthPrice()\n        );\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                             INTERNAL FUNCTIONS                             */\n    /* -------------------------------------------------------------------------- */\n\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,,,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                               ADMIN FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    function setFeed(\n        address token,\n        AggregatorV3Interface _feed\n    ) external adminOnly {\n        feed[token] = _feed;\n        emit UpdateFeed(token, address(_feed));\n    }\n}"
    }
  ]
}