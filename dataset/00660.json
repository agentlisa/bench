{
  "Title": "GovernanceProxy DOS via `updateDelay()`",
  "Content": "##### Description\n\nIf an admin mistakenly calls `updateDelay(someImportantFunction, SUPER_BIG_NUMBER)` in GovernanceProxy, restoring a lower delay for `someImportantFunction` becomes impossible. To execute `updateDelay(someImportantFunction, LOW_NUMBER)`, one would need to wait out the initially set `SUPER_BIG_NUMBER` delay:\n```\nfunction _computeDelay(\n    bytes calldata data\n    ) internal view returns (uint64) {\n    bytes4 selector = bytes4(data[:4]);\n\n    // special case for `updateDelay`, we want to set the delay\n    // as the delay for the current function for which the delay\n    // will be changed, rather than a generic delay for `updateDelay` itself\n    // for all the other functions, we use their actual delay\n    if (selector == GovernanceProxy.updateDelay.selector) {\n        bytes memory callData = data[4:];\n        (selector, ) = abi.decode(callData, (bytes4, uint256));\n    }\n\n    return delays[selector];\n}\n```\nhttps://github.com/ConicFinance/protocol/blob/7a66d26ef84f93059a811a189655e17c11d95f5c/contracts/access/GovernanceProxy.sol#L196-L210\n\nIn this situation, function calls might effectively get blocked for an excessively long period.\n\n##### Recommendation\n\nWe recommended capping the maximum delay at a reasonable number.\n\n",
  "Impact": "MEDIUM",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/access/GovernanceProxy.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\npragma solidity 0.8.17;\n\nimport \"@openzeppelin/contracts/utils/Address.sol\";\n\nimport \"./SimpleAccessControl.sol\";\nimport \"../../interfaces/access/IGovernanceProxy.sol\";\n\ncontract GovernanceProxy is IGovernanceProxy, SimpleAccessControl {\n    using Address for address;\n\n    bytes32 public constant GOVERNANCE_ROLE = \"GOVERNANCE\";\n    bytes32 public constant VETO_ROLE = \"VETO\";\n\n    uint64 public nextChangeId;\n\n    /// @notice mapping from function selector to delay in seconds\n    /// NOTE: For simplicity, delays are only based on selectors and not on the\n    /// contract address, which means that if two functions in different\n    /// contracts have the exact same name and arguments, they will share the same delay\n    mapping(bytes4 => uint64) public override delays;\n\n    /// @dev array of pending changes to execute\n    /// We perform linear searches through this using the change ID, so in theory\n    /// this could run out of gas. However, in practice, the number of pending\n    /// changes should never become large enough for this to become an issue\n    Change[] internal pendingChanges;\n\n    /// @dev this is an array of all changes that have ended, regardless of their status\n    /// this is only used to make it easier to query but should not be used from within\n    /// any contract as the length is completely unbounded\n    Change[] internal endedChanges;\n\n    constructor(address governance, address veto) {\n        _grantRole(GOVERNANCE_ROLE, governance);\n        _grantRole(VETO_ROLE, veto);\n    }\n\n    /// @return change the pending change with the given id if found\n    /// this reverts if the change is not found\n    function getPendingChange(uint64 changeId) external view returns (Change memory change) {\n        (change, ) = _findPendingChange(changeId);\n    }\n\n    /// @return all the pending changes\n    /// this should typically be quite small so no need for pagination\n    function getPendingChanges() external view returns (Change[] memory) {\n        return pendingChanges;\n    }\n\n    /// @return total number of ended changes\n    function getEndedChangesCount() external view returns (uint256) {\n        return endedChanges.length;\n    }\n\n    /// @return all the ended changes\n    /// this can become large so `getEndedChanges(uint256 offset, uint256 n)`\n    /// is the preferred way to query\n    function getEndedChanges() external view returns (Change[] memory) {\n        return endedChanges;\n    }\n\n    /// @return `n` ended changes starting from offset\n    /// This is useful is you want to paginate through the changes\n    /// note that the changes are in chronological order of execution/cancelation\n    /// which means that it might be useful to start paginatign from the end of the array\n    function getEndedChanges(uint256 offset, uint256 n) external view returns (Change[] memory) {\n        Change[] memory paginated = new Change[](n);\n        for (uint256 i; i < n; i++) {\n            paginated[i] = endedChanges[offset + i];\n        }\n        return paginated;\n    }\n\n    /// @notice Requests a list of function calls to be executed as a change\n    /// @dev If the change requires no delay, it will be executed immediately\n    /// @param calls the calls to be executed\n    /// this should be fully encoded including the selectors and the abi-encoded arguments\n    /// Changes can only be requested by governance\n    function requestChange(Call[] calldata calls) external onlyRole(GOVERNANCE_ROLE) {\n        // Calculating the maximum delay for all calls\n        uint64 maxDelay;\n        for (uint256 i; i < calls.length; i++) {\n            uint64 delay = _computeDelay(calls[i].data);\n            if (delay > maxDelay) maxDelay = delay;\n        }\n\n        (Change storage change, uint256 index) = _requestChange(maxDelay, calls);\n\n        // If the change requires no delay, execute it immediately\n        if (maxDelay == 0) {\n            _executeChange(change, index);\n        }\n    }\n\n    /// @notice Executes a change\n    /// The deadline of the change must be past\n    /// Anyone can execute a pending change but in practice, this will be called by governance too\n    function executeChange(uint64 id) external {\n        (Change storage change, uint256 index) = _findPendingChange(id);\n        _executeChange(change, index);\n    }\n\n    /// @notice Cancels a pending change\n    /// Both governance and users having veto power can cancel a pending change\n    function cancelChange(uint64 id) external {\n        require(\n            hasRole(GOVERNANCE_ROLE, msg.sender) || hasRole(VETO_ROLE, msg.sender),\n            \"not authorized\"\n        );\n\n        (Change storage change, uint256 index) = _findPendingChange(id);\n        emit ChangeCanceled(id);\n        _endChange(change, index, Status.Canceled);\n    }\n\n    // the following functions should be called through `executeChange`\n\n    function updateDelay(bytes4 selector, uint64 delay) external override {\n        require(msg.sender == address(this), \"not authorized\");\n        delays[selector] = delay;\n        emit DelayUpdated(selector, delay);\n    }\n\n    function grantRole(bytes32 role, address account) external override {\n        require(msg.sender == address(this), \"not authorized\");\n        require(role == GOVERNANCE_ROLE || role == VETO_ROLE, \"invalid role\");\n        _grantRole(role, account);\n    }\n\n    function revokeRole(bytes32 role, address account) external override {\n        require(msg.sender == address(this), \"not authorized\");\n        require(\n            role != GOVERNANCE_ROLE || getRoleMemberCount(role) > 1,\n            \"need at least one governor\"\n        );\n        _revokeRole(role, account);\n    }\n\n    // internal helpers\n\n    function _requestChange(\n        uint64 delay,\n        Call[] calldata calls\n    ) internal returns (Change storage change, uint256 index) {\n        uint64 id = nextChangeId++;\n        change = pendingChanges.push();\n        change.id = id;\n        change.requestedAt = uint64(block.timestamp);\n        change.endedAt = 0;\n        change.delay = delay;\n        change.status = Status.Pending;\n        for (uint256 i; i < calls.length; i++) {\n            change.calls.push(calls[i]);\n        }\n\n        index = pendingChanges.length - 1;\n\n        emit ChangeRequested(calls, delay, id);\n    }\n\n    function _executeChange(Change storage change, uint256 index) internal {\n        require(\n            change.requestedAt + change.delay <= block.timestamp,\n            \"deadline has not been reached\"\n        );\n\n        for (uint256 i; i < change.calls.length; i++) {\n            change.calls[i].target.functionCall(change.calls[i].data);\n        }\n\n        emit ChangeExecuted(change.id);\n        _endChange(change, index, Status.Executed);\n    }\n\n    function _endChange(Change storage change, uint256 index, Status status) internal {\n        change.status = status;\n        change.endedAt = uint64(block.timestamp);\n        endedChanges.push(change);\n        _removePendingChange(index);\n    }\n\n    function _removePendingChange(uint256 index) internal {\n        pendingChanges[index] = pendingChanges[pendingChanges.length - 1];\n        pendingChanges.pop();\n    }\n\n    function _findPendingChange(uint64 id) internal view returns (Change storage, uint256 index) {\n        for (uint256 i; i < pendingChanges.length; i++) {\n            if (pendingChanges[i].id == id) {\n                return (pendingChanges[i], i);\n            }\n        }\n        revert(\"change not found\");\n    }\n\n    function _computeDelay(bytes calldata data) internal view returns (uint64) {\n        bytes4 selector = bytes4(data[:4]);\n\n        // special case for `updateDelay`, we want to set the delay\n        // as the delay for the current function for which the delay\n        // will be changed, rather than a generic delay for `updateDelay` itself\n        // for all the other functions, we use their actual delay\n        if (selector == GovernanceProxy.updateDelay.selector) {\n            bytes memory callData = data[4:];\n            (selector, ) = abi.decode(callData, (bytes4, uint256));\n        }\n\n        return delays[selector];\n    }\n}"
    }
  ]
}