{
  "Title": "M-4: No check if Arbitrum L2 sequencer is down in Chainlink feeds",
  "Content": "# Issue M-4: No check if Arbitrum L2 sequencer is down in Chainlink feeds \n\nSource: https://github.com/sherlock-audit/2023-01-sentiment-judging/issues/16 \n\n## Found by \n0xdeadbeef\n\n## Summary\n\nUsing Chainlink in L2 chains such as Arbitrum requires to check if the sequencer is down to avoid prices from looking like they are fresh although they are not.\n\nThe bug could be leveraged by malicious actors to take advantage of the sequencer downtime.\n\n## Vulnerability Detail\n\nThe new `GLPOracle` is used the get the the price of GLP. There is no check that the sequencer is down:\nhttps://github.com/sherlock-audit/2023-01-sentiment/blob/main/oracle/src/gmx/GLPOracle.sol#L47\n```solidity\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,, uint updatedAt,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (block.timestamp - updatedAt >= 86400)\n            revert Errors.StalePrice(address(0), address(ethUsdPriceFeed));\n\n        if (answer <= 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n```\n\n## Impact\n\nThe impact depends on the usage of the GLP. \nIf it is used as part of the collateral for lenders:\n* Users can get better borrows if the price is above the actual price\n* Users can avoid liquidations if the price is under the actual price \n\n## Code Snippet\n\n## Tool used\n\nVS Code\n\nManual Review\n\n## Recommendation\n\nIt is recommended to follow the code example of Chainlink: \nhttps://docs.chain.link/data-feeds/l2-sequencer-feeds#example-code\n\n## Discussion\n\n**zobront**\n\nFix confirmed.\n\n\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/37",
  "Code": [
    {
      "filename": "oracle/src/gmx/GLPOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.17;\n\nimport {Errors} from \"../utils/Errors.sol\";\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {IGLPManager} from \"./IGLPManager.sol\";\nimport {AggregatorV3Interface} from \"../chainlink/AggregatorV3Interface.sol\";\n\n/**\n    @title GLP Oracle\n*/\ncontract GLPOracle is IOracle {\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice address of gmx manager\n    IGLPManager public immutable manager;\n\n    /// @notice ETH USD Chainlink price feed\n    AggregatorV3Interface immutable ethUsdPriceFeed;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _manager address of gmx vault\n        @param _ethFeed address of eth usdc chainlink feed\n    */\n    constructor(IGLPManager _manager, AggregatorV3Interface _ethFeed) {\n        manager = _manager;\n        ethUsdPriceFeed = _ethFeed;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @inheritdoc IOracle\n    function getPrice(address) external view returns (uint) {\n        return manager.getPrice(false) / (getEthPrice() * 1e4);\n    }\n\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,, uint updatedAt,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (block.timestamp - updatedAt >= 86400)\n            revert Errors.StalePrice(address(0), address(ethUsdPriceFeed));\n\n        if (answer <= 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n}"
    }
  ]
}