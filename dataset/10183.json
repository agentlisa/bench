{
  "Title": "[M-03] `SafeMath` not completely used in yield source contracts",
  "Content": "_Submitted by shw, also found by cmichel_\n\n`SafeMath` is not completely used at the following lines of yield source contracts, which could potentially cause arithmetic underflow and overflow:\n1. [line 78](https://github.com/code-423n4/2021-06-pooltogether/blob/main/contracts/yield-source/SushiYieldSource.sol#L78) in `SushiYieldSource`\n2. [line 67](https://github.com/code-423n4/2021-06-pooltogether/blob/main/contracts/yield-source/BadgerYieldSource.sol#L67) in `BadgerYieldSource`\n3. line [91](https://github.com/code-423n4/2021-06-pooltogether/blob/main/contracts/yield-source/IdleYieldSource.sol#L91) and [98](https://github.com/code-423n4/2021-06-pooltogether/blob/main/contracts/yield-source/IdleYieldSource.sol#L98) in `IdleYieldSource`\n\nRecommend using the `SafeMath` library functions in the above lines.\n\n**[asselstine (PoolTogether) confirmed and disagreed with severity](https://github.com/code-423n4/2021-06-pooltogether-findings/issues/114#issuecomment-868022899):**\n > While the arithmetic ceiling is quite high, if an overflow occurred this would significantly disrupt the yield sources.  I'd qualify this issue higher as `2 (Med Risk)`.\n\n**[dmvt (judge) commented](https://github.com/code-423n4/2021-06-pooltogether-findings/issues/114#issuecomment-904646481):**\n > I agree with the sponsor's risk evaluation. Increasing to medium.\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/reports/2021-06-pooltogether",
  "Code": [
    {
      "filename": "contracts/yield-source/SushiYieldSource.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0\n\npragma solidity 0.6.12;\n\nimport { IYieldSource } from \"@pooltogether/yield-source-interface/contracts/IYieldSource.sol\";\nimport \"@openzeppelin/contracts/math/SafeMath.sol\";\n\nimport \"./ISushiBar.sol\";\nimport \"./ISushi.sol\";\n\n/// @title A pooltogether yield source for sushi token\n/// @author Steffel Fenix\ncontract SushiYieldSource is IYieldSource {\n    \n    using SafeMath for uint256;\n    \n    ISushiBar public immutable sushiBar;\n    ISushi public immutable sushiAddr;\n    \n    mapping(address => uint256) public balances;\n\n    constructor(ISushiBar _sushiBar, ISushi _sushiAddr) public {\n        sushiBar = _sushiBar;\n        sushiAddr = _sushiAddr;\n    }\n\n    /// @notice Returns the ERC20 asset token used for deposits.\n    /// @return The ERC20 asset token\n    function depositToken() public view override returns (address) {\n        return address(sushiAddr);\n    }\n\n    /// @notice Returns the total balance (in asset tokens).  This includes the deposits and interest.\n    /// @return The underlying balance of asset tokens\n    function balanceOfToken(address addr) public override returns (uint256) {\n        if (balances[addr] == 0) return 0;\n\n        uint256 totalShares = sushiBar.totalSupply();\n        uint256 barSushiBalance = sushiAddr.balanceOf(address(sushiBar));\n\n        return balances[addr].mul(barSushiBalance).div(totalShares);       \n    }\n\n    /// @notice Allows assets to be supplied on other user's behalf using the `to` param.\n    /// @param amount The amount of `token()` to be supplied\n    /// @param to The user whose balance will receive the tokens\n    function supplyTokenTo(uint256 amount, address to) public override {\n        sushiAddr.transferFrom(msg.sender, address(this), amount);\n        sushiAddr.approve(address(sushiBar), amount);\n\n        ISushiBar bar = sushiBar;\n        uint256 beforeBalance = bar.balanceOf(address(this));\n        \n        bar.enter(amount);\n        \n        uint256 afterBalance = bar.balanceOf(address(this));\n        uint256 balanceDiff = afterBalance.sub(beforeBalance);\n        \n        balances[to] = balances[to].add(balanceDiff);\n    }\n\n    /// @notice Redeems tokens from the yield source to the msg.sender, it burns yield bearing tokens and returns token to the sender.\n    /// @param amount The amount of `token()` to withdraw.  Denominated in `token()` as above.\n    /// @dev The maxiumum that can be called for token() is calculated by balanceOfToken() above.\n    /// @return The actual amount of tokens that were redeemed. This may be different from the amount passed due to the fractional math involved. \n    function redeemToken(uint256 amount) public override returns (uint256) {\n        ISushiBar bar = sushiBar;\n        ISushi sushi = sushiAddr;\n\n        uint256 totalShares = bar.totalSupply();\n        if(totalShares == 0) return 0; \n\n        uint256 barSushiBalance = sushi.balanceOf(address(bar));\n        if(barSushiBalance == 0) return 0;\n\n        uint256 sushiBeforeBalance = sushi.balanceOf(address(this));\n\n        uint256 requiredShares = ((amount.mul(totalShares) + totalShares)).div(barSushiBalance);\n        if(requiredShares == 0) return 0;\n        \n        uint256 requiredSharesBalance = requiredShares.sub(1);\n        bar.leave(requiredSharesBalance);\n\n        uint256 sushiAfterBalance = sushi.balanceOf(address(this));\n        \n        uint256 sushiBalanceDiff = sushiAfterBalance.sub(sushiBeforeBalance);\n\n        balances[msg.sender] = balances[msg.sender].sub(requiredSharesBalance);\n        sushi.transfer(msg.sender, sushiBalanceDiff);\n        \n        return (sushiBalanceDiff);\n    }\n\n}"
    },
    {
      "filename": "contracts/yield-source/BadgerYieldSource.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0\n\npragma solidity 0.6.12;\n\nimport { IYieldSource } from \"@pooltogether/yield-source-interface/contracts/IYieldSource.sol\";\nimport \"@openzeppelin/contracts/math/SafeMath.sol\";\nimport \"./IBadgerSett.sol\";\nimport \"./IBadger.sol\";\nimport \"hardhat/console.sol\";\n\n/// @title A pooltogether yield source for badger sett\n/// @author Steffel Fenix, 0xkarl\ncontract BadgerYieldSource is IYieldSource {\n    using SafeMath for uint256;\n    IBadgerSett private immutable badgerSett;\n    IBadger private immutable badger;\n    mapping(address => uint256) private balances;\n\n    constructor(address badgerSettAddr, address badgerAddr) public {\n        badgerSett = IBadgerSett(badgerSettAddr);\n        badger = IBadger(badgerAddr);\n    }\n\n    /// @notice Returns the ERC20 asset token used for deposits.\n    /// @return The ERC20 asset token\n    function depositToken() public view override returns (address) {\n        return (address(badger));\n    }\n\n    /// @notice Returns the total balance (in asset tokens).  This includes the deposits and interest.\n    /// @return The underlying balance of asset tokens\n    function balanceOfToken(address addr) public override returns (uint256) {\n        if (balances[addr] == 0) return 0;\n\n        uint256 totalShares = badgerSett.totalSupply();\n        uint256 badgerSettBadgerBalance = badger.balanceOf(address(badgerSett));\n        return (balances[addr].mul(badgerSettBadgerBalance).div(totalShares));\n    }\n\n    /// @notice Allows assets to be supplied on other user's behalf using the `to` param.\n    /// @param amount The amount of `token()` to be supplied\n    /// @param to The user whose balance will receive the tokens\n    function supplyTokenTo(uint256 amount, address to) public override {\n        badger.transferFrom(msg.sender, address(this), amount);\n        badger.approve(address(badgerSett), amount);\n\n        uint256 beforeBalance = badgerSett.balanceOf(address(this));\n        badgerSett.deposit(amount);\n        uint256 afterBalance = badgerSett.balanceOf(address(this));\n        uint256 balanceDiff = afterBalance.sub(beforeBalance);\n        balances[to] = balances[to].add(balanceDiff);\n    }\n\n    /// @notice Redeems tokens from the yield source to the msg.sender, it burns yield bearing tokens and returns token to the sender.\n    /// @param amount The amount of `token()` to withdraw.  Denominated in `token()` as above.\n    /// @return The actual amount of tokens that were redeemed.\n    function redeemToken(uint256 amount) public override returns (uint256) {\n        uint256 totalShares = badgerSett.totalSupply();\n        if (totalShares == 0) return 0;\n\n        uint256 badgerSettBadgerBalance = badgerSett.balance();\n        if (badgerSettBadgerBalance == 0) return 0;\n\n        uint256 badgerBeforeBalance = badger.balanceOf(address(this));\n\n        uint256 requiredShares =\n            ((amount.mul(totalShares) + totalShares)).div(\n                badgerSettBadgerBalance\n            );\n        if (requiredShares == 0) return 0;\n\n        uint256 requiredSharesBalance = requiredShares.sub(1);\n        badgerSett.withdraw(requiredSharesBalance);\n\n        uint256 badgerAfterBalance = badger.balanceOf(address(this));\n        uint256 badgerBalanceDiff = badgerAfterBalance.sub(badgerBeforeBalance);\n\n        balances[msg.sender] = balances[msg.sender].sub(requiredSharesBalance);\n        badger.transfer(msg.sender, badgerBalanceDiff);\n        return (badgerBalanceDiff);\n    }\n}"
    },
    {
      "filename": "contracts/yield-source/IdleYieldSource.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0\n\npragma solidity 0.8.4;\n\nimport \"@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/ERC20Upgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/utils/SafeERC20Upgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/security/ReentrancyGuardUpgradeable.sol\";\nimport \"./interfaces/pooltogether/IProtocolYieldSource.sol\";\nimport \"./interfaces/idle/IIdleToken.sol\";\nimport \"./access/AssetManager.sol\";\n\n/// @title An pooltogether yield source for Idle token\n/// @author Sunny Radadiya\ncontract IdleYieldSource is IProtocolYieldSource, Initializable, ReentrancyGuardUpgradeable, ERC20Upgradeable, AssetManager  {\n    using SafeERC20Upgradeable for IERC20Upgradeable;\n\n    address public idleToken;\n    address public underlyingAsset;\n    uint256 public constant ONE_IDLE_TOKEN = 10**18;\n\n    /// @notice Emitted when the yield source is initialized\n    event IdleYieldSourceInitialized(address indexed idleToken);\n\n    /// @notice Emitted when asset tokens are redeemed from the yield source\n    event RedeemedToken(\n        address indexed from,\n        uint256 shares,\n        uint256 amount\n    );\n\n    /// @notice Emitted when asset tokens are supplied to the yield source\n    event SuppliedTokenTo(\n        address indexed from,\n        uint256 shares,\n        uint256 amount,\n        address indexed to\n    );\n\n    /// @notice Emitted when asset tokens are supplied to sponsor the yield source\n    event Sponsored(\n        address indexed from,\n        uint256 amount\n    );\n\n    /// @notice Emitted when ERC20 tokens other than yield source's idleToken are withdrawn from the yield source\n    event TransferredERC20(\n        address indexed from,\n        address indexed to,\n        uint256 amount,\n        address indexed token\n    );\n\n    /// @notice Initializes the yield source with Idle Token\n    /// @param _idleToken Idle Token address\n    function initialize(\n        address _idleToken\n    ) public initializer {\n\n        __Ownable_init();\n\n        idleToken = _idleToken;\n        underlyingAsset = IIdleToken(idleToken).token();\n\n        IERC20Upgradeable(underlyingAsset).safeApprove(idleToken, type(uint256).max);\n        emit IdleYieldSourceInitialized(idleToken);\n    }\n\n    /// @notice Returns the ERC20 asset token used for deposits.\n    /// @return The ERC20 asset token\n    function depositToken() external view override returns (address) {\n        return underlyingAsset;\n    }\n\n    /// @notice Returns the total balance (in asset tokens).  This includes the deposits and interest.\n    /// @return The underlying balance of asset tokens\n    function balanceOfToken(address addr) external view override returns (uint256) {\n        return _sharesToToken(balanceOf(addr));\n    }\n\n    /// @notice Calculates the balance of Total idle Tokens Contract hasv\n    /// @return balance of Idle Tokens\n    function _totalShare() internal view returns(uint256) {\n        return IIdleToken(idleToken).balanceOf(address(this));\n    }\n\n    /// @notice Calculates the number of shares that should be mint or burned when a user deposit or withdraw\n    /// @param tokens Amount of tokens\n    /// return Number of shares\n    function _tokenToShares(uint256 tokens) internal view returns (uint256 shares) {\n        shares = (tokens * ONE_IDLE_TOKEN) / _price();\n    }\n\n    /// @notice Calculates the number of tokens a user has in the yield source\n    /// @param shares Amount of shares\n    /// return Number of tokens\n    function _sharesToToken(uint256 shares) internal view returns (uint256 tokens) { \n        tokens = (shares * _price()) / ONE_IDLE_TOKEN;\n    }\n\n    /// @notice Calculates the current price per share\n    /// @return avg idleToken price for this contract\n    function _price() internal view returns (uint256) {\n      return IIdleToken(idleToken).tokenPriceWithFee(address(this));\n    }\n\n    /// @notice Deposit asset tokens to Idle\n    /// @param mintAmount The amount of asset tokens to be deposited\n    /// @return number of minted tokens\n    function _depositToIdle(uint256 mintAmount) internal returns (uint256) {\n        IERC20Upgradeable(underlyingAsset).safeTransferFrom(msg.sender, address(this), mintAmount);\n        return IIdleToken(idleToken).mintIdleToken(mintAmount, false, address(0));\n    }\n\n    /// @notice Allows assets to be supplied on other user's behalf using the `to` param.\n    /// @param mintAmount The amount of `token()` to be supplied\n    /// @param to The user whose balance will receive the tokens\n    function supplyTokenTo(uint256 mintAmount, address to) external nonReentrant override {\n        uint256 mintedTokenShares = _tokenToShares(mintAmount);\n        _depositToIdle(mintAmount);\n        _mint(to, mintedTokenShares);\n        emit SuppliedTokenTo(msg.sender, mintedTokenShares, mintAmount, to);\n    }\n\n    /// @notice Redeems tokens from the yield source from the msg.sender, it burn yield bearing tokens and return token to the sender.\n    /// @param redeemAmount The amount of `token()` to withdraw.  Denominated in `token()` as above.\n    /// @return redeemedUnderlyingAsset The actual amount of tokens that were redeemed.\n    function redeemToken(uint256 redeemAmount) external override nonReentrant returns (uint256 redeemedUnderlyingAsset) {\n        uint256 redeemedShare = _tokenToShares(redeemAmount);\n        _burn(msg.sender, redeemedShare);\n        redeemedUnderlyingAsset = IIdleToken(idleToken).redeemIdleToken(redeemedShare);        \n        IERC20Upgradeable(underlyingAsset).safeTransfer(msg.sender, redeemedUnderlyingAsset);\n        emit RedeemedToken(msg.sender, redeemedShare, redeemAmount);\n    }\n\n    /// @notice Transfer ERC20 tokens other than the idleTokens held by this contract to the recipient address\n    /// @dev This function is only callable by the owner or asset manager\n    /// @param erc20Token The ERC20 token to transfer\n    /// @param to The recipient of the tokens\n    /// @param amount The amount of tokens to transfer\n    function transferERC20(address erc20Token, address to, uint256 amount) external override onlyOwnerOrAssetManager {\n        require(erc20Token != idleToken, \"IdleYieldSource/idleDai-transfer-not-allowed\");\n        IERC20Upgradeable(erc20Token).safeTransfer(to, amount);\n        emit TransferredERC20(msg.sender, to, amount, erc20Token);\n    }\n\n    /// @notice Allows someone to deposit into the yield source without receiving any shares\n    /// @dev This allows anyone to distribute tokens among the share holders\n    /// @param amount The amount of tokens to deposit\n    function sponsor(uint256 amount) external override {\n        _depositToIdle(amount);\n        emit Sponsored(msg.sender, amount);\n    }\n}"
    }
  ]
}