{
  "Title": "[G-03] Use a more recent version of solidity",
  "Content": "\nUse a solidity version of at least 0.8.2 to get compiler automatic inlining\nUse a solidity version of at least 0.8.3 to get better struct packing and cheaper multiple storage reads\nUse a solidity version of at least 0.8.4 to get custom errors, which are cheaper at deployment than `revert()/require()` strings\nUse a solidity version of at least 0.8.10 to have external calls skip contract existence checks if the external call has a return value\n\n1.  File: core-contracts/contracts/ERC721Payable.sol (line [2](https://github.com/code-423n4/2022-03-joyn/blob/c9297ccd925ebb2c44dbc6eaa3effd8db5d2368a/core-contracts/contracts/ERC721Payable.sol#L2))\n\n```solidity\npragma solidity ^0.8.0;\n```\n\n2.  File: core-contracts/contracts/CoreProxy.sol (line [2](https://github.com/code-423n4/2022-03-joyn/blob/c9297ccd925ebb2c44dbc6eaa3effd8db5d2368a/core-contracts/contracts/CoreProxy.sol#L2))\n\n```solidity\npragma solidity ^0.8.0;\n```\n\n3.  File: core-contracts/contracts/ERC721Claimable.sol (line [2](https://github.com/code-423n4/2022-03-joyn/blob/c9297ccd925ebb2c44dbc6eaa3effd8db5d2368a/core-contracts/contracts/ERC721Claimable.sol#L2))\n\n```solidity\npragma solidity ^0.8.0;\n```\n\n4.  File: core-contracts/contracts/CoreCollection.sol (line [2](https://github.com/code-423n4/2022-03-joyn/blob/c9297ccd925ebb2c44dbc6eaa3effd8db5d2368a/core-contracts/contracts/CoreCollection.sol#L2))\n\n```solidity\npragma solidity ^0.8.0;\n```\n\n5.  File: core-contracts/contracts/CoreFactory.sol (line [2](https://github.com/code-423n4/2022-03-joyn/blob/c9297ccd925ebb2c44dbc6eaa3effd8db5d2368a/core-contracts/contracts/CoreFactory.sol#L2))\n\n```solidity\npragma solidity ^0.8.0;\n```\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/contests/2022-03-joyn-contest",
  "Code": [
    {
      "filename": "core-contracts/contracts/ERC721Payable.sol",
      "content": "//SPDX-License-Identifier: Unlicense\npragma solidity ^0.8.0;\nimport {IERC20} from '@openzeppelin/contracts/token/ERC20/IERC20.sol';\n\nabstract contract ERC721Payable {\n  uint256 public mintFee;\n  IERC20 public payableToken;\n  bool public isForSale;\n  address public royaltyVault;\n  address public splitFactory;\n  event NewPayment(\n    address from,\n    address to,\n    uint256 amount,\n    bool royaltyVaultPayment\n  );\n\n  // ---------------- MODIFIER ----------------\n\n  modifier onlyVaultUninitialized() {\n    require(\n      !royaltyVaultInitialized(),\n      'CoreCollection: Royalty Vault already initialized'\n    );\n    _;\n  }\n\n  modifier onlyVaultInitialized() {\n    require(\n      royaltyVaultInitialized(),\n      'CoreCollection: Royalty Vault not initialized'\n    );\n    _;\n  }\n\n  // ---------------- VIEW ----------------\n\n  function royaltyVaultInitialized() public view returns (bool) {\n    return royaltyVault != address(0);\n  }\n\n  // ---------------- INTERNAL ----------------\n\n  /**\n   * @notice Handles the transfer of ERC20 tokens when a token gets minted\n   * @dev Tokens are transferred to the Royalty Vault if the vault is set\n   * Otherwise, tokens get transferred to the ERC721 collection contract\n   * @param _amount The amount of ERC20 to be transferred\n   */\n  function _handlePayment(uint256 _amount) internal {\n    address recipient = royaltyVaultInitialized()\n      ? royaltyVault\n      : address(this);\n    payableToken.transferFrom(msg.sender, recipient, _amount);\n    emit NewPayment(msg.sender, recipient, _amount, royaltyVaultInitialized());\n  }\n}"
    },
    {
      "filename": "core-contracts/contracts/CoreProxy.sol",
      "content": "//SPDX-License-Identifier: Unlicense\r\npragma solidity ^0.8.0;\r\n\r\nimport {Ownable} from \"@openzeppelin/contracts/access/Ownable.sol\";\r\n\r\nimport {ICoreFactory} from \"../interfaces/ICoreFactory.sol\";\r\n\r\ncontract CoreProxy is Ownable {\r\n    address private immutable _implement;\r\n\r\n    constructor(address _imp) {\r\n        _implement = _imp;\r\n    }\r\n\r\n    fallback() external {\r\n        address _impl = implement();\r\n        assembly {\r\n            let ptr := mload(0x40)\r\n            calldatacopy(ptr, 0, calldatasize())\r\n            let result := delegatecall(gas(), _impl, ptr, calldatasize(), 0, 0)\r\n            let size := returndatasize()\r\n            returndatacopy(ptr, 0, size)\r\n\r\n            switch result\r\n            case 0 {\r\n                revert(ptr, size)\r\n            }\r\n            default {\r\n                return(ptr, size)\r\n            }\r\n        }\r\n    }\r\n\r\n    function implement() public view returns (address) {\r\n        return _implement;\r\n    }\r\n}"
    },
    {
      "filename": "core-contracts/contracts/ERC721Claimable.sol",
      "content": "//SPDX-License-Identifier: Unlicense\npragma solidity ^0.8.0;\n\nimport {MerkleProof} from '@openzeppelin/contracts/utils/cryptography/MerkleProof.sol';\n\nabstract contract ERC721Claimable {\n  bytes32 private _merkleRoot;\n  mapping(address => uint256) private _claimedAmount;\n\n  // ---------------- MODIFIER ----------------\n\n  modifier onlyValidRoot(bytes32 root) {\n    require(root != bytes32(0), 'ERC721Claimable: Not valid root');\n    _;\n  }\n\n  modifier onlyClaimableSet() {\n    require(claimableSet(), 'ERC721Claimable: No claimable');\n    _;\n  }\n\n  modifier onlyNotClaimableSet() {\n    require(!claimableSet(), 'ERC721Claimable: Claimable is already set');\n    _;\n  }\n\n  // ---------------- VIEW ----------------\n\n  function claimableSet() public view returns (bool) {\n    return getMerkleRoot() != bytes32(0);\n  }\n\n  function verifyProof(\n    bytes32[] memory proof,\n    bytes32 root,\n    bytes32 leaf\n  ) public pure returns (bool) {\n    return MerkleProof.verify(proof, root, leaf);\n  }\n\n  function processProof(bytes32[] memory proof, bytes32 leaf)\n    public\n    pure\n    returns (bytes32)\n  {\n    return MerkleProof.processProof(proof, leaf);\n  }\n\n  /**\n   * @notice Verifies whether an address can claim tokens\n   * @dev \n   * @param who Claimer address\n   * @param claimableAmount Amount airdropped to claimer\n   * @param claimedAmount Amount of tokens claimer wants to claim\n   * @param merkleProof Proof\n   */\n  function canClaim(\n    address who,\n    uint256 claimableAmount,\n    uint256 claimedAmount,\n    bytes32[] calldata merkleProof\n  ) public view returns (bool) {\n    require(\n      verifyProof(merkleProof, getMerkleRoot(), getNode(who, claimableAmount)),\n      'ERC721Claimable: Invalid proof'\n    );\n\n    return _claimedAmount[who] + claimedAmount <= claimableAmount;\n  }\n\n  function getMerkleRoot() public view returns (bytes32) {\n    return _merkleRoot;\n  }\n\n  // ---------------- INTERNAL ----------------\n\n  function _setMerkelRoot(bytes32 root) internal {\n    _merkleRoot = root;\n  }\n\n  function _claim(address claimer, uint256 claimedAmount) internal {\n    _claimedAmount[claimer] += claimedAmount;\n  }\n\n  // ---------------- PRIVATE ----------------\n\n  /**\n   * @dev get Node hash of given data.\n   * @param who {address} Membership contract address\n   * @param claimableAmount {uint256} token id which claiming person owns\n   * @return {bytes32} node hash\n   */\n  function getNode(address who, uint256 claimableAmount)\n    private\n    pure\n    returns (bytes32)\n  {\n    return keccak256(abi.encodePacked(who, claimableAmount));\n  }\n}"
    },
    {
      "filename": "core-contracts/contracts/CoreCollection.sol",
      "content": "//SPDX-License-Identifier: Unlicense\r\npragma solidity ^0.8.0;\r\n\r\nimport {Ownable} from \"@openzeppelin/contracts/access/Ownable.sol\";\r\nimport {ERC721} from \"@openzeppelin/contracts/token/ERC721/ERC721.sol\";\r\nimport {ERC721Enumerable} from \"@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol\";\r\nimport {IERC20} from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\r\nimport {IERC721} from \"@openzeppelin/contracts/token/ERC721/IERC721.sol\";\r\n\r\nimport {ERC721Payable} from \"./ERC721Payable.sol\";\r\nimport {ERC721Claimable} from \"./ERC721Claimable.sol\";\r\nimport {IRoyaltyVault} from \"@chestrnft/royalty-vault/interfaces/IRoyaltyVault.sol\";\r\n\r\ncontract CoreCollection is\r\n    Ownable,\r\n    ERC721Claimable,\r\n    ERC721Enumerable,\r\n    ERC721Payable\r\n{\r\n    bool public initialized;\r\n    string private _name;\r\n    string private _symbol;\r\n    string private _baseUri;\r\n    uint256 public maxSupply;\r\n    uint256 public startingIndex;\r\n    uint256 public startingIndexBlock;\r\n    string public HASHED_PROOF = \"\";\r\n\r\n    event ClaimInitialized(bytes32 root);\r\n    event NewCollectionMeta(string name, string symbol);\r\n    event NewClaim(address claimedBy, address to, uint256 tokenId);\r\n    event StartingIndexSet(uint256 index);\r\n    event RoyaltyVaultInitialized(address royaltyVault);\r\n    event NewHashedProof(string proof);\r\n    event NewWithdrawal(address to, uint256 amount);\r\n\r\n    constructor() ERC721(\"\", \"\") {}\r\n\r\n    // ----------------- MODIFIER -----------------\r\n\r\n    modifier onlyInitialized() {\r\n        require(initialized, \"CoreCollection: Not initialized\");\r\n        _;\r\n    }\r\n\r\n    modifier onlyUnInitialized() {\r\n        require(!initialized, \"CoreCollection: Already initialized\");\r\n        _;\r\n    }\r\n\r\n    modifier onlyValidSupply(uint256 _maxSupply) {\r\n        require(\r\n            _maxSupply > 0,\r\n            \"CoreCollection: Max supply should be greater than 0\"\r\n        );\r\n        _;\r\n    }\r\n\r\n    modifier tokenExists(uint256 _tokenId) {\r\n        require(_exists(_tokenId), \"CoreCollection: Invalid token id\");\r\n        _;\r\n    }\r\n\r\n    // ----------------- EXTERNAL -----------------\r\n\r\n    /**\r\n     * @notice Initializes the collection\r\n     * @dev This method is being called from the CoreFactory contract\r\n     * @param _collectionName Name of the collection\r\n     * @param _collectionSymbol Symbol of the collection\r\n     * @param _collectionURI Base URI for the collection\r\n     * @param _maxSupply The maximum number of tokens that can be minted\r\n     * @param _mintFee The price of a token in this collection\r\n     * @param _payableToken The address of the ERC20 this collection uses to settle transactions\r\n     * @param _isForSale Whether or not tokens from this collection can be purchased. If false, tokens can only be claimed\r\n     * @param _splitFactory base URI for the collection\r\n     */\r\n    function initialize(\r\n        string memory _collectionName,\r\n        string memory _collectionSymbol,\r\n        string memory _collectionURI,\r\n        uint256 _maxSupply,\r\n        uint256 _mintFee,\r\n        address _payableToken,\r\n        bool _isForSale,\r\n        address _splitFactory\r\n    ) external onlyOwner onlyValidSupply(_maxSupply) {\r\n        _name = _collectionName;\r\n        _symbol = _collectionSymbol;\r\n        _baseUri = _collectionURI;\r\n        maxSupply = _maxSupply;\r\n        mintFee = _mintFee;\r\n        payableToken = IERC20(_payableToken);\r\n        isForSale = _isForSale;\r\n        splitFactory = _splitFactory;\r\n        initialized = true;\r\n    }\r\n\r\n    /**\r\n     * @notice Allows the collection owner to airdrop tokens\r\n     * @dev The Merkle tree defines for each address how much token can be claimed\r\n     * @dev This method can only be called once\r\n     * @param _root A Merkle root\r\n     */\r\n    function initializeClaims(bytes32 _root)\r\n        external\r\n        onlyOwner\r\n        onlyNotClaimableSet\r\n        onlyValidRoot(_root)\r\n    {\r\n        _setMerkelRoot(_root);\r\n        emit ClaimInitialized(_root);\r\n    }\r\n\r\n    /**\r\n     * @notice Allows the collection owner to change the collection's name and symbol\r\n     * @dev This function is only callable by the collection's owner\r\n     * @param _collectionName A collection name\r\n     * @param _collectionSymbol A collection symbol\r\n     */\r\n    function setCollectionMeta(\r\n        string memory _collectionName,\r\n        string memory _collectionSymbol\r\n    ) external onlyOwner {\r\n        _name = _collectionName;\r\n        _symbol = _collectionSymbol;\r\n        emit NewCollectionMeta(_collectionName, _collectionSymbol);\r\n    }\r\n\r\n    /**\r\n     * @notice This function is called to mint tokens from this ERC721 collection\r\n     * @dev The collection must be initialized first\r\n     * @param to Token recipient\r\n     * @param isClaim Whether the user want claim a token that has been airdropped to him or want to purchase the token\r\n     * @param claimableAmount The amount of tokens the user has been airdropped\r\n     * @param amount The amount of tokens the user wants to mint\r\n     * @param merkleProof A merkle proof. Needed to verify if the user can claim a token\r\n     */\r\n    function mintToken(\r\n        address to,\r\n        bool isClaim,\r\n        uint256 claimableAmount,\r\n        uint256 amount,\r\n        bytes32[] calldata merkleProof\r\n    ) external onlyInitialized {\r\n        require(amount > 0, \"CoreCollection: Amount should be greater than 0\");\r\n        require(\r\n            totalSupply() + amount <= maxSupply,\r\n            \"CoreCollection: Over Max Supply\"\r\n        );\r\n\r\n        if (isClaim) {\r\n            require(claimableSet(), \"CoreCollection: No claimable\");\r\n            require(\r\n                canClaim(msg.sender, claimableAmount, amount, merkleProof),\r\n                \"CoreCollection: Can't claim\"\r\n            );\r\n            _claim(msg.sender, amount);\r\n        } else {\r\n            require(isForSale, \"CoreCollection: Not for sale\");\r\n            if (mintFee > 0) {\r\n                _handlePayment(mintFee * amount);\r\n            }\r\n        }\r\n\r\n        batchMint(to, amount, isClaim);\r\n    }\r\n\r\n    /**\r\n     * @notice Allows the contract owner to withdraw the funds generated by the token sales\r\n     * @dev If a royalty vault isn't set, tokens are kept within this contract and can be withdrawn by the token owner\r\n     */\r\n    function withdraw() external onlyOwner {\r\n        uint256 amount = payableToken.balanceOf(address(this));\r\n        payableToken.transferFrom(address(this), msg.sender, amount);\r\n        emit NewWithdrawal(msg.sender, amount);\r\n    }\r\n\r\n    /**\r\n     * @notice Set royalty vault address for collection\r\n     * @dev All revenue (Primary sales + royalties from secondardy sales) \r\n     * from the collection are transferred to the vault when the vault is initialized\r\n     * @param _royaltyVault The address of the royalty vault\r\n     */\r\n    function setRoyaltyVault(address _royaltyVault)\r\n        external\r\n        onlyVaultUninitialized\r\n    {\r\n        require(\r\n            msg.sender == splitFactory || msg.sender == owner(),\r\n            \"CoreCollection: Only Split Factory or owner can initialize vault.\"\r\n        );\r\n        royaltyVault = _royaltyVault;\r\n        emit RoyaltyVaultInitialized(_royaltyVault);\r\n    }\r\n\r\n    /**\r\n     * @notice Set a provenance hash\r\n     * @dev This hash is used to verify the minting ordering of a collection (Ã  la BAYC)\r\n     * This hash is generated off-chain\r\n     * @param _proof The SHA256 generated hash\r\n     */\r\n    function setHashedProof(string calldata _proof) external onlyOwner {\r\n        require(\r\n            bytes(HASHED_PROOF).length == 0,\r\n            \"CoreCollection: Hashed Proof is set\"\r\n        );\r\n\r\n        HASHED_PROOF = _proof;\r\n        emit NewHashedProof(_proof);\r\n    }\r\n\r\n    // ----------------- PUBLIC -----------------\r\n\r\n    /**\r\n     * @notice Set the mint starting index\r\n     * @dev The starting index can only be generated once\r\n     */\r\n    function setStartingIndex() public {\r\n        require(\r\n            startingIndex == 0,\r\n            \"CoreCollection: Starting index is already set\"\r\n        );\r\n\r\n        startingIndex =\r\n            (uint256(\r\n                keccak256(abi.encodePacked(\"CoreCollection\", block.number))\r\n            ) % maxSupply) +\r\n            1;\r\n        startingIndexBlock = uint256(block.number);\r\n        emit StartingIndexSet(startingIndex);\r\n    }\r\n\r\n    // ---------------- VIEW ----------------\r\n\r\n    function name() public view override returns (string memory) {\r\n        return _name;\r\n    }\r\n\r\n    function symbol() public view override returns (string memory) {\r\n        return _symbol;\r\n    }\r\n\r\n    function baseURI() public view returns (string memory) {\r\n        return _baseUri;\r\n    }\r\n\r\n    function _baseURI() internal view override returns (string memory) {\r\n        return _baseUri;\r\n    }\r\n\r\n    // ---------------- PRIVATE ----------------\r\n\r\n    /**\r\n     * @notice Mint token\r\n     * @dev A starting index is calculated at the time of first mint\r\n     * returns a tokenId\r\n     * @param _to Token recipient\r\n     */\r\n    function mint(address _to) private returns (uint256 tokenId) {\r\n        if (startingIndex == 0) {\r\n            setStartingIndex();\r\n        }\r\n        tokenId = ((startingIndex + totalSupply()) % maxSupply) + 1;\r\n        _mint(_to, tokenId);\r\n    }\r\n\r\n    /**\r\n     * @notice Mint tokens in batch\r\n     * @param _to Token recipient\r\n     * @param _amount Number of tokens to include in batch\r\n     * @param _isClaim Whether the batch mint is an airdrop or not\r\n     */\r\n    function batchMint(\r\n        address _to,\r\n        uint256 _amount,\r\n        bool _isClaim\r\n    ) private {\r\n        for (uint256 i = 0; i < _amount; i++) {\r\n            uint256 tokenId = mint(_to);\r\n            if (_isClaim) {\r\n                emit NewClaim(msg.sender, _to, tokenId);\r\n            }\r\n        }\r\n    }\r\n\r\n    // ---------------- INTERNAL ----------------\r\n\r\n    /**\r\n     * @notice This hook transfers tokens sitting in the royalty vault to the split contract\r\n     * @dev The split contract is a contract that allows a team to share revenue together\r\n     * @param _from Transfer sender\r\n     * @param _to Transfer recipient\r\n     * @param _tokenId TokenId of token being transferred\r\n     */\r\n    function _beforeTokenTransfer(\r\n        address _from,\r\n        address _to,\r\n        uint256 _tokenId\r\n    ) internal virtual override {\r\n        super._beforeTokenTransfer(_from, _to, _tokenId);\r\n\r\n        if (\r\n            royaltyVault != address(0) &&\r\n            IRoyaltyVault(royaltyVault).getVaultBalance() > 0\r\n        ) {\r\n            IRoyaltyVault(royaltyVault).sendToSplitter();\r\n        }\r\n    }\r\n}"
    },
    {
      "filename": "core-contracts/contracts/CoreFactory.sol",
      "content": "//SPDX-License-Identifier: Unlicense\r\npragma solidity ^0.8.0;\r\n\r\nimport {CoreProxy} from './CoreProxy.sol';\r\nimport './utils/structs/Collection.sol';\r\nimport {CoreCollection} from './CoreCollection.sol';\r\nimport {ICoreCollection} from '../interfaces/ICoreCollection.sol';\r\n\r\ncontract CoreFactory {\r\n  struct Project {\r\n    string id;\r\n    address creator;\r\n  }\r\n\r\n  event NewProject(string id, address creator);\r\n  event NewCollection(\r\n    string collectionId,\r\n    address collection,\r\n    string projectId\r\n  );\r\n\r\n  address public immutable collection;\r\n  address public immutable splitFactory;\r\n  mapping(string => Project) public projects;\r\n  mapping(string => address) public collections;\r\n\r\n  constructor(address _collection, address _splitFactory) {\r\n    collection = _collection;\r\n    splitFactory = _splitFactory;\r\n  }\r\n\r\n  // ---------------- MODIFIER ----------------\r\n\r\n  modifier onlyAvailableProject(string memory _projectId) {\r\n    require(\r\n      projects[_projectId].creator == address(0),\r\n      'CoreFactory: Unavailable project id'\r\n    );\r\n    _;\r\n  }\r\n\r\n  modifier onlyProjectOwner(string memory _projectId) {\r\n    require(\r\n      projects[_projectId].creator == msg.sender,\r\n      'CoreFactory: Not an owner of the project'\r\n    );\r\n    _;\r\n  }\r\n\r\n  modifier onlyAvailableCollection(string memory _collectionId) {\r\n    require(\r\n      collections[_collectionId] == address(0),\r\n      'CoreFactory: Unavailable collection id'\r\n    );\r\n    _;\r\n  }\r\n\r\n  // ---------------- EXTERNAL ----------------\r\n\r\n  /**\r\n   * @notice Allows to create a project as well as deploy its collection(s)\r\n   * For adding a collection to a project, use the addCollection() method.\r\n   * @dev Projects have unique identifiers.\r\n   * Collections are deployed using a proxy pattern. This is mainly for gas optimization purposes\r\n   * and to support future contract upgrades.\r\n   * Collections ownership are transferred to the caller.\r\n   * @param _projectId Project id which is a unique identifier\r\n   * @param _collections An array of Collection that needs to be deployed\r\n   */\r\n  function createProject(\r\n    string memory _projectId,\r\n    Collection[] memory _collections\r\n  ) external onlyAvailableProject(_projectId) {\r\n    require(\r\n      _collections.length > 0,\r\n      'CoreFactory: should have more at least one collection'\r\n    );\r\n\r\n    for (uint256 i; i < _collections.length; i++) {\r\n      Collection memory _collection = _collections[i];\r\n      address coreCollection = _createCollection(_collection);\r\n\r\n      if (_collection.claimsMerkleRoot != bytes32(0)) {\r\n        ICoreCollection(coreCollection).initializeClaims(\r\n          _collection.claimsMerkleRoot\r\n        );\r\n      }\r\n\r\n      emit NewCollection(_collection.id, coreCollection, _projectId);\r\n\r\n      ICoreCollection(coreCollection).transferOwnership(msg.sender);\r\n    }\r\n    Project memory project;\r\n    project.id = _projectId;\r\n    project.creator = msg.sender;\r\n    projects[_projectId] = project;\r\n\r\n    emit NewProject(_projectId, msg.sender);\r\n  }\r\n\r\n  /**\r\n   * @notice Allows to add a collection to a project\r\n   * @dev Can only be called by project creator\r\n   * Collection's ownership is transferred to the caller\r\n   * @param _projectId Project id which is a unique identifier\r\n   * @param _collection Collection that needs to be deployed\r\n   */\r\n  function addCollection(\r\n    string memory _projectId,\r\n    Collection memory _collection\r\n  ) external onlyProjectOwner(_projectId) returns (address) {\r\n    address coreCollection = _createCollection(_collection);\r\n\r\n    if (_collection.claimsMerkleRoot != bytes32(0)) {\r\n      ICoreCollection(coreCollection).initializeClaims(\r\n        _collection.claimsMerkleRoot\r\n      );\r\n    }\r\n\r\n    emit NewCollection(_collection.id, coreCollection, _projectId);\r\n\r\n    ICoreCollection(coreCollection).transferOwnership(msg.sender);\r\n    return coreCollection;\r\n  }\r\n\r\n  // ---------------- VIEW ----------------\r\n\r\n  function getProject(string memory _projectId)\r\n    external\r\n    view\r\n    returns (Project memory)\r\n  {\r\n    return projects[_projectId];\r\n  }\r\n\r\n  // ---------------- PRIVATE ----------------\r\n\r\n  /**\r\n   * @notice Instanciates/Deploys a collection\r\n   * @param _collection Collection that needs to be deployed\r\n   */\r\n  function _createCollection(Collection memory _collection)\r\n    private\r\n    onlyAvailableCollection(_collection.id)\r\n    returns (address)\r\n  {\r\n    address coreCollection = address(\r\n      new CoreProxy{salt: keccak256(abi.encodePacked(_collection.id))}(\r\n        collection\r\n      )\r\n    );\r\n\r\n    ICoreCollection(coreCollection).initialize(\r\n      _collection.name,\r\n      _collection.symbol,\r\n      _collection.baseURI,\r\n      _collection.maxSupply,\r\n      _collection.mintFee,\r\n      _collection.payableToken,\r\n      _collection.isForSale,\r\n      splitFactory\r\n    );\r\n\r\n    collections[_collection.id] = coreCollection;\r\n    return coreCollection;\r\n  }\r\n}"
    }
  ]
}