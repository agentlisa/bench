{
  "Title": "[G-13]  State variables should be cached in stack variables rather than re-reading them from storage",
  "Content": "The instances below point to the second+ access of a state variable within a function. Caching of a state variable replaces each Gwarmaccess (**100 gas**) with a much cheaper stack read. Other less obvious fixes/optimizations include having local memory caches of state variable structs, or having local caches of state variable contracts/addresses.\n\n*There are 2 instances of this issue:*\n\n```solidity\nFile: src/UniswapOracleFundingRateController.sol\n\n/// @audit pool on line 102 - (valid but excluded finding)\n103:          _lastTwapTick = UniswapHelpers.poolCurrentTick(pool);\n\n/// @audit pool on line 112 - (valid but excluded finding)\n112:          if (pool != address(0) && !UniswapHelpers.poolsHaveSameTokens(pool, _pool)) revert PoolTokensDoNotMatch();\n\n```\nhttps://github.com/with-backed/papr/blob/1933da2e38ff9d47c17e2749d6088bbbd40bfa68/src/UniswapOracleFundingRateController.sol#L103\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/reports/2022-12-backed",
  "Code": [
    {
      "filename": "src/UniswapOracleFundingRateController.sol",
      "content": "//SPDX-License-Identifier: BUSL-1.1\npragma solidity ^0.8.17;\n\nimport {ERC20} from \"solmate/tokens/ERC20.sol\";\nimport {FixedPointMathLib} from \"solmate/utils/FixedPointMathLib.sol\";\nimport {SafeCastLib} from \"solmate/utils/SafeCastLib.sol\";\n\nimport {OracleLibrary} from \"src/libraries/OracleLibrary.sol\";\nimport {UniswapHelpers} from \"src/libraries/UniswapHelpers.sol\";\nimport {\n    IUniswapOracleFundingRateController,\n    IFundingRateController\n} from \"src/interfaces/IUniswapOracleFundingRateController.sol\";\n\ncontract UniswapOracleFundingRateController is IUniswapOracleFundingRateController {\n    /// @inheritdoc IFundingRateController\n    ERC20 public immutable underlying;\n    /// @inheritdoc IFundingRateController\n    ERC20 public immutable papr;\n    /// @inheritdoc IFundingRateController\n    uint256 public fundingPeriod;\n    /// @inheritdoc IUniswapOracleFundingRateController\n    address public pool;\n    /// @dev the max value of target / mark, used as a guard in _multiplier\n    uint256 public immutable targetMarkRatioMax;\n    /// @dev the min value of target / mark, used as a guard in _multiplier\n    uint256 public immutable targetMarkRatioMin;\n    // single slot, write together\n    uint128 internal _target;\n    int56 internal _lastCumulativeTick;\n    uint48 internal _lastUpdated;\n    int24 internal _lastTwapTick;\n\n    constructor(ERC20 _underlying, ERC20 _papr, uint256 _targetMarkRatioMax, uint256 _targetMarkRatioMin) {\n        underlying = _underlying;\n        papr = _papr;\n\n        targetMarkRatioMax = _targetMarkRatioMax;\n        targetMarkRatioMin = _targetMarkRatioMin;\n\n        _setFundingPeriod(4 weeks);\n    }\n\n    /// @inheritdoc IFundingRateController\n    function updateTarget() public override returns (uint256 nTarget) {\n        if (_lastUpdated == block.timestamp) {\n            return _target;\n        }\n\n        (int56 latestCumulativeTick, int24 latestTwapTick) = _latestTwapTickAndTickCumulative();\n        nTarget = _newTarget(latestTwapTick, _target);\n\n        _target = SafeCastLib.safeCastTo128(nTarget);\n        // will not overflow for 8000 years\n        _lastUpdated = uint48(block.timestamp);\n        _lastCumulativeTick = latestCumulativeTick;\n        _lastTwapTick = latestTwapTick;\n\n        emit UpdateTarget(nTarget);\n    }\n\n    /// @inheritdoc IFundingRateController\n    function newTarget() public view override returns (uint256) {\n        if (_lastUpdated == block.timestamp) {\n            return _target;\n        }\n        (, int24 latestTwapTick) = _latestTwapTickAndTickCumulative();\n        return _newTarget(latestTwapTick, _target);\n    }\n\n    /// @inheritdoc IFundingRateController\n    function mark() public view returns (uint256) {\n        if (_lastUpdated == block.timestamp) {\n            return _mark(_lastTwapTick);\n        }\n        (, int24 latestTwapTick) = _latestTwapTickAndTickCumulative();\n        return _mark(latestTwapTick);\n    }\n\n    /// @inheritdoc IFundingRateController\n    function lastUpdated() external view override returns (uint256) {\n        return _lastUpdated;\n    }\n\n    /// @inheritdoc IFundingRateController\n    function target() external view override returns (uint256) {\n        return _target;\n    }\n\n    /// @notice initializes the controller, setting pool and target\n    /// @dev assumes pool is initialized, does not check that pool tokens\n    /// match papr and underlying\n    /// @param target the start value of target\n    /// @param _pool the pool address to use\n    function _init(uint256 target, address _pool) internal {\n        if (_lastUpdated != 0) revert AlreadyInitialized();\n\n        _setPool(_pool);\n\n        _lastUpdated = uint48(block.timestamp);\n        _target = SafeCastLib.safeCastTo128(target);\n        _lastCumulativeTick = OracleLibrary.latestCumulativeTick(pool);\n        _lastTwapTick = UniswapHelpers.poolCurrentTick(pool);\n\n        emit UpdateTarget(target);\n    }\n\n    /// @notice Updates `pool`\n    /// @dev reverts if new pool does not have same token0 and token1 as `pool`\n    /// @dev if pool = address(0), does NOT check that tokens match papr and underlying\n    function _setPool(address _pool) internal {\n        if (pool != address(0) && !UniswapHelpers.poolsHaveSameTokens(pool, _pool)) revert PoolTokensDoNotMatch();\n        if (!UniswapHelpers.isUniswapPool(_pool)) revert InvalidUniswapV3Pool();\n\n        pool = _pool;\n\n        emit SetPool(_pool);\n    }\n\n    /// @notice Updates fundingPeriod\n    /// @dev reverts if period is longer than 90 days or less than 7\n    function _setFundingPeriod(uint256 _fundingPeriod) internal {\n        if (_fundingPeriod < 7 days) revert FundingPeriodTooShort();\n        if (_fundingPeriod > 90 days) revert FundingPeriodTooLong();\n\n        fundingPeriod = _fundingPeriod;\n\n        emit SetFundingPeriod(_fundingPeriod);\n    }\n\n    /// @dev internal function to allow optimized SLOADs\n    function _newTarget(int24 latestTwapTick, uint256 cachedTarget) internal view returns (uint256) {\n        return FixedPointMathLib.mulWadDown(cachedTarget, _multiplier(_mark(latestTwapTick), cachedTarget));\n    }\n\n    /// @dev internal function to allow optimized SLOADs\n    function _mark(int24 twapTick) internal view returns (uint256) {\n        return OracleLibrary.getQuoteAtTick(twapTick, 1e18, address(papr), address(underlying));\n    }\n\n    /// @dev reverts if block.timestamp - _lastUpdated == 0\n    function _latestTwapTickAndTickCumulative() internal view returns (int56 tickCumulative, int24 twapTick) {\n        tickCumulative = OracleLibrary.latestCumulativeTick(pool);\n        twapTick = OracleLibrary.timeWeightedAverageTick(\n            _lastCumulativeTick, tickCumulative, int56(uint56(block.timestamp - _lastUpdated))\n        );\n    }\n\n    /// @notice The multiplier to apply to target() to get newTarget()\n    /// @dev Computes the funding rate for the time since _lastUpdates\n    /// 1 = 1e18, i.e.\n    /// > 1e18 means positive funding rate\n    /// < 1e18 means negative funding rate\n    /// sub 1e18 to get percent change\n    /// @return multiplier used to obtain newTarget()\n    function _multiplier(uint256 _mark_, uint256 cachedTarget) internal view returns (uint256) {\n        uint256 period = block.timestamp - _lastUpdated;\n        uint256 periodRatio = FixedPointMathLib.divWadDown(period, fundingPeriod);\n        uint256 targetMarkRatio;\n        if (_mark_ == 0) {\n            targetMarkRatio = targetMarkRatioMax;\n        } else {\n            targetMarkRatio = FixedPointMathLib.divWadDown(cachedTarget, _mark_);\n            if (targetMarkRatio > targetMarkRatioMax) {\n                targetMarkRatio = targetMarkRatioMax;\n            } else if (targetMarkRatio < targetMarkRatioMin) {\n                targetMarkRatio = targetMarkRatioMin;\n            }\n        }\n\n        // safe to cast because targetMarkRatio > 0\n        return uint256(FixedPointMathLib.powWad(int256(targetMarkRatio), int256(periodRatio)));\n    }\n}"
    }
  ]
}