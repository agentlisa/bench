{
  "Title": "Ambiguous comment in `LibEthUsdOracle::getPercentDifference` NatSpec",
  "Content": "The following comment in the NatSpec of [`LibEthUsdOracle::getPercentDifference`](https://github.com/BeanstalkFarms/Beanstalk/blob/c7a20e56a0a6659c09314a877b440198eff0cd81/protocol/contracts/libraries/Oracle/LibEthUsdOracle.sol#L80-L83) is ambiguous:\n> *Gets the percent difference between two values with 18 decimal precision.*\n\nThis function returns a percentage difference with 18 decimal precision but does not necessarily require that the values it takes as argument should be of 18 decimal precision; they should, however, both be of the same precision, which in this case is 6 decimals. It is recommended to reword the comment to make clearer these intentions and behaviours.",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "protocol/contracts/libraries/Oracle/LibEthUsdOracle.sol",
      "content": "/**\n * SPDX-License-Identifier: MIT\n **/\n\npragma solidity =0.7.6;\npragma experimental ABIEncoderV2;\n\nimport {LibChainlinkOracle} from \"./LibChainlinkOracle.sol\";\nimport {LibUniswapOracle} from \"./LibUniswapOracle.sol\";\nimport {SafeMath} from \"@openzeppelin/contracts/math/SafeMath.sol\";\n\n/**\n * @title Eth Usd Oracle Library\n * @notice Contains functionalty to fetch a manipulation resistant ETH/USD price.\n * @dev\n * The Oracle uses a greey approach to return the average price between the\n * current price returned ETH/USD Chainlink Oracle and either the ETH/USDC\n * Uniswap V3 0.3 fee pool and the ETH/USDT Uniswap V3 0.3 fee pool depending\n * on which is closer. \n \n * The approach is greedy as if the ETH/USDC Uniswap price is sufficiently close\n * to the Chainlink Oracle price (See {MAX_GREEDY_DIFFERENCE}), then the Oracle\n * will not check the ETH/USDT Uniswap Price to save gas.\n * \n * There are several conditions that will cause the oracle to fail:\n * 1. If the price in both Uniswap pools deviate from the Chainlink price \n *    by a sufficiently large percent (See {MAX_DIFFERENCE}).\n * 2. If the Chainlink Oracle is broken or frozen (See: {LibChainlinkOracle}).\n **/\nlibrary LibEthUsdOracle {\n\n    using SafeMath for uint256;\n\n    // The maximum percent different such that it is acceptable to use the greedy approach.\n    uint256 constant MAX_GREEDY_DIFFERENCE = 0.005e18; // 0.5%\n\n    // The maximum percent difference such that the oracle assumes no manipulation is occuring.\n    uint256 constant MAX_DIFFERENCE = 0.02e18; // 2%\n    uint256 constant ONE = 1e18;\n\n    /**\n     * @dev Returns the ETH/USD price.\n     * Return value has 6 decimal precision.\n     * Returns 0 if the Eth Usd Oracle cannot fetch a manipulation resistant price.\n    **/\n    function getEthUsdPrice() internal view returns (uint256) {\n\n        uint256 chainlinkPrice = LibChainlinkOracle.getEthUsdPrice();\n        // Check if the chainlink price is broken or frozen.\n        if (chainlinkPrice == 0) return 0;\n\n        uint256 usdcPrice = LibUniswapOracle.getEthUsdcPrice();\n        uint256 usdcChainlinkPercentDiff = getPercentDifference(usdcPrice, chainlinkPrice);\n\n        // Check if the USDC price and the Chainlink Price are sufficiently close enough\n        // to warrant using the greedy approach.\n        if (usdcChainlinkPercentDiff < MAX_GREEDY_DIFFERENCE) {\n            return chainlinkPrice.add(usdcPrice).div(2);\n        }\n\n        uint256 usdtPrice = LibUniswapOracle.getEthUsdtPrice();\n        uint256 usdtChainlinkPercentDiff = getPercentDifference(usdtPrice, chainlinkPrice);\n\n        // Check whether the USDT or USDC price is closer to the Chainlink price.\n        if (usdtChainlinkPercentDiff < usdcChainlinkPercentDiff) {\n            // Check whether the USDT price is too far from the Chainlink price.\n            if (usdtChainlinkPercentDiff < MAX_DIFFERENCE) {\n                return chainlinkPrice.add(usdtPrice).div(2);\n            }\n            return 0;\n        } else {\n            // Check whether the USDC price is too far from the Chainlink price.\n            if (usdcChainlinkPercentDiff < MAX_DIFFERENCE) {\n                return chainlinkPrice.add(usdcPrice).div(2);\n            }\n            return 0;\n        }\n    }\n\n    /**\n     * Gets the percent difference between two values with 18 decimal precision.\n     */\n    function getPercentDifference(uint x, uint y) internal pure returns (uint256 percentDifference) {\n        percentDifference = x.mul(ONE).div(y);\n        percentDifference = x > y ?\n            percentDifference - ONE :\n            ONE - percentDifference; // SafeMath unnecessary due to conditional check\n    }\n}"
    }
  ]
}