{
  "Title": "[L-02] Non-exploitable reentrancy",
  "Content": "There is no reentrancy guard in this function, and if used with a token that has transfer callbacks, such as an ERC777, the caller can reenter before `balances` is updated. I don't currently see a way to exploit this\n\n*There is 1 instance of this issue:*\n```solidity\nFile: protocol/contracts/tokenomics/AmmGauge.sol   #1\n\n130          uint256 oldBal = IERC20(ammToken).balanceOf(address(this));\n131          IERC20(ammToken).safeTransfer(dst, amount);\n132          uint256 newBal = IERC20(ammToken).balanceOf(address(this));\n133          uint256 unstaked = oldBal - newBal;\n134          balances[msg.sender] -= unstaked;\n135:         totalStaked -= unstaked;\n```\nhttps://github.com/code-423n4/2022-05-backd/blob/2a5664d35cde5b036074edef3c1369b984d10010/protocol/contracts/tokenomics/AmmGauge.sol#L130-L135\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/contests/2022-05-backd-tokenomics-contest",
  "Code": [
    {
      "filename": "protocol/contracts/tokenomics/AmmGauge.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\npragma solidity 0.8.10;\n\nimport \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\n\nimport \"../../interfaces/IController.sol\";\nimport \"../../interfaces/tokenomics/IAmmGauge.sol\";\n\nimport \"../../libraries/ScaledMath.sol\";\nimport \"../../libraries/Errors.sol\";\nimport \"../../libraries/AddressProviderHelpers.sol\";\n\nimport \"../access/Authorization.sol\";\n\ncontract AmmGauge is Authorization, IAmmGauge {\n    using AddressProviderHelpers for IAddressProvider;\n    using ScaledMath for uint256;\n    using SafeERC20 for IERC20;\n\n    IController public immutable controller;\n\n    mapping(address => uint256) public balances;\n\n    // All the data fields required for the staking tracking\n    uint256 public ammStakedIntegral;\n    uint256 public totalStaked;\n    mapping(address => uint256) public perUserStakedIntegral;\n    mapping(address => uint256) public perUserShare;\n\n    address public immutable ammToken;\n    bool public killed;\n    uint48 public ammLastUpdated;\n\n    event RewardClaimed(address indexed account, uint256 amount);\n\n    constructor(IController _controller, address _ammToken)\n        Authorization(_controller.addressProvider().getRoleManager())\n    {\n        ammToken = _ammToken;\n        controller = _controller;\n        ammLastUpdated = uint48(block.timestamp);\n    }\n\n    /**\n     * @notice Shut down the gauge.\n     * @dev Accrued inflation can still be claimed from the gauge after shutdown.\n     * @return `true` if successful.\n     */\n    function kill() external override returns (bool) {\n        require(msg.sender == address(controller.inflationManager()), Error.UNAUTHORIZED_ACCESS);\n        poolCheckpoint();\n        killed = true;\n        return true;\n    }\n\n    function claimRewards(address beneficiary) external virtual override returns (uint256) {\n        require(\n            msg.sender == beneficiary || _roleManager().hasRole(Roles.GAUGE_ZAP, msg.sender),\n            Error.UNAUTHORIZED_ACCESS\n        );\n        _userCheckpoint(beneficiary);\n        uint256 amount = perUserShare[beneficiary];\n        if (amount <= 0) return 0;\n        perUserShare[beneficiary] = 0;\n        controller.inflationManager().mintRewards(beneficiary, amount);\n        emit RewardClaimed(beneficiary, amount);\n        return amount;\n    }\n\n    function stake(uint256 amount) external virtual override returns (bool) {\n        return stakeFor(msg.sender, amount);\n    }\n\n    function unstake(uint256 amount) external virtual override returns (bool) {\n        return unstakeFor(msg.sender, amount);\n    }\n\n    function getAmmToken() external view override returns (address) {\n        return ammToken;\n    }\n\n    function isAmmToken(address token) external view override returns (bool) {\n        return token == ammToken;\n    }\n\n    function claimableRewards(address user) external view virtual override returns (uint256) {\n        uint256 ammStakedIntegral_ = ammStakedIntegral;\n        if (!killed && totalStaked > 0) {\n            ammStakedIntegral_ += (controller.inflationManager().getAmmRateForToken(ammToken) *\n                (block.timestamp - uint256(ammLastUpdated))).scaledDiv(totalStaked);\n        }\n        return\n            perUserShare[user] +\n            balances[user].scaledMul(ammStakedIntegral_ - perUserStakedIntegral[user]);\n    }\n\n    /**\n     * @notice Stake amount of AMM token on behalf of another account.\n     * @param account Account for which tokens will be staked.\n     * @param amount Amount of token to stake.\n     * @return `true` if success.\n     */\n    function stakeFor(address account, uint256 amount) public virtual override returns (bool) {\n        require(amount > 0, Error.INVALID_AMOUNT);\n\n        _userCheckpoint(account);\n\n        uint256 oldBal = IERC20(ammToken).balanceOf(address(this));\n        IERC20(ammToken).safeTransferFrom(msg.sender, address(this), amount);\n        uint256 newBal = IERC20(ammToken).balanceOf(address(this));\n        uint256 staked = newBal - oldBal;\n        balances[account] += staked;\n        totalStaked += staked;\n        emit AmmStaked(account, ammToken, amount);\n        return true;\n    }\n\n    /**\n     * @notice Unstake amount of AMM token and send to another account.\n     * @param dst Account to which unstaked AMM tokens will be sent.\n     * @param amount Amount of token to unstake.\n     * @return `true` if success.\n     */\n    function unstakeFor(address dst, uint256 amount) public virtual override returns (bool) {\n        require(amount > 0, Error.INVALID_AMOUNT);\n        require(balances[msg.sender] >= amount, Error.INSUFFICIENT_BALANCE);\n\n        _userCheckpoint(msg.sender);\n\n        uint256 oldBal = IERC20(ammToken).balanceOf(address(this));\n        IERC20(ammToken).safeTransfer(dst, amount);\n        uint256 newBal = IERC20(ammToken).balanceOf(address(this));\n        uint256 unstaked = oldBal - newBal;\n        balances[msg.sender] -= unstaked;\n        totalStaked -= unstaked;\n        emit AmmUnstaked(msg.sender, ammToken, amount);\n        return true;\n    }\n\n    function poolCheckpoint() public virtual override returns (bool) {\n        if (killed) {\n            return false;\n        }\n        uint256 currentRate = controller.inflationManager().getAmmRateForToken(ammToken);\n        // Update the integral of total token supply for the pool\n        uint256 timeElapsed = block.timestamp - uint256(ammLastUpdated);\n        if (totalStaked > 0) {\n            ammStakedIntegral += (currentRate * timeElapsed).scaledDiv(totalStaked);\n        }\n        ammLastUpdated = uint48(block.timestamp);\n        return true;\n    }\n\n    function _userCheckpoint(address user) internal virtual returns (bool) {\n        poolCheckpoint();\n        perUserShare[user] += balances[user].scaledMul(\n            ammStakedIntegral - perUserStakedIntegral[user]\n        );\n        perUserStakedIntegral[user] = ammStakedIntegral;\n        return true;\n    }\n}"
    }
  ]
}