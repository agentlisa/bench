{
  "Title": "M-3: Front Run of addBlackList() function",
  "Content": "# Issue M-3: Front Run of addBlackList() function \n\nSource: https://github.com/sherlock-audit/2023-02-telcoin-judging/issues/43 \n\n## Found by \nInspex, J4de, 0xAgro, gmx\n\n## Summary\n\n**Front Run of addBlackList() function** \n\n## Vulnerability Detail\n\nFront running can be done either by sending a tx with a higher gas price (usually tx are ordered in a block by the gas price / total fee), or by paying an additional fee to the validator if they manage to run their tx without reverting (i.e. by sending additional ETH to block.coinbase, hoping validator will notice it).\n\n## Impact\n\nMalicious user could listen the mempool in order to check if he sees a tx of blacklisting for his address , if it happens he could front run this tx by sending a tx with higher gas fee to transfer his funds to prevent them to be removed by removeBlackFunds() function\n\n## Code Snippet\n\nhttps://github.com/sherlock-audit/2023-02-telcoin/blob/main/telcoin-audit/contracts/stablecoin/Stablecoin.sol#L159\n\n## Tool used\n\nManual Review\n\n## Recommendation\nUse the same mechanism as in StakingModule.sol to prevent user from withdrawing their funds if blacklisted so that front running won't be useful\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/49",
  "Code": [
    {
      "filename": "telcoin-audit/contracts/stablecoin/Stablecoin.sol",
      "content": "// SPDX-License-Identifier: Unlicensed\npragma solidity ^0.8.6;\n\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/presets/ERC20PresetMinterPauserUpgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/extensions/draft-ERC20PermitUpgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/utils/SafeERC20Upgradeable.sol\";\nimport \"../interfaces/IBlacklist.sol\";\n\n/**\n * @title Stablecoin\n * @author Amir Shirif Telcoin, LLC.\n *\n * @notice This is an ERC20 standard coin with advanced capabilities to allow for\n * minting and burning. This coin is pegged to a fiat currency and its value is\n * intended to reflect the value of its native currency\n * @dev Blacklisting has been included to prevent this currency from being used for illicit or nefarious activities\n */\ncontract Stablecoin is ERC20PresetMinterPauserUpgradeable, ERC20PermitUpgradeable, IBlacklist {\n  using SafeERC20Upgradeable for IERC20Upgradeable;\n\n  event NameUpdated(string indexed name);\n  event SymbolUpdated(string indexed symbol);\n  event DescriptionUpdated(string indexed description);\n\n  error AlreadyBlacklisted(address holder);\n  error NotBlacklisted(address holder);\n\n  mapping (address => bool) private _blacklist;\n\n  bytes32 public constant META_ROLE = keccak256(\"META_ROLE\");\n  bytes32 public constant SUPPORT_ROLE = keccak256(\"SUPPORT_ROLE\");\n  bytes32 public constant BURNER_ROLE = keccak256(\"BURNER_ROLE\");\n  bytes32 public constant BLACKLISTER_ROLE = keccak256(\"BLACKLISTER_ROLE\");\n\n  string private _name;\n  string private _symbol;\n  uint8 private _decimals;\n  string private _description;\n\n  /**\n   * @notice initializes the contract\n   * @dev this function is called with proxy deployment to update state data\n   * @dev uses initializer modifier to only allow one initialization per proxy\n   * @param name_ is a string representing the token name\n   * @param symbol_ is a string representing the token symbol\n   * @param decimal_ is an int representing the number of decimals for the token\n   * @param decimal_ is an int representing the number of decimals for the token\n   */\n  function init(string memory name_, string memory symbol_, uint8 decimal_, string memory description_) external reinitializer(2) {\n    __ERC20PresetMinterPauser_init(name_, symbol_);\n    __ERC20Permit_init(name_);\n    _name = name_;\n    _symbol = symbol_;\n    _decimals = decimal_;\n    _description = description_;\n  }\n\n  /**\n   * @notice Returns the name of the token.\n   */\n  function name() public view override returns (string memory) {\n    return _name;\n  }\n\n  /**\n   * @notice Returns the symbol of the token\n   */\n  function symbol() public view override returns (string memory) {\n    return _symbol;\n  }\n\n  /**\n   * @notice Returns the number of decimal places\n   */\n  function decimals() public view override returns (uint8) {\n    return _decimals;\n  }\n\n  /**\n   * @notice Returns a currency description\n   */\n  function description() public view returns (string memory) {\n    return _description;\n  }\n\n  /**\n   * @notice Updates the _name field to a new string\n   * @dev restricted to META_ROLE\n   * @param name_ is the new _name\n   */\n  function updateName(string memory name_) external onlyRole(META_ROLE) {\n    _name = name_;\n    emit NameUpdated(_name);\n  }\n\n  /**\n   * @notice Updates the _symbol field to a new string\n   * @dev restricted to META_ROLE\n   * @param symbol_ is the new _symbol\n   */\n  function updateSymbol(string memory symbol_) external onlyRole(META_ROLE) {\n    _symbol = symbol_;\n    emit SymbolUpdated(_symbol);\n  }\n\n  /**\n   * @notice Updates the _description field to a new string\n   * @dev restricted to META_ROLE\n   * @param description_ is the new _description\n   */\n  function updateDescription(string memory description_) external onlyRole(META_ROLE) {\n    _description = description_;\n    emit DescriptionUpdated(_description);\n  }\n\n  /**\n   * @notice Removes tokens from circulation from the callers's address\n   * @dev See {ERC20BurnableUpgradeable-burn}.\n   * @dev restricted to BURNER_ROLE\n   * @param amount is the quantity that is to be burned\n   *\n   * Emits a {Transfer} event.\n   */\n  function burn(uint256 amount) public override onlyRole(BURNER_ROLE) {\n    _burn(_msgSender(), amount);\n  }\n\n  /**\n   * @notice Removes tokens from circulation from any address\n   * @dev See {ERC20BurnableUpgradeable-burnFrom}.\n   * @dev restricted to BURNER_ROLE\n   * @param account the address the tokens are being burned from\n   * @param amount the quantity that is to be burned\n   *\n   * Emits a {Transfer} event.\n   */\n  function burnFrom(address account, uint256 amount) public override onlyRole(BURNER_ROLE) {\n    _spendAllowance(account, _msgSender(), amount);\n    _burn(account, amount);\n  }\n\n  /**\n   * @notice Returns a boolean representing that an address is blacklisted\n   * @dev See {IBlacklist-blacklisted}.\n   * @param holder is the address being evaluated\n   * @return bool indicating whether the address is blacklisted\n   */\n  function blacklisted(address holder) public view override returns (bool) {\n    return _blacklist[holder];\n  }\n\n  /**\n   * @notice Adds an address to the mapping of blacklisted addresses\n   * @dev See {IBlacklist-addBlackList}.\n   * @param holder is the address being added to the blacklist\n   *\n   * Emits a {AddedBlacklist} event.\n   */\n  function addBlackList(address holder) external onlyRole(BLACKLISTER_ROLE) override {\n    if (blacklisted(holder)) revert AlreadyBlacklisted(holder);\n    _blacklist[holder] = true;\n    emit AddedBlacklist(holder);\n    removeBlackFunds(holder);\n  }\n\n  /**\n   * @notice Removes an address to the mapping of blacklisted addresses\n   * @dev See {IBlacklist-removeBlackList}.\n   * @param holder is the address being removed from the blacklist\n   *\n   * Emits a {RemovedBlacklist} event.\n   */\n  function removeBlackList(address holder) external onlyRole(BLACKLISTER_ROLE) override {\n    if (!blacklisted(holder)) revert NotBlacklisted(holder);\n    _blacklist[holder] = false;\n    emit RemovedBlacklist(holder);\n  }\n\n  /**\n   * @notice Removes funds from blacklisted address\n   * @param holder is the address having its funds removed\n   */\n  function removeBlackFunds(address holder) internal {\n    uint256 funds = balanceOf(holder);\n    _transfer(holder, _msgSender(), funds);\n  }\n\n  /**\n   * @notice checks if destination has been previously blacklisted\n   * @param destination is the address being checked for status\n   */\n  function _beforeTokenTransfer(address, address destination, uint256) internal view override(ERC20Upgradeable, ERC20PresetMinterPauserUpgradeable) {\n    require(!blacklisted(destination), \"Stablecoin: destination cannot be blacklisted address\");\n  }\n\n  /**\n   * @notice sends tokens accidently sent to contract\n   * @dev restricted to SUPPORT_ROLE\n   * @param token currency stuck in contract\n   * @param destination address where funds are returned\n   * @param amount is the amount being transferred\n   */\n  function erc20Rescue(IERC20Upgradeable token, address destination, uint256 amount) external onlyRole(SUPPORT_ROLE) {\n    token.safeTransfer(destination, amount);\n  }\n}"
    }
  ]
}