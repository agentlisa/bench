{
  "Title": "[L01] Use of transfer function is potentially unsafe [core and sample]",
  "Content": "**Client reported:** *The Ethereum Foundation identified this issue during the audit*\n\n\nThe `withdrawTo` function in `StakeManager` and the `transfer` function in `SimpleWallet` both uses Solidity’s built-in `transfer` function (on [line 129](https://github.com/eth-infinitism/account-abstraction/blob/8832d6e04b9f4f706f612261c6e46b3f1745d61a/contracts/StakeManager.sol#L129) and [line 52](https://github.com/eth-infinitism/account-abstraction/blob/8832d6e04b9f4f706f612261c6e46b3f1745d61a/contracts/samples/SimpleWallet.sol#L52), respectively) to send ether to a destination address. The use of `transfer` for this purpose is [no longer recommended](https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/).\n\n\nConsider using the `call` function or OpenZeppelin’s [sendValue function](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v4.5.0/contracts/utils/Address.sol#L60), and adhere to the checks-effects-interactions pattern when sending value to an external address. This pattern is already implemented in [the `compensate` function](https://github.com/eth-infinitism/account-abstraction/blob/8832d6e04b9f4f706f612261c6e46b3f1745d61a/contracts/EntryPoint.sol#L80) of the `EntryPoint` contract.\n\n\n***Update**: Partially fixed in pull request [#57](https://github.com/eth-infinitism/account-abstraction/pull/57/files). The `SimpleWallet` contract’s `transfer` function was left unchanged.*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/StakeManager.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-only\npragma solidity ^0.8;\n\nimport \"hardhat/console.sol\";\n\n/**\n * manage deposit of sender or paymaster, to pay for gas.\n * paymaster must stake some of the deposit.\n */\ncontract StakeManager {\n\n    /// minimum number of blocks to after 'unlock' before amount can be withdrawn.\n    uint32 immutable public unstakeDelaySec;\n\n    constructor(uint32 _unstakeDelaySec) {\n        unstakeDelaySec = _unstakeDelaySec;\n    }\n\n    event Deposited(\n        address indexed account,\n        uint256 totalDeposit,\n        uint256 unstakeDelaySec\n    );\n\n\n    /// Emitted once a stake is scheduled for withdrawal\n    event DepositUnstaked(\n        address indexed account,\n        uint256 withdrawTime\n    );\n\n    event Withdrawn(\n        address indexed account,\n        address withdrawAddress,\n        uint256 withdrawAmount\n    );\n\n    /// @param amount of ether deposited for this account\n    /// @param unstakeDelaySec - time the deposit is locked, after calling unlock (or zero if deposit is not locked)\n    /// @param withdrawTime - first block timestamp where 'withdrawTo' will be callable, or zero if not locked\n    struct DepositInfo {\n        uint112 amount;\n        uint32 unstakeDelaySec;\n        uint64 withdrawTime;\n    }\n\n    /// maps accounts to their deposits\n    mapping(address => DepositInfo) public deposits;\n\n    function getDepositInfo(address account) external view returns (DepositInfo memory info) {\n        return deposits[account];\n    }\n\n    function balanceOf(address account) public view returns (uint) {\n        return deposits[account].amount;\n    }\n\n    receive() external payable {\n        depositTo(msg.sender);\n    }\n\n    function internalIncrementDeposit(address account, uint amount) internal {\n        deposits[account].amount += uint112(amount);\n    }\n\n    function internalDecrementDeposit(address account, uint amount) internal {\n        deposits[account].amount -= uint112(amount);\n    }\n\n    /**\n     * add to the deposit of the given account\n     */\n    function depositTo(address account) public payable {\n        internalIncrementDeposit(account, msg.value);\n        DepositInfo storage info = deposits[account];\n        emit Deposited(msg.sender, info.amount, info.unstakeDelaySec);\n    }\n\n    /**\n     * stake the account's deposit.\n     * any pending unstakeDeposit is first cancelled.\n     * can also set (or increase) the deposit with call.\n     * @param _unstakeDelaySec the new lock time before the deposit can be withdrawn.\n     */\n    function addStakeTo(address account, uint32 _unstakeDelaySec) public payable {\n        DepositInfo storage info = deposits[account];\n        require(_unstakeDelaySec >= info.unstakeDelaySec, \"cannot decrease unstake time\");\n        uint112 amount = deposits[msg.sender].amount + uint112(msg.value);\n        deposits[account] = DepositInfo(\n            amount,\n            _unstakeDelaySec,\n            0);\n        emit Deposited(account, amount, _unstakeDelaySec);\n    }\n\n    /**\n     * attempt to unstake the deposit.\n     * the value can be withdrawn (using withdrawTo) after the unstake delay.\n     */\n    function unstakeDeposit() external {\n        DepositInfo storage info = deposits[msg.sender];\n        require(info.withdrawTime == 0, \"already unstaking\");\n        require(info.unstakeDelaySec != 0, \"not staked\");\n        uint64 withdrawTime = uint64(block.timestamp) + info.unstakeDelaySec;\n        info.withdrawTime = withdrawTime;\n        emit DepositUnstaked(msg.sender, withdrawTime);\n    }\n\n    /**\n     * withdraw from the deposit.\n     * will fail if the deposit is already staked or too low.\n     * after a paymaster unlocks and withdraws some of the value, it must call addStake() to stake the value again.\n     * @param withdrawAddress the address to send withdrawn value.\n     * @param withdrawAmount the amount to withdraw.\n     */\n    function withdrawTo(address payable withdrawAddress, uint withdrawAmount) external {\n        DepositInfo memory info = deposits[msg.sender];\n        if (info.unstakeDelaySec != 0) {\n            require(info.withdrawTime > 0, \"must call unstakeDeposit() first\");\n            require(info.withdrawTime <= block.timestamp, \"Withdrawal is not due\");\n        }\n        require(withdrawAmount <= info.amount, \"Withdraw amount too large\");\n\n        // store the remaining value, with stake info cleared.\n        deposits[msg.sender] = DepositInfo(\n            info.amount - uint112(withdrawAmount),\n            0,\n            0);\n        withdrawAddress.transfer(withdrawAmount);\n        emit Withdrawn(msg.sender, withdrawAddress, withdrawAmount);\n    }\n\n    /**\n     * check if the given account is staked and didn't unlock it yet.\n     * @param account the account (paymaster) to check\n     * @param requiredStake the minimum deposit\n     * @param requiredDelaySec the minimum required stake time.\n     */\n    function isStaked(address account, uint requiredStake, uint requiredDelaySec) public view returns (bool) {\n        DepositInfo memory info = deposits[account];\n        return info.amount >= requiredStake &&\n        info.unstakeDelaySec >= requiredDelaySec &&\n        info.withdrawTime == 0;\n    }\n}"
    },
    {
      "filename": "contracts/utils/Address.sol",
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)\n\npragma solidity ^0.8.1;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     *\n     * [IMPORTANT]\n     * ====\n     * You shouldn't rely on `isContract` to protect against flash loan attacks!\n     *\n     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets\n     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract\n     * constructor.\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize/address.code.length, which returns 0\n        // for contracts in construction, since the code is only stored at the end\n        // of the constructor execution.\n\n        return account.code.length > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        require(isContract(target), \"Address: call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        require(isContract(target), \"Address: static call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(isContract(target), \"Address: delegate call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the\n     * revert reason using the provided one.\n     *\n     * _Available since v4.3._\n     */\n    function verifyCallResult(\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal pure returns (bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}"
    }
  ]
}