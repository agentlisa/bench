{
  "Title": "M-7: UniswapPriceAdaptor fails after updating impact",
  "Content": "# Issue M-7: UniswapPriceAdaptor fails after updating impact \n\nSource: https://github.com/sherlock-audit/2023-04-jojo-judging/issues/364 \n\n## Found by \nast3ros\n## Summary\n\nThe `impact` variable can have a maximum value of `uint32` (=4.294.967.295) after updating. This is too low and will cause the `UniswapPriceAdaptor#getMarkPrice()` function to revert.\n\n## Vulnerability Detail\n\nWhen initialized, the `impact` variable is a `uint256`. However, in the `updateImpact` function, the newImpact is a `uint32`.\n\n```javascript\n    function updateImpact(uint32 newImpact) external onlyOwner {\n        emit UpdateImpact(impact, newImpact);\n        impact = newImpact;\n    }\n```\n\nThe new `impact` variable will be too small because in the getMarkPrice() function, we need `diff * 1e18 / JOJOPriceFeed <= impact`:\n        \n        require(diff * 1e18 / JOJOPriceFeed <= impact, \"deviation is too big\");\n\nThe result of `diff * 1e18 / JOJOPriceFeed <= impact` is a number with e18 power. It is very likely that it is larger than the `impact` variable which is a `uint32`. The function getMarkPrice() will revert.\n\n## Impact\n\nThe UniswapPriceAdaptor will malfunction and not return the price from Uniswap Oracle.\n\n## Code Snippet\n\nhttps://github.com/sherlock-audit/2023-04-jojo/blob/main/smart-contract-EVM/contracts/adaptor/uniswapPriceAdaptor.sol#L52\nhttps://github.com/sherlock-audit/2023-04-jojo/blob/main/smart-contract-EVM/contracts/adaptor/uniswapPriceAdaptor.sol#L61-L64\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\nChange the newImpact argument from uint32 to uint256.\n\n```diff\n-    function updateImpact(uint32 newImpact) external onlyOwner {\n+    function updateImpact(uint256 newImpact) external onlyOwner {        \n        emit UpdateImpact(impact, newImpact);\n        impact = newImpact;\n    }\n\n```\n\n\n\n## Discussion\n\n**JoscelynFarr**\n\nfix link:\nhttps://github.com/JOJOexchange/smart-contract-EVM/commit/6138d912bdfe7e644b74a182749ab79bb5dc6028\nhttps://github.com/JOJOexchange/JUSDV1/commit/cc6c7518719406923e4678478b7ea0eebfa0b079\n\n**JoscelynFarr**\n\nAnd we also think this is a low issues to update uint32 to uint256\n\n**thangtranth**\n\nEscalate for 10 USDC.\n\nThe impact of this issue is that “The UniswapPriceAdaptor will malfunction and not return the price from Uniswap Oracle”. This breaks the functionality of the protocol as explained above and therefore it definitely qualifies as a medium issue. The simplicity of the fix does not imply a low severity issue.\n\n**sherlock-admin**\n\n > Escalate for 10 USDC.\n> \n> The impact of this issue is that “The UniswapPriceAdaptor will malfunction and not return the price from Uniswap Oracle”. This breaks the functionality of the protocol as explained above and therefore it definitely qualifies as a medium issue. The simplicity of the fix does not imply a low severity issue.\n\nYou've created a valid escalation for 10 USDC!\n\nTo remove the escalation from consideration: Delete your comment.\n\nYou may delete or edit your escalation comment anytime before the 48-hour escalation window closes. After that, the escalation becomes final.\n\n**JoscelynFarr**\n\nOk\n\n**hrishibhat**\n\nResult:\nMedium\nUnique\nThis is a valid medium issue as pointed out by the escalation \n\n**sherlock-admin**\n\nEscalations have been resolved successfully!\n\nEscalation status:\n- [thangtranth](https://github.com/sherlock-audit/2023-04-jojo-judging/issues/364/#issuecomment-1568663166): accepted\n\n**IAm0x52**\n\nFix looks good. Type of newImpact changed from uint32 to uint256\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/70",
  "Code": [
    {
      "filename": "smart-contract-EVM/contracts/adaptor/uniswapPriceAdaptor.sol",
      "content": "/*\n    Copyright 2022 JOJO Exchange\n    SPDX-License-Identifier: BUSL-1.1\n*/\n\npragma solidity 0.8.9;\n\nimport \"@openzeppelin/contracts/utils/math/SafeCast.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"./emergencyOracle.sol\";\nimport \"@mean-finance/uniswap-v3-oracle/solidity/interfaces/IStaticOracle.sol\";\n\n\ncontract UniswapPriceAdaptor is Ownable{\n\n    IStaticOracle public immutable UNISWAP_V3_ORACLE;\n    uint8 public immutable decimal;\n    address public immutable baseToken;\n    address public immutable quoteToken;\n    // query period\n    uint32 public period;\n    address public priceFeedOracle;\n    uint256 public impact;\n\n\n    event UpdatePools(address[] oldPools, address[] newPools);\n    event UpdatePeriod(uint32 oldPeriod, uint32 newPeriod);\n    event UpdateImpact(uint256 oldImpact, uint256 newImpact);\n\n    constructor(\n        address _uniswapAdaptor,\n        uint8 _decimal,\n        address _baseToken,\n        address _quoteToken,\n        uint32 _period,\n        address _priceFeedOracle,\n        uint256 _impact\n    ) {\n        UNISWAP_V3_ORACLE = IStaticOracle(_uniswapAdaptor);\n        decimal = _decimal;\n        baseToken = _baseToken;\n        quoteToken = _quoteToken;\n        period = _period;\n        priceFeedOracle = _priceFeedOracle;\n        impact = _impact;\n    }\n\n    function getMarkPrice() external view returns (uint256) {\n        (uint256 uniswapPriceFeed,) = IStaticOracle(UNISWAP_V3_ORACLE).quoteAllAvailablePoolsWithTimePeriod(uint128(10**decimal), baseToken, quoteToken, period);\n        uint256 JOJOPriceFeed = EmergencyOracle(priceFeedOracle).getMarkPrice();\n        uint256 diff = JOJOPriceFeed >= uniswapPriceFeed ? JOJOPriceFeed - uniswapPriceFeed : uniswapPriceFeed - JOJOPriceFeed;\n        require(diff * 1e18 / JOJOPriceFeed <= impact, \"deviation is too big\");\n        return uniswapPriceFeed;\n    }\n\n    function updatePeriod(uint32 newPeriod) external onlyOwner {\n        emit UpdatePeriod(period, newPeriod);\n        period = newPeriod;\n    }\n\n    function updateImpact(uint32 newImpact) external onlyOwner {\n        emit UpdateImpact(impact, newImpact);\n        impact = newImpact;\n    }\n}"
    },
    {
      "filename": "smart-contract-EVM/contracts/adaptor/uniswapPriceAdaptor.sol",
      "content": "/*\n    Copyright 2022 JOJO Exchange\n    SPDX-License-Identifier: BUSL-1.1\n*/\n\npragma solidity 0.8.9;\n\nimport \"@openzeppelin/contracts/utils/math/SafeCast.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"./emergencyOracle.sol\";\nimport \"@mean-finance/uniswap-v3-oracle/solidity/interfaces/IStaticOracle.sol\";\n\n\ncontract UniswapPriceAdaptor is Ownable{\n\n    IStaticOracle public immutable UNISWAP_V3_ORACLE;\n    uint8 public immutable decimal;\n    address public immutable baseToken;\n    address public immutable quoteToken;\n    // query period\n    uint32 public period;\n    address public priceFeedOracle;\n    uint256 public impact;\n\n\n    event UpdatePools(address[] oldPools, address[] newPools);\n    event UpdatePeriod(uint32 oldPeriod, uint32 newPeriod);\n    event UpdateImpact(uint256 oldImpact, uint256 newImpact);\n\n    constructor(\n        address _uniswapAdaptor,\n        uint8 _decimal,\n        address _baseToken,\n        address _quoteToken,\n        uint32 _period,\n        address _priceFeedOracle,\n        uint256 _impact\n    ) {\n        UNISWAP_V3_ORACLE = IStaticOracle(_uniswapAdaptor);\n        decimal = _decimal;\n        baseToken = _baseToken;\n        quoteToken = _quoteToken;\n        period = _period;\n        priceFeedOracle = _priceFeedOracle;\n        impact = _impact;\n    }\n\n    function getMarkPrice() external view returns (uint256) {\n        (uint256 uniswapPriceFeed,) = IStaticOracle(UNISWAP_V3_ORACLE).quoteAllAvailablePoolsWithTimePeriod(uint128(10**decimal), baseToken, quoteToken, period);\n        uint256 JOJOPriceFeed = EmergencyOracle(priceFeedOracle).getMarkPrice();\n        uint256 diff = JOJOPriceFeed >= uniswapPriceFeed ? JOJOPriceFeed - uniswapPriceFeed : uniswapPriceFeed - JOJOPriceFeed;\n        require(diff * 1e18 / JOJOPriceFeed <= impact, \"deviation is too big\");\n        return uniswapPriceFeed;\n    }\n\n    function updatePeriod(uint32 newPeriod) external onlyOwner {\n        emit UpdatePeriod(period, newPeriod);\n        period = newPeriod;\n    }\n\n    function updateImpact(uint32 newImpact) external onlyOwner {\n        emit UpdateImpact(impact, newImpact);\n        impact = newImpact;\n    }\n}"
    }
  ]
}