{
  "Title": "[N-03] Unused function",
  "Content": "\n[`AddressProviderService::_onlyGov`](https://github.com/code-423n4/2023-10-brahma/blob/main/contracts/src/core/AddressProviderService.sol#L62-L66) is not used anywhere in the code.\n\nConsider removing it as having unused functions can cause confusion and is generally considered bad practice.\n\n**[0xsomeone (judge) commented](https://github.com/code-423n4/2023-10-brahma-findings/issues/424#issuecomment-1782795032):**\n > Decent report with well-elaborated findings. L-01 may be a design decision (i.e. a critical update is needed due to a vulnerability and thus `SubAccount` implementations are forced to update).\n\n**[0xad1onchain (Brahma) acknowledged and commented](https://github.com/code-423n4/2023-10-brahma-findings/issues/424#issuecomment-1783611665):**\n > Thanks for the report. I agree with all Non-critical findings.\n >\n> L-01: SafeModerator and ConsoleFallbackHandler can only be updated by governance and may be done in extreme scenarios. Only registries are kept immutable because they hold state.\n>\n> L-02: While I totally understand your point that attacker deployed the same bytecode at the deterministic `CREATE2` address, for some reason safe changed their safe deployer implementation and removed the initializer as a part of address determination, leading us to question if there is a possible exploit. We chose to keep it safe and make sure we use an address that we deploy.\n\n***\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2023-10-brahma",
  "Code": [
    {
      "filename": "contracts/src/core/AddressProviderService.sol",
      "content": "/// SPDX-License-Identifier: BUSL-1.1\n\n/// Copyright (C) 2023 Brahma.fi\n\npragma solidity 0.8.19;\n\nimport {IAddressProviderService} from \"../../interfaces/IAddressProviderService.sol\";\nimport {AddressProvider} from \"../core/AddressProvider.sol\";\nimport {Constants} from \"./Constants.sol\";\n\n/**\n * @title AddressProviderService\n * @author Brahma.fi\n * @notice Provides a base contract for services to resolve other services through AddressProvider\n * @dev This contract is designed to be inheritable by other contracts\n *  Provides quick and easy access to all contracts in Console Ecosystem\n */\nabstract contract AddressProviderService is IAddressProviderService, Constants {\n    error InvalidAddressProvider();\n    error NotGovernance(address);\n    error InvalidAddress();\n\n    /// @notice address of addressProvider\n    // solhint-disable-next-line immutable-vars-naming\n    AddressProvider public immutable addressProvider;\n\n    constructor(address _addressProvider) {\n        if (_addressProvider == address(0)) revert InvalidAddressProvider();\n        addressProvider = AddressProvider(_addressProvider);\n    }\n\n    /**\n     * @inheritdoc IAddressProviderService\n     */\n    function addressProviderTarget() external view override returns (address) {\n        return address(addressProvider);\n    }\n\n    /**\n     * @notice Helper to get registry address from address provider\n     * @param _key keccak256 key corresponding to registry\n     * @return registry address\n     */\n    function _getRegistry(bytes32 _key) internal view returns (address registry) {\n        registry = addressProvider.getRegistry(_key);\n        _notNull(registry);\n    }\n\n    /**\n     * @notice Helper to get authorized address from address provider\n     * @param _key keccak256 key corresponding to authorized address\n     * @return authorizedAddress\n     */\n    function _getAuthorizedAddress(bytes32 _key) internal view returns (address authorizedAddress) {\n        authorizedAddress = addressProvider.getAuthorizedAddress(_key);\n        _notNull(authorizedAddress);\n    }\n\n    /**\n     * @notice Checks if msg.sender is governance\n     */\n    function _onlyGov() internal view {\n        if (msg.sender != addressProvider.governance()) {\n            revert NotGovernance(msg.sender);\n        }\n    }\n\n    /**\n     * @notice Helper to revert if address is null\n     * @param _addr address to check\n     */\n    function _notNull(address _addr) internal pure {\n        if (_addr == address(0)) revert InvalidAddress();\n    }\n}"
    }
  ]
}