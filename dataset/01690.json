{
  "Title": "M-3: Possible precision loss in `_checkpositionsRange` function",
  "Content": "# Issue M-3: Possible precision loss in `_checkpositionsRange` function \n\nSource: https://github.com/sherlock-audit/2023-06-real-wagmi-judging/issues/82 \n\n## Found by \n0xdice91, BugBusters, ash, ginlee, josephdara, lil.eth, shealtielanz, tsvetanovv, twcctop\n## Summary\nThe `_checkpositionsRange` function contains a potential precision loss issue. The problem arises from the expression ((_range / 2) % _tickSpacing != 0).\n\n## Vulnerability Detail\nThe vulnerability lies in the calculation of `(_range / 2) % _tickSpacing`. The code assumes that _range and _tickSpacing are both int24 variables, representing signed 24-bit integers. However, when  `_range` is 9 and `_tickSpacing` is, for example, 3, the division operation (_range / 2) will truncate the fractional part, resulting in 4 instead of 4.5. Subsequently, the modulo operation (4 % _tickSpacing) will evaluate to 1. This can lead to incorrect comparisons and unexpected behavior.\n\n## Impact\nIt can potentially lead to incorrect validation and can also affect the correctness of the positions range check, potentially allowing invalid positions to be considered valid\n\n## Code Snippet\n```solidity\nfunction _checkpositionsRange(\n        int24 _range,\n        int24 _tickSpacing,\n        int24 tickSpacingOffset\n    ) private pure {\n        ErrLib.requirement(\n            (_range > _tickSpacing) &&\n                (_range % _tickSpacing == 0) &&\n                (((_range / 2) % _tickSpacing != 0) || tickSpacingOffset != 0),\n            ErrLib.ErrorCode.INVALID_POSITIONS_RANGE \n        );\n    }\n```\nhttps://github.com/sherlock-audit/2023-06-real-wagmi/blob/82a234a5c2c1fc1921c63265a9349b71d84675c4/concentrator/contracts/MultiStrategy.sol#L93-L104\n## Tool used\n\nManual Review\n\n## Recommendation\n\n\n\n\n## Discussion\n\n**0xffff11**\n\nSeems like a valid medium to me. Reference from tickSpacing: https://docs.uniswap.org/contracts/v3/reference/core/libraries/Tick\n\n**fann95**\n\nFIXED  https://github.com/RealWagmi/concentrator/blob/fbccf1caf28edbb23db8c7e0409d0c40c3a56461/contracts/MultiStrategy.sol#L98\n\n**0xffff11**\n\n> FIXED https://github.com/RealWagmi/concentrator/blob/fbccf1caf28edbb23db8c7e0409d0c40c3a56461/contracts/MultiStrategy.sol#L98\n\nThis link seems wrong, it does not bring me to a PR, please check\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/88",
  "Code": [
    {
      "filename": "concentrator/contracts/MultiStrategy.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\npragma solidity 0.8.18;\n\nimport { IMultiStrategy } from \"./interfaces/IMultiStrategy.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport { ErrLib } from \"./libraries/ErrLib.sol\";\nimport { IUniswapV3Pool } from \"@uniswap/v3-core/contracts/interfaces/IUniswapV3Pool.sol\";\nimport { IMultipool } from \"./interfaces/IMultipool.sol\";\n\ncontract MultiStrategy is IMultiStrategy, Ownable {\n    event SetNewStrategy(Strategy[] strategy);\n    event SetMultipool(address multipool);\n\n    error InvalidFee(uint24 fee);\n\n    Strategy[] private currentStrategy;\n\n    address public immutable token0;\n    address public immutable token1;\n\n    address public immutable multiFactory;\n    IMultipool public multipool;\n\n    uint256 public constant MAX_WEIGHT_UINT256 = 10000;\n\n    constructor(address _token0, address _token1, address _manager) {\n        token0 = _token0;\n        token1 = _token1;\n        multiFactory = msg.sender;\n        _transferOwnership(_manager);\n    }\n\n    /**\n     * @notice function sets corresponding multipool address\n     * @param _multipool  address of corresponding multipool\n     */\n    function setMultipool(address _multipool) external {\n        ErrLib.requirement(multiFactory == msg.sender, ErrLib.ErrorCode.FORBIDDEN);\n        multipool = IMultipool(_multipool);\n    }\n\n    /// @inheritdoc IMultiStrategy\n    function strategySize() external view returns (uint256) {\n        return currentStrategy.length;\n    }\n\n    /// @inheritdoc IMultiStrategy\n    function getStrategyAt(uint256 index) external view returns (Strategy memory) {\n        return currentStrategy[index];\n    }\n\n    /**\n     * @notice Sets a new strategy for the contract\n     * @dev This function can only be called by the owner of the contract\n     * @param _currentStrategy A list of Strategy structs representing the new strategy\n     */\n    function setStrategy(Strategy[] calldata _currentStrategy) external onlyOwner {\n        delete currentStrategy;\n        uint256 weightSum;\n        uint24 checkSortedFee;\n        for (uint256 i = 0; i < _currentStrategy.length; ) {\n            Strategy memory sPosition = _currentStrategy[i];\n            ErrLib.requirement(\n                checkSortedFee <= sPosition.poolFeeAmt,\n                ErrLib.ErrorCode.SHOULD_BE_SORTED_BY_FEE\n            );\n            checkSortedFee = sPosition.poolFeeAmt;\n            IMultipool.UnderlyingPool memory uPool = multipool.underlyingTrustedPools(\n                sPosition.poolFeeAmt\n            );\n            if (uPool.poolAddress == address(0)) {\n                revert InvalidFee(sPosition.poolFeeAmt);\n            }\n            _checkpositionsRange(\n                sPosition.positionRange,\n                uPool.tickSpacing,\n                sPosition.tickSpacingOffset\n            );\n            ErrLib.requirement(\n                sPosition.tickSpacingOffset % uPool.tickSpacing == 0,\n                ErrLib.ErrorCode.INVALID_TICK_SPACING\n            );\n            weightSum += sPosition.weight;\n            currentStrategy.push(_currentStrategy[i]);\n            unchecked {\n                ++i;\n            }\n        }\n        ErrLib.requirement(weightSum == MAX_WEIGHT_UINT256, ErrLib.ErrorCode.INVALID_WEIGHTS_SUM);\n        emit SetNewStrategy(_currentStrategy);\n    }\n\n    function _checkpositionsRange(\n        int24 _range,\n        int24 _tickSpacing,\n        int24 tickSpacingOffset\n    ) private pure {\n        ErrLib.requirement(\n            (_range > _tickSpacing) &&\n                (_range % _tickSpacing == 0) &&\n                (((_range / 2) % _tickSpacing != 0) || tickSpacingOffset != 0),\n            ErrLib.ErrorCode.INVALID_POSITIONS_RANGE\n        );\n    }\n}"
    }
  ]
}