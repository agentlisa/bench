{
  "Title": "M-4: No check for active Arbitrum Sequencer in WSTETH Oracle",
  "Content": "# Issue M-4: No check for active Arbitrum Sequencer in WSTETH Oracle \n\nSource: https://github.com/sherlock-audit/2022-11-sentiment-judging/issues/3 \n\n## Found by \npashov, obront\n\n## Summary\n\nChainlink recommends that all Optimistic L2 oracles consult the Sequencer Uptime Feed to ensure that the sequencer is live before trusting the data returned by the oracle. This check is implemented in ArbiChainlinkOracle.sol, but is skipped in WSTETHOracle.sol.\n\n## Vulnerability Detail\n\nIf the Arbitrum Sequencer goes down, oracle data will not be kept up to date, and thus could become stale. However, users are able to continue to interact with the protocol directly through the L1 optimistic rollup contract. You can review Chainlink docs on [L2 Sequencer Uptime Feeds](https://docs.chain.link/docs/data-feeds/l2-sequencer-feeds/) for more details on this.\n\nAs a result, users may be able to use the protocol while oracle feeds are stale. This could cause many problems, but as a simple example:\n- A user has an account with 100 tokens, valued at 1 ETH each, and no borrows\n- The Arbitrum sequencer goes down temporarily\n- While it's down, the price of the token falls to 0.5 ETH each\n- The current value of the user's account is 50 ETH, so they should be able to borrow a maximum of 200 ETH to keep account healthy (`(200 + 50) / 200 = 1.2`)\n- Because of the stale price, the protocol lets them borrow 400 ETH (`(400 + 100) / 400 = 1.2`)\n\n## Impact\n\nIf the Arbitrum sequencer goes down, the protocol will allow users to continue to operate at the previous (stale) rates.\n\n## Code Snippet\n\nhttps://github.com/sherlock-audit/2022-11-sentiment/blob/main/oracle-merged/src/wsteth/WSTETHOracle.sol#L45-L57\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\nAdd the same check to WSTETHOracle.sol that exists in ArbiChainlinkOracle.sol:\n\n```solidity\nfunction getPrice(address token) external view override returns (uint) {\n    if (!isSequencerActive()) revert Errors.L2SequencerUnavailable();\n    ...\n}\n```\n\n```solidity\nfunction isSequencerActive() internal view returns (bool) {\n    (, int256 answer, uint256 startedAt,,) = sequencer.latestRoundData();\n    if (block.timestamp - startedAt <= GRACE_PERIOD_TIME || answer == 1)\n        return false;\n    return true;\n}\n```\n\n## Discussion\n\n**r0ohafza**\n\nFix PR: https://github.com/sentimentxyz/oracle/pull/46\n\n**zobront**\n\nPR confirmed.\n\n\n\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/17",
  "Code": [
    {
      "filename": "oracle-merged/src/wsteth/WSTETHOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.17;\n\nimport {Errors} from \"../utils/Errors.sol\";\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {FixedPointMathLib} from \"solmate/utils/FixedPointMathLib.sol\";\nimport {AggregatorV3Interface} from \"../chainlink/AggregatorV3Interface.sol\";\n\n/**\n    @title WSTETH Oracle\n    @notice Oracle that returns price of wsteth\n    arbi:0x5979D7b546E38E414F7E9822514be443A4800529\n*/\ncontract WSTETHOracle is IOracle {\n    using FixedPointMathLib for uint;\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice WSTETH/STETH chainlink price feed\n    AggregatorV3Interface immutable WSTETHFeed;\n\n    /// @notice STETH/USD chainlink price feed\n    AggregatorV3Interface immutable STETHFeed;\n\n    /// @notice ETH USD Chainlink price feed\n    AggregatorV3Interface immutable ethUsdPriceFeed;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    constructor(\n        AggregatorV3Interface _WSTETHFeed,\n        AggregatorV3Interface _STETHFeed,\n        AggregatorV3Interface _ethUsdPriceFeed\n    )\n    {\n        WSTETHFeed = _WSTETHFeed;\n        STETHFeed = _STETHFeed;\n        ethUsdPriceFeed = _ethUsdPriceFeed;\n    }\n\n    /// @inheritdoc IOracle\n    function getPrice(address token) external view returns (uint) {\n        (, int answer,, uint updatedAt,) =\n            WSTETHFeed.latestRoundData();\n\n        if (block.timestamp - updatedAt >= 86400)\n            revert Errors.StalePrice(token, address(WSTETHFeed));\n\n        if (answer <= 0)\n            revert Errors.NegativePrice(token, address(WSTETHFeed));\n\n        return uint(answer).mulWadDown(getSTETHPrice());\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                             INTERNAL FUNCTIONS                             */\n    /* -------------------------------------------------------------------------- */\n\n    function getSTETHPrice() internal view returns (uint) {\n        (, int answer,, uint updatedAt,) =\n            STETHFeed.latestRoundData();\n\n        if (block.timestamp - updatedAt >= 86400)\n            revert Errors.StalePrice(address(0), address(WSTETHFeed));\n\n        if (answer <= 0)\n            revert Errors.NegativePrice(address(0), address(WSTETHFeed));\n\n        return (\n            (uint(answer)*1e18)/getEthPrice()\n        );\n    }\n\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,, uint updatedAt,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (block.timestamp - updatedAt >= 86400)\n            revert Errors.StalePrice(address(0), address(ethUsdPriceFeed));\n\n        if (answer <= 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n}"
    }
  ]
}