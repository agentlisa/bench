{
  "Title": "[G-06] `<array>.length` should not be looked up in every loop of a `for`-loop",
  "Content": "\nEven memory arrays incur the overhead of bit tests and bit shifts to calculate the array length. Storage array length checks incur an extra Gwarmaccess (100 gas) PER-LOOP.\n\n1.  File: contracts/BaseIndex.sol (line [78](https://github.com/code-423n4/2022-04-phuture/blob/594459d0865fb6603ba388b53f3f01648f5bb6fb/contracts/BaseIndex.sol#L78))\n\n```solidity\n        for (uint i; i < _assets.length; ++i) {\n```\n\n2.  File: contracts/ManagedIndexReweightingLogic.sol (line [50](https://github.com/code-423n4/2022-04-phuture/blob/594459d0865fb6603ba388b53f3f01648f5bb6fb/contracts/ManagedIndexReweightingLogic.sol#L50))\n\n```solidity\n        for (uint i; i < _updatedAssets.length; ++i) {\n```\n\n3.  File: contracts/ManagedIndexReweightingLogic.sol (line [96](https://github.com/code-423n4/2022-04-phuture/blob/594459d0865fb6603ba388b53f3f01648f5bb6fb/contracts/ManagedIndexReweightingLogic.sol#L96))\n\n```solidity\n        for (uint i; i < _inactiveAssets.length; ++i) {\n```\n\n4.  File: contracts/ManagedIndex.sol (line [30](https://github.com/code-423n4/2022-04-phuture/blob/594459d0865fb6603ba388b53f3f01648f5bb6fb/contracts/ManagedIndex.sol#L30))\n\n```solidity\n        for (uint i; i < _assets.length; ++i) {\n```\n\n5.  File: contracts/TopNMarketCapIndex.sol (line [48](https://github.com/code-423n4/2022-04-phuture/blob/594459d0865fb6603ba388b53f3f01648f5bb6fb/contracts/TopNMarketCapIndex.sol#L48))\n\n```solidity\n        for (uint i; i < _assets.length; ++i) {\n```\n\n6.  File: contracts/TopNMarketCapReweightingLogic.sol (line [104](https://github.com/code-423n4/2022-04-phuture/blob/594459d0865fb6603ba388b53f3f01648f5bb6fb/contracts/TopNMarketCapReweightingLogic.sol#L104))\n\n```solidity\n        for (uint i; i < _inactiveAssets.length; ++i) {\n```\n\n7.  File: contracts/TrackedIndex.sol (line [35](https://github.com/code-423n4/2022-04-phuture/blob/594459d0865fb6603ba388b53f3f01648f5bb6fb/contracts/TrackedIndex.sol#L35))\n\n```solidity\n        for (uint i; i < _assets.length; ++i) {\n```\n\n",
  "Impact": "GAS",
  "Source": "https://code4rena.com/contests/2022-04-phuture-finance-contest",
  "Code": [
    {
      "filename": "contracts/BaseIndex.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"@openzeppelin/contracts/access/IAccessControl.sol\";\nimport \"@openzeppelin/contracts/security/ReentrancyGuard.sol\";\nimport \"@openzeppelin/contracts/utils/introspection/ERC165Checker.sol\";\n\nimport \"./libraries/IndexLibrary.sol\";\n\nimport \"./interfaces/IIndex.sol\";\nimport \"./interfaces/IIndexLogic.sol\";\nimport \"./interfaces/IIndexFactory.sol\";\nimport \"./interfaces/IPhuturePriceOracle.sol\";\n\nimport \"./PhutureIndex.sol\";\n\n/// @title Base index\n/// @notice Contains common logic for all indices\nabstract contract BaseIndex is PhutureIndex, ReentrancyGuard, IIndex {\n    using ERC165Checker for address;\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Role allows configure index related data/components\n    bytes32 internal constant INDEX_MANAGER_ROLE = keccak256(\"INDEX_MANAGER_ROLE\");\n\n    /// @notice Checks if msg.sender has the given role's permission\n    modifier onlyRole(bytes32 role) {\n        require(IAccessControl(registry).hasRole(role, msg.sender), \"GovernableIndex: FORBIDDEN\");\n        _;\n    }\n\n    constructor(address _factory) {\n        require(_factory.supportsInterface(type(IIndexFactory).interfaceId), \"BaseIndex: INTERFACE\");\n\n        factory = _factory;\n        lastTransferTime = block.timestamp;\n        registry = IIndexFactory(_factory).registry();\n        vTokenFactory = IIndexFactory(_factory).vTokenFactory();\n    }\n\n    /// @inheritdoc IIndex\n    function mint(address _recipient) external override nonReentrant {\n        (bool success, bytes memory data) = IIndexRegistry(registry).indexLogic().delegatecall(\n            abi.encodeWithSelector(IIndexLogic.mint.selector, _recipient)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"BaseIndex: MINT_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n    }\n\n    /// @inheritdoc IIndex\n    function burn(address _recipient) external override nonReentrant {\n        (bool success, bytes memory data) = IIndexRegistry(registry).indexLogic().delegatecall(\n            abi.encodeWithSelector(IIndexLogic.burn.selector, _recipient)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"BaseIndex: BURN_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n    }\n\n    /// @inheritdoc IIndex\n    function anatomy() external view override returns (address[] memory _assets, uint8[] memory _weights) {\n        _assets = assets.values();\n        _weights = new uint8[](_assets.length);\n        for (uint i; i < _assets.length; ++i) {\n            _weights[i] = weightOf[_assets[i]];\n        }\n    }\n\n    /// @inheritdoc IIndex\n    function inactiveAnatomy() external view override returns (address[] memory _assets) {\n        _assets = inactiveAssets.values();\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IIndex).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/ManagedIndexReweightingLogic.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"@openzeppelin/contracts/access/IAccessControl.sol\";\nimport \"@openzeppelin/contracts/utils/introspection/ERC165.sol\";\n\nimport \"./libraries/IndexLibrary.sol\";\n\nimport \"./interfaces/IvToken.sol\";\nimport \"./interfaces/IOrderer.sol\";\nimport \"./interfaces/IvTokenFactory.sol\";\nimport \"./interfaces/IIndexRegistry.sol\";\nimport \"./interfaces/IManagedIndexReweightingLogic.sol\";\n\nimport \"./IndexLayout.sol\";\n\n/// @title ManagedIndex reweighting logic\n/// @notice Contains reweighting logic\ncontract ManagedIndexReweightingLogic is IndexLayout, IManagedIndexReweightingLogic, ERC165 {\n    using FullMath for uint;\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Asset role\n    bytes32 internal constant ASSET_ROLE = keccak256(\"ASSET_ROLE\");\n\n    /// @inheritdoc IManagedIndexReweightingLogic\n    function reweight(address[] calldata _updatedAssets, uint8[] calldata _updatedWeights) external override {\n        require(\n            _updatedAssets.length > 1 &&\n                _updatedWeights.length == _updatedAssets.length &&\n                _updatedAssets.length <= IIndexRegistry(registry).maxComponents(),\n            \"ManagedIndex: INVALID\"\n        );\n\n        uint virtualEvaluationInBase;\n        IPhuturePriceOracle oracle = IPhuturePriceOracle(IIndexRegistry(registry).priceOracle());\n        for (uint i; i < assets.length(); ++i) {\n            uint priceAssetPerBaseInUQ = oracle.refreshedAssetPerBaseInUQ(assets.at(i));\n            uint availableAssets = IvToken(IvTokenFactory(vTokenFactory).createOrReturnVTokenOf(assets.at(i)))\n                .assetBalanceOf(address(this));\n            virtualEvaluationInBase += availableAssets.mulDiv(FixedPoint112.Q112, priceAssetPerBaseInUQ);\n        }\n\n        IOrderer orderer = IOrderer(IIndexRegistry(registry).orderer());\n        uint orderId = orderer.placeOrder();\n\n        uint _totalWeight = IndexLibrary.MAX_WEIGHT;\n\n        for (uint i; i < _updatedAssets.length; ++i) {\n            address asset = _updatedAssets[i];\n            require(asset != address(0), \"ManagedIndex: ZERO\");\n\n            uint8 newWeight = _updatedWeights[i];\n\n            if (i > 0) {\n                // makes sure that there are no duplicate assets\n                require(_updatedAssets[i - 1] < asset, \"ManagedIndex: SORT\");\n            }\n\n            if (newWeight > 0) {\n                require(IAccessControl(registry).hasRole(ASSET_ROLE, asset), \"ManagedIndex: INVALID_ASSET\");\n                assets.add(asset);\n                inactiveAssets.remove(asset);\n\n                uint8 prevWeight = weightOf[asset];\n                if (prevWeight != newWeight) {\n                    emit UpdateAnatomy(asset, newWeight);\n                }\n\n                _totalWeight = _totalWeight + newWeight - prevWeight;\n                weightOf[asset] = newWeight;\n\n                uint amountInBase = (virtualEvaluationInBase * weightOf[asset]) / IndexLibrary.MAX_WEIGHT;\n                uint amountInAsset = amountInBase.mulDiv(oracle.refreshedAssetPerBaseInUQ(asset), FixedPoint112.Q112);\n                (uint newShares, uint oldShares) = IvToken(IvTokenFactory(vTokenFactory).createOrReturnVTokenOf(asset))\n                    .shareChange(address(this), amountInAsset);\n\n                if (newShares > oldShares) {\n                    orderer.addOrderDetails(orderId, asset, newShares - oldShares, IOrderer.OrderSide.Buy);\n                } else if (oldShares > newShares) {\n                    orderer.addOrderDetails(orderId, asset, oldShares - newShares, IOrderer.OrderSide.Sell);\n                }\n            } else {\n                require(assets.remove(asset), \"ManagedIndex: INVALID\");\n                inactiveAssets.add(asset);\n\n                _totalWeight -= weightOf[asset];\n\n                delete weightOf[asset];\n\n                emit UpdateAnatomy(asset, 0);\n            }\n        }\n        address[] memory _inactiveAssets = inactiveAssets.values();\n        for (uint i; i < _inactiveAssets.length; ++i) {\n            uint shares = IvToken(IvTokenFactory(vTokenFactory).vTokenOf(_inactiveAssets[i])).balanceOf(address(this));\n            if (shares > 0) {\n                orderer.addOrderDetails(orderId, _inactiveAssets[i], shares, IOrderer.OrderSide.Sell);\n            } else {\n                inactiveAssets.remove(_inactiveAssets[i]);\n            }\n        }\n        require(_totalWeight == IndexLibrary.MAX_WEIGHT, \"ManagedIndex: MAX\");\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IManagedIndexReweightingLogic).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/ManagedIndexReweightingLogic.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"@openzeppelin/contracts/access/IAccessControl.sol\";\nimport \"@openzeppelin/contracts/utils/introspection/ERC165.sol\";\n\nimport \"./libraries/IndexLibrary.sol\";\n\nimport \"./interfaces/IvToken.sol\";\nimport \"./interfaces/IOrderer.sol\";\nimport \"./interfaces/IvTokenFactory.sol\";\nimport \"./interfaces/IIndexRegistry.sol\";\nimport \"./interfaces/IManagedIndexReweightingLogic.sol\";\n\nimport \"./IndexLayout.sol\";\n\n/// @title ManagedIndex reweighting logic\n/// @notice Contains reweighting logic\ncontract ManagedIndexReweightingLogic is IndexLayout, IManagedIndexReweightingLogic, ERC165 {\n    using FullMath for uint;\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Asset role\n    bytes32 internal constant ASSET_ROLE = keccak256(\"ASSET_ROLE\");\n\n    /// @inheritdoc IManagedIndexReweightingLogic\n    function reweight(address[] calldata _updatedAssets, uint8[] calldata _updatedWeights) external override {\n        require(\n            _updatedAssets.length > 1 &&\n                _updatedWeights.length == _updatedAssets.length &&\n                _updatedAssets.length <= IIndexRegistry(registry).maxComponents(),\n            \"ManagedIndex: INVALID\"\n        );\n\n        uint virtualEvaluationInBase;\n        IPhuturePriceOracle oracle = IPhuturePriceOracle(IIndexRegistry(registry).priceOracle());\n        for (uint i; i < assets.length(); ++i) {\n            uint priceAssetPerBaseInUQ = oracle.refreshedAssetPerBaseInUQ(assets.at(i));\n            uint availableAssets = IvToken(IvTokenFactory(vTokenFactory).createOrReturnVTokenOf(assets.at(i)))\n                .assetBalanceOf(address(this));\n            virtualEvaluationInBase += availableAssets.mulDiv(FixedPoint112.Q112, priceAssetPerBaseInUQ);\n        }\n\n        IOrderer orderer = IOrderer(IIndexRegistry(registry).orderer());\n        uint orderId = orderer.placeOrder();\n\n        uint _totalWeight = IndexLibrary.MAX_WEIGHT;\n\n        for (uint i; i < _updatedAssets.length; ++i) {\n            address asset = _updatedAssets[i];\n            require(asset != address(0), \"ManagedIndex: ZERO\");\n\n            uint8 newWeight = _updatedWeights[i];\n\n            if (i > 0) {\n                // makes sure that there are no duplicate assets\n                require(_updatedAssets[i - 1] < asset, \"ManagedIndex: SORT\");\n            }\n\n            if (newWeight > 0) {\n                require(IAccessControl(registry).hasRole(ASSET_ROLE, asset), \"ManagedIndex: INVALID_ASSET\");\n                assets.add(asset);\n                inactiveAssets.remove(asset);\n\n                uint8 prevWeight = weightOf[asset];\n                if (prevWeight != newWeight) {\n                    emit UpdateAnatomy(asset, newWeight);\n                }\n\n                _totalWeight = _totalWeight + newWeight - prevWeight;\n                weightOf[asset] = newWeight;\n\n                uint amountInBase = (virtualEvaluationInBase * weightOf[asset]) / IndexLibrary.MAX_WEIGHT;\n                uint amountInAsset = amountInBase.mulDiv(oracle.refreshedAssetPerBaseInUQ(asset), FixedPoint112.Q112);\n                (uint newShares, uint oldShares) = IvToken(IvTokenFactory(vTokenFactory).createOrReturnVTokenOf(asset))\n                    .shareChange(address(this), amountInAsset);\n\n                if (newShares > oldShares) {\n                    orderer.addOrderDetails(orderId, asset, newShares - oldShares, IOrderer.OrderSide.Buy);\n                } else if (oldShares > newShares) {\n                    orderer.addOrderDetails(orderId, asset, oldShares - newShares, IOrderer.OrderSide.Sell);\n                }\n            } else {\n                require(assets.remove(asset), \"ManagedIndex: INVALID\");\n                inactiveAssets.add(asset);\n\n                _totalWeight -= weightOf[asset];\n\n                delete weightOf[asset];\n\n                emit UpdateAnatomy(asset, 0);\n            }\n        }\n        address[] memory _inactiveAssets = inactiveAssets.values();\n        for (uint i; i < _inactiveAssets.length; ++i) {\n            uint shares = IvToken(IvTokenFactory(vTokenFactory).vTokenOf(_inactiveAssets[i])).balanceOf(address(this));\n            if (shares > 0) {\n                orderer.addOrderDetails(orderId, _inactiveAssets[i], shares, IOrderer.OrderSide.Sell);\n            } else {\n                inactiveAssets.remove(_inactiveAssets[i]);\n            }\n        }\n        require(_totalWeight == IndexLibrary.MAX_WEIGHT, \"ManagedIndex: MAX\");\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IManagedIndexReweightingLogic).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/ManagedIndex.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"./interfaces/IManagedIndex.sol\";\nimport \"./interfaces/IIndexFactory.sol\";\nimport \"./interfaces/IManagedIndexReweightingLogic.sol\";\n\nimport \"./BaseIndex.sol\";\n\n/// @title Managed index\n/// @notice Contains initialization and reweighting logic\ncontract ManagedIndex is IManagedIndex, BaseIndex {\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Role for index reweighting\n    bytes32 private REWEIGHT_INDEX_ROLE;\n\n    constructor() BaseIndex(msg.sender) {\n        REWEIGHT_INDEX_ROLE = keccak256(abi.encodePacked(\"REWEIGHT_PERMISSION\", address(this)));\n    }\n\n    /// @notice Index initialization with assets and their weights\n    /// @dev Method is called by factory\n    /// @param _assets Assets list for the index\n    /// @param _weights List of assets corresponding weights\n    function initialize(address[] calldata _assets, uint8[] calldata _weights) external {\n        require(msg.sender == factory, \"ManagedIndex: FORBIDDEN\");\n\n        for (uint i; i < _assets.length; ++i) {\n            address asset = _assets[i];\n            uint8 weight = _weights[i];\n\n            weightOf[asset] = weight;\n            assets.add(asset);\n\n            emit UpdateAnatomy(asset, weight);\n        }\n    }\n\n    /// @inheritdoc IManagedIndex\n    /// @dev Assets total weight should be equal to 255\n    function reweight(address[] calldata _updatedAssets, uint8[] calldata _updatedWeights) external override {\n        require(\n            IAccessControl(registry).hasRole(INDEX_MANAGER_ROLE, msg.sender) ||\n                IAccessControl(registry).hasRole(REWEIGHT_INDEX_ROLE, msg.sender),\n            \"ManagedIndex: FORBIDDEN\"\n        );\n        (bool success, bytes memory data) = IIndexFactory(factory).reweightingLogic().delegatecall(\n            abi.encodeWithSelector(IManagedIndexReweightingLogic.reweight.selector, _updatedAssets, _updatedWeights)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"ManagedIndex: REWEIGH_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IManagedIndex).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/TopNMarketCapIndex.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"./interfaces/IIndexFactory.sol\";\nimport \"./interfaces/IReweightableIndex.sol\";\nimport \"./interfaces/ITopNMarketCapIndexReweightingLogic.sol\";\n\nimport \"./BaseIndex.sol\";\n\n/// @title Top N market capitalization index\n/// @notice Contains initialization and reweighting logic\n/// @dev This index reweighs according to the latest data from the TopNMarketCapCategories contract\ncontract TopNMarketCapIndex is IReweightableIndex, BaseIndex {\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Orderer role\n    bytes32 internal constant ORDERER_ROLE = keccak256(\"ORDERER_ROLE\");\n\n    /// @notice Number of assets used in the index\n    uint8 public topN;\n    /// @notice Category identifier used for the given index\n    uint public category;\n\n    /// @notice Latest category snapshot to track diff\n    uint private snapshot;\n\n    constructor() BaseIndex(msg.sender) {}\n\n    /// @notice Initializes index with provided set of parameters\n    /// @param _topN Number of assets used in the index\n    /// @param _category Category identifier used for the given index\n    /// @param _snapshot Initial snapshot from the category\n    /// @param _assets Assets list\n    /// @param _capitalizations List of corresponding assets capitalizations\n    /// @param _totalCapitalization Total capitalization of assets\n    function initialize(\n        uint8 _topN,\n        uint _category,\n        uint _snapshot,\n        address[] calldata _assets,\n        uint[] calldata _capitalizations,\n        uint _totalCapitalization\n    ) external {\n        require(msg.sender == factory, \"TopNMarketCapIndex: FORBIDDEN\");\n\n        uint8 _totalWeight;\n        for (uint i; i < _assets.length; ++i) {\n            uint _i = _assets.length - 1 - i;\n            address asset = _assets[_i];\n            uint8 weight = _i == 0\n                ? IndexLibrary.MAX_WEIGHT - _totalWeight\n                : uint8((_capitalizations[_i] * type(uint8).max) / _totalCapitalization);\n            weightOf[asset] = weight;\n            require(asset != address(0), \"TopNMarketCapIndex: ZERO\");\n            if (weight > 0) {\n                assets.add(asset);\n                _totalWeight += weight;\n                emit UpdateAnatomy(asset, weight);\n            }\n        }\n        snapshot = _snapshot;\n        category = _category;\n        topN = _topN;\n    }\n\n    /// @notice Reweighs index assets according to the latest market cap data for specified category\n    function reweight() external override onlyRole(ORDERER_ROLE) {\n        (bool success, bytes memory data) = IIndexFactory(factory).reweightingLogic().delegatecall(\n            abi.encodeWithSelector(ITopNMarketCapIndexReweightingLogic.reweight.selector, category, snapshot, topN)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"TopNMarketCapIndex: REWEIGH_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n        snapshot = abi.decode(data, (uint));\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IReweightableIndex).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/TopNMarketCapReweightingLogic.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"@openzeppelin/contracts/access/IAccessControl.sol\";\nimport \"@openzeppelin/contracts/utils/introspection/ERC165.sol\";\n\nimport \"./libraries/IndexLibrary.sol\";\n\nimport \"./interfaces/IvToken.sol\";\nimport \"./interfaces/IOrderer.sol\";\nimport \"./interfaces/IvTokenFactory.sol\";\nimport \"./interfaces/IIndexRegistry.sol\";\nimport \"./interfaces/ITopNMarketCapCategories.sol\";\nimport \"./interfaces/ITopNMarketCapIndexFactory.sol\";\nimport \"./interfaces/ITopNMarketCapIndexReweightingLogic.sol\";\n\nimport \"./IndexLayout.sol\";\n\n/// @title TopNMarketCapIndex reweighing logic\n/// @notice Contains reweighting logic\ncontract TopNMarketCapIndexReweightingLogic is IndexLayout, ITopNMarketCapIndexReweightingLogic, ERC165 {\n    using FullMath for uint;\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Asset role\n    bytes32 internal constant ASSET_ROLE = keccak256(\"ASSET_ROLE\");\n\n    /// @inheritdoc ITopNMarketCapIndexReweightingLogic\n    function reweight(\n        uint _category,\n        uint _snapshotId,\n        uint _topN\n    ) external override returns (uint) {\n        IPhuturePriceOracle oracle = IPhuturePriceOracle(IIndexRegistry(registry).priceOracle());\n        uint virtualEvaluationInBase;\n        for (uint i; i < assets.length(); ++i) {\n            uint priceAssetPerBaseInUQ = oracle.refreshedAssetPerBaseInUQ(assets.at(i));\n            uint availableAssets = IvToken(IvTokenFactory(vTokenFactory).createOrReturnVTokenOf(assets.at(i)))\n                .assetBalanceOf(address(this));\n            virtualEvaluationInBase += availableAssets.mulDiv(FixedPoint112.Q112, priceAssetPerBaseInUQ);\n        }\n        ITopNMarketCapCategories.DiffDetails memory diff = ITopNMarketCapCategories(\n            ITopNMarketCapIndexFactory(factory).marketCapCategories()\n        ).assetDiff(_category, _snapshotId, _topN);\n\n        IOrderer orderer = IOrderer(IIndexRegistry(registry).orderer());\n        uint orderId = orderer.placeOrder();\n\n        uint8 _totalWeight;\n        for (uint _i; _i < diff.assetCount; ++_i) {\n            uint i = diff.assetCount - 1 - _i;\n            address asset = diff.assets[i].asset;\n            IvToken vToken = IvToken(IvTokenFactory(vTokenFactory).createOrReturnVTokenOf(asset));\n            if (diff.assets[i].isRemoved) {\n                {\n                    uint shares = vToken.balanceOf(address(this));\n                    if (shares > 0) {\n                        inactiveAssets.add(asset);\n                    }\n                    delete weightOf[asset];\n                    assets.remove(asset);\n                    emit UpdateAnatomy(asset, 0);\n                }\n            } else {\n                // diff guarantees that updated assets go before the removed assets\n                require(IAccessControl(registry).hasRole(ASSET_ROLE, asset), \"TopNMarketCapIndex: INVALID_ASSET\");\n                {\n                    uint8 weight;\n                    if (i == 0) {\n                        weight = IndexLibrary.MAX_WEIGHT - _totalWeight;\n                    } else {\n                        weight = uint8(\n                            (diff.assets[i].capitalizationInBase * type(uint8).max) / diff.totalCapitalizationInBase\n                        );\n                    }\n                    weightOf[asset] = weight;\n                    _totalWeight += weight;\n                    if (weight > 0) {\n                        assets.add(asset);\n                        inactiveAssets.remove(asset);\n                    } else {\n                        assets.remove(asset);\n                        inactiveAssets.add(asset);\n                    }\n                    emit UpdateAnatomy(asset, weight);\n                }\n                {\n                    uint amountInBase = (virtualEvaluationInBase * weightOf[asset]) / IndexLibrary.MAX_WEIGHT;\n                    uint amountInAsset = amountInBase.mulDiv(\n                        oracle.refreshedAssetPerBaseInUQ(asset),\n                        FixedPoint112.Q112\n                    );\n                    (uint newShares, uint oldShares) = vToken.shareChange(address(this), amountInAsset);\n                    if (newShares > oldShares) {\n                        orderer.addOrderDetails(orderId, asset, newShares - oldShares, IOrderer.OrderSide.Buy);\n                    } else if (oldShares > newShares) {\n                        orderer.addOrderDetails(orderId, asset, oldShares - newShares, IOrderer.OrderSide.Sell);\n                    }\n                }\n            }\n        }\n        address[] memory _inactiveAssets = inactiveAssets.values();\n        for (uint i; i < _inactiveAssets.length; ++i) {\n            uint shares = IvToken(IvTokenFactory(vTokenFactory).vTokenOf(_inactiveAssets[i])).balanceOf(address(this));\n            if (shares > 0) {\n                orderer.addOrderDetails(orderId, _inactiveAssets[i], shares, IOrderer.OrderSide.Sell);\n            } else {\n                inactiveAssets.remove(_inactiveAssets[i]);\n            }\n        }\n        return diff.snapshotId;\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return\n            _interfaceId == type(ITopNMarketCapIndexReweightingLogic).interfaceId ||\n            super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/TrackedIndex.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"./interfaces/IIndexFactory.sol\";\nimport \"./interfaces/IReweightableIndex.sol\";\nimport \"./interfaces/ITrackedIndexReweightingLogic.sol\";\n\nimport \"./BaseIndex.sol\";\n\n/// @title Tracked index\n/// @notice  Contains initialization and reweighting logic\ncontract TrackedIndex is IReweightableIndex, BaseIndex {\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Orderer role\n    bytes32 internal constant ORDERER_ROLE = keccak256(\"ORDERER_ROLE\");\n\n    constructor() BaseIndex(msg.sender) {}\n\n    /// @notice Initializes index with provided set of parameters\n    /// @param _assets Assets list\n    /// @param _capitalizations List of corresponding assets capitalizations\n    /// @param _totalCapitalization Total capitalization of assets\n    function initialize(\n        address[] calldata _assets,\n        uint[] calldata _capitalizations,\n        uint _totalCapitalization\n    ) external {\n        require(msg.sender == factory, \"TrackedIndex: FORBIDDEN\");\n\n        uint8 totalWeight;\n        uint maxCapitalization = _capitalizations[0];\n        address maxCapitalizationAsset = _assets[0];\n        for (uint i; i < _assets.length; ++i) {\n            address asset = _assets[i];\n            uint8 weight = uint8((_capitalizations[i] * type(uint8).max) / _totalCapitalization);\n            if (_capitalizations[i] > maxCapitalization) {\n                emit UpdateAnatomy(maxCapitalizationAsset, weightOf[maxCapitalizationAsset]);\n                maxCapitalization = _capitalizations[i];\n                maxCapitalizationAsset = asset;\n            }\n            weightOf[asset] = weight;\n            totalWeight += weight;\n            assets.add(asset);\n            if (asset != maxCapitalizationAsset) {\n                emit UpdateAnatomy(asset, weight);\n            }\n        }\n        if (totalWeight < IndexLibrary.MAX_WEIGHT) {\n            weightOf[maxCapitalizationAsset] += IndexLibrary.MAX_WEIGHT - totalWeight;\n        }\n        emit UpdateAnatomy(maxCapitalizationAsset, weightOf[maxCapitalizationAsset]);\n    }\n\n    /// @notice Reweighs index assets according to the latest market cap data\n    function reweight() external override onlyRole(ORDERER_ROLE) {\n        (bool success, bytes memory data) = IIndexFactory(factory).reweightingLogic().delegatecall(\n            abi.encodeWithSelector(ITrackedIndexReweightingLogic.reweight.selector)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"TrackedIndex: REWEIGH_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IReweightableIndex).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    }
  ]
}