{
  "Title": "[L19] Redundant and fragile ERC20 transfer",
  "Content": "The [`payOracleFeesErc20`](https://github.com/UMAprotocol/protocol/blob/9d403ddb5f2f07194daefe7da51e0e0a6306f2c4/core/contracts/oracle/implementation/Store.sol#L67) function of the `Store` contract allows the caller to deposit ERC20 tokens into the `Store` contract. As the transfer of tokens is [executed via the ERC20 `transferFrom` function](https://github.com/UMAprotocol/protocol/blob/9d403ddb5f2f07194daefe7da51e0e0a6306f2c4/core/contracts/oracle/implementation/Store.sol#L71), the caller must have previously approved the tokens to the `Store` contract. The `payOracleFeesErc20` function will attempt to transfer *all* approved tokens from the caller to the contract, so the amount to be transferred is programmatically [determined by querying the ERC20 `allowance` function](https://github.com/UMAprotocol/protocol/blob/9d403ddb5f2f07194daefe7da51e0e0a6306f2c4/core/contracts/oracle/implementation/Store.sol#L69).\n\n\nThis implementation can be considered suboptimal for a few reasons. Firstly, the approve-then-pull pattern is typically used as a workaround so that contracts can respond to receiving tokens. Since the `payOracleFeesErc20` function does not emit any events or perform any internal actions, the workaround is unnecessary. Secondly, the allowance is not typically considered to be equivalent to a payment. It is common practice to assign a high allowance and allow contracts to retrieve the required amount when necessary. Lastly, there are some ERC20 tokens that use an allowance of `2 ** 256 - 1` as a flag to indicate “infinite” allowance. Two well-known examples include the [DAI token](https://github.com/makerdao/dss/blob/1.0.4/src/dai.sol#L72) and Compound Finance’s [CTokens](https://github.com/compound-finance/compound-protocol/blob/master/contracts/CToken.sol#L68). In such a case, `payOracleFeesErc20` will be unable to retrieve tokens when granted an “infinite” allowance.\n\n\nConsider removing the `payOracleFeesErc20` function in favor of direct ERC20 transfers. Alternatively, consider letting the caller determine how many tokens are to be transferred from their accounts to the `Store` contract.\n\n\n**Update:** *Fixed in [PR#1255](https://github.com/UMAprotocol/protocol/pull/1255). The `payOracleFeesErc20` function explicitly accepts the amount to transfer.*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "core/contracts/oracle/implementation/Store.sol",
      "content": "pragma solidity ^0.6.0;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/math/SafeMath.sol\";\nimport \"../../common/implementation/FixedPoint.sol\";\nimport \"../../common/implementation/MultiRole.sol\";\nimport \"../../common/implementation/Withdrawable.sol\";\nimport \"../interfaces/StoreInterface.sol\";\n\n\n/**\n * @title An implementation of Store that can accept Oracle fees in ETH or any arbitrary ERC20 token.\n */\ncontract Store is StoreInterface, MultiRole, Withdrawable {\n    using SafeMath for uint;\n    using FixedPoint for FixedPoint.Unsigned;\n    using FixedPoint for uint;\n\n    /****************************************\n     *    INTERNAL VARIABLES AND STORAGE    *\n     ****************************************/\n\n    enum Roles { Owner, Withdrawer }\n\n    FixedPoint.Unsigned public fixedOracleFeePerSecond; // Percentage of 1 E.g., .1 is 10% Oracle fee.\n    FixedPoint.Unsigned public weeklyDelayFee; // Percentage of 1 E.g., .1 is 10% weekly delay fee.\n\n    mapping(address => FixedPoint.Unsigned) public finalFees;\n    uint public constant SECONDS_PER_WEEK = 604800;\n\n    /****************************************\n     *                EVENTS                *\n     ****************************************/\n\n    event NewFixedOracleFeePerSecond(FixedPoint.Unsigned newOracleFee);\n\n    /**\n     * @notice Construct the Store contract.\n     */\n    constructor() public {\n        _createExclusiveRole(uint(Roles.Owner), uint(Roles.Owner), msg.sender);\n        createWithdrawRole(uint(Roles.Withdrawer), uint(Roles.Owner), msg.sender);\n    }\n\n    /****************************************\n     *  ORACLE FEE CALCULATION AND PAYMENT  *\n     ****************************************/\n\n    /**\n     * @notice Pays Oracle fees in ETH to the store.\n     * @dev To be used by contracts whose margin currency is ETH.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function payOracleFees() external override payable {\n        require(msg.value > 0);\n    }\n\n    /**\n     * @notice Pays oracle fees in the margin currency, erc20Address, to the store.\n     * @dev To be used if the margin currency is an ERC20 token rather than ETH.\n     * All approved tokens are transferred.\n     * @param erc20Address address of the ERC20 token used to pay the fee.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function payOracleFeesErc20(address erc20Address) external override {\n        IERC20 erc20 = IERC20(erc20Address);\n        uint authorizedAmount = erc20.allowance(msg.sender, address(this));\n        require(authorizedAmount > 0);\n        require(erc20.transferFrom(msg.sender, address(this), authorizedAmount));\n    }\n\n    /**\n     * @notice Computes the regular oracle fees that a contract should pay for a period.\n     * @param startTime defines the beginning time from which the fee is paid.\n     * @param endTime end time until which the fee is paid.\n     * @param pfc \"profit from corruption\", or the maximum amount of margin currency that a\n     * token sponsor could extract from the contract through corrupting the price feed in their favor.\n     * @return regularFee amount owed for the duration from start to end time for the given pfc.\n     * @return latePenalty penalty percentage, if any, for paying the fee after the deadline.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function computeRegularFee(uint startTime, uint endTime, FixedPoint.Unsigned calldata pfc)\n        external\n        override\n        view\n        returns (FixedPoint.Unsigned memory regularFee, FixedPoint.Unsigned memory latePenalty)\n    {\n        uint timeDiff = endTime.sub(startTime);\n\n        // Multiply by the unscaled `timeDiff` first, to get more accurate results.\n        regularFee = pfc.mul(timeDiff).mul(fixedOracleFeePerSecond);\n        // `weeklyDelayFee` is already scaled up.\n        latePenalty = pfc.mul(weeklyDelayFee.mul(timeDiff.div(SECONDS_PER_WEEK)));\n\n        return (regularFee, latePenalty);\n    }\n\n    /**\n     * @notice Computes the final oracle fees that a contract should pay at settlement.\n     * @param currency token used to pay the final fee.\n     * @return finalFee amount due denominated in units of `currency`.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function computeFinalFee(address currency) external override view returns (FixedPoint.Unsigned memory finalFee) {\n        finalFee = finalFees[currency];\n    }\n\n    /****************************************\n     *   ADMIN STATE MODIFYING FUNCTIONS    *\n     ****************************************/\n\n    /**\n     * @notice Sets a new oracle fee per second.\n     * @param newOracleFee new fee per second charged to use the oracle.\n     */\n    function setFixedOracleFeePerSecond(FixedPoint.Unsigned memory newOracleFee)\n        public\n        onlyRoleHolder(uint(Roles.Owner))\n    {\n        // Oracle fees at or over 100% don't make sense.\n        require(newOracleFee.isLessThan(1));\n        fixedOracleFeePerSecond = newOracleFee;\n        emit NewFixedOracleFeePerSecond(newOracleFee);\n    }\n\n    /**\n     * @notice Sets a new weekly delay fee.\n     * @param newWeeklyDelayFee fee escalation per week of late fee payment.\n     */\n    function setWeeklyDelayFee(FixedPoint.Unsigned memory newWeeklyDelayFee) public onlyRoleHolder(uint(Roles.Owner)) {\n        weeklyDelayFee = newWeeklyDelayFee;\n    }\n\n    /**\n     * @notice Sets a new final fee for a particular currency.\n     * @param currency defines the token currency used to pay the final fee.\n     * @param finalFee final fee amount.\n     */\n    function setFinalFee(address currency, FixedPoint.Unsigned memory finalFee)\n        public\n        onlyRoleHolder(uint(Roles.Owner))\n    {\n        finalFees[currency] = finalFee;\n    }\n}"
    },
    {
      "filename": "src/dai.sol",
      "content": "// Copyright (C) 2017, 2018, 2019 dbrock, rain, mrchico\n\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU Affero General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n//\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU Affero General Public License for more details.\n//\n// You should have received a copy of the GNU Affero General Public License\n// along with this program.  If not, see <https://www.gnu.org/licenses/>.\n\npragma solidity ^0.5.12;\n\nimport \"./lib.sol\";\n\ncontract Dai is LibNote {\n    // --- Auth ---\n    mapping (address => uint) public wards;\n    function rely(address guy) external note auth { wards[guy] = 1; }\n    function deny(address guy) external note auth { wards[guy] = 0; }\n    modifier auth {\n        require(wards[msg.sender] == 1, \"Dai/not-authorized\");\n        _;\n    }\n\n    // --- ERC20 Data ---\n    string  public constant name     = \"Dai Stablecoin\";\n    string  public constant symbol   = \"DAI\";\n    string  public constant version  = \"1\";\n    uint8   public constant decimals = 18;\n    uint256 public totalSupply;\n\n    mapping (address => uint)                      public balanceOf;\n    mapping (address => mapping (address => uint)) public allowance;\n    mapping (address => uint)                      public nonces;\n\n    event Approval(address indexed src, address indexed guy, uint wad);\n    event Transfer(address indexed src, address indexed dst, uint wad);\n\n    // --- Math ---\n    function add(uint x, uint y) internal pure returns (uint z) {\n        require((z = x + y) >= x);\n    }\n    function sub(uint x, uint y) internal pure returns (uint z) {\n        require((z = x - y) <= x);\n    }\n\n    // --- EIP712 niceties ---\n    bytes32 public DOMAIN_SEPARATOR;\n    // bytes32 public constant PERMIT_TYPEHASH = keccak256(\"Permit(address holder,address spender,uint256 nonce,uint256 expiry,bool allowed)\");\n    bytes32 public constant PERMIT_TYPEHASH = 0xea2aa0a1be11a07ed86d755c93467f4f82362b452371d1ba94d1715123511acb;\n\n    constructor(uint256 chainId_) public {\n        wards[msg.sender] = 1;\n        DOMAIN_SEPARATOR = keccak256(abi.encode(\n            keccak256(\"EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)\"),\n            keccak256(bytes(name)),\n            keccak256(bytes(version)),\n            chainId_,\n            address(this)\n        ));\n    }\n\n    // --- Token ---\n    function transfer(address dst, uint wad) external returns (bool) {\n        return transferFrom(msg.sender, dst, wad);\n    }\n    function transferFrom(address src, address dst, uint wad)\n        public returns (bool)\n    {\n        require(balanceOf[src] >= wad, \"Dai/insufficient-balance\");\n        if (src != msg.sender && allowance[src][msg.sender] != uint(-1)) {\n            require(allowance[src][msg.sender] >= wad, \"Dai/insufficient-allowance\");\n            allowance[src][msg.sender] = sub(allowance[src][msg.sender], wad);\n        }\n        balanceOf[src] = sub(balanceOf[src], wad);\n        balanceOf[dst] = add(balanceOf[dst], wad);\n        emit Transfer(src, dst, wad);\n        return true;\n    }\n    function mint(address usr, uint wad) external auth {\n        balanceOf[usr] = add(balanceOf[usr], wad);\n        totalSupply    = add(totalSupply, wad);\n        emit Transfer(address(0), usr, wad);\n    }\n    function burn(address usr, uint wad) external {\n        require(balanceOf[usr] >= wad, \"Dai/insufficient-balance\");\n        if (usr != msg.sender && allowance[usr][msg.sender] != uint(-1)) {\n            require(allowance[usr][msg.sender] >= wad, \"Dai/insufficient-allowance\");\n            allowance[usr][msg.sender] = sub(allowance[usr][msg.sender], wad);\n        }\n        balanceOf[usr] = sub(balanceOf[usr], wad);\n        totalSupply    = sub(totalSupply, wad);\n        emit Transfer(usr, address(0), wad);\n    }\n    function approve(address usr, uint wad) external returns (bool) {\n        allowance[msg.sender][usr] = wad;\n        emit Approval(msg.sender, usr, wad);\n        return true;\n    }\n\n    // --- Alias ---\n    function push(address usr, uint wad) external {\n        transferFrom(msg.sender, usr, wad);\n    }\n    function pull(address usr, uint wad) external {\n        transferFrom(usr, msg.sender, wad);\n    }\n    function move(address src, address dst, uint wad) external {\n        transferFrom(src, dst, wad);\n    }\n\n    // --- Approve by signature ---\n    function permit(address holder, address spender, uint256 nonce, uint256 expiry,\n                    bool allowed, uint8 v, bytes32 r, bytes32 s) external\n    {\n        bytes32 digest =\n            keccak256(abi.encodePacked(\n                \"\\x19\\x01\",\n                DOMAIN_SEPARATOR,\n                keccak256(abi.encode(PERMIT_TYPEHASH,\n                                     holder,\n                                     spender,\n                                     nonce,\n                                     expiry,\n                                     allowed))\n        ));\n\n        require(holder != address(0), \"Dai/invalid-address-0\");\n        require(holder == ecrecover(digest, v, r, s), \"Dai/invalid-permit\");\n        require(expiry == 0 || now <= expiry, \"Dai/permit-expired\");\n        require(nonce == nonces[holder]++, \"Dai/invalid-nonce\");\n        uint wad = allowed ? uint(-1) : 0;\n        allowance[holder][spender] = wad;\n        emit Approval(holder, spender, wad);\n    }\n}"
    }
  ]
}