{
  "Title": "[L10] Functions not failing early",
  "Content": "The [`createPerpetual` function of the `PerpetualCreator` contract](https://github.com/UMAprotocol/protocol/blob/1631ef7ad29aaeba756ef3b9a01c667e1343df85/packages/core/contracts/financial-templates/perpetual-multiparty/PerpetualCreator.sol#L74) will run most of its logic before eventually [reverting when the `params.collateralAddress` is not approved](https://github.com/UMAprotocol/protocol/blob/1631ef7ad29aaeba756ef3b9a01c667e1343df85/packages/core/contracts/financial-templates/perpetual-multiparty/PerpetualCreator.sol#L93). Similarly, the [`withdraw` function of the `PerpetualPositionManager` contract](https://github.com/UMAprotocol/protocol/blob/1631ef7ad29aaeba756ef3b9a01c667e1343df85/packages/core/contracts/financial-templates/perpetual-multiparty/PerpetualPositionManager.sol#L217) reverts [when `collateralAmount` is equal to zero](https://github.com/UMAprotocol/protocol/blob/1631ef7ad29aaeba756ef3b9a01c667e1343df85/packages/core/contracts/financial-templates/perpetual-multiparty/PerpetualPositionManager.sol#L226), but only after already getting the `positionData` [using the nontrivial `_getPositionData`](https://github.com/UMAprotocol/protocol/blob/1631ef7ad29aaeba756ef3b9a01c667e1343df85/packages/core/contracts/financial-templates/perpetual-multiparty/PerpetualPositionManager.sol#L225).\n\n\nConsider validating the parameters at the start of the function. This makes the requirements clearer, the helper functions can assume they are getting valid values, and reduces the amount of wasted gas in case of a revert.\n\n\n**Update**: *Partially fixed in [PR#2306](https://github.com/UMAprotocol/protocol/pull/2306). UMAâ€™s statement for the issue:*\n\n\n\n> \n>  This PR moves the required checks to the top of the function to follow the Checks-Effects-Interactions pattern.\n> \n> \n>  Note that the audit recommends checking that an identifier is registered within the `IdentifierWhitelist` to preserve gas within the `PerpetualCreator` in the case of a reverting call. This change was chosen to not be implemented as this function is not called very often (only by perpetual deployers) so reverting calls will be far less common than other public methods. Additionally, to enable the `PerpetualCreator` to be able to verify if an identifier is registered the `PerpetualCreator` would need to import two additional interfaces (`OracleInterfaces` & `IdentifierWhitelistInterface`) as well as implement additional methods and logic to repeat an existing check that occurs within the constructor of the `PerpetualPositionManager`. This additional complexity & code is not warranted to save a small amount of gas for a perpetual deployer.\n> \n> \n> \n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "packages/core/contracts/financial-templates/perpetual-multiparty/PerpetualCreator.sol",
      "content": "// SPDX-License-Identifier: AGPL-3.0-only\npragma solidity ^0.6.0;\npragma experimental ABIEncoderV2;\n\nimport \"../../common/interfaces/ExpandedIERC20.sol\";\nimport \"../../common/interfaces/IERC20Standard.sol\";\nimport \"../../oracle/implementation/ContractCreator.sol\";\nimport \"../../common/implementation/Testable.sol\";\nimport \"../../common/implementation/AddressWhitelist.sol\";\nimport \"../../common/implementation/Lockable.sol\";\nimport \"../common/TokenFactory.sol\";\nimport \"../common/SyntheticToken.sol\";\nimport \"./PerpetualLib.sol\";\nimport \"./ConfigStore.sol\";\n\n/**\n * @title Perpetual Contract creator.\n * @notice Factory contract to create and register new instances of perpetual contracts.\n * Responsible for constraining the parameters used to construct a new perpetual. This creator contains a number of constraints\n * that are applied to newly created  contract. These constraints can evolve over time and are\n * initially constrained to conservative values in this first iteration. Technically there is nothing in the\n * Perpetual contract requiring these constraints. However, because `createPerpetual()` is intended\n * to be the only way to create valid financial contracts that are registered with the DVM (via _registerContract),\n  we can enforce deployment configurations here.\n */\ncontract PerpetualCreator is ContractCreator, Testable, Lockable {\n    using FixedPoint for FixedPoint.Unsigned;\n\n    /****************************************\n     *     PERP CREATOR DATA STRUCTURES      *\n     ****************************************/\n\n    // Immutable params for perpetual contract.\n    struct Params {\n        address collateralAddress;\n        bytes32 priceFeedIdentifier;\n        bytes32 fundingRateIdentifier;\n        string syntheticName;\n        string syntheticSymbol;\n        FixedPoint.Unsigned collateralRequirement;\n        FixedPoint.Unsigned disputeBondPct;\n        FixedPoint.Unsigned sponsorDisputeRewardPct;\n        FixedPoint.Unsigned disputerDisputeRewardPct;\n        FixedPoint.Unsigned minSponsorTokens;\n        FixedPoint.Unsigned tokenScaling;\n        uint256 withdrawalLiveness;\n        uint256 liquidationLiveness;\n    }\n    // Address of TokenFactory used to create a new synthetic token.\n    address public tokenFactoryAddress;\n\n    event CreatedPerpetual(address indexed perpetualAddress, address indexed deployerAddress);\n    event CreatedConfigStore(address indexed configStoreAddress, address indexed ownerAddress);\n\n    /**\n     * @notice Constructs the Perpetual contract.\n     * @param _finderAddress UMA protocol Finder used to discover other protocol contracts.\n     * @param _tokenFactoryAddress ERC20 token factory used to deploy synthetic token instances.\n     * @param _timerAddress Contract that stores the current time in a testing environment.\n     */\n    constructor(\n        address _finderAddress,\n        address _tokenFactoryAddress,\n        address _timerAddress\n    ) public ContractCreator(_finderAddress) Testable(_timerAddress) nonReentrant() {\n        tokenFactoryAddress = _tokenFactoryAddress;\n    }\n\n    /**\n     * @notice Creates an instance of perpetual and registers it within the registry.\n     * @param params is a `ConstructorParams` object from Perpetual.\n     * @return address of the deployed contract.\n     */\n    function createPerpetual(Params memory params, ConfigStore.ConfigSettings memory configSettings)\n        public\n        nonReentrant()\n        returns (address)\n    {\n        // Create new config settings store for this contract and reset ownership to the deployer.\n        ConfigStore configStore = new ConfigStore(configSettings, timerAddress);\n        configStore.transferOwnership(msg.sender);\n        CreatedConfigStore(address(configStore), configStore.owner());\n\n        // Create a new synthetic token using the params.\n        require(bytes(params.syntheticName).length != 0, \"Missing synthetic name\");\n        require(bytes(params.syntheticSymbol).length != 0, \"Missing synthetic symbol\");\n        TokenFactory tf = TokenFactory(tokenFactoryAddress);\n\n        // If the collateral token does not have a `decimals()` method,\n        // then a default precision of 18 will be applied to the newly created synthetic token.\n        uint8 syntheticDecimals = _getSyntheticDecimals(params.collateralAddress);\n        ExpandedIERC20 tokenCurrency = tf.createToken(params.syntheticName, params.syntheticSymbol, syntheticDecimals);\n        address derivative = PerpetualLib.deploy(_convertParams(params, tokenCurrency, address(configStore)));\n\n        // Give permissions to new derivative contract and then hand over ownership.\n        tokenCurrency.addMinter(derivative);\n        tokenCurrency.addBurner(derivative);\n        tokenCurrency.resetOwner(derivative);\n\n        _registerContract(new address[](0), address(derivative));\n\n        emit CreatedPerpetual(address(derivative), msg.sender);\n\n        return address(derivative);\n    }\n\n    /****************************************\n     *          PRIVATE FUNCTIONS           *\n     ****************************************/\n\n    // Converts createPerpetual params to Perpetual constructor params.\n    function _convertParams(\n        Params memory params,\n        ExpandedIERC20 newTokenCurrency,\n        address configStore\n    ) private view returns (Perpetual.ConstructorParams memory constructorParams) {\n        // Known from creator deployment.\n        constructorParams.finderAddress = finderAddress;\n        constructorParams.timerAddress = timerAddress;\n\n        // Enforce configuration constraints.\n        require(params.withdrawalLiveness != 0, \"Withdrawal liveness cannot be 0\");\n        require(params.liquidationLiveness != 0, \"Liquidation liveness cannot be 0\");\n        _requireWhitelistedCollateral(params.collateralAddress);\n\n        // We don't want perpetual deployers to be able to intentionally or unintentionally set\n        // liveness periods that could induce arithmetic overflow, but we also don't want\n        // to be opinionated about what livenesses are \"correct\", so we will somewhat\n        // arbitrarily set the liveness upper bound to 100 years (5200 weeks). In practice, liveness\n        // periods even greater than a few days would make the perpetual unusable for most users.\n        require(params.withdrawalLiveness < 5200 weeks, \"Withdrawal liveness too large\");\n        require(params.liquidationLiveness < 5200 weeks, \"Liquidation liveness too large\");\n\n        // To avoid precision loss or overflows, prevent the token scaling from being too large or too small.\n        FixedPoint.Unsigned memory minScaling = FixedPoint.Unsigned(1e8); // 1e-10\n        FixedPoint.Unsigned memory maxScaling = FixedPoint.Unsigned(1e28); // 1e10\n        require(\n            params.tokenScaling.isGreaterThan(minScaling) && params.tokenScaling.isLessThan(maxScaling),\n            \"Invalid tokenScaling\"\n        );\n\n        // Input from function call.\n        constructorParams.configStoreAddress = configStore;\n        constructorParams.tokenAddress = address(newTokenCurrency);\n        constructorParams.collateralAddress = params.collateralAddress;\n        constructorParams.priceFeedIdentifier = params.priceFeedIdentifier;\n        constructorParams.fundingRateIdentifier = params.fundingRateIdentifier;\n        constructorParams.collateralRequirement = params.collateralRequirement;\n        constructorParams.disputeBondPct = params.disputeBondPct;\n        constructorParams.sponsorDisputeRewardPct = params.sponsorDisputeRewardPct;\n        constructorParams.disputerDisputeRewardPct = params.disputerDisputeRewardPct;\n        constructorParams.minSponsorTokens = params.minSponsorTokens;\n        constructorParams.withdrawalLiveness = params.withdrawalLiveness;\n        constructorParams.liquidationLiveness = params.liquidationLiveness;\n        constructorParams.tokenScaling = params.tokenScaling;\n    }\n\n    // IERC20Standard.decimals() will revert if the collateral contract has not implemented the decimals() method,\n    // which is possible since the method is only an OPTIONAL method in the ERC20 standard:\n    // https://eips.ethereum.org/EIPS/eip-20#methods.\n    function _getSyntheticDecimals(address _collateralAddress) public view returns (uint8 decimals) {\n        try IERC20Standard(_collateralAddress).decimals() returns (uint8 _decimals) {\n            return _decimals;\n        } catch {\n            return 18;\n        }\n    }\n}"
    }
  ]
}