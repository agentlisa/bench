{
  "Title": "[10] The `_pendingOwner` can be reset via `_renounceOwnership()`",
  "Content": "\nAssume `transferOwnership()` has been called and the `_pendingOwner` has been set. The function `_renounceOwnership()` can now be used to reset the `_pendingOwner`, which means that the next `acceptOwnership()` will fail. \n\n*Note: After the timeout period of  `_renounceOwnership()`, the original situation is restored, so `_renounceOwnership()` has to be done twice to renounce ownership.*\n\n*Note: Calling `transferOwnership()` again will also update the `_pendingOwner`.*\n\nThis might be an unexpected situation.\n\n### Proof of Concept\n[LSP14Ownable2Step.sol#L106C2-L179](https://github.com/lukso-network/lsp-smart-contracts/blob/v0.10.2/contracts/LSP14Ownable2Step/LSP14Ownable2Step.sol#L106C2-L179)\n```solidity\nabstract contract LSP14Ownable2Step is ... {\n\n    function _renounceOwnership() ...  {\n        ...\n        if (currentBlock > confirmationPeriodEnd || _renounceOwnershipStartedAt == 0 ) {\n            _renounceOwnershipStartedAt = currentBlock;\n            delete _pendingOwner;\n            ...\n            return;\n        }\n        ...\n    }\n}\n```\n\n### Recommended Mitigation Steps\nDouble check if this is the intended behaviour.\nConsider to notify the `UniversalReceiver` of the fact that `OwnershipTransferStarted` is no longer relevant.\n\n\n**[CJ42 (LUKSO) confirmed and commented](https://github.com/code-423n4/2023-06-lukso-findings/issues/15#issuecomment-1655261363):**\n > Ok, we confirm. Report is of great quality.<br>\n> However, we might have to go through the points individually. Some of them can be implemented and fixed, other might have to be discussed if we make the changes or not (e.g: nb 8).\n\n**[Trust (judge) commented](https://github.com/code-423n4/2023-06-lukso-findings/issues/15#issuecomment-1663409063):**\n > 01 - Non-Critical<br>\n> 02 - Non-Critical<br>\n> 03 - Low<br>\n> 04 - Low +<br>\n> 05 - Non-Critical<br>\n> 06 - Non-Critical<br>\n> 07 - Low<br>\n> 08 - Low<br>\n> 09 - Low<br>\n> 10 - Low +\n\n***\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2023-06-lukso",
  "Code": [
    {
      "filename": "contracts/LSP14Ownable2Step/LSP14Ownable2Step.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.4;\n\n// interfaces\nimport {ILSP14Ownable2Step} from \"./ILSP14Ownable2Step.sol\";\n\n// modules\nimport {\n    OwnableUnset\n} from \"@erc725/smart-contracts/contracts/custom/OwnableUnset.sol\";\n\n// libraries\nimport {LSP1Utils} from \"../LSP1UniversalReceiver/LSP1Utils.sol\";\n\n// errors\nimport \"./LSP14Errors.sol\";\n\n// constants\nimport {\n    _TYPEID_LSP14_OwnershipTransferStarted,\n    _TYPEID_LSP14_OwnershipTransferred_SenderNotification,\n    _TYPEID_LSP14_OwnershipTransferred_RecipientNotification\n} from \"./LSP14Constants.sol\";\n\n/**\n * @title LSP14Ownable2Step\n * @author Fabian Vogelsteller <fabian@lukso.network>, Jean Cavallera (CJ42), Yamen Merhi (YamenMerhi), Daniel Afteni (B00ste)\n * @dev This contract is a modified version of the OwnableUnset implementation, where transferring and renouncing ownership\n *      works as a 2 steps process. This can be used as a confirmation mechanism to prevent potential mistakes when\n *      transferring ownership of the contract, where the control of the contract could be lost forever.\n */\nabstract contract LSP14Ownable2Step is ILSP14Ownable2Step, OwnableUnset {\n    using LSP1Utils for address;\n\n    /**\n     * @dev The number of block that MUST pass before one is able to\n     *  confirm renouncing ownership\n     */\n    uint256 public constant RENOUNCE_OWNERSHIP_CONFIRMATION_DELAY = 200;\n\n    /**\n     * @dev The number of blocks during which one can renounce ownership\n     */\n    uint256 public constant RENOUNCE_OWNERSHIP_CONFIRMATION_PERIOD = 200;\n\n    /**\n     * @dev The block number saved when initiating the process of renouncing ownerhsip\n     */\n    uint256 private _renounceOwnershipStartedAt;\n\n    /**\n     * @dev see `pendingOwner()`\n     */\n    address private _pendingOwner;\n\n    /**\n     * @inheritdoc ILSP14Ownable2Step\n     */\n    function pendingOwner() public view virtual returns (address) {\n        return _pendingOwner;\n    }\n\n    /**\n     * @inheritdoc ILSP14Ownable2Step\n     */\n    function transferOwnership(\n        address newOwner\n    ) public virtual override(OwnableUnset, ILSP14Ownable2Step) onlyOwner {\n        _transferOwnership(newOwner);\n\n        address currentOwner = owner();\n        emit OwnershipTransferStarted(currentOwner, newOwner);\n\n        newOwner.tryNotifyUniversalReceiver(\n            _TYPEID_LSP14_OwnershipTransferStarted,\n            \"\"\n        );\n        require(\n            currentOwner == owner(),\n            \"LSP14: newOwner MUST accept ownership in a separate transaction\"\n        );\n    }\n\n    /**\n     * @inheritdoc ILSP14Ownable2Step\n     */\n    function acceptOwnership() public virtual {\n        address previousOwner = owner();\n\n        _acceptOwnership();\n\n        previousOwner.tryNotifyUniversalReceiver(\n            _TYPEID_LSP14_OwnershipTransferred_SenderNotification,\n            \"\"\n        );\n\n        msg.sender.tryNotifyUniversalReceiver(\n            _TYPEID_LSP14_OwnershipTransferred_RecipientNotification,\n            \"\"\n        );\n    }\n\n    /**\n     * @inheritdoc ILSP14Ownable2Step\n     */\n    function renounceOwnership()\n        public\n        virtual\n        override(OwnableUnset, ILSP14Ownable2Step)\n        onlyOwner\n    {\n        _renounceOwnership();\n    }\n\n    // --- Internal methods\n\n    /**\n     * @dev set the pending owner of the contract and cancel any renounce ownership\n     * process that was previously started.\n     *\n     * @param newOwner The address of the new pending owner.\n     *\n     * Requirements:\n     * - `newOwner` cannot be the address of the contract itself.\n     */\n    function _transferOwnership(address newOwner) internal virtual {\n        if (newOwner == address(this)) revert CannotTransferOwnershipToSelf();\n\n        _pendingOwner = newOwner;\n        delete _renounceOwnershipStartedAt;\n    }\n\n    /**\n     * @dev set the pending owner of the contract as the new owner.\n     */\n    function _acceptOwnership() internal virtual {\n        require(\n            msg.sender == pendingOwner(),\n            \"LSP14: caller is not the pendingOwner\"\n        );\n\n        _setOwner(msg.sender);\n        delete _pendingOwner;\n    }\n\n    /**\n     * @dev initiate or confirm the process of renouncing ownership\n     * after a specific delay of blocks have passed.\n     */\n    function _renounceOwnership() internal virtual {\n        uint256 currentBlock = block.number;\n        uint256 confirmationPeriodStart = _renounceOwnershipStartedAt +\n            RENOUNCE_OWNERSHIP_CONFIRMATION_DELAY;\n        uint256 confirmationPeriodEnd = confirmationPeriodStart +\n            RENOUNCE_OWNERSHIP_CONFIRMATION_PERIOD;\n\n        // On the creation of a new network, `currentBlock` will be smaller than `confirmationPeriodEnd`,\n        // `_renounceOwnershipStartedAt == 0` will indicate that a renounceOwnership call is happening for the first time\n        if (\n            currentBlock > confirmationPeriodEnd ||\n            _renounceOwnershipStartedAt == 0\n        ) {\n            _renounceOwnershipStartedAt = currentBlock;\n            delete _pendingOwner;\n            emit RenounceOwnershipStarted();\n            return;\n        }\n\n        if (currentBlock < confirmationPeriodStart) {\n            revert NotInRenounceOwnershipInterval(\n                confirmationPeriodStart,\n                confirmationPeriodEnd\n            );\n        }\n\n        _setOwner(address(0));\n        delete _renounceOwnershipStartedAt;\n        emit OwnershipRenounced();\n    }\n}"
    }
  ]
}