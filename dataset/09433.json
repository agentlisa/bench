{
  "Title": "[M-06] Duplicate asset can be added",
  "Content": "_Submitted by csanuragjain_\n\n[ManagedIndex.sol#L35](https://github.com/code-423n4/2022-04-phuture/blob/main/contracts/ManagedIndex.sol#L35)<br>\n[TopNMarketCapIndex.sol#L57](https://github.com/code-423n4/2022-04-phuture/blob/main/contracts/TopNMarketCapIndex.sol#L57)<br>\n[TrackedIndex.sol#L45](https://github.com/code-423n4/2022-04-phuture/blob/main/contracts/TrackedIndex.sol#L45)<br>\n\nInitialize function can be called multiple times with same asset. Calling with same asset will make duplicate entries in assets list. Any function reading assets will get impacted and would retrieve duplicate asset\n\n### Proof of Concept\n\n1.  Observe that initialize function can be called multiple times\n2.  Admin calls initialize function with asset X\n3.  asset X gets added in assets object\n4.  Admin again calls initialize function with asset X\n5.  asset X again gets added in assets object making duplicate entries\n\n### Recommended Mitigation Steps\n\nAdd a check to fail if assets already contains the passed asset argument. Also add a modifier so that initialize could only be called once.\n\n    require(!assets.contain(asset), \"Asset already exists\");\n\n**[olivermehr (Phuture Finance) disputed](https://github.com/code-423n4/2022-04-phuture-findings/issues/23)**\n\n**[jn-lp (Phuture Finance) commented](https://github.com/code-423n4/2022-04-phuture-findings/issues/23#issuecomment-1123743337):**\n > We require caller of `initialize` method to be a factory (which is non-upgradable contract), so it can't be called twice\n> \n> _see:_\n> ```sol\n> require(msg.sender == factory, \"ManagedIndex: FORBIDDEN\");\n> ```\n\n**[moose-code (judge) commented](https://github.com/code-423n4/2022-04-phuture-findings/issues/23#issuecomment-1135966246):**\n > Given the factory contract is not supplied it makes it impossible to know these things and hence siding with the warden for the disclosure. \n> \n> \" to be a factory (which is non-upgradable contract)\" i.e. one can't know this if the factory is not supplied or documented. \n\n\n\n***\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-04-phuture-finance-contest",
  "Code": [
    {
      "filename": "contracts/ManagedIndex.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"./interfaces/IManagedIndex.sol\";\nimport \"./interfaces/IIndexFactory.sol\";\nimport \"./interfaces/IManagedIndexReweightingLogic.sol\";\n\nimport \"./BaseIndex.sol\";\n\n/// @title Managed index\n/// @notice Contains initialization and reweighting logic\ncontract ManagedIndex is IManagedIndex, BaseIndex {\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Role for index reweighting\n    bytes32 private REWEIGHT_INDEX_ROLE;\n\n    constructor() BaseIndex(msg.sender) {\n        REWEIGHT_INDEX_ROLE = keccak256(abi.encodePacked(\"REWEIGHT_PERMISSION\", address(this)));\n    }\n\n    /// @notice Index initialization with assets and their weights\n    /// @dev Method is called by factory\n    /// @param _assets Assets list for the index\n    /// @param _weights List of assets corresponding weights\n    function initialize(address[] calldata _assets, uint8[] calldata _weights) external {\n        require(msg.sender == factory, \"ManagedIndex: FORBIDDEN\");\n\n        for (uint i; i < _assets.length; ++i) {\n            address asset = _assets[i];\n            uint8 weight = _weights[i];\n\n            weightOf[asset] = weight;\n            assets.add(asset);\n\n            emit UpdateAnatomy(asset, weight);\n        }\n    }\n\n    /// @inheritdoc IManagedIndex\n    /// @dev Assets total weight should be equal to 255\n    function reweight(address[] calldata _updatedAssets, uint8[] calldata _updatedWeights) external override {\n        require(\n            IAccessControl(registry).hasRole(INDEX_MANAGER_ROLE, msg.sender) ||\n                IAccessControl(registry).hasRole(REWEIGHT_INDEX_ROLE, msg.sender),\n            \"ManagedIndex: FORBIDDEN\"\n        );\n        (bool success, bytes memory data) = IIndexFactory(factory).reweightingLogic().delegatecall(\n            abi.encodeWithSelector(IManagedIndexReweightingLogic.reweight.selector, _updatedAssets, _updatedWeights)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"ManagedIndex: REWEIGH_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IManagedIndex).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/TopNMarketCapIndex.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"./interfaces/IIndexFactory.sol\";\nimport \"./interfaces/IReweightableIndex.sol\";\nimport \"./interfaces/ITopNMarketCapIndexReweightingLogic.sol\";\n\nimport \"./BaseIndex.sol\";\n\n/// @title Top N market capitalization index\n/// @notice Contains initialization and reweighting logic\n/// @dev This index reweighs according to the latest data from the TopNMarketCapCategories contract\ncontract TopNMarketCapIndex is IReweightableIndex, BaseIndex {\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Orderer role\n    bytes32 internal constant ORDERER_ROLE = keccak256(\"ORDERER_ROLE\");\n\n    /// @notice Number of assets used in the index\n    uint8 public topN;\n    /// @notice Category identifier used for the given index\n    uint public category;\n\n    /// @notice Latest category snapshot to track diff\n    uint private snapshot;\n\n    constructor() BaseIndex(msg.sender) {}\n\n    /// @notice Initializes index with provided set of parameters\n    /// @param _topN Number of assets used in the index\n    /// @param _category Category identifier used for the given index\n    /// @param _snapshot Initial snapshot from the category\n    /// @param _assets Assets list\n    /// @param _capitalizations List of corresponding assets capitalizations\n    /// @param _totalCapitalization Total capitalization of assets\n    function initialize(\n        uint8 _topN,\n        uint _category,\n        uint _snapshot,\n        address[] calldata _assets,\n        uint[] calldata _capitalizations,\n        uint _totalCapitalization\n    ) external {\n        require(msg.sender == factory, \"TopNMarketCapIndex: FORBIDDEN\");\n\n        uint8 _totalWeight;\n        for (uint i; i < _assets.length; ++i) {\n            uint _i = _assets.length - 1 - i;\n            address asset = _assets[_i];\n            uint8 weight = _i == 0\n                ? IndexLibrary.MAX_WEIGHT - _totalWeight\n                : uint8((_capitalizations[_i] * type(uint8).max) / _totalCapitalization);\n            weightOf[asset] = weight;\n            require(asset != address(0), \"TopNMarketCapIndex: ZERO\");\n            if (weight > 0) {\n                assets.add(asset);\n                _totalWeight += weight;\n                emit UpdateAnatomy(asset, weight);\n            }\n        }\n        snapshot = _snapshot;\n        category = _category;\n        topN = _topN;\n    }\n\n    /// @notice Reweighs index assets according to the latest market cap data for specified category\n    function reweight() external override onlyRole(ORDERER_ROLE) {\n        (bool success, bytes memory data) = IIndexFactory(factory).reweightingLogic().delegatecall(\n            abi.encodeWithSelector(ITopNMarketCapIndexReweightingLogic.reweight.selector, category, snapshot, topN)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"TopNMarketCapIndex: REWEIGH_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n        snapshot = abi.decode(data, (uint));\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IReweightableIndex).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    },
    {
      "filename": "contracts/TrackedIndex.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity >=0.8.7;\n\nimport \"./interfaces/IIndexFactory.sol\";\nimport \"./interfaces/IReweightableIndex.sol\";\nimport \"./interfaces/ITrackedIndexReweightingLogic.sol\";\n\nimport \"./BaseIndex.sol\";\n\n/// @title Tracked index\n/// @notice  Contains initialization and reweighting logic\ncontract TrackedIndex is IReweightableIndex, BaseIndex {\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    /// @notice Orderer role\n    bytes32 internal constant ORDERER_ROLE = keccak256(\"ORDERER_ROLE\");\n\n    constructor() BaseIndex(msg.sender) {}\n\n    /// @notice Initializes index with provided set of parameters\n    /// @param _assets Assets list\n    /// @param _capitalizations List of corresponding assets capitalizations\n    /// @param _totalCapitalization Total capitalization of assets\n    function initialize(\n        address[] calldata _assets,\n        uint[] calldata _capitalizations,\n        uint _totalCapitalization\n    ) external {\n        require(msg.sender == factory, \"TrackedIndex: FORBIDDEN\");\n\n        uint8 totalWeight;\n        uint maxCapitalization = _capitalizations[0];\n        address maxCapitalizationAsset = _assets[0];\n        for (uint i; i < _assets.length; ++i) {\n            address asset = _assets[i];\n            uint8 weight = uint8((_capitalizations[i] * type(uint8).max) / _totalCapitalization);\n            if (_capitalizations[i] > maxCapitalization) {\n                emit UpdateAnatomy(maxCapitalizationAsset, weightOf[maxCapitalizationAsset]);\n                maxCapitalization = _capitalizations[i];\n                maxCapitalizationAsset = asset;\n            }\n            weightOf[asset] = weight;\n            totalWeight += weight;\n            assets.add(asset);\n            if (asset != maxCapitalizationAsset) {\n                emit UpdateAnatomy(asset, weight);\n            }\n        }\n        if (totalWeight < IndexLibrary.MAX_WEIGHT) {\n            weightOf[maxCapitalizationAsset] += IndexLibrary.MAX_WEIGHT - totalWeight;\n        }\n        emit UpdateAnatomy(maxCapitalizationAsset, weightOf[maxCapitalizationAsset]);\n    }\n\n    /// @notice Reweighs index assets according to the latest market cap data\n    function reweight() external override onlyRole(ORDERER_ROLE) {\n        (bool success, bytes memory data) = IIndexFactory(factory).reweightingLogic().delegatecall(\n            abi.encodeWithSelector(ITrackedIndexReweightingLogic.reweight.selector)\n        );\n        if (!success) {\n            if (data.length == 0) {\n                revert(\"TrackedIndex: REWEIGH_FAILED\");\n            } else {\n                assembly {\n                    revert(add(32, data), mload(data))\n                }\n            }\n        }\n    }\n\n    /// @inheritdoc ERC165\n    function supportsInterface(bytes4 _interfaceId) public view virtual override returns (bool) {\n        return _interfaceId == type(IReweightableIndex).interfaceId || super.supportsInterface(_interfaceId);\n    }\n}"
    }
  ]
}