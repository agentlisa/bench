{
  "Title": "[09] `rawCall` does permit calls to `MsgValueSimulator` with the `_isSystem` flag set to false (it will revert)",
  "Content": "In [MsgValueSimulator, `fallback`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/contracts/MsgValueSimulator.sol#L35C45-L35C59) can only be called with the `systemCall` flag set (not the ABI one, which is retrieved through `SystemContractHelper.getExtraAbiData`)\n\n```solidity\n    fallback(bytes calldata _data) external onlySystemCall returns (bytes memory)\n```\n\n[ISystemContract, lines 15 to 21](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/contracts/interfaces/ISystemContract.sol#L15C1-L21C6)\n\n```solidity\n    modifier onlySystemCall() {\n        require(\n            SystemContractHelper.isSystemCall() || SystemContractHelper.isSystemContract(msg.sender),\n                \"This method require system call flag\"\n        );\n        _;\n    }\n```\n\nHowever, in [EfficientCall, function `rawCall`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/contracts/libraries/EfficientCall.sol#L146) does let the door open for doing calls to `MsgValueSimulator` with the `_isSystem` flag set to `false` even if the corresponding ABI parameter is hard-coded to `true`.\n\n[EfficientCall, lines 139 to 146](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/contracts/libraries/EfficientCall.sol#L139C1-L146C85)\n\n```solidity\n            _loadFarCallABIIntoActivePtr(_gas, _data, false, true); <========= isSystem hard-coded to true\n\n            // If there is provided `msg.value` call the `MsgValueSimulator` to forward ether.\n            address msgValueSimulator = MSG_VALUE_SYSTEM_CONTRACT;\n            address callAddr = SYSTEM_CALL_BY_REF_CALL_ADDRESS;\n            // We need to supply the mask to the MsgValueSimulator to denote\n            // that the call should be a system one.\n            uint256 forwardMask = _isSystem ? MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT : 0; <========= may be false\n```\n\nConsider just using `MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT`, as the `_isSystem` parameter is expected to be used for non-zero value calls.\n\n```diff\n    function rawCall(\n        uint256 _gas,\n        address _address,\n        uint256 _value,\n        bytes calldata _data,\n        bool _isSystem\n    ) internal returns (bool success) {\n        if (_value == 0) {\n            _loadFarCallABIIntoActivePtr(_gas, _data, false, _isSystem);\n\n            address callAddr = RAW_FAR_CALL_BY_REF_CALL_ADDRESS;\n            assembly {\n                success := call(_address, callAddr, 0, 0, 0xFFFF, 0, 0)\n            }\n        } else {\n            _loadFarCallABIIntoActivePtr(_gas, _data, false, true);\n\n            // If there is provided `msg.value` call the `MsgValueSimulator` to forward ether.\n            address msgValueSimulator = MSG_VALUE_SYSTEM_CONTRACT;\n            address callAddr = SYSTEM_CALL_BY_REF_CALL_ADDRESS;\n-           // We need to supply the mask to the MsgValueSimulator to denote\n-           // that the call should be a system one.\n-           uint256 forwardMask = _isSystem ? MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT : 0;\n+           uint256 forwardMask = MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT; \n\n            assembly {\n                success := call(msgValueSimulator, callAddr, _value, _address, 0xFFFF, forwardMask, 0)\n            }\n        }\n    }\n```\n\nAs seen in [SystemContractsCaller, lines 103 to 112](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/contracts/libraries/SystemContractsCaller.sol#L103C1-L112C10), the comments are the same, so it seems it was copy-pasted.\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2023-10-zksync",
  "Code": [
    {
      "filename": "code/system-contracts/contracts/MsgValueSimulator.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.0;\n\nimport \"./libraries/Utils.sol\";\nimport \"./libraries/EfficientCall.sol\";\nimport \"./interfaces/ISystemContract.sol\";\nimport {SystemContractHelper} from \"./libraries/SystemContractHelper.sol\";\nimport {MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT, ETH_TOKEN_SYSTEM_CONTRACT} from \"./Constants.sol\";\n\n/**\n * @author Matter Labs\n * @custom:security-contact security@matterlabs.dev\n * @notice The contract responsible for simulating transactions with `msg.value` inside zkEVM.\n * @dev It accepts value and whether the call should be system in the first extraAbi param and\n * the address to call in the second extraAbi param, transfers the funds and uses `mimicCall` to continue the\n * call with the same msg.sender.\n */\ncontract MsgValueSimulator is ISystemContract {\n    /// @notice Extract value, isSystemCall and to from the extraAbi params.\n    /// @dev The contract accepts value, the callee and whether the call should a system one via its ABI params.\n    /// @dev The first ABI param contains the value in the [0..127] bits. The 128th contains\n    /// the flag whether or not the call should be a system one.\n    /// The second ABI params contains the callee.\n    function _getAbiParams() internal view returns (uint256 value, bool isSystemCall, address to) {\n        value = SystemContractHelper.getExtraAbiData(0);\n        uint256 addressAsUint = SystemContractHelper.getExtraAbiData(1);\n        uint256 mask = SystemContractHelper.getExtraAbiData(2);\n\n        isSystemCall = (mask & MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT) != 0;\n\n        to = address(uint160(addressAsUint));\n    }\n\n    fallback(bytes calldata _data) external onlySystemCall returns (bytes memory) {\n        (uint256 value, bool isSystemCall, address to) = _getAbiParams();\n\n        // Prevent mimic call to the MsgValueSimulator to prevent an unexpected change of callee.\n        require(to != address(this), \"MsgValueSimulator calls itself\");\n\n        if (value != 0) {\n            (bool success, ) = address(ETH_TOKEN_SYSTEM_CONTRACT).call(\n                abi.encodeCall(ETH_TOKEN_SYSTEM_CONTRACT.transferFromTo, (msg.sender, to, value))\n            );\n\n            // If the transfer of ETH fails, we do the most Ethereum-like behaviour in such situation: revert(0,0)\n            if (!success) {\n                assembly {\n                    revert(0, 0)\n                }\n            }\n        }\n\n        // For the next call this `msg.value` will be used.\n        SystemContractHelper.setValueForNextFarCall(Utils.safeCastToU128(value));\n\n        return EfficientCall.mimicCall(gasleft(), to, _data, msg.sender, false, isSystemCall);\n    }\n}"
    },
    {
      "filename": "code/system-contracts/contracts/interfaces/ISystemContract.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.0;\n\nimport {SystemContractHelper} from \"../libraries/SystemContractHelper.sol\";\nimport {BOOTLOADER_FORMAL_ADDRESS} from \"../Constants.sol\";\n\n/// @dev Solidity does not allow exporting modifiers via libraries, so\n/// the only way to do reuse modifiers is to have a base contract\n/// @dev Never add storage variables into this contract as some\n/// system contracts rely on this abstract contract as on interface!\nabstract contract ISystemContract {\n    /// @notice Modifier that makes sure that the method\n    /// can only be called via a system call.\n    modifier onlySystemCall() {\n        require(\n            SystemContractHelper.isSystemCall() || SystemContractHelper.isSystemContract(msg.sender),\n            \"This method require system call flag\"\n        );\n        _;\n    }\n\n    /// @notice Modifier that makes sure that the method\n    /// can only be called from a system contract.\n    modifier onlyCallFromSystemContract() {\n        require(\n            SystemContractHelper.isSystemContract(msg.sender),\n            \"This method require the caller to be system contract\"\n        );\n        _;\n    }\n\n    /// @notice Modifier that makes sure that the method\n    /// can only be called from a special given address.\n    modifier onlyCallFrom(address caller) {\n        require(msg.sender == caller, \"Inappropriate caller\");\n        _;\n    }\n\n    /// @notice Modifier that makes sure that the method\n    /// can only be called from the bootloader.\n    modifier onlyCallFromBootloader() {\n        require(msg.sender == BOOTLOADER_FORMAL_ADDRESS, \"Callable only by the bootloader\");\n        _;\n    }\n}"
    },
    {
      "filename": "code/system-contracts/contracts/libraries/EfficientCall.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.0;\n\nimport \"./SystemContractHelper.sol\";\nimport \"./Utils.sol\";\nimport {SHA256_SYSTEM_CONTRACT, KECCAK256_SYSTEM_CONTRACT, MSG_VALUE_SYSTEM_CONTRACT} from \"../Constants.sol\";\n\n/**\n * @author Matter Labs\n * @custom:security-contact security@matterlabs.dev\n * @notice This library is used to perform ultra-efficient calls using zkEVM-specific features.\n * @dev EVM calls always accept a memory slice as input and return a memory slice as output.\n * Therefore, even if the user has a ready-made calldata slice, they still need to copy it to memory\n * before calling. This is especially inefficient for large inputs (proxies, multi-calls, etc.).\n * In turn, zkEVM operates over a fat pointer, which is a set of (memory page, offset, start, length) in the memory/calldata/returndata.\n * This allows forwarding the calldata slice as is, without copying it to memory.\n * @dev Fat pointer is not just an integer, it is an extended data type supported on the VM level.\n * zkEVM creates the wellformed fat pointers for all the calldata/returndata regions, later\n * the contract may manipulate the already created fat pointers to forward a slice of the data, but not\n * to create new fat pointers!\n * @dev The allowed operation on fat pointers are:\n * 1. `ptr.add` - Transforms `ptr.offset` into `ptr.offset + u32(_value)`. If overflow happens then it panics.\n * 2. `ptr.sub` - Transforms `ptr.offset` into `ptr.offset - u32(_value)`. If underflow happens then it panics.\n * 3. `ptr.pack` - Do the concatenation between the lowest 128 bits of the pointer itself and the highest 128 bits of `_value`. It is typically used to prepare the ABI for external calls.\n * 4. `ptr.shrink` - Transforms `ptr.length` into `ptr.length - u32(_shrink)`. If underflow happens then it panics.\n * @dev The call opcodes accept the fat pointer and change it to its canonical form before passing it to the child call\n * 1. `ptr.start` is transformed into `ptr.offset + ptr.start`\n * 2. `ptr.length` is transformed into `ptr.length - ptr.offset`\n * 3. `ptr.offset` is transformed into `0`\n */\nlibrary EfficientCall {\n    /// @notice Call the `keccak256` without copying calldata to memory.\n    /// @param _data The preimage data.\n    /// @return The `keccak256` hash.\n    function keccak(bytes calldata _data) internal view returns (bytes32) {\n        bytes memory returnData = staticCall(gasleft(), KECCAK256_SYSTEM_CONTRACT, _data);\n        require(returnData.length == 32, \"keccak256 returned invalid data\");\n        return bytes32(returnData);\n    }\n\n    /// @notice Call the `sha256` precompile without copying calldata to memory.\n    /// @param _data The preimage data.\n    /// @return The `sha256` hash.\n    function sha(bytes calldata _data) internal view returns (bytes32) {\n        bytes memory returnData = staticCall(gasleft(), SHA256_SYSTEM_CONTRACT, _data);\n        require(returnData.length == 32, \"sha returned invalid data\");\n        return bytes32(returnData);\n    }\n\n    /// @notice Perform a `call` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _value The `msg.value` to send.\n    /// @param _data The calldata to use for the call.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return returnData The copied to memory return data.\n    function call(\n        uint256 _gas,\n        address _address,\n        uint256 _value,\n        bytes calldata _data,\n        bool _isSystem\n    ) internal returns (bytes memory returnData) {\n        bool success = rawCall(_gas, _address, _value, _data, _isSystem);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `staticCall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return returnData The copied to memory return data.\n    function staticCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data\n    ) internal view returns (bytes memory returnData) {\n        bool success = rawStaticCall(_gas, _address, _data);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `delegateCall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return returnData The copied to memory return data.\n    function delegateCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data\n    ) internal returns (bytes memory returnData) {\n        bool success = rawDelegateCall(_gas, _address, _data);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `mimicCall` (a call with custom msg.sender) without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @param _whoToMimic The `msg.sender` for the next call.\n    /// @param _isConstructor Whether the call should contain the `isConstructor` flag.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return returnData The copied to memory return data.\n    function mimicCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data,\n        address _whoToMimic,\n        bool _isConstructor,\n        bool _isSystem\n    ) internal returns (bytes memory returnData) {\n        bool success = rawMimicCall(_gas, _address, _data, _whoToMimic, _isConstructor, _isSystem);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `call` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _value The `msg.value` to send.\n    /// @param _data The calldata to use for the call.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return success whether the call was successful.\n    function rawCall(\n        uint256 _gas,\n        address _address,\n        uint256 _value,\n        bytes calldata _data,\n        bool _isSystem\n    ) internal returns (bool success) {\n        if (_value == 0) {\n            _loadFarCallABIIntoActivePtr(_gas, _data, false, _isSystem);\n\n            address callAddr = RAW_FAR_CALL_BY_REF_CALL_ADDRESS;\n            assembly {\n                success := call(_address, callAddr, 0, 0, 0xFFFF, 0, 0)\n            }\n        } else {\n            _loadFarCallABIIntoActivePtr(_gas, _data, false, true);\n\n            // If there is provided `msg.value` call the `MsgValueSimulator` to forward ether.\n            address msgValueSimulator = MSG_VALUE_SYSTEM_CONTRACT;\n            address callAddr = SYSTEM_CALL_BY_REF_CALL_ADDRESS;\n            // We need to supply the mask to the MsgValueSimulator to denote\n            // that the call should be a system one.\n            uint256 forwardMask = _isSystem ? MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT : 0;\n\n            assembly {\n                success := call(msgValueSimulator, callAddr, _value, _address, 0xFFFF, forwardMask, 0)\n            }\n        }\n    }\n\n    /// @notice Perform a `staticCall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return success whether the call was successful.\n    function rawStaticCall(uint256 _gas, address _address, bytes calldata _data) internal view returns (bool success) {\n        _loadFarCallABIIntoActivePtr(_gas, _data, false, false);\n\n        address callAddr = RAW_FAR_CALL_BY_REF_CALL_ADDRESS;\n        assembly {\n            success := staticcall(_address, callAddr, 0, 0xFFFF, 0, 0)\n        }\n    }\n\n    /// @notice Perform a `delegatecall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return success whether the call was successful.\n    function rawDelegateCall(uint256 _gas, address _address, bytes calldata _data) internal returns (bool success) {\n        _loadFarCallABIIntoActivePtr(_gas, _data, false, false);\n\n        address callAddr = RAW_FAR_CALL_BY_REF_CALL_ADDRESS;\n        assembly {\n            success := delegatecall(_address, callAddr, 0, 0xFFFF, 0, 0)\n        }\n    }\n\n    /// @notice Perform a `mimicCall` (call with custom msg.sender) without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @param _whoToMimic The `msg.sender` for the next call.\n    /// @param _isConstructor Whether the call should contain the `isConstructor` flag.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return success whether the call was successful.\n    /// @dev If called not in kernel mode, it will result in a revert (enforced by the VM)\n    function rawMimicCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data,\n        address _whoToMimic,\n        bool _isConstructor,\n        bool _isSystem\n    ) internal returns (bool success) {\n        _loadFarCallABIIntoActivePtr(_gas, _data, _isConstructor, _isSystem);\n\n        address callAddr = MIMIC_CALL_BY_REF_CALL_ADDRESS;\n        uint256 cleanupMask = ADDRESS_MASK;\n        assembly {\n            // Clearing values before usage in assembly, since Solidity\n            // doesn't do it by default\n            _whoToMimic := and(_whoToMimic, cleanupMask)\n\n            success := call(_address, callAddr, 0, 0, _whoToMimic, 0, 0)\n        }\n    }\n\n    /// @dev Verify that a low-level call was successful, and revert if it wasn't, by bubbling the revert reason.\n    /// @param _success Whether the call was successful.\n    /// @return returnData The copied to memory return data.\n    function _verifyCallResult(bool _success) private pure returns (bytes memory returnData) {\n        if (_success) {\n            uint256 size;\n            assembly {\n                size := returndatasize()\n            }\n\n            returnData = new bytes(size);\n            assembly {\n                returndatacopy(add(returnData, 0x20), 0, size)\n            }\n        } else {\n            propagateRevert();\n        }\n    }\n\n    /// @dev Propagate the revert reason from the current call to the caller.\n    function propagateRevert() internal pure {\n        assembly {\n            let size := returndatasize()\n            returndatacopy(0, 0, size)\n            revert(0, size)\n        }\n    }\n\n    /// @dev Load the far call ABI into active ptr, that will be used for the next call by reference.\n    /// @param _gas The gas to be passed to the call.\n    /// @param _data The calldata to be passed to the call.\n    /// @param _isConstructor Whether the call is a constructor call.\n    /// @param _isSystem Whether the call is a system call.\n    function _loadFarCallABIIntoActivePtr(\n        uint256 _gas,\n        bytes calldata _data,\n        bool _isConstructor,\n        bool _isSystem\n    ) private view {\n        SystemContractHelper.loadCalldataIntoActivePtr();\n\n        uint256 dataOffset;\n        assembly {\n            dataOffset := _data.offset\n        }\n\n        // Safe to cast, offset is never bigger than `type(uint32).max`\n        SystemContractHelper.ptrAddIntoActive(uint32(dataOffset));\n        // Safe to cast, `data.length` is never bigger than `type(uint32).max`\n        uint32 shrinkTo = uint32(msg.data.length - (_data.length + dataOffset));\n        SystemContractHelper.ptrShrinkIntoActive(shrinkTo);\n\n        uint32 gas = Utils.safeCastToU32(_gas);\n        uint256 farCallAbi = SystemContractsCaller.getFarCallABIWithEmptyFatPointer(\n            gas,\n            // Only rollup is supported for now\n            0,\n            CalldataForwardingMode.ForwardFatPointer,\n            _isConstructor,\n            _isSystem\n        );\n        SystemContractHelper.ptrPackIntoActivePtr(farCallAbi);\n    }\n}"
    },
    {
      "filename": "code/system-contracts/contracts/libraries/EfficientCall.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.0;\n\nimport \"./SystemContractHelper.sol\";\nimport \"./Utils.sol\";\nimport {SHA256_SYSTEM_CONTRACT, KECCAK256_SYSTEM_CONTRACT, MSG_VALUE_SYSTEM_CONTRACT} from \"../Constants.sol\";\n\n/**\n * @author Matter Labs\n * @custom:security-contact security@matterlabs.dev\n * @notice This library is used to perform ultra-efficient calls using zkEVM-specific features.\n * @dev EVM calls always accept a memory slice as input and return a memory slice as output.\n * Therefore, even if the user has a ready-made calldata slice, they still need to copy it to memory\n * before calling. This is especially inefficient for large inputs (proxies, multi-calls, etc.).\n * In turn, zkEVM operates over a fat pointer, which is a set of (memory page, offset, start, length) in the memory/calldata/returndata.\n * This allows forwarding the calldata slice as is, without copying it to memory.\n * @dev Fat pointer is not just an integer, it is an extended data type supported on the VM level.\n * zkEVM creates the wellformed fat pointers for all the calldata/returndata regions, later\n * the contract may manipulate the already created fat pointers to forward a slice of the data, but not\n * to create new fat pointers!\n * @dev The allowed operation on fat pointers are:\n * 1. `ptr.add` - Transforms `ptr.offset` into `ptr.offset + u32(_value)`. If overflow happens then it panics.\n * 2. `ptr.sub` - Transforms `ptr.offset` into `ptr.offset - u32(_value)`. If underflow happens then it panics.\n * 3. `ptr.pack` - Do the concatenation between the lowest 128 bits of the pointer itself and the highest 128 bits of `_value`. It is typically used to prepare the ABI for external calls.\n * 4. `ptr.shrink` - Transforms `ptr.length` into `ptr.length - u32(_shrink)`. If underflow happens then it panics.\n * @dev The call opcodes accept the fat pointer and change it to its canonical form before passing it to the child call\n * 1. `ptr.start` is transformed into `ptr.offset + ptr.start`\n * 2. `ptr.length` is transformed into `ptr.length - ptr.offset`\n * 3. `ptr.offset` is transformed into `0`\n */\nlibrary EfficientCall {\n    /// @notice Call the `keccak256` without copying calldata to memory.\n    /// @param _data The preimage data.\n    /// @return The `keccak256` hash.\n    function keccak(bytes calldata _data) internal view returns (bytes32) {\n        bytes memory returnData = staticCall(gasleft(), KECCAK256_SYSTEM_CONTRACT, _data);\n        require(returnData.length == 32, \"keccak256 returned invalid data\");\n        return bytes32(returnData);\n    }\n\n    /// @notice Call the `sha256` precompile without copying calldata to memory.\n    /// @param _data The preimage data.\n    /// @return The `sha256` hash.\n    function sha(bytes calldata _data) internal view returns (bytes32) {\n        bytes memory returnData = staticCall(gasleft(), SHA256_SYSTEM_CONTRACT, _data);\n        require(returnData.length == 32, \"sha returned invalid data\");\n        return bytes32(returnData);\n    }\n\n    /// @notice Perform a `call` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _value The `msg.value` to send.\n    /// @param _data The calldata to use for the call.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return returnData The copied to memory return data.\n    function call(\n        uint256 _gas,\n        address _address,\n        uint256 _value,\n        bytes calldata _data,\n        bool _isSystem\n    ) internal returns (bytes memory returnData) {\n        bool success = rawCall(_gas, _address, _value, _data, _isSystem);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `staticCall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return returnData The copied to memory return data.\n    function staticCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data\n    ) internal view returns (bytes memory returnData) {\n        bool success = rawStaticCall(_gas, _address, _data);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `delegateCall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return returnData The copied to memory return data.\n    function delegateCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data\n    ) internal returns (bytes memory returnData) {\n        bool success = rawDelegateCall(_gas, _address, _data);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `mimicCall` (a call with custom msg.sender) without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @param _whoToMimic The `msg.sender` for the next call.\n    /// @param _isConstructor Whether the call should contain the `isConstructor` flag.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return returnData The copied to memory return data.\n    function mimicCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data,\n        address _whoToMimic,\n        bool _isConstructor,\n        bool _isSystem\n    ) internal returns (bytes memory returnData) {\n        bool success = rawMimicCall(_gas, _address, _data, _whoToMimic, _isConstructor, _isSystem);\n        returnData = _verifyCallResult(success);\n    }\n\n    /// @notice Perform a `call` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _value The `msg.value` to send.\n    /// @param _data The calldata to use for the call.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return success whether the call was successful.\n    function rawCall(\n        uint256 _gas,\n        address _address,\n        uint256 _value,\n        bytes calldata _data,\n        bool _isSystem\n    ) internal returns (bool success) {\n        if (_value == 0) {\n            _loadFarCallABIIntoActivePtr(_gas, _data, false, _isSystem);\n\n            address callAddr = RAW_FAR_CALL_BY_REF_CALL_ADDRESS;\n            assembly {\n                success := call(_address, callAddr, 0, 0, 0xFFFF, 0, 0)\n            }\n        } else {\n            _loadFarCallABIIntoActivePtr(_gas, _data, false, true);\n\n            // If there is provided `msg.value` call the `MsgValueSimulator` to forward ether.\n            address msgValueSimulator = MSG_VALUE_SYSTEM_CONTRACT;\n            address callAddr = SYSTEM_CALL_BY_REF_CALL_ADDRESS;\n            // We need to supply the mask to the MsgValueSimulator to denote\n            // that the call should be a system one.\n            uint256 forwardMask = _isSystem ? MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT : 0;\n\n            assembly {\n                success := call(msgValueSimulator, callAddr, _value, _address, 0xFFFF, forwardMask, 0)\n            }\n        }\n    }\n\n    /// @notice Perform a `staticCall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return success whether the call was successful.\n    function rawStaticCall(uint256 _gas, address _address, bytes calldata _data) internal view returns (bool success) {\n        _loadFarCallABIIntoActivePtr(_gas, _data, false, false);\n\n        address callAddr = RAW_FAR_CALL_BY_REF_CALL_ADDRESS;\n        assembly {\n            success := staticcall(_address, callAddr, 0, 0xFFFF, 0, 0)\n        }\n    }\n\n    /// @notice Perform a `delegatecall` without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @return success whether the call was successful.\n    function rawDelegateCall(uint256 _gas, address _address, bytes calldata _data) internal returns (bool success) {\n        _loadFarCallABIIntoActivePtr(_gas, _data, false, false);\n\n        address callAddr = RAW_FAR_CALL_BY_REF_CALL_ADDRESS;\n        assembly {\n            success := delegatecall(_address, callAddr, 0, 0xFFFF, 0, 0)\n        }\n    }\n\n    /// @notice Perform a `mimicCall` (call with custom msg.sender) without copying calldata to memory.\n    /// @param _gas The gas to use for the call.\n    /// @param _address The address to call.\n    /// @param _data The calldata to use for the call.\n    /// @param _whoToMimic The `msg.sender` for the next call.\n    /// @param _isConstructor Whether the call should contain the `isConstructor` flag.\n    /// @param _isSystem Whether the call should contain the `isSystem` flag.\n    /// @return success whether the call was successful.\n    /// @dev If called not in kernel mode, it will result in a revert (enforced by the VM)\n    function rawMimicCall(\n        uint256 _gas,\n        address _address,\n        bytes calldata _data,\n        address _whoToMimic,\n        bool _isConstructor,\n        bool _isSystem\n    ) internal returns (bool success) {\n        _loadFarCallABIIntoActivePtr(_gas, _data, _isConstructor, _isSystem);\n\n        address callAddr = MIMIC_CALL_BY_REF_CALL_ADDRESS;\n        uint256 cleanupMask = ADDRESS_MASK;\n        assembly {\n            // Clearing values before usage in assembly, since Solidity\n            // doesn't do it by default\n            _whoToMimic := and(_whoToMimic, cleanupMask)\n\n            success := call(_address, callAddr, 0, 0, _whoToMimic, 0, 0)\n        }\n    }\n\n    /// @dev Verify that a low-level call was successful, and revert if it wasn't, by bubbling the revert reason.\n    /// @param _success Whether the call was successful.\n    /// @return returnData The copied to memory return data.\n    function _verifyCallResult(bool _success) private pure returns (bytes memory returnData) {\n        if (_success) {\n            uint256 size;\n            assembly {\n                size := returndatasize()\n            }\n\n            returnData = new bytes(size);\n            assembly {\n                returndatacopy(add(returnData, 0x20), 0, size)\n            }\n        } else {\n            propagateRevert();\n        }\n    }\n\n    /// @dev Propagate the revert reason from the current call to the caller.\n    function propagateRevert() internal pure {\n        assembly {\n            let size := returndatasize()\n            returndatacopy(0, 0, size)\n            revert(0, size)\n        }\n    }\n\n    /// @dev Load the far call ABI into active ptr, that will be used for the next call by reference.\n    /// @param _gas The gas to be passed to the call.\n    /// @param _data The calldata to be passed to the call.\n    /// @param _isConstructor Whether the call is a constructor call.\n    /// @param _isSystem Whether the call is a system call.\n    function _loadFarCallABIIntoActivePtr(\n        uint256 _gas,\n        bytes calldata _data,\n        bool _isConstructor,\n        bool _isSystem\n    ) private view {\n        SystemContractHelper.loadCalldataIntoActivePtr();\n\n        uint256 dataOffset;\n        assembly {\n            dataOffset := _data.offset\n        }\n\n        // Safe to cast, offset is never bigger than `type(uint32).max`\n        SystemContractHelper.ptrAddIntoActive(uint32(dataOffset));\n        // Safe to cast, `data.length` is never bigger than `type(uint32).max`\n        uint32 shrinkTo = uint32(msg.data.length - (_data.length + dataOffset));\n        SystemContractHelper.ptrShrinkIntoActive(shrinkTo);\n\n        uint32 gas = Utils.safeCastToU32(_gas);\n        uint256 farCallAbi = SystemContractsCaller.getFarCallABIWithEmptyFatPointer(\n            gas,\n            // Only rollup is supported for now\n            0,\n            CalldataForwardingMode.ForwardFatPointer,\n            _isConstructor,\n            _isSystem\n        );\n        SystemContractHelper.ptrPackIntoActivePtr(farCallAbi);\n    }\n}"
    },
    {
      "filename": "code/system-contracts/contracts/libraries/SystemContractsCaller.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8;\n\nimport {MSG_VALUE_SYSTEM_CONTRACT, MSG_VALUE_SIMULATOR_IS_SYSTEM_BIT} from \"../Constants.sol\";\nimport \"./Utils.sol\";\n\n// Addresses used for the compiler to be replaced with the\n// zkSync-specific opcodes during the compilation.\n// IMPORTANT: these are just compile-time constants and are used\n// only if used in-place by Yul optimizer.\naddress constant TO_L1_CALL_ADDRESS = address((1 << 16) - 1);\naddress constant CODE_ADDRESS_CALL_ADDRESS = address((1 << 16) - 2);\naddress constant PRECOMPILE_CALL_ADDRESS = address((1 << 16) - 3);\naddress constant META_CALL_ADDRESS = address((1 << 16) - 4);\naddress constant MIMIC_CALL_CALL_ADDRESS = address((1 << 16) - 5);\naddress constant SYSTEM_MIMIC_CALL_CALL_ADDRESS = address((1 << 16) - 6);\naddress constant MIMIC_CALL_BY_REF_CALL_ADDRESS = address((1 << 16) - 7);\naddress constant SYSTEM_MIMIC_CALL_BY_REF_CALL_ADDRESS = address((1 << 16) - 8);\naddress constant RAW_FAR_CALL_CALL_ADDRESS = address((1 << 16) - 9);\naddress constant RAW_FAR_CALL_BY_REF_CALL_ADDRESS = address((1 << 16) - 10);\naddress constant SYSTEM_CALL_CALL_ADDRESS = address((1 << 16) - 11);\naddress constant SYSTEM_CALL_BY_REF_CALL_ADDRESS = address((1 << 16) - 12);\naddress constant SET_CONTEXT_VALUE_CALL_ADDRESS = address((1 << 16) - 13);\naddress constant SET_PUBDATA_PRICE_CALL_ADDRESS = address((1 << 16) - 14);\naddress constant INCREMENT_TX_COUNTER_CALL_ADDRESS = address((1 << 16) - 15);\naddress constant PTR_CALLDATA_CALL_ADDRESS = address((1 << 16) - 16);\naddress constant CALLFLAGS_CALL_ADDRESS = address((1 << 16) - 17);\naddress constant PTR_RETURNDATA_CALL_ADDRESS = address((1 << 16) - 18);\naddress constant EVENT_INITIALIZE_ADDRESS = address((1 << 16) - 19);\naddress constant EVENT_WRITE_ADDRESS = address((1 << 16) - 20);\naddress constant LOAD_CALLDATA_INTO_ACTIVE_PTR_CALL_ADDRESS = address((1 << 16) - 21);\naddress constant LOAD_LATEST_RETURNDATA_INTO_ACTIVE_PTR_CALL_ADDRESS = address((1 << 16) - 22);\naddress constant PTR_ADD_INTO_ACTIVE_CALL_ADDRESS = address((1 << 16) - 23);\naddress constant PTR_SHRINK_INTO_ACTIVE_CALL_ADDRESS = address((1 << 16) - 24);\naddress constant PTR_PACK_INTO_ACTIVE_CALL_ADDRESS = address((1 << 16) - 25);\naddress constant MULTIPLICATION_HIGH_ADDRESS = address((1 << 16) - 26);\naddress constant GET_EXTRA_ABI_DATA_ADDRESS = address((1 << 16) - 27);\n\n// All the offsets are in bits\nuint256 constant META_GAS_PER_PUBDATA_BYTE_OFFSET = 0 * 8;\nuint256 constant META_HEAP_SIZE_OFFSET = 8 * 8;\nuint256 constant META_AUX_HEAP_SIZE_OFFSET = 12 * 8;\nuint256 constant META_SHARD_ID_OFFSET = 28 * 8;\nuint256 constant META_CALLER_SHARD_ID_OFFSET = 29 * 8;\nuint256 constant META_CODE_SHARD_ID_OFFSET = 30 * 8;\n\n/// @notice The way to forward the calldata:\n/// - Use the current heap (i.e. the same as on EVM).\n/// - Use the auxiliary heap.\n/// - Forward via a pointer\n/// @dev Note, that currently, users do not have access to the auxiliary\n/// heap and so the only type of forwarding that will be used by the users\n/// are UseHeap and ForwardFatPointer for forwarding a slice of the current calldata\n/// to the next call.\nenum CalldataForwardingMode {\n    UseHeap,\n    ForwardFatPointer,\n    UseAuxHeap\n}\n\n/**\n * @author Matter Labs\n * @custom:security-contact security@matterlabs.dev\n * @notice A library that allows calling contracts with the `isSystem` flag.\n * @dev It is needed to call ContractDeployer and NonceHolder.\n */\nlibrary SystemContractsCaller {\n    /// @notice Makes a call with the `isSystem` flag."
    }
  ]
}