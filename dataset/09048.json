{
  "Title": "[01] The function `_name()` returns dirty data",
  "Content": "\n**Context:** [Seaport.sol#L41](https://github.com/code-423n4/2022-05-opensea-seaport/blob/main/contracts/Seaport.sol#L41), [ConsiderationBase.sol#L100](https://github.com/code-423n4/2022-05-opensea-seaport/blob/4140473b1f85d0df602548ad260b1739ddd734a5/contracts/lib/ConsiderationBase.sol#L100)\n\nThe function `name()` of Seaport.sol returns dirty data.<br>\nThis may create issues with frontends that expect clean data. In fact, Etherscan is having trouble decoding it!<br>\nhttps://etherscan.io/address/0x00000000006cee72100d161c57ada5bb2be1ca79#readContract and click name! There is a junk character at the end.<br>\n<img width=\"108\" alt=\"image\" src=\"https://user-images.githubusercontent.com/5469459/169769394-21818bb9-d85f-459d-b276-d4cdd190ee35.png\">\n\nThis also could have negative impact on composability.\n\nThe external function `name()` gets its value from `_name()` in contract Seaport.\n```solidity\ncontract ReferenceConsideration is ConsiderationInterface, ReferenceOrderCombiner {\n    ...\n    function name() external pure override returns (string memory contractName) {\n        contractName = _name();\n    }\n    ...\n}\n```\n```solidity\ncontract Seaport is Consideration {\n    ...\n    function _name() internal pure override returns (string memory) {\n        // Return the name of the contract.\n        assembly {\n            mstore(0, 0x20)\n            mstore(0x27, 0x07536561706f7274)\n            return(0, 0x60)\n        }\n    }\n}\n```\n \n\nFunction `_name()` is supposed to return \"Seaport\". The ABI encoded data has offset as the first 32 bytes (`0x20` is the offset). The offset has info length `7` followed by \"Seaport\" (`0x536561706f7274` but padded with zeros on the right).\n\nProperly encoded data would look like this:\n```\n0000000000000000000000000000000000000000000000000000000000000020\n0000000000000000000000000000000000000000000000000000000000000007\n536561706f727400000000000000000000000000000000000000000000000000\n```\n\nThe final `mstore(0x27, ...)` only writes to memory regions `[0x29, 0x49)`. The remainder will likely contain junk. You can expect the actual data to be:\n```\n0000000000000000000000000000000000000000000000000000000000000020\n0000000000000000000000000000000000000000000000000000000000000007\n536561706f72740?????????????????????????????????????????????????\n```\n\n\n\nBecause `0x40` points to the free memory pointer, you can expect the value `mload(0x40)` to be a relatively small number. So only the last few least significant bits would be set, with the rest to be `0` in usual cases. \n\n\n### Proof of Concept\n```diff\nmodified   test/foundry/FulfillBasicOrderTest.sol\n@@ -32,6 +32,25 @@ contract FulfillBasicOrderTest is BaseOrderTest {\n         uint128 tokenAmount;\n     }\n \n+    function testName() public {\n+        string memory name = consideration.name();\n+        uint rds;\n+        uint a;\n+        uint b;\n+        bytes32 c;\n+        assembly {\n+            rds := returndatasize()\n+            returndatacopy(mload(0x40), 0, returndatasize())\n+            a := mload(mload(0x40))\n+            b := mload(add(mload(0x40), 32))\n+            c := mload(add(mload(0x40), 64))\n+        }\n+        emit log_named_uint(\"returndatasize: \", rds);\n+        emit log_named_uint(\"offset: \", a);\n+        emit log_named_uint(\"length: \", b);\n+        emit log_named_bytes32(\"data: \", c);\n+    }\n+\n```\n\nreturns:\n```\n  returndatasize: : 96\n  offset: : 32\n  length: : 13\n  data: : 0x436f6e73696465726174696f6e00000000000000000000000000000000000080\n```\n\nThe final `80` character is the initial value of the free memory pointer!\n\n### Recommended Mitigation Steps\n\nClean the last word properly. This requires an additional `mstore`.\n\n#### HEVM confirmation\n\nhevm confirms this.<br>\nFile 1:\n\n```solidity\n// SPDX-License-Identifier: MIT\npragma solidity >=0.8.13;\n\ncontract Seaport {\n\tfunction f() public pure returns (string memory) {\n\t\treturn _name();\n\t}\n\n    function _name() internal pure returns (string memory) {\n        // Return the name of the contract.\n        assembly {\n            mstore(0, 0x20)\n            mstore(0x27, 0x07536561706f7274)\n            return(0, 0x60)\n        }\n    }\n}\n```\n\nFile 2:\n```solidity\n// SPDX-License-Identifier: MIT\npragma solidity >=0.8.13;\n\ncontract Seaport {\n\tfunction f() public pure returns (string memory) {\n\t\treturn _nameString();\n\t}\n\n    function _nameString() internal pure returns (string memory) {\n        // Return the name of the contract.\n        return \"Seaport\";\n    }\n}\n```\n\nRun:\n```\nhevm equivalence --code-a $(cat seaport1.bin) --code-b $(cat seaport2.bin)\nNot equal!\nCounterexample:\nCalldata:\n0x26121ff0000000000000000000000000000000\nCaller:\n0x0000000000000000000000000000000000000000\nCallvalue:\n0\n```\n\nAdding `mstore(0x49, 0x00)` before the return in the first version makes it work:\n\n```\nhevm equivalence --code-a $(cat seaport1.bin) --code-b $(cat seaport2.bin)\nExplored: 4 execution paths of A and: 4 paths of B.\nNo discrepancies found.\n```\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2022-05-opensea-seaport",
  "Code": [
    {
      "filename": "contracts/Seaport.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.13;\n\nimport { Consideration } from \"./lib/Consideration.sol\";\n\n/**\n * @title Seaport\n * @author 0age\n * @custom:coauthor d1ll0n\n * @custom:coauthor transmissions11\n * @custom:version 1\n * @notice Seaport is a generalized ETH/ERC20/ERC721/ERC1155 marketplace. It\n *         minimizes external calls to the greatest extent possible and provides\n *         lightweight methods for common routes as well as more flexible\n *         methods for composing advanced orders or groups of orders. Each order\n *         contains an arbitrary number of items that may be spent (the \"offer\")\n *         along with an arbitrary number of items that must be received back by\n *         the indicated recipients (the \"consideration\").\n */\ncontract Seaport is Consideration {\n    /**\n     * @notice Derive and set hashes, reference chainId, and associated domain\n     *         separator during deployment.\n     *\n     * @param conduitController A contract that deploys conduits, or proxies\n     *                          that may optionally be used to transfer approved\n     *                          ERC20/721/1155 tokens.\n     */\n    constructor(address conduitController) Consideration(conduitController) {}\n\n    /**\n     * @dev Internal pure function to retrieve and return the name of this\n     *      contract.\n     *\n     * @return The name of this contract.\n     */\n    function _name() internal pure override returns (string memory) {\n        // Return the name of the contract.\n        assembly {\n            mstore(0, 0x20)\n            mstore(0x27, 0x07536561706f7274)\n            return(0, 0x60)\n        }\n    }\n\n    /**\n     * @dev Internal pure function to retrieve the name of this contract as a\n     *      string that will be used to derive the name hash in the constructor.\n     *\n     * @return The name of this contract as a string.\n     */\n    function _nameString() internal pure override returns (string memory) {\n        // Return the name of the contract.\n        return \"Seaport\";\n    }\n}"
    }
  ]
}