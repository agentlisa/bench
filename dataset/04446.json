{
  "Title": "Privileged roles can have only one member",
  "Content": "The [`AddressRegistry` contract](https://github.com/InstaDApp/contract-v2/blob/4863c0c4156af7ded9cdb38b66e5f5e527c4a6d0/contracts/InstaRegistry.sol#L9) has a mapping named `registry` that maps (hashes of) a role to the address that has that role. Importantly, this means that each role can have only one address that has that role.\n\n\nTwo important roles are the `admin` and `owner` roles, which are the only two roles who have access to functions with the `isAdmin` modifier. The `setAddress` function has the `isAdmin` modifier and can be used to create new roles. It can also be used to change the address to which an existing role maps. Any revocation of roles must be done via the `setAddress` function — for example, by setting the address of a role to the zero address.\n\n\nThese access controls work as intended but are limiting in the sense that only one address can have each role. The `admin` and `owner` roles have identical power in the existing contracts. It seems as though having these two different roles is a way to hack around the limitation that each role type can have only one address associated with it.\n\n\nConsider using a more flexible role-management pattern that would allow more than one address of each role type, such as OpenZeppelin’s [Role Library](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v2.3.0/contracts/access/Roles.sol). You can see an example of how to use it in the [MinterRole contract](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v2.3.0/contracts/access/roles/MinterRole.sol).\n\n\nThis approach would allow for more flexibility when you want to have multiple addresses with the same role.\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/InstaRegistry.sol",
      "content": "pragma solidity ^0.5.2;\n\nimport \"./UserWallet.sol\";\n\n\n/// @title AddressRegistry\n/// @notice \n/// @dev \ncontract AddressRegistry {\n    event LogSetAddress(string name, address addr);\n\n    /// @notice Registry of role and address\n    mapping(bytes32 => address) registry;\n\n    /**\n     * @dev Check if msg.sender is admin or owner.\n     */\n    modifier isAdmin() {\n        require(\n            msg.sender == getAddress(\"admin\") || \n            msg.sender == getAddress(\"owner\"),\n            \"permission-denied\"\n        );\n        _;\n    }\n\n    /// @dev Get the address from system registry \n    /// @param _name (string)\n    /// @return  (address) Returns address based on role\n    function getAddress(string memory _name) public view returns(address) {\n        return registry[keccak256(abi.encodePacked(_name))];\n    }\n\n    /// @dev Set new address in system registry \n    /// @param _name (string) Role name\n    /// @param _userAddress (string) User Address\n    function setAddress(string memory _name, address _userAddress) public isAdmin {\n        registry[keccak256(abi.encodePacked(_name))] = _userAddress;\n        emit LogSetAddress(_name, _userAddress);\n    }\n}\n\n\n/// @title LogicRegistry\n/// @notice\n/// @dev LogicRegistry \ncontract LogicRegistry is AddressRegistry {\n\n    event LogEnableStaticLogic(address logicAddress);\n    event LogEnableLogic(address logicAddress);\n    event LogDisableLogic(address logicAddress);\n\n    /// @notice Map of static proxy state\n    mapping(address => bool) public logicProxiesStatic;\n    \n    /// @notice Map of logic proxy state\n    mapping(address => bool) public logicProxies;\n\n    /// @dev \n    /// @param _logicAddress (address)\n    /// @return  (bool)\n    function logic(address _logicAddress) public view returns (bool) {\n        if (logicProxiesStatic[_logicAddress] || logicProxies[_logicAddress]) {\n            return true;\n        }\n        return false;\n    }\n\n    /// @dev \n    /// @param _logicAddress (address)\n    /// @return  (bool)\n    function logicStatic(address _logicAddress) public view returns (bool) {\n        if (logicProxiesStatic[_logicAddress]) {\n            return true;\n        }\n        return false;\n    }\n\n    /// @dev Sets the static logic proxy to true\n    /// static proxies mostly contains the logic for withdrawal of assets\n    /// and can never be false to freely let user withdraw their assets\n    /// @param _logicAddress (address)\n    function enableStaticLogic(address _logicAddress) public isAdmin {\n        logicProxiesStatic[_logicAddress] = true;\n        emit LogEnableStaticLogic(_logicAddress);\n    }\n\n    /// @dev Enable logic proxy address\n    /// @param _logicAddress (address)\n    function enableLogic(address _logicAddress) public isAdmin {\n        logicProxies[_logicAddress] = true;\n        emit LogEnableLogic(_logicAddress);\n    }\n\n    /// @dev Disable logic proxy address\n    /// @param _logicAddress (address)\n    function disableLogic(address _logicAddress) public isAdmin {\n        logicProxies[_logicAddress] = false;\n        emit LogDisableLogic(_logicAddress);\n    }\n\n}\n\n\n/**\n * @dev Deploys a new proxy instance and sets msg.sender as owner of proxy\n */\ncontract WalletRegistry is LogicRegistry {\n    \n    event Created(address indexed sender, address indexed owner, address proxy);\n    event LogRecord(address indexed currentOwner, address indexed nextOwner, address proxy);\n    \n    /// @notice Address to UserWallet proxy map\n    mapping(address => UserWallet) public proxies;\n    \n    /// @dev Deploys a new proxy instance and sets custom owner of proxy\n    /// Throws if the owner already have a UserWallet\n    /// @return proxy ()\n    function build() public returns (UserWallet proxy) {\n        proxy = build(msg.sender);\n    }\n\n    /// @dev update the proxy record whenever owner changed on any proxy\n    /// Throws if msg.sender is not a proxy contract created via this contract\n    /// @return proxy () UserWallet\n    function build(address _owner) public returns (UserWallet proxy) {\n        require(proxies[_owner] == UserWallet(0), \"multiple-proxy-per-user-not-allowed\");\n        proxy = new UserWallet();\n        proxies[address(this)] = proxy; // will be changed via record() in next line execution\n        proxy.setOwner(_owner);\n        emit Created(msg.sender, _owner, address(proxy));\n    }\n\n    /// @dev Transafers ownership\n    /// @param _currentOwner (address) Current Owner\n    /// @param _nextOwner (address) Next Owner\n    function record(address _currentOwner, address _nextOwner) public {\n        require(msg.sender == address(proxies[_currentOwner]), \"invalid-proxy-or-owner\");\n        require(proxies[_nextOwner] == UserWallet(0), \"multiple-proxy-per-user-not-allowed\");\n        proxies[_nextOwner] = proxies[_currentOwner];\n        proxies[_currentOwner] = UserWallet(0);\n        emit LogRecord(_currentOwner, _nextOwner, address(proxies[_nextOwner]));\n    }\n\n}\n\n\n/// @title InstaRegistry\n/// @dev Initializing Registry\ncontract InstaRegistry is WalletRegistry {\n\n    constructor() public {\n        registry[keccak256(abi.encodePacked(\"admin\"))] = msg.sender;\n        registry[keccak256(abi.encodePacked(\"owner\"))] = msg.sender;\n    }\n}"
    }
  ]
}