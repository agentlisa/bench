{
  "Title": "Wrong Value Emitted in Event",
  "Content": "The `TokenRatioUpdated` [event](https://github.com/mantlenetworkio/mantle-v2/blob/e29d360904db5e5ec81888885f7b7250f8255895/packages/contracts-bedrock/contracts/L2/GasPriceOracle.sol#L87) is emitted in the `GasPriceOracle` contract everytime the `tokenRatio` variable is updated to a new value. Its parameters are the previous and the new value being set. However, the event [incorrectly](https://github.com/mantlenetworkio/mantle-v2/blob/e29d360904db5e5ec81888885f7b7250f8255895/packages/contracts-bedrock/contracts/L2/GasPriceOracle.sol#L84-L88) emits the new token ratio twice instead of emitting the previous token ratio followed by the new token ratio.\n\n\nConsider assigning the `previousTokenRatio` variable to the current token ratio instead of the used input parameter.\n\n\n***Update:** Resolved in [pull request #138](https://github.com/mantlenetworkio/mantle-v2/pull/138) at commit [572600a](https://github.com/mantlenetworkio/mantle-v2/tree/572600a4f163e10ae6049a9826855dd86f4e93e7).*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "packages/contracts-bedrock/contracts/L2/GasPriceOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.15;\n\nimport { Semver } from \"../universal/Semver.sol\";\nimport { Predeploys } from \"../libraries/Predeploys.sol\";\nimport { L1Block } from \"../L2/L1Block.sol\";\n\n/**\n * @custom:proxied\n * @custom:predeploy 0x420000000000000000000000000000000000000F\n * @title GasPriceOracle\n * @notice This contract maintains the variables responsible for computing the L1 portion of the\n *         total fee charged on L2. Before Bedrock, this contract held variables in state that were\n *         read during the state transition function to compute the L1 portion of the transaction\n *         fee. After Bedrock, this contract now simply proxies the L1Block contract, which has\n *         the values used to compute the L1 portion of the fee in its state.\n *\n *         The contract exposes an API that is useful for knowing how large the L1 portion of the\n *         transaction fee will be. The following events were deprecated with Bedrock:\n *         - event OverheadUpdated(uint256 overhead);\n *         - event ScalarUpdated(uint256 scalar);\n *         - event DecimalsUpdated(uint256 decimals);\n */\ncontract GasPriceOracle is Semver {\n    /**\n     * @notice Number of decimals used in the scalar.\n     */\n    uint256 public constant DECIMALS = 6;\n    uint256 public tokenRatio;\n    address public owner;\n    address public operator;\n\n    /**\n     * @custom:semver 1.0.0\n     */\n    constructor() Semver(1, 0, 0) {}\n\n    /**********\n    * Events *\n    **********/\n    event TokenRatioUpdated(uint256 indexed previousTokenRatio, uint256 indexed newTokenRatio);\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n    event OperatorUpdated(address indexed previousOperator, address indexed newOperator);\n\n    /**********\n    * Modifiers *\n    **********/\n    modifier onlyOwner() {\n        require(owner == msg.sender, \"Caller is not the owner\");\n        _;\n    }\n    modifier onlyOperator() {\n        require(operator == msg.sender, \"Caller is not the operator\");\n        _;\n    }\n\n    /**\n    * Allows the owner to modify the operator.\n    * @param _operator New operator\n     */\n    // slither-disable-next-line external-function\n    function setOperator(address _operator) public onlyOwner {\n        address previousOperator = operator;\n        operator = _operator;\n        emit OperatorUpdated(previousOperator, operator);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`_owner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address _owner) public onlyOwner {\n        require(_owner != address(0), \"new owner is the zero address\");\n        address previousOwner = owner;\n        owner = _owner;\n        emit OwnershipTransferred(previousOwner, owner);\n    }\n\n    /**\n    * Allows the operator to modify the token ratio.\n    * @param _tokenRatio New tokenRatio\n     */\n    // slither-disable-next-line external-function\n    function setTokenRatio(uint256 _tokenRatio) public onlyOperator {\n        uint256 previousTokenRatio = _tokenRatio;\n        tokenRatio = _tokenRatio;\n        emit TokenRatioUpdated(previousTokenRatio, tokenRatio);\n    }\n\n    /**\n     * @notice Computes the L1 portion of the fee based on the size of the rlp encoded input\n     *         transaction, the current L1 base fee, and the various dynamic parameters.\n     *\n     * @param _data Unsigned fully RLP-encoded transaction to get the L1 fee for.\n     *\n     * @return L1 fee that should be paid for the tx\n     */\n    function getL1Fee(bytes memory _data) external view returns (uint256) {\n        uint256 l1GasUsed = getL1GasUsed(_data);\n        uint256 l1Fee = l1GasUsed * l1BaseFee();\n        uint256 divisor = 10**DECIMALS;\n        uint256 unscaled = l1Fee * scalar();\n        uint256 scaled = unscaled / divisor;\n        return scaled;\n    }\n\n    /**\n     * @notice Retrieves the current gas price (base fee).\n     *\n     * @return Current L2 gas price (base fee).\n     */\n    function gasPrice() public view returns (uint256) {\n        return block.basefee;\n    }\n\n    /**\n     * @notice Retrieves the current base fee.\n     *\n     * @return Current L2 base fee.\n     */\n    function baseFee() public view returns (uint256) {\n        return block.basefee;\n    }\n\n    /**\n     * @notice Retrieves the current fee overhead.\n     *\n     * @return Current fee overhead.\n     */\n    function overhead() public view returns (uint256) {\n        return L1Block(Predeploys.L1_BLOCK_ATTRIBUTES).l1FeeOverhead();\n    }\n\n    /**\n     * @notice Retrieves the current fee scalar.\n     *\n     * @return Current fee scalar.\n     */\n    function scalar() public view returns (uint256) {\n        return L1Block(Predeploys.L1_BLOCK_ATTRIBUTES).l1FeeScalar();\n    }\n\n    /**\n     * @notice Retrieves the latest known L1 base fee.\n     *\n     * @return Latest known L1 base fee.\n     */\n    function l1BaseFee() public view returns (uint256) {\n        return L1Block(Predeploys.L1_BLOCK_ATTRIBUTES).basefee();\n    }\n\n    /**\n     * @custom:legacy\n     * @notice Retrieves the number of decimals used in the scalar.\n     *\n     * @return Number of decimals used in the scalar.\n     */\n    function decimals() public pure returns (uint256) {\n        return DECIMALS;\n    }\n\n    /**\n     * @notice Computes the amount of L1 gas used for a transaction. Adds the overhead which\n     *         represents the per-transaction gas overhead of posting the transaction and state\n     *         roots to L1. Adds 68 bytes of padding to account for the fact that the input does\n     *         not have a signature.\n     *\n     * @param _data Unsigned fully RLP-encoded transaction to get the L1 gas for.\n     *\n     * @return Amount of L1 gas used to publish the transaction.\n     */\n    function getL1GasUsed(bytes memory _data) public view returns (uint256) {\n        uint256 total = 0;\n        uint256 length = _data.length;\n        for (uint256 i = 0; i < length; i++) {\n            if (_data[i] == 0) {\n                total += 4;\n            } else {\n                total += 16;\n            }\n        }\n        uint256 unsigned = total + overhead();\n        return unsigned + (68 * 16);\n    }\n}"
    }
  ]
}