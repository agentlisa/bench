{
  "Title": "Unsafe cast",
  "Content": "The `OracleRouterBase` contract [uses an unsafe cast](https://github.com/OriginProtocol/origin-dollar/blob/508921bd39f988fa61b60cea372b910725ff7bd0/contracts/contracts/oracle/OracleRouter.sol#L43) to convert the signed price to a `uint256` type. Consider ensuring the price is positive before performing the type conversion. If desired, this could be achieved with OpenZeppelin's [`SafeCast` library](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v4.8.3/contracts/utils/math/SafeCast.sol#L559).\n\n\n***Update:** Resolved in [pull request #1500](https://github.com/OriginProtocol/origin-dollar/pull/1500) at commit [97a2116](https://github.com/OriginProtocol/origin-dollar/pull/1500/commits/97a2116be354f4f497b0892d38172ba0c90922ee).*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/contracts/oracle/OracleRouter.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.0;\n\nimport \"../interfaces/chainlink/AggregatorV3Interface.sol\";\nimport { IOracle } from \"../interfaces/IOracle.sol\";\nimport { Helpers } from \"../utils/Helpers.sol\";\nimport { StableMath } from \"../utils/StableMath.sol\";\n\nabstract contract OracleRouterBase is IOracle {\n    using StableMath for uint256;\n\n    uint256 constant MIN_DRIFT = 0.7e18;\n    uint256 constant MAX_DRIFT = 1.3e18;\n    address constant FIXED_PRICE = 0x0000000000000000000000000000000000000001;\n    mapping(address => uint8) internal decimalsCache;\n\n    /**\n     * @dev The price feed contract to use for a particular asset.\n     * @param asset address of the asset\n     * @return address address of the price feed for the asset\n     */\n    function feed(address asset) internal view virtual returns (address);\n\n    /**\n     * @notice Returns the total price in 18 digit unit for a given asset.\n     * @param asset address of the asset\n     * @return uint256 unit price for 1 asset unit, in 18 decimal fixed\n     */\n    function price(address asset)\n        external\n        view\n        virtual\n        override\n        returns (uint256)\n    {\n        address _feed = feed(asset);\n        require(_feed != address(0), \"Asset not available\");\n        require(_feed != FIXED_PRICE, \"Fixed price feeds not supported\");\n        (, int256 _iprice, , , ) = AggregatorV3Interface(_feed)\n            .latestRoundData();\n        uint8 decimals = getDecimals(asset);\n\n        uint256 _price = uint256(_iprice).scaleBy(18, decimals);\n        if (isStablecoin(asset)) {\n            require(_price <= MAX_DRIFT, \"Oracle: Price exceeds max\");\n            require(_price >= MIN_DRIFT, \"Oracle: Price under min\");\n        }\n        return uint256(_price);\n    }\n\n    function getDecimals(address _asset) internal view virtual returns (uint8) {\n        uint8 decimals = decimalsCache[_asset];\n        require(decimals > 0, \"Oracle: Decimals not cached\");\n        return decimals;\n    }\n\n    function cacheDecimals(address _asset) external returns (uint8) {\n        address _feed = feed(_asset);\n        require(_feed != address(0), \"Asset not available\");\n        require(_feed != FIXED_PRICE, \"Fixed price feeds not supported\");\n\n        uint8 decimals = AggregatorV3Interface(_feed).decimals();\n        decimalsCache[_asset] = decimals;\n        return decimals;\n    }\n\n    function isStablecoin(address _asset) internal view returns (bool) {\n        string memory symbol = Helpers.getSymbol(_asset);\n        bytes32 symbolHash = keccak256(abi.encodePacked(symbol));\n        return\n            symbolHash == keccak256(abi.encodePacked(\"DAI\")) ||\n            symbolHash == keccak256(abi.encodePacked(\"USDC\")) ||\n            symbolHash == keccak256(abi.encodePacked(\"USDT\"));\n    }\n}\n\ncontract OracleRouter is OracleRouterBase {\n    /**\n     * @dev The price feed contract to use for a particular asset.\n     * @param asset address of the asset\n     */\n    function feed(address asset) internal pure override returns (address) {\n        if (asset == 0x6B175474E89094C44Da98b954EedeAC495271d0F) {\n            // Chainlink: DAI/USD\n            return 0xAed0c38402a5d19df6E4c03F4E2DceD6e29c1ee9;\n        } else if (asset == 0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48) {\n            // Chainlink: USDC/USD\n            return 0x8fFfFfd4AfB6115b954Bd326cbe7B4BA576818f6;\n        } else if (asset == 0xdAC17F958D2ee523a2206206994597C13D831ec7) {\n            // Chainlink: USDT/USD\n            return 0x3E7d1eAB13ad0104d2750B8863b489D65364e32D;\n        } else if (asset == 0xc00e94Cb662C3520282E6f5717214004A7f26888) {\n            // Chainlink: COMP/USD\n            return 0xdbd020CAeF83eFd542f4De03e3cF0C28A4428bd5;\n        } else if (asset == 0x7Fc66500c84A76Ad7e9c93437bFc5Ac33E2DDaE9) {\n            // Chainlink: AAVE/USD\n            return 0x547a514d5e3769680Ce22B2361c10Ea13619e8a9;\n        } else if (asset == 0xD533a949740bb3306d119CC777fa900bA034cd52) {\n            // Chainlink: CRV/USD\n            return 0xCd627aA160A6fA45Eb793D19Ef54f5062F20f33f;\n        } else if (asset == 0x4e3FBD56CD56c3e72c1403e103b45Db9da5B9D2B) {\n            // Chainlink: CVX/USD\n            return 0xd962fC30A72A84cE50161031391756Bf2876Af5D;\n        } else if (asset == 0xae78736Cd615f374D3085123A210448E74Fc6393) {\n            // Chainlink: rETH/ETH\n            return 0x536218f9E9Eb48863970252233c8F271f554C2d0;\n        } else if (asset == 0xBe9895146f7AF43049ca1c1AE358B0541Ea49704) {\n            // Chainlink: cbETH/ETH\n            return 0xF017fcB346A1885194689bA23Eff2fE6fA5C483b;\n        } else if (asset == 0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84) {\n            // Chainlink: stETH/ETH\n            return 0x86392dC19c0b719886221c78AB11eb8Cf5c52812;\n        } else if (asset == 0x5E8422345238F34275888049021821E8E08CAa1f) {\n            // FIXED_PRICE: frxETH/ETH\n            return FIXED_PRICE;\n        } else if (asset == 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2) {\n            // FIXED_PRICE: WETH/ETH\n            return FIXED_PRICE;\n        } else {\n            revert(\"Asset not available\");\n        }\n    }\n}\n\ncontract OETHOracleRouter is OracleRouter {\n    using StableMath for uint256;\n\n    /**\n     * @notice Returns the total price in 18 digit units for a given asset.\n     *         This implementation does not (!) do range checks as the\n     *         parent OracleRouter does.\n     * @param asset address of the asset\n     * @return uint256 unit price for 1 asset unit, in 18 decimal fixed\n     */\n    function price(address asset)\n        external\n        view\n        virtual\n        override\n        returns (uint256)\n    {\n        address _feed = feed(asset);\n        if (_feed == FIXED_PRICE) {\n            return 1e18;\n        }\n        require(_feed != address(0), \"Asset not available\");\n        (, int256 _iprice, , , ) = AggregatorV3Interface(_feed)\n            .latestRoundData();\n\n        uint8 decimals = getDecimals(asset);\n        uint256 _price = uint256(_iprice).scaleBy(18, decimals);\n        return _price;\n    }\n}\n\ncontract OracleRouterDev is OracleRouterBase {\n    mapping(address => address) public assetToFeed;\n\n    function setFeed(address _asset, address _feed) external {\n        assetToFeed[_asset] = _feed;\n    }\n\n    /*\n     * The dev version of the Oracle doesn't need to gas optimize and cache the decimals\n     */\n    function getDecimals(address _asset)\n        internal\n        view\n        override\n        returns (uint8)\n    {\n        address _feed = feed(_asset);\n        require(_feed != address(0), \"Asset not available\");\n        require(_feed != FIXED_PRICE, \"Fixed price feeds not supported\");\n\n        return AggregatorV3Interface(_feed).decimals();\n    }\n\n    /**\n     * @dev The price feed contract to use for a particular asset.\n     * @param asset address of the asset\n     */\n    function feed(address asset) internal view override returns (address) {\n        return assetToFeed[asset];\n    }\n}"
    }
  ]
}