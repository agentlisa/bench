{
  "Title": "[18] Players lose their current voltage when using a battery",
  "Content": "\n*Note: At the judgeâ€™s request [here](https://github.com/code-423n4/2024-02-ai-arena-findings/issues/1695#issuecomment-1978456865), this downgraded issue from the same warden has been included in this report for completeness.*\n\nhttps://github.com/code-423n4/2024-02-ai-arena/blob/cd1a0e6d1b40168657d1aaee8223dc050e15f8cc/src/VoltageManager.sol#L93-L99\n\nWhen players deplete their voltage through battles, they have the option to replenish it using a **voltage battery**. Although the maximum voltage capacity is 100, and each battle reduces it by 10, the `useVoltageBattery()` function resets the voltage to 100, disregarding any remaining voltage a player has.\n\n```sol\nownerVoltage[msg.sender] = 100;\n```\n\nFrom a design point of view it looks good to have the number 100 as max and 0 as min value for voltage. But in this situation it is doing players a disservice.\n\n### Root cause \n\nThe core issue arises from the function overwriting the existing voltage value instead of incrementally adding to it.\n\n### Impact\n\n**Voltage batteries**, as valuable **paid** items, enable more frequent participation in ranking matches. However, players risk losing up to 90% of their replenished voltage due to the current implementation, leading to potential losses in NRN (the game's currency).\n\n### Scenario\n\nShould a player replenish their voltage without checking their current level, they risk using a battery without receiving its full value, thereby not maximizing the item's cost-effectiveness.\n\n### Proof of Concept\n\nIn `VoltageManager.t.sol`:\n```sol\nfunction testLoseVoltageFromBattery() public {\n  address player = _ownerAddress;\n  uint8 voltageSpendAmount = 10;\n  _mintGameItemForReceiver(player);\n  uint256 currentVoltage = _voltageManagerContract.ownerVoltage(player);\n  assertEq(currentVoltage, 0);\n\n  // battery gives full 100 voltage\n  _voltageManagerContract.useVoltageBattery();\n  _voltageManagerContract.spendVoltage(player, voltageSpendAmount);\n  assertEq(_voltageManagerContract.ownerVoltage(player), 100 - voltageSpendAmount); // 10 voltage just spent\n\n  // Call use battery again, for whatever reason\n  _voltageManagerContract.useVoltageBattery();\n  // battery gave only 10 voltage\n  assertEq(_voltageManagerContract.ownerVoltage(player), 100);\n}\n```\n\n### Suggested Mitigation\nModify the function to add the voltage battery's value to the existing voltage, rather than resetting it:\n```diff\nfunction useVoltageBattery() public {\n...\n- ownerVoltage[msg.sender] = 100;\n+ ownerVoltage[msg.sender] += 100;\n...\n}\n```\n\n**[hickuphh3 (judge) commented](https://github.com/code-423n4/2024-02-ai-arena-findings/issues/1695#issuecomment-1978456865):**\n> **01**: Good, refactor (explained the impact)\n> **02:** Refactor\n> **03:** Refactor\n> **04:** Refactor\n> **05:** Low\n> **06:** Refactor\n> **07:** Non-critical\n> **08:** Refactor\n> **09/14:** Refactor/Non-critical\n> **10:** Refactor\n> **11/12:** Low/Refactor\n> **13:** Refactor/Non-critical\n> **15:** Refactor\n> **16:** Non-critical\n> **17:** Non-critical\n> **18:** Low\n> \n> Overall very good and targeted recommendations, found them rather meaningful.\n\n**[brandinho (AI Arena) acknowledged](https://github.com/code-423n4/2024-02-ai-arena-findings/issues/1695#issuecomment-2018920154)**\n\n\n\n***\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2024-02-ai-arena",
  "Code": [
    {
      "filename": "src/VoltageManager.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0 <0.9.0;\n\nimport { GameItems } from \"./GameItems.sol\";\n\n/// @title VoltageManager \n/// @author ArenaX Labs Inc.\n/// @notice Manages the voltage system for AI Arena\ncontract VoltageManager {\n\n    /*//////////////////////////////////////////////////////////////\n                                EVENTS\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Event emitted when voltage amount is altered.\n    event VoltageRemaining(address spender, uint8 voltage);  \n\n    /*//////////////////////////////////////////////////////////////\n                            STATE VARIABLES\n    //////////////////////////////////////////////////////////////*/\n\n    /// The address that has owner privileges (initially the contract deployer).\n    address _ownerAddress;\n    \n    /// @dev The game items contract instance.\n    GameItems _gameItemsContractInstance;\n\n    /*//////////////////////////////////////////////////////////////\n                                MAPPINGS\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Maps the address to the allowed voltage spenders.\n    mapping(address => bool) public allowedVoltageSpenders;\n\n    /// @notice Maps the address to the voltage replenish time.\n    mapping(address => uint32) public ownerVoltageReplenishTime;\n\n    /// @notice Maps the address to the voltage.\n    mapping(address => uint8) public ownerVoltage;\n\n    /// @notice Mapping of address to admin status.\n    mapping(address => bool) public isAdmin;\n\n    /*//////////////////////////////////////////////////////////////\n                                CONSTRUCTOR\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Sets the owner address and instantiates the Game Items contract.\n    /// @param ownerAddress The address of the owner.\n    /// @param gameItemsContractAddress The address of the game items contract.\n    constructor(address ownerAddress, address gameItemsContractAddress) {\n        _ownerAddress = ownerAddress;\n        _gameItemsContractInstance = GameItems(gameItemsContractAddress);\n        isAdmin[_ownerAddress] = true;\n    } \n\n    /*//////////////////////////////////////////////////////////////\n                            EXTERNAL FUNCTIONS\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Transfers ownership from one address to another.\n    /// @dev Only the owner address is authorized to call this function.\n    /// @param newOwnerAddress The address of the new owner\n    function transferOwnership(address newOwnerAddress) external {\n        require(msg.sender == _ownerAddress);\n        _ownerAddress = newOwnerAddress;\n    }\n\n    /// @notice Adjusts admin access for a user.\n    /// @dev Only the owner address is authorized to call this function.\n    /// @param adminAddress The address of the admin.\n    /// @param access Whether the address has admin access or not.\n    function adjustAdminAccess(address adminAddress, bool access) external {\n        require(msg.sender == _ownerAddress);\n        isAdmin[adminAddress] = access;\n    }  \n\n    /// @notice Adjusts whether a given address is allowed to spend voltage\n    /// @dev Only admins are authorized to call this function.\n    /// @param allowedVoltageSpender The address of the allowed voltage spender.\n    /// @param allowed Whether the spender is allowed or not\n    function adjustAllowedVoltageSpenders(address allowedVoltageSpender, bool allowed) external {\n        require(isAdmin[msg.sender]);\n        allowedVoltageSpenders[allowedVoltageSpender] = allowed;\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                            PUBLIC FUNCTIONS\n    //////////////////////////////////////////////////////////////*/    \n\n    /// @notice Uses a voltage battery to replenish voltage for a player in AI Arena.\n    /// @dev This function can be called by any user to use the voltage battery.\n    function useVoltageBattery() public {\n        require(ownerVoltage[msg.sender] < 100);\n        require(_gameItemsContractInstance.balanceOf(msg.sender, 0) > 0);\n        _gameItemsContractInstance.burn(msg.sender, 0, 1);\n        ownerVoltage[msg.sender] = 100;\n        emit VoltageRemaining(msg.sender, ownerVoltage[msg.sender]);\n    }\n\n    /// @notice Spends voltage.\n    /// @dev This function can be called by the user or an allowed voltage spender.\n    /// @param spender The address of the spender.\n    /// @param voltageSpent The amount of voltage to spend.\n    function spendVoltage(address spender, uint8 voltageSpent) public {\n        require(spender == msg.sender || allowedVoltageSpenders[msg.sender]);\n        if (ownerVoltageReplenishTime[spender] <= block.timestamp) {\n            _replenishVoltage(spender);\n        }\n        ownerVoltage[spender] -= voltageSpent;\n        emit VoltageRemaining(spender, ownerVoltage[spender]);\n    }\n\n    /// @notice Replenishes voltage and sets the replenish time to 1 day from now\n    /// @dev This function is called internally to replenish the voltage for the owner.\n    /// @param owner The address of the owner\n    function _replenishVoltage(address owner) private {\n        ownerVoltage[owner] = 100;\n        ownerVoltageReplenishTime[owner] = uint32(block.timestamp + 1 days);\n    }    \n}"
    }
  ]
}