{
  "Title": "No time to cancel a malicious proposal in AdminVoting",
  "Content": "##### Description\n\n- https://github.com/prisma-fi/prisma-contracts/blob/c0122d27677cd4e1aaee7f1e21f807ccadf46ac8/contracts/dao/AdminVoting.sol#L211\n\nA malicious whale can create a proposal, wait for `MIN_TIME_TO_EXECUTION + 1` (currently 24 hours), and then vote and execute it in the same block, leaving no time for a guardian to cancel it.\n\nIt becomes possible because of `MIN_TIME_TO_EXECUTION < VOTING_PERIOD` and there are only two requirements to execute a proposal:\n```\nrequire(proposal.currentWeight >= proposal.requiredWeight, \"Not passed\");\nrequire(proposal.createdAt + MIN_TIME_TO_EXECUTION < block.timestamp,\n\"MIN_TIME_TO_EXECUTION\");\n```\n\n##### Recommendation\n\nWe recommend adding a delay between the point in time when the proposal gains enough votes for execution and the execution itself.\n",
  "Impact": "MEDIUM",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/dao/AdminVoting.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity 0.8.19;\n\nimport \"@openzeppelin/contracts/utils/Address.sol\";\nimport \"../dependencies/DelegatedOps.sol\";\nimport \"../dependencies/SystemStart.sol\";\nimport \"../interfaces/ITokenLocker.sol\";\n\n/**\n    @title Prisma DAO Admin Voter\n    @notice Primary ownership contract for all Prisma contracts. Allows executing\n            arbitrary function calls only after a required percentage of PRISMA\n            lockers have signalled in favor of performing the action.\n */\ncontract AdminVoting is DelegatedOps, SystemStart {\n    using Address for address;\n\n    event ProposalCreated(address indexed account, Action[] payload, uint256 week, uint256 requiredWeight);\n    event ProposalExecuted(uint256 proposalId);\n    event ProposalCancelled(uint256 proposalId);\n    event VoteCast(address indexed account, uint256 id, uint256 weight, uint256 proposalCurrentWeight);\n    event ProposalCreationMinWeightSet(uint256 weight);\n    event ProposalPassingPctSet(uint256 pct);\n\n    struct Proposal {\n        uint16 week; // week which vote weights are based upon\n        uint32 createdAt; // timestamp when the proposal was created\n        uint40 currentWeight; //  amount of weight currently voting in favor\n        uint40 requiredWeight; // amount of weight required for the proposal to be executed\n        bool processed; // set to true once the proposal is processed\n    }\n\n    struct Action {\n        address target;\n        bytes data;\n    }\n\n    uint256 public constant VOTING_PERIOD = 1 weeks;\n    uint256 public constant MIN_TIME_TO_EXECUTION = 86400;\n\n    ITokenLocker public immutable tokenLocker;\n    IPrismaCore public immutable prismaCore;\n\n    Proposal[] proposalData;\n    mapping(uint256 => Action[]) proposalPayloads;\n\n    // account -> ID -> amount of weight voted in favor\n    mapping(address => mapping(uint256 => uint256)) public accountVoteWeights;\n\n    // absolute amount of weight required to create a new proposal\n    uint256 public minCreateProposalWeight;\n    // percent of total weight that must vote for a proposal before it can be executed\n    uint256 public passingPct;\n\n    constructor(\n        address _prismaCore,\n        ITokenLocker _tokenLocker,\n        uint256 _minCreateProposalWeight,\n        uint256 _passingPct\n    ) SystemStart(_prismaCore) {\n        tokenLocker = _tokenLocker;\n        prismaCore = IPrismaCore(_prismaCore);\n\n        minCreateProposalWeight = _minCreateProposalWeight;\n        passingPct = _passingPct;\n    }\n\n    /**\n        @notice The total number of votes created\n     */\n    function getProposalCount() external view returns (uint256) {\n        return proposalData.length;\n    }\n\n    /**\n        @notice Gets information on a specific proposal\n     */\n    function getProposalData(\n        uint256 id\n    )\n        external\n        view\n        returns (\n            uint256 week,\n            uint256 createdAt,\n            uint256 currentWeight,\n            uint256 requiredWeight,\n            bool executed,\n            bool canExecute,\n            Action[] memory payload\n        )\n    {\n        Proposal memory proposal = proposalData[id];\n        payload = proposalPayloads[id];\n        canExecute = (!proposal.processed &&\n            proposal.currentWeight >= proposal.requiredWeight &&\n            proposal.createdAt + MIN_TIME_TO_EXECUTION < block.timestamp);\n\n        return (\n            proposal.week,\n            proposal.createdAt,\n            proposal.currentWeight,\n            proposal.requiredWeight,\n            proposal.processed,\n            canExecute,\n            payload\n        );\n    }\n\n    /**\n        @notice Create a new proposal\n        @param payload Tuple of [(target address, calldata), ... ] to be\n                       executed if the proposal is passed.\n     */\n    function createNewProposal(address account, Action[] calldata payload) external callerOrDelegated(account) {\n        require(payload.length > 0, \"Empty payload\");\n\n        // week is set at -1 to the active week so that weights are finalized\n        uint256 week = getWeek();\n        require(week > 0, \"No proposals in first week\");\n        week -= 1;\n\n        uint256 accountWeight = tokenLocker.getAccountWeightAt(account, week);\n        require(accountWeight >= minCreateProposalWeight, \"Not enough weight to propose\");\n        uint256 totalWeight = tokenLocker.getTotalWeightAt(week);\n        uint40 requiredWeight = uint40((totalWeight * passingPct) / 100);\n        uint256 idx = proposalData.length;\n        proposalData.push(\n            Proposal({\n                week: uint16(week),\n                createdAt: uint32(block.timestamp),\n                currentWeight: 0,\n                requiredWeight: requiredWeight,\n                processed: false\n            })\n        );\n\n        for (uint256 i = 0; i < payload.length; i++) {\n            proposalPayloads[idx].push(payload[i]);\n        }\n        emit ProposalCreated(account, payload, week, requiredWeight);\n    }\n\n    /**\n        @notice Vote in favor of a proposal\n        @dev Each account can vote once per proposal\n        @param id Proposal ID\n        @param weight Weight to allocate to this action. If set to zero, the full available\n                      account weight is used. Integrating protocols may wish to use partial\n                      weight to reflect partial support from their own users.\n     */\n    function voteForProposal(address account, uint256 id, uint256 weight) external callerOrDelegated(account) {\n        require(id < proposalData.length, \"Invalid ID\");\n        require(accountVoteWeights[account][id] == 0, \"Already voted\");\n\n        Proposal memory proposal = proposalData[id];\n        require(!proposal.processed, \"Proposal already processed\");\n        require(proposal.createdAt + VOTING_PERIOD > block.timestamp, \"Voting period has closed\");\n\n        uint256 accountWeight = tokenLocker.getAccountWeightAt(account, proposal.week);\n        if (weight == 0) {\n            weight = accountWeight;\n            require(weight > 0, \"No vote weight\");\n        } else {\n            require(weight <= accountWeight, \"Weight exceeds account weight\");\n        }\n\n        accountVoteWeights[account][id] = weight;\n        uint40 updatedWeight = uint40(proposal.currentWeight + weight);\n        proposalData[id].currentWeight = updatedWeight;\n        emit VoteCast(account, id, weight, updatedWeight);\n    }\n\n    /**\n        @notice Cancels a pending proposal\n        @dev Can only be called by the guardian to avoid malicious proposals\n             Guardians cannot cancel a proposal for their replacement\n        @param id Proposal ID\n     */\n    function cancelProposal(uint256 id) external {\n        require(msg.sender == prismaCore.guardian(), \"Only guardian can cancel proposals\");\n        require(id < proposalData.length, \"Invalid ID\");\n        // We make sure guardians cannot cancel proposals for their replacement\n        Action[] storage payload = proposalPayloads[id];\n        Action memory firstAction = payload[0];\n        bytes memory data = firstAction.data;\n        // Extract the call sig from payload data\n        bytes4 sig;\n        assembly {\n            sig := mload(add(data, 0x20))\n        }\n        require(\n            firstAction.target != address(prismaCore) || sig != IPrismaCore.setGuardian.selector,\n            \"Guardian replacement not cancellable\"\n        );\n        proposalData[id].processed = true;\n        emit ProposalCancelled(id);\n    }\n\n    /**\n        @notice Execute a proposal's payload\n        @dev Can only be called if the proposal has received sufficient vote weight,\n             and has been active for at least `MIN_TIME_TO_EXECUTION`\n        @param id Proposal ID\n     */\n    function executeProposal(uint256 id) external {\n        require(id < proposalData.length, \"Invalid ID\");\n        Proposal memory proposal = proposalData[id];\n        require(proposal.currentWeight >= proposal.requiredWeight, \"Not passed\");\n        require(proposal.createdAt + MIN_TIME_TO_EXECUTION < block.timestamp, \"MIN_TIME_TO_EXECUTION\");\n        require(!proposal.processed, \"Already processed\");\n        proposalData[id].processed = true;\n\n        Action[] storage payload = proposalPayloads[id];\n        uint256 payloadLength = payload.length;\n\n        for (uint256 i = 0; i < payloadLength; i++) {\n            payload[i].target.functionCall(payload[i].data);\n        }\n        emit ProposalExecuted(id);\n    }\n\n    /**\n        @notice Set the minimum absolute weight required to create a new proposal\n        @dev Only callable via a passing proposal that includes a call\n             to this contract and function within it's payload\n     */\n    function setMinCreateProposalWeight(uint256 weight) external returns (bool) {\n        require(msg.sender == address(this), \"Only callable via proposal\");\n        minCreateProposalWeight = weight;\n        emit ProposalCreationMinWeightSet(weight);\n        return true;\n    }\n\n    /**\n        @notice Set the required % of the total weight that must vote\n                for a proposal prior to being able to execute it\n        @dev Only callable via a passing proposal that includes a call\n             to this contract and function within it's payload\n     */\n    function setPassingPct(uint256 pct) external returns (bool) {\n        require(msg.sender == address(this), \"Only callable via proposal\");\n        require(pct <= 100, \"Invalid value\");\n        passingPct = pct;\n        emit ProposalPassingPctSet(pct);\n        return true;\n    }\n\n    /**\n        @dev Unguarded method to allow accepting ownership transfer of `PrismaCore`\n             at the end of the deployment sequence\n     */\n    function acceptTransferOwnership() external {\n        prismaCore.acceptTransferOwnership();\n    }\n}"
    }
  ]
}