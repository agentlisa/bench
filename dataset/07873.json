{
  "Title": "[M-04] Very critical Owner privileges can cause complete destruction of the project in a possible privateKey exploit",
  "Content": "# Lines of code\n\nhttps://github.com/code-423n4/2022-10-traderjoe/blob/main/src/libraries/PendingOwnable.sol#L42\n\n\n# Vulnerability details\n\n### Vulnerability details\nTypically, the contract’s owner is the account that deploys the contract. As a result, the owner is able to perform certain privileged activities.\n\nHowever, Owner privileges are numerous and there is no timelock structure in the process of using these privileges.\nThe Owner is assumed to be an EOA, since the documents do not provide information on whether the Owner will be a multisign structure.\n\nIn parallel with the private key thefts of the project owners, which have increased recently, this vulnerability has been stated as medium.\n\nSimilar vulnerability;\nPrivate keys stolen:\n\nHackers have stolen cryptocurrency worth around €552 million from a blockchain project linked to the popular online game Axie Infinity, in one of the largest cryptocurrency heists on record. Security issue : PrivateKey of the project officer was stolen:\nhttps://www.euronews.com/next/2022/03/30/blockchain-network-ronin-hit-by-552-million-crypto-heist\n\n\n### Proof of Concept\n\n`onlyOwner` powers;\n```js\n14 results - 2 files\n\nsrc/LBFactory.sol:\n  220:     function setLBPairImplementation(address _LBPairImplementation) external override onlyOwner {\n  322:     function setLBPairIgnored() external override onlyOwner {\n  355:     function setPreset() external override onlyOwner {\n  401:     function removePreset(uint16 _binStep) external override onlyOwner {\n  439:     function setFeesParametersOnPair) external override onlyOwner {\n  473:     function setFeeRecipient(address _feeRecipient) external override onlyOwner {\n  479:     function setFlashLoanFee(uint256 _flashLoanFee) external override onlyOwner {\n  490:     function setFactoryLockedState(bool _locked) external override onlyOwner {\n  498:     function addQuoteAsset(IERC20 _quoteAsset) external override onlyOwner {\n  507:     function removeQuoteAsset(IERC20 _quoteAsset) external override onlyOwner {\n  525:     function forceDecay(ILBPair _LBPair) external override onlyOwner {\n\nsrc/libraries/PendingOwnable.sol:\n  59:     function setPendingOwner(address pendingOwner_) public override onlyOwner {\n  68:     function revokePendingOwner() public override onlyOwner {\n  84:     function renounceOwnership() public override onlyOwner {\n\n\n\n```\n\n### Recommendation;\n\n1- A timelock contract should be added to use `onlyOwner` privileges. In this way, users can be warned in case of a possible security weakness.\n2- `onlyOwner` can be a Multisign wallet and this part is specified in the documentation\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-10-trader-joe-v2-contest",
  "Code": [
    {
      "filename": "src/libraries/PendingOwnable.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.0;\n\nimport \"../LBErrors.sol\";\nimport \"../interfaces/IPendingOwnable.sol\";\n\n/// @title Pending Ownable\n/// @author Trader Joe\n/// @notice Contract module which provides a basic access control mechanism, where\n/// there is an account (an owner) that can be granted exclusive access to\n/// specific functions. The ownership of this contract is transferred using the\n/// push and pull pattern, the current owner set a `pendingOwner` using\n/// {setPendingOwner} and that address can then call {becomeOwner} to become the\n/// owner of that contract. The main logic and comments comes from OpenZeppelin's\n/// Ownable contract.\n///\n/// By default, the owner account will be the one that deploys the contract. This\n/// can later be changed with {setPendingOwner} and {becomeOwner}.\n///\n/// This module is used through inheritance. It will make available the modifier\n/// `onlyOwner`, which can be applied to your functions to restrict their use to\n/// the owner\ncontract PendingOwnable is IPendingOwnable {\n    address private _owner;\n    address private _pendingOwner;\n\n    /// @notice Throws if called by any account other than the owner.\n    modifier onlyOwner() {\n        if (msg.sender != _owner) revert PendingOwnable__NotOwner();\n        _;\n    }\n\n    /// @notice Throws if called by any account other than the pending owner.\n    modifier onlyPendingOwner() {\n        if (msg.sender != _pendingOwner || msg.sender == address(0)) revert PendingOwnable__NotPendingOwner();\n        _;\n    }\n\n    /// @notice Initializes the contract setting the deployer as the initial owner\n    constructor() {\n        _transferOwnership(msg.sender);\n    }\n\n    /// @notice Returns the address of the current owner\n    /// @return The address of the current owner\n    function owner() public view override returns (address) {\n        return _owner;\n    }\n\n    /// @notice Returns the address of the current pending owner\n    /// @return The address of the current pending owner\n    function pendingOwner() public view override returns (address) {\n        return _pendingOwner;\n    }\n\n    /// @notice Sets the pending owner address. This address will be able to become\n    /// the owner of this contract by calling {becomeOwner}\n    function setPendingOwner(address pendingOwner_) public override onlyOwner {\n        if (pendingOwner_ == address(0)) revert PendingOwnable__AddressZero();\n        if (_pendingOwner != address(0)) revert PendingOwnable__PendingOwnerAlreadySet();\n        _setPendingOwner(pendingOwner_);\n    }\n\n    /// @notice Revoke the pending owner address. This address will not be able to\n    /// call {becomeOwner} to become the owner anymore.\n    /// Can only be called by the owner\n    function revokePendingOwner() public override onlyOwner {\n        if (_pendingOwner == address(0)) revert PendingOwnable__NoPendingOwner();\n        _setPendingOwner(address(0));\n    }\n\n    /// @notice Transfers the ownership to the new owner (`pendingOwner).\n    /// Can only be called by the pending owner\n    function becomeOwner() public override onlyPendingOwner {\n        _transferOwnership(msg.sender);\n    }\n\n    /// @notice Leaves the contract without owner. It will not be possible to call\n    /// `onlyOwner` functions anymore. Can only be called by the current owner.\n    ///\n    /// NOTE: Renouncing ownership will leave the contract without an owner,\n    /// thereby removing any functionality that is only available to the owner.\n    function renounceOwnership() public override onlyOwner {\n        _transferOwnership(address(0));\n    }\n\n    /// @notice Transfers ownership of the contract to a new account (`newOwner`).\n    /// Internal function without access restriction.\n    /// @param _newOwner The address of the new owner\n    function _transferOwnership(address _newOwner) internal virtual {\n        address _oldOwner = _owner;\n        _owner = _newOwner;\n        _pendingOwner = address(0);\n        emit OwnershipTransferred(_oldOwner, _newOwner);\n    }\n\n    /// @notice Push the new owner, it needs to be pulled to be effective.\n    /// Internal function without access restriction.\n    /// @param pendingOwner_ The address of the new pending owner\n    function _setPendingOwner(address pendingOwner_) internal virtual {\n        _pendingOwner = pendingOwner_;\n        emit PendingOwnerSet(pendingOwner_);\n    }\n}"
    }
  ]
}