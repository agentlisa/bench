{
  "Title": "M-9: MarginAccountHelper will be bricked if registry.marginAccount or insuranceFund ever change",
  "Content": "# Issue M-9: MarginAccountHelper will be bricked if registry.marginAccount or insuranceFund ever change \n\nSource: https://github.com/sherlock-audit/2023-04-hubble-exchange-judging/issues/170 \n\n## Found by \n0x52, crimson-rat-reach\n## Summary\n\nMarginAccountHelper#syncDeps causes the contract to refresh it's references to both marginAccount and insuranceFund. The issue is that approvals are never made to the new contracts rendering them useless.\n\n## Vulnerability Detail\n\n[MarginAccountHelper.sol#L82-L87](https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/MarginAccountHelper.sol#L82-L87)\n\n    function syncDeps(address _registry) public onlyGovernance {\n        IRegistry registry = IRegistry(_registry);\n        vusd = IVUSD(registry.vusd());\n        marginAccount = IMarginAccount(registry.marginAccount());\n        insuranceFund = IInsuranceFund(registry.insuranceFund());\n    }\n\nWhen syncDeps is called the marginAccount and insuranceFund references are updated. All transactions require approvals to one of those two contract. Since no new approvals are made, the contract will become bricked and all transactions will revert.\n\n## Impact\n\nContract will become bricked and all contracts that are integrated or depend on it will also be bricked\n\n## Code Snippet\n\n[MarginAccountHelper.sol#L82-L87](https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/MarginAccountHelper.sol#L82-L87)\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\nRemove approvals to old contracts before changing and approve new contracts after\n\n\n\n## Discussion\n\n**asquare08**\n\nValid issue but we will be using `syncDeps` mainly during the deployment. Later on, since both `marginAccount` and `insuranceFund` are upgradeable contracts, their address won't change.\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/72",
  "Code": [
    {
      "filename": "hubble-protocol/contracts/MarginAccountHelper.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity 0.8.9;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { SafeERC20 } from \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport { VUSD } from \"./VUSD.sol\";\nimport { IMarginAccount, IInsuranceFund, IVUSD, IRegistry } from \"./Interfaces.sol\";\nimport { HubbleBase } from \"./legos/HubbleBase.sol\";\n\n/**\n * @title Helper contract for (un)wrapping tokens (vusd) before/after deposting/withdrawing from margin account/insurance fund\n * @notice USDC is both the gas token and the token backing vusd; which is the currency used in the margin account and insurance fund;\n * Therefore, this contract serves as a helper to (unwrap) usdc (gas token)\n*/\ncontract MarginAccountHelper is HubbleBase {\n    using SafeERC20 for IERC20;\n\n    uint constant VUSD_IDX = 0;\n\n    IMarginAccount public marginAccount;\n    IVUSD public vusd;\n    IInsuranceFund public insuranceFund;\n\n    uint256[50] private __gap;\n\n    function initialize(\n        address _governance,\n        address _vusd,\n        address _marginAccount,\n        address _insuranceFund\n    ) external initializer {\n        _setGovernace(_governance);\n        vusd = IVUSD(_vusd);\n        marginAccount = IMarginAccount(_marginAccount);\n        insuranceFund = IInsuranceFund(_insuranceFund);\n        IERC20(_vusd).safeApprove(_marginAccount, type(uint).max);\n        IERC20(_vusd).safeApprove(_insuranceFund, type(uint).max);\n    }\n\n    /**\n     * @notice Accepts gas token (usdc), wraps it for vusd and deposits it to margin account for the trader\n     * @param amount Amount of vusd to deposit. msg.value has to be exactly 1e12 times `amount`\n    */\n    function addVUSDMarginWithReserve(uint256 amount) external payable {\n        vusd.mintWithReserve{value: msg.value}(address(this), amount);\n        marginAccount.addMarginFor(VUSD_IDX, amount, msg.sender);\n    }\n\n    /**\n     * @notice Remove margin on trader's behalf, enter the withdrawal Q and process the withdrawals\n    */\n    function removeMarginInUSD(uint256 amount) external {\n        address trader = msg.sender;\n        marginAccount.removeMarginFor(VUSD_IDX, amount, trader);\n        vusd.withdrawTo(trader, amount);\n        vusd.processWithdrawals();\n    }\n\n    /**\n    * @notice Deposit vusd to insurance fund using gas token (usdc)\n    */\n    function depositToInsuranceFund(uint256 amount) external payable {\n        vusd.mintWithReserve{value: msg.value}(address(this), amount);\n        insuranceFund.depositFor(msg.sender, amount);\n    }\n\n    /**\n    * @notice Withdraw vusd from insurance fund and get gas token (usdc)\n    */\n    function withdrawFromInsuranceFund(uint256 shares) external {\n        address user = msg.sender;\n        uint amount = insuranceFund.withdrawFor(user, shares);\n        vusd.withdrawTo(user, amount);\n        vusd.processWithdrawals();\n    }\n\n    /* ****************** */\n    /*     Governance     */\n    /* ****************** */\n\n    function syncDeps(address _registry) public onlyGovernance {\n        IRegistry registry = IRegistry(_registry);\n        vusd = IVUSD(registry.vusd());\n        marginAccount = IMarginAccount(registry.marginAccount());\n        insuranceFund = IInsuranceFund(registry.insuranceFund());\n    }\n}"
    },
    {
      "filename": "hubble-protocol/contracts/MarginAccountHelper.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity 0.8.9;\n\nimport { IERC20 } from \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport { SafeERC20 } from \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport { VUSD } from \"./VUSD.sol\";\nimport { IMarginAccount, IInsuranceFund, IVUSD, IRegistry } from \"./Interfaces.sol\";\nimport { HubbleBase } from \"./legos/HubbleBase.sol\";\n\n/**\n * @title Helper contract for (un)wrapping tokens (vusd) before/after deposting/withdrawing from margin account/insurance fund\n * @notice USDC is both the gas token and the token backing vusd; which is the currency used in the margin account and insurance fund;\n * Therefore, this contract serves as a helper to (unwrap) usdc (gas token)\n*/\ncontract MarginAccountHelper is HubbleBase {\n    using SafeERC20 for IERC20;\n\n    uint constant VUSD_IDX = 0;\n\n    IMarginAccount public marginAccount;\n    IVUSD public vusd;\n    IInsuranceFund public insuranceFund;\n\n    uint256[50] private __gap;\n\n    function initialize(\n        address _governance,\n        address _vusd,\n        address _marginAccount,\n        address _insuranceFund\n    ) external initializer {\n        _setGovernace(_governance);\n        vusd = IVUSD(_vusd);\n        marginAccount = IMarginAccount(_marginAccount);\n        insuranceFund = IInsuranceFund(_insuranceFund);\n        IERC20(_vusd).safeApprove(_marginAccount, type(uint).max);\n        IERC20(_vusd).safeApprove(_insuranceFund, type(uint).max);\n    }\n\n    /**\n     * @notice Accepts gas token (usdc), wraps it for vusd and deposits it to margin account for the trader\n     * @param amount Amount of vusd to deposit. msg.value has to be exactly 1e12 times `amount`\n    */\n    function addVUSDMarginWithReserve(uint256 amount) external payable {\n        vusd.mintWithReserve{value: msg.value}(address(this), amount);\n        marginAccount.addMarginFor(VUSD_IDX, amount, msg.sender);\n    }\n\n    /**\n     * @notice Remove margin on trader's behalf, enter the withdrawal Q and process the withdrawals\n    */\n    function removeMarginInUSD(uint256 amount) external {\n        address trader = msg.sender;\n        marginAccount.removeMarginFor(VUSD_IDX, amount, trader);\n        vusd.withdrawTo(trader, amount);\n        vusd.processWithdrawals();\n    }\n\n    /**\n    * @notice Deposit vusd to insurance fund using gas token (usdc)\n    */\n    function depositToInsuranceFund(uint256 amount) external payable {\n        vusd.mintWithReserve{value: msg.value}(address(this), amount);\n        insuranceFund.depositFor(msg.sender, amount);\n    }\n\n    /**\n    * @notice Withdraw vusd from insurance fund and get gas token (usdc)\n    */\n    function withdrawFromInsuranceFund(uint256 shares) external {\n        address user = msg.sender;\n        uint amount = insuranceFund.withdrawFor(user, shares);\n        vusd.withdrawTo(user, amount);\n        vusd.processWithdrawals();\n    }\n\n    /* ****************** */\n    /*     Governance     */\n    /* ****************** */\n\n    function syncDeps(address _registry) public onlyGovernance {\n        IRegistry registry = IRegistry(_registry);\n        vusd = IVUSD(registry.vusd());\n        marginAccount = IMarginAccount(registry.marginAccount());\n        insuranceFund = IInsuranceFund(registry.insuranceFund());\n    }\n}"
    }
  ]
}