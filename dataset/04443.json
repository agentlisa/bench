{
  "Title": "Wallet creation transactions can be made to fail — Method 1",
  "Content": "An attacker can cause all honest users’ wallet-creation transactions to fail. The honest user will still end up with a functioning wallet that they control, but their wallet software (i.e., MetaMask) will display an error saying that their wallet-creation transaction was unsuccessful (reverted). The attack works as follows:\n\n\nAn attacker can front-run all calls to [`build()`](https://github.com/InstaDApp/contract-v2/blob/4863c0c4156af7ded9cdb38b66e5f5e527c4a6d0/contracts/InstaRegistry.sol#L119) or [`build(address _owner)`](https://github.com/InstaDApp/contract-v2/blob/4863c0c4156af7ded9cdb38b66e5f5e527c4a6d0/contracts/InstaRegistry.sol#L126) with their own call to [`build(address _owner)`](https://github.com/InstaDApp/contract-v2/blob/4863c0c4156af7ded9cdb38b66e5f5e527c4a6d0/contracts/InstaRegistry.sol#L126) with the `_owner` parameter set to the honest user’s address. This results in the honest user’s transaction being reverted [at line 127 of InstaRegistry.sol](https://github.com/InstaDApp/contract-v2/blob/4863c0c4156af7ded9cdb38b66e5f5e527c4a6d0/contracts/InstaRegistry.sol#L127).\n\n\nThe honest user will still end up with a functioning wallet that they control because the attacker’s transaction will have created it for them. However, the failed transaction message could be confusing for unsophisticated users, and could also increase the complexity for front-end developers who would have to handle that edge case.\n\n\nConsider restricting access to the [`build(address _owner)` function](https://github.com/InstaDApp/contract-v2/blob/4863c0c4156af7ded9cdb38b66e5f5e527c4a6d0/contracts/InstaRegistry.sol#L126) so that it may be called only from the InstaRegistry address and (optionally) a set of whitelisted third-party addresses.\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/InstaRegistry.sol",
      "content": "pragma solidity ^0.5.2;\n\nimport \"./UserWallet.sol\";\n\n\n/// @title AddressRegistry\n/// @notice \n/// @dev \ncontract AddressRegistry {\n    event LogSetAddress(string name, address addr);\n\n    /// @notice Registry of role and address\n    mapping(bytes32 => address) registry;\n\n    /**\n     * @dev Check if msg.sender is admin or owner.\n     */\n    modifier isAdmin() {\n        require(\n            msg.sender == getAddress(\"admin\") || \n            msg.sender == getAddress(\"owner\"),\n            \"permission-denied\"\n        );\n        _;\n    }\n\n    /// @dev Get the address from system registry \n    /// @param _name (string)\n    /// @return  (address) Returns address based on role\n    function getAddress(string memory _name) public view returns(address) {\n        return registry[keccak256(abi.encodePacked(_name))];\n    }\n\n    /// @dev Set new address in system registry \n    /// @param _name (string) Role name\n    /// @param _userAddress (string) User Address\n    function setAddress(string memory _name, address _userAddress) public isAdmin {\n        registry[keccak256(abi.encodePacked(_name))] = _userAddress;\n        emit LogSetAddress(_name, _userAddress);\n    }\n}\n\n\n/// @title LogicRegistry\n/// @notice\n/// @dev LogicRegistry \ncontract LogicRegistry is AddressRegistry {\n\n    event LogEnableStaticLogic(address logicAddress);\n    event LogEnableLogic(address logicAddress);\n    event LogDisableLogic(address logicAddress);\n\n    /// @notice Map of static proxy state\n    mapping(address => bool) public logicProxiesStatic;\n    \n    /// @notice Map of logic proxy state\n    mapping(address => bool) public logicProxies;\n\n    /// @dev \n    /// @param _logicAddress (address)\n    /// @return  (bool)\n    function logic(address _logicAddress) public view returns (bool) {\n        if (logicProxiesStatic[_logicAddress] || logicProxies[_logicAddress]) {\n            return true;\n        }\n        return false;\n    }\n\n    /// @dev \n    /// @param _logicAddress (address)\n    /// @return  (bool)\n    function logicStatic(address _logicAddress) public view returns (bool) {\n        if (logicProxiesStatic[_logicAddress]) {\n            return true;\n        }\n        return false;\n    }\n\n    /// @dev Sets the static logic proxy to true\n    /// static proxies mostly contains the logic for withdrawal of assets\n    /// and can never be false to freely let user withdraw their assets\n    /// @param _logicAddress (address)\n    function enableStaticLogic(address _logicAddress) public isAdmin {\n        logicProxiesStatic[_logicAddress] = true;\n        emit LogEnableStaticLogic(_logicAddress);\n    }\n\n    /// @dev Enable logic proxy address\n    /// @param _logicAddress (address)\n    function enableLogic(address _logicAddress) public isAdmin {\n        logicProxies[_logicAddress] = true;\n        emit LogEnableLogic(_logicAddress);\n    }\n\n    /// @dev Disable logic proxy address\n    /// @param _logicAddress (address)\n    function disableLogic(address _logicAddress) public isAdmin {\n        logicProxies[_logicAddress] = false;\n        emit LogDisableLogic(_logicAddress);\n    }\n\n}\n\n\n/**\n * @dev Deploys a new proxy instance and sets msg.sender as owner of proxy\n */\ncontract WalletRegistry is LogicRegistry {\n    \n    event Created(address indexed sender, address indexed owner, address proxy);\n    event LogRecord(address indexed currentOwner, address indexed nextOwner, address proxy);\n    \n    /// @notice Address to UserWallet proxy map\n    mapping(address => UserWallet) public proxies;\n    \n    /// @dev Deploys a new proxy instance and sets custom owner of proxy\n    /// Throws if the owner already have a UserWallet\n    /// @return proxy ()\n    function build() public returns (UserWallet proxy) {\n        proxy = build(msg.sender);\n    }\n\n    /// @dev update the proxy record whenever owner changed on any proxy\n    /// Throws if msg.sender is not a proxy contract created via this contract\n    /// @return proxy () UserWallet\n    function build(address _owner) public returns (UserWallet proxy) {\n        require(proxies[_owner] == UserWallet(0), \"multiple-proxy-per-user-not-allowed\");\n        proxy = new UserWallet();\n        proxies[address(this)] = proxy; // will be changed via record() in next line execution\n        proxy.setOwner(_owner);\n        emit Created(msg.sender, _owner, address(proxy));\n    }\n\n    /// @dev Transafers ownership\n    /// @param _currentOwner (address) Current Owner\n    /// @param _nextOwner (address) Next Owner\n    function record(address _currentOwner, address _nextOwner) public {\n        require(msg.sender == address(proxies[_currentOwner]), \"invalid-proxy-or-owner\");\n        require(proxies[_nextOwner] == UserWallet(0), \"multiple-proxy-per-user-not-allowed\");\n        proxies[_nextOwner] = proxies[_currentOwner];\n        proxies[_currentOwner] = UserWallet(0);\n        emit LogRecord(_currentOwner, _nextOwner, address(proxies[_nextOwner]));\n    }\n\n}\n\n\n/// @title InstaRegistry\n/// @dev Initializing Registry\ncontract InstaRegistry is WalletRegistry {\n\n    constructor() public {\n        registry[keccak256(abi.encodePacked(\"admin\"))] = msg.sender;\n        registry[keccak256(abi.encodePacked(\"owner\"))] = msg.sender;\n    }\n}"
    },
    {
      "filename": "contracts/InstaRegistry.sol",
      "content": "pragma solidity ^0.5.2;\n\nimport \"./UserWallet.sol\";\n\n\n/// @title AddressRegistry\n/// @notice \n/// @dev \ncontract AddressRegistry {\n    event LogSetAddress(string name, address addr);\n\n    /// @notice Registry of role and address\n    mapping(bytes32 => address) registry;\n\n    /**\n     * @dev Check if msg.sender is admin or owner.\n     */\n    modifier isAdmin() {\n        require(\n            msg.sender == getAddress(\"admin\") || \n            msg.sender == getAddress(\"owner\"),\n            \"permission-denied\"\n        );\n        _;\n    }\n\n    /// @dev Get the address from system registry \n    /// @param _name (string)\n    /// @return  (address) Returns address based on role\n    function getAddress(string memory _name) public view returns(address) {\n        return registry[keccak256(abi.encodePacked(_name))];\n    }\n\n    /// @dev Set new address in system registry \n    /// @param _name (string) Role name\n    /// @param _userAddress (string) User Address\n    function setAddress(string memory _name, address _userAddress) public isAdmin {\n        registry[keccak256(abi.encodePacked(_name))] = _userAddress;\n        emit LogSetAddress(_name, _userAddress);\n    }\n}\n\n\n/// @title LogicRegistry\n/// @notice\n/// @dev LogicRegistry \ncontract LogicRegistry is AddressRegistry {\n\n    event LogEnableStaticLogic(address logicAddress);\n    event LogEnableLogic(address logicAddress);\n    event LogDisableLogic(address logicAddress);\n\n    /// @notice Map of static proxy state\n    mapping(address => bool) public logicProxiesStatic;\n    \n    /// @notice Map of logic proxy state\n    mapping(address => bool) public logicProxies;\n\n    /// @dev \n    /// @param _logicAddress (address)\n    /// @return  (bool)\n    function logic(address _logicAddress) public view returns (bool) {\n        if (logicProxiesStatic[_logicAddress] || logicProxies[_logicAddress]) {\n            return true;\n        }\n        return false;\n    }\n\n    /// @dev \n    /// @param _logicAddress (address)\n    /// @return  (bool)\n    function logicStatic(address _logicAddress) public view returns (bool) {\n        if (logicProxiesStatic[_logicAddress]) {\n            return true;\n        }\n        return false;\n    }\n\n    /// @dev Sets the static logic proxy to true\n    /// static proxies mostly contains the logic for withdrawal of assets\n    /// and can never be false to freely let user withdraw their assets\n    /// @param _logicAddress (address)\n    function enableStaticLogic(address _logicAddress) public isAdmin {\n        logicProxiesStatic[_logicAddress] = true;\n        emit LogEnableStaticLogic(_logicAddress);\n    }\n\n    /// @dev Enable logic proxy address\n    /// @param _logicAddress (address)\n    function enableLogic(address _logicAddress) public isAdmin {\n        logicProxies[_logicAddress] = true;\n        emit LogEnableLogic(_logicAddress);\n    }\n\n    /// @dev Disable logic proxy address\n    /// @param _logicAddress (address)\n    function disableLogic(address _logicAddress) public isAdmin {\n        logicProxies[_logicAddress] = false;\n        emit LogDisableLogic(_logicAddress);\n    }\n\n}\n\n\n/**\n * @dev Deploys a new proxy instance and sets msg.sender as owner of proxy\n */\ncontract WalletRegistry is LogicRegistry {\n    \n    event Created(address indexed sender, address indexed owner, address proxy);\n    event LogRecord(address indexed currentOwner, address indexed nextOwner, address proxy);\n    \n    /// @notice Address to UserWallet proxy map\n    mapping(address => UserWallet) public proxies;\n    \n    /// @dev Deploys a new proxy instance and sets custom owner of proxy\n    /// Throws if the owner already have a UserWallet\n    /// @return proxy ()\n    function build() public returns (UserWallet proxy) {\n        proxy = build(msg.sender);\n    }\n\n    /// @dev update the proxy record whenever owner changed on any proxy\n    /// Throws if msg.sender is not a proxy contract created via this contract\n    /// @return proxy () UserWallet\n    function build(address _owner) public returns (UserWallet proxy) {\n        require(proxies[_owner] == UserWallet(0), \"multiple-proxy-per-user-not-allowed\");\n        proxy = new UserWallet();\n        proxies[address(this)] = proxy; // will be changed via record() in next line execution\n        proxy.setOwner(_owner);\n        emit Created(msg.sender, _owner, address(proxy));\n    }\n\n    /// @dev Transafers ownership\n    /// @param _currentOwner (address) Current Owner\n    /// @param _nextOwner (address) Next Owner\n    function record(address _currentOwner, address _nextOwner) public {\n        require(msg.sender == address(proxies[_currentOwner]), \"invalid-proxy-or-owner\");\n        require(proxies[_nextOwner] == UserWallet(0), \"multiple-proxy-per-user-not-allowed\");\n        proxies[_nextOwner] = proxies[_currentOwner];\n        proxies[_currentOwner] = UserWallet(0);\n        emit LogRecord(_currentOwner, _nextOwner, address(proxies[_nextOwner]));\n    }\n\n}\n\n\n/// @title InstaRegistry\n/// @dev Initializing Registry\ncontract InstaRegistry is WalletRegistry {\n\n    constructor() public {\n        registry[keccak256(abi.encodePacked(\"admin\"))] = msg.sender;\n        registry[keccak256(abi.encodePacked(\"owner\"))] = msg.sender;\n    }\n}"
    }
  ]
}