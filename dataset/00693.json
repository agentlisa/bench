{
  "Title": "Missing Docstrings",
  "Content": "Throughout the [codebase](https://github.com/matter-labs/era-contracts/tree/518bfff51085743dc85d2824d823aaf4bb3e82d8/), there are several parts that do not have docstrings:\n\n\n* The [`Admin.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/facets/Admin.sol#L14), [`Executor.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/facets/Executor.sol#L21), [`Getters.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/facets/Getters.sol#L19), and [`Mailbox.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/facets/Mailbox.sol#L27) contracts are missing docstrings for the `getName` constant.\n* The [`IAdmin.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/interfaces/IAdmin.sol), [`IExecutor.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/interfaces/IExecutor.sol), [`IGetters.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/interfaces/IGetters.sol), [`ILegacyGetters.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/interfaces/ILegacyGetters.sol), [`IMailbox.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/interfaces/IMailbox.sol), [`IVerifier.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/interfaces/IVerifier.sol), and [`IZkSync.sol`](https://github.com/matter-labs/era-contracts/blob/518bfff51085743dc85d2824d823aaf4bb3e82d8/ethereum/contracts/zksync/interfaces/IZkSync.sol) interfaces are missing docstrings for multiple function declarations.\n\n\nConsider thoroughly documenting all functions (and their parameters) that are part of any contract's public API. Functions implementing sensitive functionality, even if not public, should be clearly documented as well. When writing docstrings, consider following the [Ethereum Natural Specification Format](https://solidity.readthedocs.io/en/develop/natspec-format.html) (NatSpec).\n\n\n***Update:** Resolved in [pull request #127](https://github.com/matter-labs/era-contracts/pull/127) at commit [d4bd820](https://github.com/matter-labs/era-contracts/pull/127/commits/d4bd820cae13b43fd04869226055fdff5c9ea8e5).*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "ethereum/contracts/zksync/facets/Admin.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity 0.8.20;\n\nimport \"../interfaces/IAdmin.sol\";\nimport \"../libraries/Diamond.sol\";\nimport {L2_TX_MAX_GAS_LIMIT} from \"../Config.sol\";\nimport \"./Base.sol\";\n\n/// @title Admin Contract controls access rights for contract management.\n/// @author Matter Labs\n/// @custom:security-contact security@matterlabs.dev\ncontract AdminFacet is Base, IAdmin {\n    string public constant override getName = \"AdminFacet\";\n\n    /// @notice Starts the transfer of governor rights. Only the current governor can propose a new pending one.\n    /// @notice New governor can accept governor rights by calling `acceptGovernor` function.\n    /// @param _newPendingGovernor Address of the new governor\n    function setPendingGovernor(address _newPendingGovernor) external onlyGovernor {\n        // Save previous value into the stack to put it into the event later\n        address oldPendingGovernor = s.pendingGovernor;\n        // Change pending governor\n        s.pendingGovernor = _newPendingGovernor;\n        emit NewPendingGovernor(oldPendingGovernor, _newPendingGovernor);\n    }\n\n    /// @notice Accepts transfer of governor rights. Only pending governor can accept the role.\n    function acceptGovernor() external {\n        address pendingGovernor = s.pendingGovernor;\n        require(msg.sender == pendingGovernor, \"n4\"); // Only proposed by current governor address can claim the governor rights\n\n        address previousGovernor = s.governor;\n        s.governor = pendingGovernor;\n        delete s.pendingGovernor;\n\n        emit NewPendingGovernor(pendingGovernor, address(0));\n        emit NewGovernor(previousGovernor, pendingGovernor);\n    }\n\n    /// @notice Starts the transfer of admin rights. Only the current governor or admin can propose a new pending one.\n    /// @notice New admin can accept admin rights by calling `acceptAdmin` function.\n    /// @param _newPendingAdmin Address of the new admin\n    function setPendingAdmin(address _newPendingAdmin) external onlyGovernor {\n        // Save previous value into the stack to put it into the event later\n        address oldPendingAdmin = s.pendingAdmin;\n        // Change pending admin\n        s.pendingAdmin = _newPendingAdmin;\n        emit NewPendingAdmin(oldPendingAdmin, _newPendingAdmin);\n    }\n\n    /// @notice Accepts transfer of admin rights. Only pending admin can accept the role.\n    function acceptAdmin() external {\n        address pendingAdmin = s.pendingAdmin;\n        require(msg.sender == pendingAdmin, \"n4\"); // Only proposed by current admin address can claim the admin rights\n\n        address previousAdmin = s.admin;\n        s.admin = pendingAdmin;\n        delete s.pendingAdmin;\n\n        emit NewPendingAdmin(pendingAdmin, address(0));\n        emit NewAdmin(previousAdmin, pendingAdmin);\n    }\n\n    /// @notice Change validator status (active or not active)\n    /// @param _validator Validator address\n    /// @param _active Active flag\n    function setValidator(address _validator, bool _active) external onlyGovernorOrAdmin {\n        s.validators[_validator] = _active;\n        emit ValidatorStatusUpdate(_validator, _active);\n    }\n\n    /// @notice Change zk porter availability\n    /// @param _zkPorterIsAvailable The availability of zk porter shard\n    function setPorterAvailability(bool _zkPorterIsAvailable) external onlyGovernor {\n        // Change the porter availability\n        s.zkPorterIsAvailable = _zkPorterIsAvailable;\n        emit IsPorterAvailableStatusUpdate(_zkPorterIsAvailable);\n    }\n\n    /// @notice Change the max L2 gas limit for L1 -> L2 transactions\n    /// @param _newPriorityTxMaxGasLimit The maximum number of L2 gas that a user can request for L1 -> L2 transactions\n    function setPriorityTxMaxGasLimit(uint256 _newPriorityTxMaxGasLimit) external onlyGovernor {\n        require(_newPriorityTxMaxGasLimit <= L2_TX_MAX_GAS_LIMIT, \"n5\");\n\n        uint256 oldPriorityTxMaxGasLimit = s.priorityTxMaxGasLimit;\n        s.priorityTxMaxGasLimit = _newPriorityTxMaxGasLimit;\n        emit NewPriorityTxMaxGasLimit(oldPriorityTxMaxGasLimit, _newPriorityTxMaxGasLimit);\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                            UPGRADE EXECUTION\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Executes a proposed governor upgrade\n    /// @dev Only the current governor can execute the upgrade\n    /// @param _diamondCut The diamond cut parameters to be executed\n    function executeUpgrade(Diamond.DiamondCutData calldata _diamondCut) external onlyGovernor {\n        Diamond.diamondCut(_diamondCut);\n        emit ExecuteUpgrade(_diamondCut);\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                            CONTRACT FREEZING\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Instantly pause the functionality of all freezable facets & their selectors\n    /// @dev Only the governance mechanism may freeze Diamond Proxy\n    function freezeDiamond() external onlyGovernor {\n        Diamond.DiamondStorage storage diamondStorage = Diamond.getDiamondStorage();\n\n        require(!diamondStorage.isFrozen, \"a9\"); // diamond proxy is frozen already\n        diamondStorage.isFrozen = true;\n\n        emit Freeze();\n    }\n\n    /// @notice Unpause the functionality of all freezable facets & their selectors\n    /// @dev Both the governor and its owner can unfreeze Diamond Proxy\n    function unfreezeDiamond() external onlyGovernorOrAdmin {\n        Diamond.DiamondStorage storage diamondStorage = Diamond.getDiamondStorage();\n\n        require(diamondStorage.isFrozen, \"a7\"); // diamond proxy is not frozen\n        diamondStorage.isFrozen = false;\n\n        emit Unfreeze();\n    }\n}"
    }
  ]
}