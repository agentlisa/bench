{
  "Title": "M-6: No clear threshold on when the oracle is updated will cause stale prices to be accepted",
  "Content": "# Issue M-6: No clear threshold on when the oracle is updated will cause stale prices to be accepted \n\nSource: https://github.com/sherlock-audit/2023-04-unitasprotocol-judging/issues/150 \n\n## Found by \n0xGoodess, 0xJuda, Juntao, Norah, PRAISE, PawelK, carrotsmuggler, ctf\\_sec, kutugu, mau, stopthecap, thekmj, toshii\n## Summary\nNo clear threshold on when the oracle is updated will cause stale prices to be accepted\n\n## Vulnerability Detail\n\nAccording to the team, the oracle is updated depending on the gas fees. \n\n\"\" the oracle price update is limited by cost of gas fee during updating. we want to have a balance between reaching the true price vs saving the cost. Once we move to cheaper chains like arbitrum or matic, we will update the oracle a lot more frequently\njust fyi \"\"\n\nNot having a clear point in time when the oracle will be updated, will cause that in times when the network is very expensive to include transactions, stale prices of assets/stables will be accepted as the current price, causing wrong/stale prices be fetched as if they were the latest.\n\n## Impact\nWrong/stale prices will be used in times when gas fees are very expensive\n\n## Code Snippet8\n\nhttps://github.com/sherlock-audit/2023-04-unitasprotocol/blob/d5328421bea80e3b0fd4595e4eb6b732a40e421e/Unitas-Protocol/src/XOracle.sol#L34-L46\n\n## Tool used\n\nManual Review\n\n## Recommendation\nAdd a clear threshold when the prices should be updated (ex. each 2 minutes), potentially not using the gas fees as an indicator to when updating the oracle.\n\n\n\n## Discussion\n\n**SunXrex**\n\nFixed: https://github.com/xrex-inc/Unitas-Protocol/pull/9\n\n**0xffff11**\n\nFix looks good. \n\nThe new  implementation includes a default threshold of 1 day and a threshold according to each token that unitas uses, which can be changed with a specific setter function. If a threshold is not added for whatever reason or token, it will return the default 1 day threshold.\n\nIf the threshold is less than `type(uint32).max` , basically always, the stale price will be checked and validated:\n\n```solidity\n if (threshold < type(uint32).max) {\n            uint64 priceTimestamp = prices[asset].timestamp;\n            _require(\n                priceTimestamp > block.timestamp || block.timestamp - priceTimestamp <= threshold,\n                Errors.PRICE_STALE\n            );\n ```\n\n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/73",
  "Code": [
    {
      "filename": "Unitas-Protocol/src/XOracle.sol",
      "content": "// SPDX-License-Identifier: BSD-3-Clause\npragma solidity ^0.8.19;\n\nimport \"@openzeppelin/contracts/access/AccessControl.sol\";\nimport \"./interfaces/IOracle.sol\";\n\ncontract XOracle is AccessControl {\n    bytes32 public constant FEEDER_ROLE = keccak256(\"FEEDER_ROLE\");\n    bytes32 public constant GUARDIAN_ROLE = keccak256(\"GUARDIAN_ROLE\");\n    mapping (address => IOracle.Price) public prices;\n\n    event newPrice(address indexed _asset, uint64 _timestamp, uint256 _price);\n\n    constructor() {\n        // Grant the contract deployer the default admin role: it will be able\n        // to grant and revoke any roles\n        _setRoleAdmin(GUARDIAN_ROLE, GUARDIAN_ROLE);\n        _setRoleAdmin(FEEDER_ROLE, GUARDIAN_ROLE);\n        \n        _grantRole(GUARDIAN_ROLE, msg.sender);\n        _grantRole(FEEDER_ROLE, msg.sender);\n    }\n\n    // ========================= FEEDER FUNCTIONS ====================================\n\n    function putPrice(address asset, uint64 timestamp, uint256 price) public onlyRole(FEEDER_ROLE) {\n        uint64 prev_timestamp = prices[asset].timestamp;\n        uint256 prev_price = prices[asset].price;\n        require(prev_timestamp < timestamp, \"Outdated timestamp\");\n        prices[asset] = IOracle.Price(asset, timestamp, prev_timestamp, price, prev_price);\n        emit newPrice(asset, timestamp, price);\n    }\n\n    function updatePrices(IOracle.NewPrice[] calldata _array) external onlyRole(FEEDER_ROLE) {\n        uint256 arrLength = _array.length;\n        for(uint256 i=0; i<arrLength; ){\n            address asset = _array[i].asset;\n            uint64 timestamp = _array[i].timestamp;\n            uint256 price = _array[i].price;\n            putPrice(asset, timestamp, price);\n            unchecked {\n                i++;\n            }\n        }\n    }\n\n    // ========================= VIEW FUNCTIONS ====================================\n\n    function getPrice(address asset) public view returns (uint64, uint64, uint256, uint256) {\n        return (\n            prices[asset].timestamp,\n            prices[asset].prev_timestamp,\n            prices[asset].price,\n            prices[asset].prev_price\n        );\n    }\n\n    function getLatestPrice(address asset) public view returns (uint256) {\n        return prices[asset].price;\n    }\n\n    // ========================= PURE FUNCTIONS ====================================\n\n    function decimals() public pure returns (uint8) {\n        return 18;\n    }\n}"
    }
  ]
}