{
  "Title": "[M-03] Chainlink's `latestRoundData` might return stale or incorrect results",
  "Content": "_Submitted by WatchPug, also found by cmichel, defsec, and ye0lde_\n\n<https://github.com/code-423n4/2021-12-perennial/blob/fd7c38823833a51ae0c6ae3856a3d93a7309c0e4/protocol/contracts/oracle/ChainlinkOracle.sol#L50-L60>\n\n```solidity\nfunction sync() public {\n    (, int256 feedPrice, , uint256 timestamp, ) = feed.latestRoundData();\n    Fixed18 price = Fixed18Lib.ratio(feedPrice, SafeCast.toInt256(_decimalOffset));\n\n    if (priceAtVersion.length == 0 || timestamp > timestampAtVersion[currentVersion()] + minDelay) {\n        priceAtVersion.push(price);\n        timestampAtVersion.push(timestamp);\n\n        emit Version(currentVersion(), timestamp, price);\n    }\n}\n```\n\nOn `ChainlinkOracle.sol`, we are using `latestRoundData`, but there is no check if the return value indicates stale data. This could lead to stale prices according to the Chainlink documentation:\n\n*   <https://docs.chain.link/docs/historical-price-data/#historical-rounds>\n*   <https://docs.chain.link/docs/faq/#how-can-i-check-if-the-answer-to-a-round-is-being-carried-over-from-a-previous-round>\n\n#### Recommendation\n\nConsider adding missing checks for stale data.\n\nFor example:\n\n```solidity\n(uint80 roundID, int256 feedPrice, , uint256 timestamp, uint80 answeredInRound) = feed.latestRoundData();\nrequire(feedPrice > 0, \"Chainlink price <= 0\"); \nrequire(answeredInRound >= roundID, \"Stale price\");\nrequire(timestamp != 0, \"Round not complete\");\n```\n\n**[kbrizzle (Perennial) confirmed](https://github.com/code-423n4/2021-12-perennial-findings/issues/24)**\n\n**[Alex the Entreprenerd (judge) commented](https://github.com/code-423n4/2021-12-perennial-findings/issues/24#issuecomment-1001295232):**\n > Agree with the finding, while you can get started with Chainlink's feed can be used with one line of code, in a production environment it is best to ensure that the data is fresh and within rational bounds\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/reports/2021-12-perennial",
  "Code": [
    {
      "filename": "protocol/contracts/oracle/ChainlinkOracle.sol",
      "content": "// SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.10;\n\nimport \"@openzeppelin/contracts/utils/math/SafeCast.sol\";\nimport \"../interfaces/IOracle.sol\";\nimport \"../utils/types/UFixed18.sol\";\nimport \"../utils/unstructured/UOwnable.sol\";\n\n/**\n * @title ChainlinkOracle\n * @notice Chainlink implementation of the IOracle interface.\n * @dev One instance per Chainlink price feed should be deployed. Multiple products may use the same\n *      ChainlinkOracle instance if their payoff functions are based on the same underlying oracle.\n */\ncontract ChainlinkOracle is IOracle, UOwnable {\n\n    event MinDelayUpdated(uint256 newMinDelay);\n\n    /// @dev Chainlink price feed to read from\n    IChainlinkFeed public feed;\n\n    /// @dev Mapping of historical price at each oracle version\n    Fixed18[] public priceAtVersion;\n\n    /// @dev Mapping of historical timestamp at each oracle version\n    uint256[] public timestampAtVersion;\n\n    /// @dev Decimal offset used to normalize chainlink price to 18 decimals\n    uint256 private _decimalOffset;\n\n    /// @dev Minimum timestamp delay before committed a new version\n    uint256 public minDelay;\n\n    /**\n     * @notice Initializes the contract state\n     * @param feed_ Chainlink price feed\n     */\n    constructor(IChainlinkFeed feed_) {\n        feed = feed_;\n        _decimalOffset = 10 ** feed_.decimals();\n        minDelay = 30 minutes;\n\n        sync();\n        UOwnable__initialize();\n    }\n\n    /**\n     * @notice Checks for a new price and updates the oracle version if one is found\n     */\n    function sync() public {\n        (, int256 feedPrice, , uint256 timestamp, ) = feed.latestRoundData();\n        Fixed18 price = Fixed18Lib.ratio(feedPrice, SafeCast.toInt256(_decimalOffset));\n\n        if (priceAtVersion.length == 0 || timestamp > timestampAtVersion[currentVersion()] + minDelay) {\n            priceAtVersion.push(price);\n            timestampAtVersion.push(timestamp);\n\n            emit Version(currentVersion(), timestamp, price);\n        }\n    }\n\n    /**\n     * @notice Returns the current oracle version\n     * @return Current oracle version\n     */\n    function currentVersion() public view returns (uint256) {\n        return priceAtVersion.length - 1;\n    }\n\n    /**\n     * @notice Updates the minimum delay before a new version can be committed\n     * @param newMinDelay New minimum delay\n     */\n    function updateMinDelay(uint256 newMinDelay) onlyOwner external {\n        minDelay = newMinDelay;\n        emit MinDelayUpdated(newMinDelay);\n    }\n}\n\ninterface IChainlinkFeed {\n    function decimals() external view returns (uint8);\n    function latestRoundData() external view returns (\n        uint80 roundID,\n        int price,\n        uint startedAt,\n        uint timeStamp,\n        uint80 answeredInRound\n    );\n}"
    }
  ]
}