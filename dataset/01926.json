{
  "Title": "[L-01] Quorum initialized to 0.03% instead of 30% due to overridden denominator",
  "Content": "When `OptimismGovernorV5.sol` is initialized, the `quorumNumerator` is set to `30`:\n\n```solidity\nfunction initialize(IVotesUpgradeable _votingToken, address _manager) public initializer {\n__Governor_init(\"Optimism\");\n__GovernorCountingSimple_init();\n__GovernorVotes_init(_votingToken);\n__GovernorVotesQuorumFraction_init({quorumNumeratorValue: 30});\n__GovernorSettings_init({initialVotingDelay: 6575, initialVotingPeriod: 46027, initialProposalThreshold: 0});\n\nmanager = _manager;\n}\n```\n\nThis value is intended to represent a 30% quorum when the denominator is set to 100, which is the default value set by OpenZeppelin and is represented in [GovernorVotesQuorumFractionUpgradeableV2.sol#L68-L70](https://github.com/voteagora/optimism-gov/blob/35f441738bd7864bd37949a40842486bc0ac51b0/src/lib/v2/GovernorVotesQuorumFractionUpgradeableV2.sol#L68)\n\n```solidity\nfunction quorumDenominator() public view virtual returns (uint256) {\nreturn 100;\n}\n```\n\nHowever, this value is overriden in `OptimismGovernorV5.sol`:\n\n```solidity\nfunction quorumDenominator() public view virtual override returns (uint256) {\n// Configurable to 3 decimal points of percentage\nreturn 100_000;\n}\n```\n\nWhen quorum is calculated, we perform the following math:\n\n```solidity\nfunction quorum(uint256 blockNumber) public view virtual override returns (uint256) {\nreturn (token.getPastTotalSupply(blockNumber) * quorumNumerator(blockNumber)) / quorumDenominator();\n}\n```\n\nThe result is that the quorum is represented as `tokenSupply * 30 / 100_000 = tokenSupply * 0.0003`, or 0.03% of supply.\n\n**Proof of Concept**\n\nThe following test can be dropped in to `OptimismGovernorV5.t.sol` to show the error:\n\n```solidity\nfunction testZachQuorumCalculationIncorrect() public {\nuint256 snapshot = block.number + governor.votingDelay();\nvm.roll(snapshot + 1);\n\nconsole2.log(op.getPastTotalSupply(snapshot));\nconsole2.log(governor.quorum(snapshot));\n}\n```\n\n```\nLogs:\n101000000000000000000 // total supply\n30300000000000000 // quorum (= 0.03% of total supply)\n```\n\n**Recommendation**\n\nChange the value set in in the `initialize()` function to `30_000` to represent 30%:\n\n````diff\n```solidity\nfunction initialize(IVotesUpgradeable _votingToken, address _manager) public initializer {\n__Governor_init(\"Optimism\");\n__GovernorCountingSimple_init();\n__GovernorVotes_init(_votingToken);\n-   __GovernorVotesQuorumFraction_init({quorumNumeratorValue: 30});\n+   __GovernorVotesQuorumFraction_init({quorumNumeratorValue: 30_000});\n__GovernorSettings_init({initialVotingDelay: 6575, initialVotingPeriod: 46027, initialProposalThreshold: 0});\n\nmanager = _manager;\n}\n````\n\n**Review**\n\nFixed by removing the `initialize()` function (since the proxy has already been initialized and is just being upgraded) in [6aa306ea5df526bd49e88073daa0da27c5b56e5e](https://github.com/voteagora/optimism-gov/commit/6aa306ea5df526bd49e88073daa0da27c5b56e5e)",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "src/lib/v2/GovernorVotesQuorumFractionUpgradeableV2.sol",
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.8.0) (governance/extensions/GovernorVotesQuorumFraction.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./GovernorVotesUpgradeableV2.sol\";\nimport \"@openzeppelin/contracts-upgradeable/utils/CheckpointsUpgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/utils/math/SafeCastUpgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol\";\n\n/**\n * Modifications:\n * - Inherited `GovernorUpgradeableV2`\n */\nabstract contract GovernorVotesQuorumFractionUpgradeableV2 is Initializable, GovernorVotesUpgradeableV2 {\n    using CheckpointsUpgradeable for CheckpointsUpgradeable.History;\n\n    uint256 private _quorumNumerator; // DEPRECATED\n    CheckpointsUpgradeable.History internal _quorumNumeratorHistory;\n\n    event QuorumNumeratorUpdated(uint256 oldQuorumNumerator, uint256 newQuorumNumerator);\n\n    /**\n     * @dev Initialize quorum as a fraction of the token's total supply.\n     *\n     * The fraction is specified as `numerator / denominator`. By default the denominator is 100, so quorum is\n     * specified as a percent: a numerator of 10 corresponds to quorum being 10% of total supply. The denominator can be\n     * customized by overriding {quorumDenominator}.\n     */\n    function __GovernorVotesQuorumFraction_init(uint256 quorumNumeratorValue) internal onlyInitializing {\n        __GovernorVotesQuorumFraction_init_unchained(quorumNumeratorValue);\n    }\n\n    function __GovernorVotesQuorumFraction_init_unchained(uint256 quorumNumeratorValue) internal onlyInitializing {\n        _updateQuorumNumerator(quorumNumeratorValue);\n    }\n\n    /**\n     * @dev Returns the current quorum numerator. See {quorumDenominator}.\n     */\n    function quorumNumerator() public view virtual returns (uint256) {\n        return _quorumNumeratorHistory._checkpoints.length == 0 ? _quorumNumerator : _quorumNumeratorHistory.latest();\n    }\n\n    /**\n     * @dev Returns the quorum numerator at a specific block number. See {quorumDenominator}.\n     */\n    function quorumNumerator(uint256 blockNumber) public view virtual returns (uint256) {\n        // If history is empty, fallback to old storage\n        uint256 length = _quorumNumeratorHistory._checkpoints.length;\n        if (length == 0) {\n            return _quorumNumerator;\n        }\n\n        // Optimistic search, check the latest checkpoint\n        CheckpointsUpgradeable.Checkpoint memory latest = _quorumNumeratorHistory._checkpoints[length - 1];\n        if (latest._blockNumber <= blockNumber) {\n            return latest._value;\n        }\n\n        // Otherwise, do the binary search\n        return _quorumNumeratorHistory.getAtBlock(blockNumber);\n    }\n\n    /**\n     * @dev Returns the quorum denominator. Defaults to 100, but may be overridden.\n     */\n    function quorumDenominator() public view virtual returns (uint256) {\n        return 100;\n    }\n\n    /**\n     * @dev Returns the quorum for a block number, in terms of number of votes: `supply * numerator / denominator`.\n     */\n    function quorum(uint256 blockNumber) public view virtual override returns (uint256) {\n        return (token.getPastTotalSupply(blockNumber) * quorumNumerator(blockNumber)) / quorumDenominator();\n    }\n\n    /**\n     * @dev Changes the quorum numerator.\n     *\n     * Emits a {QuorumNumeratorUpdated} event.\n     *\n     * Requirements:\n     *\n     * - Must be called through a governance proposal.\n     * - New numerator must be smaller or equal to the denominator.\n     */\n    function updateQuorumNumerator(uint256 newQuorumNumerator) external virtual onlyGovernance {\n        _updateQuorumNumerator(newQuorumNumerator);\n    }\n\n    /**\n     * @dev Changes the quorum numerator.\n     *\n     * Emits a {QuorumNumeratorUpdated} event.\n     *\n     * Requirements:\n     *\n     * - New numerator must be smaller or equal to the denominator.\n     */\n    function _updateQuorumNumerator(uint256 newQuorumNumerator) internal virtual {\n        require(\n            newQuorumNumerator <= quorumDenominator(),\n            \"GovernorVotesQuorumFraction: quorumNumerator over quorumDenominator\"\n        );\n\n        uint256 oldQuorumNumerator = quorumNumerator();\n\n        // Make sure we keep track of the original numerator in contracts upgraded from a version without checkpoints.\n        if (oldQuorumNumerator != 0 && _quorumNumeratorHistory._checkpoints.length == 0) {\n            _quorumNumeratorHistory._checkpoints.push(\n                CheckpointsUpgradeable.Checkpoint({\n                    _blockNumber: 0,\n                    _value: SafeCastUpgradeable.toUint224(oldQuorumNumerator)\n                })\n            );\n        }\n\n        // Set new quorum for future proposals\n        _quorumNumeratorHistory.push(newQuorumNumerator);\n\n        emit QuorumNumeratorUpdated(oldQuorumNumerator, newQuorumNumerator);\n    }\n\n    /**\n     * @dev This empty reserved space is put in place to allow future versions to add new\n     * variables without shifting down storage in the inheritance chain.\n     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps\n     */\n    uint256[48] private __gap;\n}"
    }
  ]
}