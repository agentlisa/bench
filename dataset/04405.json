{
  "Title": "[M03] Unnecessary calls to drip function of pot contract",
  "Content": "When `rho == now`, [`pot.drip`](https://github.com/makerdao/dss/blob/effdda3657f71fd6efc3465dc661b375d1bacc3e/src/pot.sol#L137) is a gas-intensive no-op. This means every call to the [`CDaiDelegate` `accrueInterest` function](https://github.com/compound-finance/compound-protocol/blob/9ea64ddd166a78b264ba8006f688880085eeed13/contracts/CDaiDelegate.sol#L101) (within a given Ethereum block and after `pot.drip` has been called) calls [`pot.drip`](https://github.com/compound-finance/compound-protocol/blob/9ea64ddd166a78b264ba8006f688880085eeed13/contracts/CDaiDelegate.sol#L103) unnecessarily. Consider wrapping [line 103 of `CDaiDelegate.sol`](https://github.com/compound-finance/compound-protocol/blob/9ea64ddd166a78b264ba8006f688880085eeed13/contracts/CDaiDelegate.sol#L103) in an `if (PotLike(potAddress).rho() != now) { ... }` statement. This could provide gas savings (on average) when cDai and/or the DSR is getting a lot of use. See [this GitHub issue](https://github.com/makerdao/dss/issues/87) for more information.\n\n\n",
  "Impact": "MEDIUM",
  "Source": "",
  "Code": [
    {
      "filename": "src/pot.sol",
      "content": "/// pot.sol -- Dai Savings Rate\n\n// Copyright (C) 2018 Rain <rainbreak@riseup.net>\n//\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU Affero General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n//\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU Affero General Public License for more details.\n//\n// You should have received a copy of the GNU Affero General Public License\n// along with this program.  If not, see <https://www.gnu.org/licenses/>.\n\npragma solidity 0.5.11;\n\nimport \"./lib.sol\";\n\n/*\n   \"Savings Dai\" is obtained when Dai is deposited into\n   this contract. Each \"Savings Dai\" accrues Dai interest\n   at the \"Dai Savings Rate\".\n\n   This contract does not implement a user tradeable token\n   and is intended to be used with adapters.\n\n         --- `save` your `dai` in the `pot` ---\n\n   - `dsr`: the Dai Savings Rate\n   - `pie`: user balance of Savings Dai\n\n   - `join`: start saving some dai\n   - `exit`: remove some dai\n   - `drip`: perform rate collection\n\n*/\n\ncontract VatLike {\n    function move(address,address,uint256) external;\n    function suck(address,address,uint256) external;\n}\n\ncontract Pot is DSNote {\n    // --- Auth ---\n    mapping (address => uint) public wards;\n    function rely(address guy) external note auth { wards[guy] = 1; }\n    function deny(address guy) external note auth { wards[guy] = 0; }\n    modifier auth { require(wards[msg.sender] == 1); _; }\n\n    // --- Data ---\n    mapping (address => uint256) public pie;  // user Savings Dai\n\n    uint256 public Pie;  // total Savings Dai\n    uint256 public dsr;  // the Dai Savings Rate\n    uint256 public chi;  // the Rate Accumulator\n\n    VatLike public vat;  // CDP engine\n    address public vow;  // debt engine\n    uint256 public rho;  // time of last drip\n\n    uint256 public live;  // Access Flag\n\n    // --- Init ---\n    constructor(address vat_) public {\n        wards[msg.sender] = 1;\n        vat = VatLike(vat_);\n        dsr = ONE;\n        chi = ONE;\n        rho = now;\n        live = 1;\n    }\n\n    // --- Math ---\n    uint256 constant ONE = 10 ** 27;\n    function rpow(uint x, uint n, uint base) internal pure returns (uint z) {\n        assembly {\n            switch x case 0 {switch n case 0 {z := base} default {z := 0}}\n            default {\n                switch mod(n, 2) case 0 { z := base } default { z := x }\n                let half := div(base, 2)  // for rounding.\n                for { n := div(n, 2) } n { n := div(n,2) } {\n                    let xx := mul(x, x)\n                    if iszero(eq(div(xx, x), x)) { revert(0,0) }\n                    let xxRound := add(xx, half)\n                    if lt(xxRound, xx) { revert(0,0) }\n                    x := div(xxRound, base)\n                    if mod(n,2) {\n                        let zx := mul(z, x)\n                        if and(iszero(iszero(x)), iszero(eq(div(zx, x), z))) { revert(0,0) }\n                        let zxRound := add(zx, half)\n                        if lt(zxRound, zx) { revert(0,0) }\n                        z := div(zxRound, base)\n                    }\n                }\n            }\n        }\n    }\n\n    function rmul(uint x, uint y) internal pure returns (uint z) {\n        z = mul(x, y) / ONE;\n    }\n\n    function add(uint x, uint y) internal pure returns (uint z) {\n        require((z = x + y) >= x);\n    }\n\n    function sub(uint x, uint y) internal pure returns (uint z) {\n        require((z = x - y) <= x);\n    }\n\n    function mul(uint x, uint y) internal pure returns (uint z) {\n        require(y == 0 || (z = x * y) / y == x);\n    }\n\n    // --- Administration ---\n    function file(bytes32 what, uint256 data) external note auth {\n        require(live == 1);\n        require(now == rho);\n        if (what == \"dsr\") dsr = data;\n        else revert();\n    }\n\n    function file(bytes32 what, address addr) external note auth {\n        if (what == \"vow\") vow = addr;\n        else revert();\n    }\n\n    function cage() external note auth {\n        live = 0;\n        dsr = ONE;\n    }\n\n    // --- Savings Rate Accumulation ---\n    function drip() external note {\n        require(now >= rho);\n        uint tmp = rmul(rpow(dsr, now - rho, ONE), chi);\n        uint chi_ = sub(tmp, chi);\n        chi = tmp;\n        rho = now;\n        vat.suck(address(vow), address(this), mul(Pie, chi_));\n    }\n\n    // --- Savings Dai Management ---\n    function join(uint wad) external note {\n        require(now == rho);\n        pie[msg.sender] = add(pie[msg.sender], wad);\n        Pie             = add(Pie,             wad);\n        vat.move(msg.sender, address(this), mul(chi, wad));\n    }\n\n    function exit(uint wad) external note {\n        pie[msg.sender] = sub(pie[msg.sender], wad);\n        Pie             = sub(Pie,             wad);\n        vat.move(address(this), msg.sender, mul(chi, wad));\n    }\n}"
    }
  ]
}