{
  "Title": "[L-05] An immutable split is unable to collect funds if it has itself set as a split receiver",
  "Content": "\nAn immutable split cannot collect funds if it has itself set as a split receiver. This is because the `ImmutableSplitsDriver` contract lacks the functionality to collect available funds.\n\n### Findings\n\n[ImmutableSplitsDriver.sol#L66](https://github.com/code-423n4/2023-01-drips/blob/9fd776b50f4be23ca038b1d0426e63a69c7a511d/src/ImmutableSplitsDriver.sol#L66)\n\n```solidity\n53: function createSplits(SplitsReceiver[] calldata receivers, UserMetadata[] calldata userMetadata)\n54:     public\n55:     whenNotPaused\n56:     returns (uint256 userId)\n57: {\n58:     userId = nextUserId();\n59:     StorageSlot.getUint256Slot(_counterSlot).value++;\n60:     uint256 weightSum = 0;\n61:     for (uint256 i = 0; i < receivers.length; i++) {\n62:         weightSum += receivers[i].weight;\n63:     }\n64:     require(weightSum == totalSplitsWeight, \"Invalid total receivers weight\");\n65:     emit CreatedSplits(userId, dripsHub.hashSplits(receivers));\n66:     dripsHub.setSplits(userId, receivers);\n67:     if (userMetadata.length > 0) dripsHub.emitUserMetadata(userId, userMetadata);\n68: }\n```\n\n### Recommended Mitigation Steps\n\nConsider preventing setting one of the receivers to the user ID of the newly created immutable split.\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/contests/2023-01-drips-protocol-contest",
  "Code": [
    {
      "filename": "src/ImmutableSplitsDriver.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-only\npragma solidity ^0.8.17;\n\nimport {DripsHub, SplitsReceiver, UserMetadata} from \"./DripsHub.sol\";\nimport {Managed} from \"./Managed.sol\";\nimport {StorageSlot} from \"openzeppelin-contracts/utils/StorageSlot.sol\";\n\n/// @notice A DripsHub driver implementing immutable splits configurations.\n/// Anybody can create a new user ID and configure its splits configuration,\n/// but nobody can update its configuration afterwards, it's immutable.\ncontract ImmutableSplitsDriver is Managed {\n    /// @notice The DripsHub address used by this driver.\n    DripsHub public immutable dripsHub;\n    /// @notice The driver ID which this driver uses when calling DripsHub.\n    uint32 public immutable driverId;\n    /// @notice The required total splits weight of each splits configuration\n    uint32 public immutable totalSplitsWeight;\n    /// @notice The ERC-1967 storage slot holding a single `uint256` counter of created identities.\n    bytes32 private immutable _counterSlot = _erc1967Slot(\"eip1967.immutableSplitsDriver.storage\");\n\n    /// @notice Emitted when an immutable splits configuration is created.\n    /// @param userId The user ID\n    /// @param receiversHash The splits receivers list hash\n    event CreatedSplits(uint256 indexed userId, bytes32 indexed receiversHash);\n\n    /// @param _dripsHub The drips hub to use.\n    /// @param _driverId The driver ID to use when calling DripsHub.\n    constructor(DripsHub _dripsHub, uint32 _driverId) {\n        dripsHub = _dripsHub;\n        driverId = _driverId;\n        totalSplitsWeight = _dripsHub.TOTAL_SPLITS_WEIGHT();\n    }\n\n    /// @notice The ID of the next user to be created.\n    /// @return userId The user ID.\n    function nextUserId() public view returns (uint256 userId) {\n        return (uint256(driverId) << 224) + StorageSlot.getUint256Slot(_counterSlot).value;\n    }\n\n    /// @notice Creates a new user ID, configures its splits configuration and emits its metadata.\n    /// The configuration is immutable and nobody can control the user ID after its creation.\n    /// Calling this function is the only way and the only chance to emit metadata for that user.\n    /// @param receivers The list of the user's splits receivers to be set.\n    /// Must be sorted by the splits receivers' addresses, deduplicated and without 0 weights.\n    /// Each splits receiver will be getting `weight / totalSplitsWeight`\n    /// share of the funds collected by the user.\n    /// The sum of the receivers' weights must be equal to `totalSplitsWeight`,\n    /// or in other words the configuration must be splitting 100% of received funds.\n    /// @param userMetadata The list of user metadata to emit for the created user.\n    /// The keys and the values are not standardized by the protocol, it's up to the user\n    /// to establish and follow conventions to ensure compatibility with the consumers.\n    /// @return userId The new user ID with `receivers` configured.\n    function createSplits(SplitsReceiver[] calldata receivers, UserMetadata[] calldata userMetadata)\n        public\n        whenNotPaused\n        returns (uint256 userId)\n    {\n        userId = nextUserId();\n        StorageSlot.getUint256Slot(_counterSlot).value++;\n        uint256 weightSum = 0;\n        for (uint256 i = 0; i < receivers.length; i++) {\n            weightSum += receivers[i].weight;\n        }\n        require(weightSum == totalSplitsWeight, \"Invalid total receivers weight\");\n        emit CreatedSplits(userId, dripsHub.hashSplits(receivers));\n        dripsHub.setSplits(userId, receivers);\n        if (userMetadata.length > 0) dripsHub.emitUserMetadata(userId, userMetadata);\n    }\n}"
    }
  ]
}