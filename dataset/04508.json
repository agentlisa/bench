{
  "Title": "Missing zero addressÂ checks",
  "Content": "In several places in the code, addresses are passed as parameters to functions. In many of these instances, the functions do not validate that the passed address is not the address `0`. In the `Controller` contract, this happens in functions [`configureController`](https://github.com/centrehq/centre-tokens/blob/4b9ebf3941a48e41e7363fee729035610a71ca66/contracts/minting/Controller.sol#L56) (which only checks `_worker`) and [`removeController`](https://github.com/centrehq/centre-tokens/blob/4b9ebf3941a48e41e7363fee729035610a71ca66/contracts/minting/Controller.sol#L66).\n\n\nIn the [`MintController`](https://github.com/centrehq/centre-tokens/blob/4b9ebf3941a48e41e7363fee729035610a71ca66/contracts/minting/MintController.sol) contract, on the other hand, it might be expected that the constructor sets the `minterManager` variable to `0`, but the [`setMinterManager`](https://github.com/centrehq/centre-tokens/blob/4b9ebf3941a48e41e7363fee729035610a71ca66/contracts/minting/MintController.sol#L60) function does not prevent setting it to `0`.\n\n\nWhile this does not currently pose a security risk, consider adding checks for the passed addresses being nonzero to prevent unexpected behavior where required, or documenting the fact that a zero address is indeed a valid parameter.\n\n\n***Update:*** *functions in* `Controller` *now check* [*all*](https://github.com/centrehq/centre-tokens/blob/fbb6cfeb503ece6719cd456f82e6ef1602145db3/contracts/minting/Controller.sol#L85) [*passed*](https://github.com/centrehq/centre-tokens/blob/fbb6cfeb503ece6719cd456f82e6ef1602145db3/contracts/minting/Controller.sol#L87) [*arguments*](https://github.com/centrehq/centre-tokens/blob/fbb6cfeb503ece6719cd456f82e6ef1602145db3/contracts/minting/Controller.sol#L102) *to prevent zero addresses from being set. No changes were introduced in this respect in* `MintController`.\n\n\n",
  "Impact": "MEDIUM",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/minting/Controller.sol",
      "content": "/**\n* Copyright CENTRE SECZ 2018\n*\n* Permission is hereby granted, free of charge, to any person obtaining a copy\n* of this software and associated documentation files (the \"Software\"), to deal\n* in the Software without restriction, including without limitation the rights\n* to use, copy, modify, merge, publish, distribute, sublicense, and/or sell\n* copies of the Software, and to permit persons to whom the Software is furnished to\n* do so, subject to the following conditions:\n*\n* The above copyright notice and this permission notice shall be included in all\n* copies or substantial portions of the Software.\n*\n* THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR\n* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,\n* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE\n* AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,\n* WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN\n* CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.\n*/\n\npragma solidity ^0.4.24;\n\nimport './../Ownable.sol';\n\n/**\n * @title Controller\n * @dev Generic implementation of the owner-controller-worker model.\n *\n */\ncontract Controller is Ownable {\n    // controllers[controller] = worker\n    // The controller manages a single worker address.\n    mapping(address => address) public controllers;\n\n    event ControllerConfigured(address indexed _controller, address indexed _worker);\n    event ControllerRemoved(address indexed _controller);\n\n    /**\n     * @dev ensure that the caller is the controller of a non-zero worker address\n     */\n    modifier onlyController() {\n        require(controllers[msg.sender] != address(0));\n        _;\n    }\n\n    constructor() public {\n    }\n\n    // onlyOwner functions\n\n    /**\n     * @dev set the controller of a particular _worker\n     * Argument _worker must not be 0x00, call removeController(_controller) instead.\n     */\n    function configureController(address _controller, address _worker) onlyOwner public returns (bool) {\n        require(_worker != address(0));\n        controllers[_controller] = _worker;\n        emit ControllerConfigured(_controller, _worker);\n        return true;\n    }\n\n    /**\n     * @dev disables a controller by setting its worker to address(0);\n     */\n    function removeController(address _controller) onlyOwner public returns (bool) {\n        controllers[_controller] = address(0);\n        emit ControllerRemoved(_controller);\n        return true;\n    }\n}"
    },
    {
      "filename": "contracts/minting/Controller.sol",
      "content": "/**\n* Copyright CENTRE SECZ 2018\n*\n* Permission is hereby granted, free of charge, to any person obtaining a copy\n* of this software and associated documentation files (the \"Software\"), to deal\n* in the Software without restriction, including without limitation the rights\n* to use, copy, modify, merge, publish, distribute, sublicense, and/or sell\n* copies of the Software, and to permit persons to whom the Software is\n* furnished to do so, subject to the following conditions:\n*\n* The above copyright notice and this permission notice shall be included in all\n* copies or substantial portions of the Software.\n*\n* THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR\n* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,\n* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE\n* AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER\n* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,\n* OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN\n* THE SOFTWARE.\n*/\n\npragma solidity ^0.4.24;\n\nimport \"../Ownable.sol\";\n\n/**\n * @title Controller\n * @notice Generic implementation of the owner-controller-worker model.\n * One owner manages many controllers. Each controller manages one worker.\n * Workers may be reused across different controllers.\n */\ncontract Controller is Ownable {\n    /**\n     * @notice A controller manages a single worker address.\n     * controllers[controller] = worker\n     */\n    mapping(address => address) internal controllers;\n\n    event ControllerConfigured(\n        address indexed _controller,\n        address indexed _worker\n    );\n    event ControllerRemoved(address indexed _controller);\n\n    /**\n     * @notice Ensures that caller is the controller of a non-zero worker \n     * address.\n     */\n    modifier onlyController() {\n        require(controllers[msg.sender] != address(0), \n            \"The value of controllers[msg.sender] must be non-zero\");\n        _;\n    }\n\n    /**\n     * @notice Gets the worker at address _controller.\n     */\n    function getWorker(\n        address _controller\n    )\n        external\n        view\n        returns (address)\n    {\n        return controllers[_controller];\n    }\n\n    // onlyOwner functions\n\n    /**\n     * @notice Configure a controller with the given worker.\n     * @param _controller The controller to be configured with a worker.\n     * @param _worker The worker to be set for the newly configured controller.\n     * _worker must not be a non-zero address. To disable a worker,\n     * use removeController instead.\n     */\n    function configureController(\n        address _controller,\n        address _worker\n    )\n        public \n        onlyOwner \n    {\n        require(_controller != address(0), \n            \"Controller must be a non-zero address\");\n        require(_worker != address(0), \"Worker must be a non-zero address\");\n        controllers[_controller] = _worker;\n        emit ControllerConfigured(_controller, _worker);\n    }\n\n    /**\n     * @notice disables a controller by setting its worker to address(0).\n     * @param _controller The controller to disable.\n     */\n    function removeController(\n        address _controller\n    )\n        public \n        onlyOwner \n    {\n        require(_controller != address(0), \n            \"Controller must be a non-zero address\");\n        require(controllers[_controller] != address(0), \n            \"Worker must be a non-zero address\");\n        controllers[_controller] = address(0);\n        emit ControllerRemoved(_controller);\n    }\n}"
    }
  ]
}