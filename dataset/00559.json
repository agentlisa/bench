{
  "Title": "Imprecise Refresh Requirement [samples]",
  "Content": "The `OracleHelper` contract is configured for a [price feed that updates every day](https://github.com/eth-infinitism/account-abstraction/blob/9879c931ce92f0bee1bca1d1b1352eeb98b9a120/contracts/samples/utils/OracleHelper.sol#L161-L162) but still accepts prices that are two days old. On a well-functioning feed, the price should never be more than one day old.\n\n\nConsider using this more restrictive requirement (with a possible small buffer).\n\n\n***Update:** Resolved in [pull request #424](https://github.com/eth-infinitism/account-abstraction/pull/424).*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/samples/utils/OracleHelper.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0\npragma solidity ^0.8.12;\n\n/* solhint-disable not-rely-on-time */\n\nimport \"@uniswap/v3-periphery/contracts/interfaces/ISwapRouter.sol\";\n\nimport \"./IOracle.sol\";\n\n/// @title Helper functions for dealing with various forms of price feed oracles.\n/// @notice Maintains a price cache and updates the current price if needed.\n/// In the best case scenario we have a direct oracle from the token to the native asset.\n/// Also support tokens that have no direct price oracle to the native asset.\n/// Sometimes oracles provide the price in the opposite direction of what we need in the moment.\nabstract contract OracleHelper {\n\n    event TokenPriceUpdated(uint256 currentPrice, uint256 previousPrice, uint256 cachedPriceTimestamp);\n\n    uint256 private constant PRICE_DENOMINATOR = 1e26;\n\n    struct OracleHelperConfig {\n\n        /// @notice The price cache will be returned without even fetching the oracles for this number of seconds\n        uint48 cacheTimeToLive;\n\n        /// @notice The Oracle contract used to fetch the latest token prices\n        IOracle tokenOracle;\n\n        /// @notice The Oracle contract used to fetch the latest ETH prices\n        IOracle nativeOracle;\n\n        /// @notice If 'true' we will fetch price directly from tokenOracle\n        /// @notice If 'false' we will use nativeOracle to establish a token price through a shared third currency\n        bool tokenToNativeOracle;\n\n        /// @notice 'false' if price is dollars-per-token (or ether-per-token), 'true' if price is tokens-per-dollar\n        bool tokenOracleReverse;\n\n        /// @notice 'false' if price is dollars-per-ether, 'true' if price is ether-per-dollar\n        bool nativeOracleReverse;\n\n        /// @notice The price update threshold percentage that triggers a price update (1e6 = 100%)\n        uint48 priceUpdateThreshold;\n\n    }\n\n    /// @notice The cached token price from the Oracle, always in (ether-per-token) * PRICE_DENOMINATOR format\n    uint256 public cachedPrice;\n\n    /// @notice The timestamp of a block when the cached price was updated\n    uint48 public cachedPriceTimestamp;\n\n    OracleHelperConfig private oracleHelperConfig;\n\n    /// @notice The \"10^(tokenOracle.decimals)\" value used for the price calculation\n    uint128 private tokenOracleDecimalPower;\n\n    /// @notice The \"10^(nativeOracle.decimals)\" value used for the price calculation\n    uint128 private nativeOracleDecimalPower;\n\n    constructor (\n        OracleHelperConfig memory _oracleHelperConfig\n    ) {\n        cachedPrice = type(uint256).max; // initialize the storage slot to invalid value\n        _setOracleConfiguration(\n            _oracleHelperConfig\n        );\n    }\n\n    function _setOracleConfiguration(\n        OracleHelperConfig memory _oracleHelperConfig\n    ) private {\n        oracleHelperConfig = _oracleHelperConfig;\n        require(_oracleHelperConfig.priceUpdateThreshold <= 1e6, \"TPM: update threshold too high\");\n        tokenOracleDecimalPower = uint128(10 ** oracleHelperConfig.tokenOracle.decimals());\n        nativeOracleDecimalPower = uint128(10 ** oracleHelperConfig.nativeOracle.decimals());\n    }\n\n    /// @notice Updates the token price by fetching the latest price from the Oracle.\n    function updateCachedPrice(bool force) public returns (uint256 newPrice) {\n        uint256 cacheTimeToLive = oracleHelperConfig.cacheTimeToLive;\n        uint256 cacheAge = block.timestamp - cachedPriceTimestamp;\n        if (!force && cacheAge <= cacheTimeToLive) {\n            return cachedPrice;\n        }\n        uint256 priceUpdateThreshold = oracleHelperConfig.priceUpdateThreshold;\n        IOracle tokenOracle = oracleHelperConfig.tokenOracle;\n        IOracle nativeOracle = oracleHelperConfig.nativeOracle;\n\n        uint256 _cachedPrice = cachedPrice;\n        uint256 tokenPrice = fetchPrice(tokenOracle);\n        uint256 nativeAssetPrice = 1;\n        // If the 'TokenOracle' returns the price in the native asset units there is no need to fetch native asset price\n        if (!oracleHelperConfig.tokenToNativeOracle) {\n            nativeAssetPrice = fetchPrice(nativeOracle);\n        }\n        uint256 price = calculatePrice(\n            tokenPrice,\n            nativeAssetPrice,\n            oracleHelperConfig.tokenOracleReverse,\n            oracleHelperConfig.nativeOracleReverse\n        );\n        uint256 priceNewByOld = price * PRICE_DENOMINATOR / _cachedPrice;\n\n        bool updateRequired = force ||\n            priceNewByOld > PRICE_DENOMINATOR + priceUpdateThreshold ||\n            priceNewByOld < PRICE_DENOMINATOR - priceUpdateThreshold;\n        if (!updateRequired) {\n            return _cachedPrice;\n        }\n        uint256 previousPrice = _cachedPrice;\n        _cachedPrice = price;\n        cachedPrice = _cachedPrice;\n        cachedPriceTimestamp = uint48(block.timestamp);\n        emit TokenPriceUpdated(_cachedPrice, previousPrice, cachedPriceTimestamp);\n        return _cachedPrice;\n    }\n\n    /**\n     * Calculate the effective price of the selected token denominated in native asset.\n     *\n     * @param tokenPrice - the price of the token relative to a native asset or a bridging asset like the U.S. dollar.\n     * @param nativeAssetPrice - the price of the native asset relative to a bridging asset or 1 if no bridging needed.\n     * @param tokenOracleReverse - flag indicating direction of the \"tokenPrice\".\n     * @param nativeOracleReverse - flag indicating direction of the \"nativeAssetPrice\".\n     * @return the ether-per-token price multiplied by the PRICE_DENOMINATOR constant.\n     */\n    function calculatePrice(\n        uint256 tokenPrice,\n        uint256 nativeAssetPrice,\n        bool tokenOracleReverse,\n        bool nativeOracleReverse\n    ) private view returns (uint256){\n        // tokenPrice is normalized as dollars-per-token\n        if (tokenOracleReverse) {\n            // inverting tokenPrice that was tokens-per-dollar (or tokens-per-ether)\n            tokenPrice = PRICE_DENOMINATOR * tokenOracleDecimalPower / tokenPrice;\n        } else {\n            // tokenPrice already dollars-per-token (or ethers-per-token)\n            tokenPrice = PRICE_DENOMINATOR * tokenPrice / tokenOracleDecimalPower;\n        }\n\n        if (nativeOracleReverse) {\n            // multiplying by nativeAssetPrice that is  ethers-per-dollar\n            // => result = (dollar / token) * (ether / dollar) = ether / token\n            return nativeAssetPrice * tokenPrice / nativeOracleDecimalPower;\n        } else {\n            // dividing by nativeAssetPrice that is dollars-per-ether\n            // => result = (dollar / token) / (dollar / ether) = ether / token\n            return tokenPrice * nativeOracleDecimalPower / nativeAssetPrice;\n        }\n    }\n\n    /// @notice Fetches the latest price from the given Oracle.\n    /// @dev This function is used to get the latest price from the tokenOracle or nativeOracle.\n    /// @param _oracle The Oracle contract to fetch the price from.\n    /// @return price The latest price fetched from the Oracle.\n    function fetchPrice(IOracle _oracle) internal view returns (uint256 price) {\n        (uint80 roundId, int256 answer,, uint256 updatedAt, uint80 answeredInRound) = _oracle.latestRoundData();\n        require(answer > 0, \"TPM: Chainlink price <= 0\");\n        // 2 days old price is considered stale since the price is updated every 24 hours\n        require(updatedAt >= block.timestamp - 60 * 60 * 24 * 2, \"TPM: Incomplete round\");\n        require(answeredInRound >= roundId, \"TPM: Stale price\");\n        price = uint256(answer);\n    }\n}"
    }
  ]
}