{
  "Title": "Solidity Version Is Not Fixed and Its Use Is Inconsistent",
  "Content": "In the codebase, there are [some](https://github.com/scroll-tech/scroll/blob/45e1305de5f7a1dc5aadadd7d3bb6726a5dee949/contracts/src/libraries/gateway/ScrollGatewayBase.sol#L3) contracts whose pragma statement does not use a fixed version, whereas [others](https://github.com/scroll-tech/scroll/blob/45e1305de5f7a1dc5aadadd7d3bb6726a5dee949/contracts/src/L2/gateways/L2ERC1155Gateway.sol#L3) are correctly using a fixed one.\n\n\nConsider reviewing all the contracts and always using the same fixed Solidity pragma version in all of them. This will help improve consistency and avoid compiling contracts with unexpected compiler versions.\n\n\n***Update:** Acknowledged, not resolved. The Scroll team stated:*\n\n\n\n> *We prefer to leave base contracts and interfaces using `^0.8.0` such that the third party can inherit it more easily.*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/src/libraries/gateway/ScrollGatewayBase.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.16;\n\nimport {OwnableUpgradeable} from \"@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol\";\nimport {ReentrancyGuardUpgradeable} from \"@openzeppelin/contracts-upgradeable/security/ReentrancyGuardUpgradeable.sol\";\n\nimport {IScrollGateway} from \"./IScrollGateway.sol\";\nimport {IScrollMessenger} from \"../IScrollMessenger.sol\";\nimport {IScrollGatewayCallback} from \"../callbacks/IScrollGatewayCallback.sol\";\nimport {ScrollConstants} from \"../constants/ScrollConstants.sol\";\nimport {ITokenRateLimiter} from \"../../rate-limiter/ITokenRateLimiter.sol\";\n\n/// @title ScrollGatewayBase\n/// @notice The `ScrollGatewayBase` is a base contract for gateway contracts used in both in L1 and L2.\nabstract contract ScrollGatewayBase is ReentrancyGuardUpgradeable, OwnableUpgradeable, IScrollGateway {\n    /*************\n     * Constants *\n     *************/\n\n    /// @inheritdoc IScrollGateway\n    address public immutable override counterpart;\n\n    /// @inheritdoc IScrollGateway\n    address public immutable override router;\n\n    /// @inheritdoc IScrollGateway\n    address public immutable override messenger;\n\n    /*************\n     * Variables *\n     *************/\n\n    /// @dev The storage slot used as counterpart gateway contract, which is deprecated now.\n    address private __counterpart;\n\n    /// @dev The storage slot used as gateway router contract, which is deprecated now.\n    address private __router;\n\n    /// @dev The storage slot used as scroll messenger contract, which is deprecated now.\n    address private __messenger;\n\n    /// @dev The storage slot used as token rate limiter contract, which is deprecated now.\n    address private __rateLimiter;\n\n    /// @dev The storage slots for future usage.\n    uint256[46] private __gap;\n\n    /**********************\n     * Function Modifiers *\n     **********************/\n\n    modifier onlyCallByCounterpart() {\n        // check caller is messenger\n        if (_msgSender() != messenger) {\n            revert ErrorCallerIsNotMessenger();\n        }\n\n        // check cross domain caller is counterpart gateway\n        if (counterpart != IScrollMessenger(messenger).xDomainMessageSender()) {\n            revert ErrorCallerIsNotCounterpartGateway();\n        }\n        _;\n    }\n\n    modifier onlyInDropContext() {\n        // check caller is messenger\n        if (_msgSender() != messenger) {\n            revert ErrorCallerIsNotMessenger();\n        }\n\n        // check we are dropping message in ScrollMessenger.\n        if (ScrollConstants.DROP_XDOMAIN_MESSAGE_SENDER != IScrollMessenger(messenger).xDomainMessageSender()) {\n            revert ErrorNotInDropMessageContext();\n        }\n        _;\n    }\n\n    /***************\n     * Constructor *\n     ***************/\n\n    constructor(\n        address _counterpart,\n        address _router,\n        address _messenger\n    ) {\n        if (_counterpart == address(0) || _messenger == address(0)) {\n            revert ErrorZeroAddress();\n        }\n\n        counterpart = _counterpart;\n        router = _router;\n        messenger = _messenger;\n    }\n\n    function _initialize(\n        address,\n        address,\n        address\n    ) internal {\n        ReentrancyGuardUpgradeable.__ReentrancyGuard_init();\n        OwnableUpgradeable.__Ownable_init();\n    }\n\n    /**********************\n     * Internal Functions *\n     **********************/\n\n    /// @dev Internal function to forward calldata to target contract.\n    /// @param _to The address of contract to call.\n    /// @param _data The calldata passed to the contract.\n    function _doCallback(address _to, bytes memory _data) internal {\n        if (_data.length > 0 && _to.code.length > 0) {\n            IScrollGatewayCallback(_to).onScrollGatewayCallback(_data);\n        }\n    }\n}"
    }
  ]
}