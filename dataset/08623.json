{
  "Title": "[M-02] Twav.sol#_getTwav() will revert when timestamp > 4294967296",
  "Content": "# Lines of code\n\nhttps://github.com/code-423n4/2022-06-nibbl/blob/8c3dbd6adf350f35c58b31723d42117765644110/contracts/Twav/Twav.sol#L35-L42\n\n\n# Vulnerability details\n\n```solidity\nfunction _getTwav() internal view returns(uint256 _twav){\n    if (twavObservations[TWAV_BLOCK_NUMBERS - 1].timestamp != 0) {\n        uint8 _index = ((twavObservationsIndex + TWAV_BLOCK_NUMBERS) - 1) % TWAV_BLOCK_NUMBERS;\n        TwavObservation memory _twavObservationCurrent = twavObservations[(_index)];\n        TwavObservation memory _twavObservationPrev = twavObservations[(_index + 1) % TWAV_BLOCK_NUMBERS];\n        _twav = (_twavObservationCurrent.cumulativeValuation - _twavObservationPrev.cumulativeValuation) / (_twavObservationCurrent.timestamp - _twavObservationPrev.timestamp);\n    }\n}\n```\n\nSince `_blockTimestamp` is `uint32`, subtraction underflow is desired at `_twavObservationCurrent.timestamp - _twavObservationPrev.timestamp`.\n\nSee: https://github.com/Uniswap/v2-periphery/blob/master/contracts/examples/ExampleOracleSimple.sol#L43\n\n```solidity\nfunction update() external {\n    (uint price0Cumulative, uint price1Cumulative, uint32 blockTimestamp) =\n        UniswapV2OracleLibrary.currentCumulativePrices(address(pair));\n    uint32 timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired\n```\n\nBecause the solidity version used by the current implementation is `0.8.10`, and there are some breaking changes in Solidity v0.8.0:\n\n> Arithmetic operations revert on underflow and overflow. \n\nRef: https://docs.soliditylang.org/en/v0.8.13/080-breaking-changes.html#silent-changes-of-the-semantics\n\nThe timestamp subtraction may revert due to underflow.\n\n### Impact\n\nSince `_getTwav()` is used in `NibblVault.sol#_rejectBuyout()`, if it reverts and there is a `buyout`, an essential feature of the `NibblVault` contract will be unavailable, causing users' funds to be frozen in the contract.\n \n### Recommendation\n\nChange to:\n\n```solidity\nfunction _updateTWAV(uint256 _valuation, uint32 _blockTimestamp) internal {\n    uint32 _timeElapsed; \n    unchecked {\n        _timeElapsed = _blockTimestamp - lastBlockTimeStamp;\n    }\n\n    uint256 _prevCumulativeValuation = twavObservations[((twavObservationsIndex + TWAV_BLOCK_NUMBERS) - 1) % TWAV_BLOCK_NUMBERS].cumulativeValuation;\n    unchecked {\n        twavObservations[twavObservationsIndex] = TwavObservation(_blockTimestamp, _prevCumulativeValuation + (_valuation * _timeElapsed)); //add the previous observation to make it cumulative\n    }\n    \n    twavObservationsIndex = (twavObservationsIndex + 1) % TWAV_BLOCK_NUMBERS;\n    lastBlockTimeStamp = _blockTimestamp;\n}\n```\n\n```solidity\nfunction _getTwav() internal view returns(uint256 _twav){\n    if (twavObservations[TWAV_BLOCK_NUMBERS - 1].timestamp != 0) {\n        uint8 _index = ((twavObservationsIndex + TWAV_BLOCK_NUMBERS) - 1) % TWAV_BLOCK_NUMBERS;\n        TwavObservation memory _twavObservationCurrent = twavObservations[(_index)];\n        TwavObservation memory _twavObservationPrev = twavObservations[(_index + 1) % TWAV_BLOCK_NUMBERS];\n        unchecked {\n            _twav = (_twavObservationCurrent.cumulativeValuation - _twavObservationPrev.cumulativeValuation) / (_twavObservationCurrent.timestamp - _twavObservationPrev.timestamp);\n        }\n    }\n}\n```\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-06-nibbl-contest",
  "Code": [
    {
      "filename": "contracts/Twav/Twav.sol",
      "content": "// SPDX-License-Identifier: BUSL-1.1\n\npragma solidity 0.8.10;\ncontract Twav {\n    struct TwavObservation {\n        uint32 timestamp;\n        uint256 cumulativeValuation;\n    }\n\n    /// @notice current index of twavObservations index\n    uint8 public twavObservationsIndex;\n    uint8 private constant TWAV_BLOCK_NUMBERS = 4; //TWAV of last 4 Blocks \n    uint32 public lastBlockTimeStamp;\n\n    /// @notice record of TWAV \n    TwavObservation[TWAV_BLOCK_NUMBERS] public twavObservations;\n\n    /// @notice updates twavObservations array\n    /// @param _blockTimestamp timestamp of the block\n    /// @param _valuation current valuation\n    function _updateTWAV(uint256 _valuation, uint32 _blockTimestamp) internal {\n        uint32 _timeElapsed; \n        unchecked {\n            _timeElapsed = _blockTimestamp - lastBlockTimeStamp;\n        }\n\n        uint256 _prevCumulativeValuation = twavObservations[((twavObservationsIndex + TWAV_BLOCK_NUMBERS) - 1) % TWAV_BLOCK_NUMBERS].cumulativeValuation;\n        twavObservations[twavObservationsIndex] = TwavObservation(_blockTimestamp, _prevCumulativeValuation + (_valuation * _timeElapsed)); //add the previous observation to make it cumulative\n        twavObservationsIndex = (twavObservationsIndex + 1) % TWAV_BLOCK_NUMBERS;\n        lastBlockTimeStamp = _blockTimestamp;\n    }\n\n    /// @notice returns the TWAV of the last 4 blocks\n    /// @return _twav TWAV of the last 4 blocks\n    function _getTwav() internal view returns(uint256 _twav){\n        if (twavObservations[TWAV_BLOCK_NUMBERS - 1].timestamp != 0) {\n            uint8 _index = ((twavObservationsIndex + TWAV_BLOCK_NUMBERS) - 1) % TWAV_BLOCK_NUMBERS;\n            TwavObservation memory _twavObservationCurrent = twavObservations[(_index)];\n            TwavObservation memory _twavObservationPrev = twavObservations[(_index + 1) % TWAV_BLOCK_NUMBERS];\n            _twav = (_twavObservationCurrent.cumulativeValuation - _twavObservationPrev.cumulativeValuation) / (_twavObservationCurrent.timestamp - _twavObservationPrev.timestamp);\n        }\n    }\n\n    function getTwavObservations() public view returns(TwavObservation[TWAV_BLOCK_NUMBERS] memory) {\n        return twavObservations;\n    }\n}"
    }
  ]
}