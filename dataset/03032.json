{
  "Title": "M-1: Lack of price freshness check in `ChainlinkOracle.sol#getPrice()` allows a stale price to be used",
  "Content": "# Issue M-1: Lack of price freshness check in `ChainlinkOracle.sol#getPrice()` allows a stale price to be used \nSource: https://github.com/sherlock-audit/2022-08-sentiment-judging/tree/main/002-M \n## Found by \ndefsec, icedpeachtea, oyc\\_109, Lambda, 0xNineDec, Avci, ladboy233, JohnSmith, jonatascm, Ruhum, csanuragjain, PwnPatrol, WATCHPUG, 0xNazgul, xiaoming90, 0x52, 0xf15ers, ellahi, pashov, rbserver, GalloDaSballo, Chom, \\_\\_141345\\_\\_, cccz, devtooligan, Bahurum, HonorLt, GimelSec, Dravee, Olivierdem\n\n## Summary\n\n`ChainlinkOracle` should use the `updatedAt` value from the latestRoundData() function to make sure that the latest answer is recent enough to be used.\n\n## Vulnerability Detail\n\nIn the current implementation of `ChainlinkOracle.sol#getPrice()`, there is no freshness check. This could lead to stale prices being used.\n\nIf the market price of the token drops very quickly (\"flash crashes\"), and Chainlink's feed does not get updated in time, the smart contract will continue to believe the token is worth more than the market value.\n\nChainlink also advise developers to check for the `updatedAt` before using the price:\n\n> Your application should track the latestTimestamp variable or use the updatedAt value from the latestRoundData() function to make sure that the latest answer is recent enough for your application to use it. If your application detects that the reported answer is not updated within the heartbeat or within time limits that you determine are acceptable for your application, pause operation or switch to an alternate operation mode while identifying the cause of the delay.\n\nAnd they have this heartbeat concept:\n\n> Chainlink Price Feeds do not provide streaming data. Rather, the aggregator updates its latestAnswer when the value deviates beyond a specified threshold or when the heartbeat idle time has passed. You can find the heartbeat and deviation values for each data feed at data.chain.link or in the Contract Addresses lists.\n\nThe `Heartbeat` on Arbitrum is usually `1h`.\n\nSource: https://docs.chain.link/docs/arbitrum-price-feeds/\n\n## Impact\n\nA stale price can cause the malfunction of multiple features across the protocol:\n\n1. `_valueInWei()` is using the price to calculate the value of the loan and collateral; A stale price will make the price calculation inaccurate so that certain accounts may not be liquidated when they should be, or be liquidated when they should not be.\n\n2. `ChainlinkOracle.sol#getPrice()` is used to calculate the value of various LPTokens (Aave, Balancer, Compound, Curve, and Uniswap). If the price is not accurate, it will lead to a deviation in the LPToken price and affect the calculation of asset prices.\n\n3. Stale asset prices can lead to bad debts to the protocol as the collateral assets can be overvalued, and the collateral value can not cover the loans.\n\n## Code Snippet\n\nhttps://github.com/sentimentxyz/oracle/blob/59b26a3d8c295208437aad36c470386c9729a4bc/src/chainlink/ChainlinkOracle.sol#L49-L59\n\n```solidity\n    function getPrice(address token) external view virtual returns (uint) {\n        (, int answer,,,) =\n            feed[token].latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(token, address(feed[token]));\n\n        return (\n            (uint(answer)*1e18)/getEthPrice()\n        );\n    }\n```\n\nhttps://github.com/sentimentxyz/oracle/blob/59b26a3d8c295208437aad36c470386c9729a4bc/src/chainlink/ChainlinkOracle.sol#L65-L73\n\n```solidity\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,,,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n```\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\nConsider adding the missing freshness check for stale price:\n\n```solidity\n    function getPrice(address token) external view virtual returns (uint) {\n        (, int answer,,,) =\n            feed[token].latestRoundData();\n        uint validPeriod = feedValidPeriod[token];\n\n        require(block.timestamp - updatedAt < validPeriod, \"freshness check failed.\")\n\n        if (answer <= 0)\n            revert Errors.NegativePrice(token, address(feed[token]));\n\n        return (\n            (uint(answer)*1e18)/getEthPrice()\n        );\n    }\n```\n\nThe `validPeriod` can be based on the `Heartbeat` of the feed.\n\n## Sentiment Team\nFixed as recommended. PR [here](https://github.com/sentimentxyz/oracle/pull/38).\n\n## Lead Senior Watson\nConfirmed fix. \n\n",
  "Impact": "MEDIUM",
  "Source": "https://app.sherlock.xyz/audits/contests/1",
  "Code": [
    {
      "filename": "src/chainlink/ChainlinkOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {Ownable} from \"../utils/Ownable.sol\";\nimport {Errors} from \"../utils/Errors.sol\";\nimport {AggregatorV3Interface} from \"./AggregatorV3Interface.sol\";\n\n/**\n    @title Chain Link Oracle\n    @notice Oracle to fetch price using chainlink oracles\n*/\ncontract ChainlinkOracle is Ownable, IOracle {\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice ETH USD Chainlink price feed\n    AggregatorV3Interface immutable ethUsdPriceFeed;\n\n    /// @notice Mapping of token to token/usd chainlink price feed\n    mapping(address => AggregatorV3Interface) public feed;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                   EVENTS                                   */\n    /* -------------------------------------------------------------------------- */\n\n    event UpdateFeed(address indexed token, address indexed feed);\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _ethUsdPriceFeed ETH USD Chainlink price feed\n    */\n    constructor(AggregatorV3Interface _ethUsdPriceFeed) Ownable(msg.sender) {\n        ethUsdPriceFeed = _ethUsdPriceFeed;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @inheritdoc IOracle\n    /// @dev feed[token].latestRoundData should return price scaled by 8 decimals\n    function getPrice(address token) external view virtual returns (uint) {\n        (, int answer,,,) =\n            feed[token].latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(token, address(feed[token]));\n\n        return (\n            (uint(answer)*1e18)/getEthPrice()\n        );\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                             INTERNAL FUNCTIONS                             */\n    /* -------------------------------------------------------------------------- */\n\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,,,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                               ADMIN FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    function setFeed(\n        address token,\n        AggregatorV3Interface _feed\n    ) external adminOnly {\n        feed[token] = _feed;\n        emit UpdateFeed(token, address(_feed));\n    }\n}"
    },
    {
      "filename": "src/chainlink/ChainlinkOracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport {IOracle} from \"../core/IOracle.sol\";\nimport {Ownable} from \"../utils/Ownable.sol\";\nimport {Errors} from \"../utils/Errors.sol\";\nimport {AggregatorV3Interface} from \"./AggregatorV3Interface.sol\";\n\n/**\n    @title Chain Link Oracle\n    @notice Oracle to fetch price using chainlink oracles\n*/\ncontract ChainlinkOracle is Ownable, IOracle {\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice ETH USD Chainlink price feed\n    AggregatorV3Interface immutable ethUsdPriceFeed;\n\n    /// @notice Mapping of token to token/usd chainlink price feed\n    mapping(address => AggregatorV3Interface) public feed;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                   EVENTS                                   */\n    /* -------------------------------------------------------------------------- */\n\n    event UpdateFeed(address indexed token, address indexed feed);\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _ethUsdPriceFeed ETH USD Chainlink price feed\n    */\n    constructor(AggregatorV3Interface _ethUsdPriceFeed) Ownable(msg.sender) {\n        ethUsdPriceFeed = _ethUsdPriceFeed;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @inheritdoc IOracle\n    /// @dev feed[token].latestRoundData should return price scaled by 8 decimals\n    function getPrice(address token) external view virtual returns (uint) {\n        (, int answer,,,) =\n            feed[token].latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(token, address(feed[token]));\n\n        return (\n            (uint(answer)*1e18)/getEthPrice()\n        );\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                             INTERNAL FUNCTIONS                             */\n    /* -------------------------------------------------------------------------- */\n\n    function getEthPrice() internal view returns (uint) {\n        (, int answer,,,) =\n            ethUsdPriceFeed.latestRoundData();\n\n        if (answer < 0)\n            revert Errors.NegativePrice(address(0), address(ethUsdPriceFeed));\n\n        return uint(answer);\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                               ADMIN FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    function setFeed(\n        address token,\n        AggregatorV3Interface _feed\n    ) external adminOnly {\n        feed[token] = _feed;\n        emit UpdateFeed(token, address(_feed));\n    }\n}"
    }
  ]
}