{
  "Title": "[L-02] The Contract Should Approve(0) first",
  "Content": "\nSome tokens (like USDT L199) do not work when changing the allowance from an existing non-zero allowance value.\nThey must first be approved by zero and then the actual allowance must be approved.\n\n    IERC20(token).approve(address(operator), 0);\n    IERC20(token).approve(address(operator), amount);\n\n### Proof of Concept\n\nNavigate to the following contract functions:\n\n[Collateral.sol#L76](https://github.com/code-423n4/2022-03-prepo/blob/d62d7146b27fd39a5f1358ffde08766724886cf5/contracts/core/Collateral.sol#L76)<br>\n[SingleStrategyController.sol#L60](https://github.com/code-423n4/2022-03-prepo/blob/d62d7146b27fd39a5f1358ffde08766724886cf5/contracts/core/SingleStrategyController.sol#L60)<br>\n[SingleStrategyController.sol#L62](https://github.com/code-423n4/2022-03-prepo/blob/d62d7146b27fd39a5f1358ffde08766724886cf5/contracts/core/SingleStrategyController.sol#L62)<br>\n\n### Recommended Mitigation Steps\n\nApprove with a zero amount first before setting the actual amount.\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/contests/2022-03-prepo-contest",
  "Code": [
    {
      "filename": "contracts/core/Collateral.sol",
      "content": "// SPDX-License-Identifier: UNLICENSED\npragma solidity =0.8.7;\n\nimport \"./interfaces/ICollateral.sol\";\nimport \"./interfaces/IStrategyController.sol\";\nimport \"./interfaces/IHook.sol\";\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/ERC20Upgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/token/ERC20/utils/SafeERC20Upgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol\";\nimport \"@openzeppelin/contracts-upgradeable/security/ReentrancyGuardUpgradeable.sol\";\n\ncontract Collateral is\n    ICollateral,\n    ERC20Upgradeable,\n    OwnableUpgradeable,\n    ReentrancyGuardUpgradeable\n{\n    using SafeERC20Upgradeable for IERC20Upgradeable;\n\n    bool private _depositsAllowed;\n    bool private _withdrawalsAllowed;\n    address private _treasury;\n    uint256 private _mintingFee;\n    uint256 private _redemptionFee;\n    IERC20Upgradeable private _baseToken;\n    IStrategyController private _strategyController;\n\n    uint256 private _delayedWithdrawalExpiry;\n    mapping(address => WithdrawalRequest) private _accountToWithdrawalRequest;\n\n    IHook private _depositHook;\n    IHook private _withdrawHook;\n\n    uint256 private constant FEE_DENOMINATOR = 1000000;\n    uint256 private constant FEE_LIMIT = 50000;\n\n    function initialize(address _newBaseToken, address _newTreasury)\n        public\n        initializer\n    {\n        __Ownable_init_unchained();\n        __ReentrancyGuard_init_unchained();\n        __ERC20_init_unchained(\n            string(\"prePO Collateral Token\"),\n            string(\"preCT\")\n        );\n        _baseToken = IERC20Upgradeable(_newBaseToken);\n        _treasury = _newTreasury;\n    }\n\n    function deposit(uint256 _amount)\n        external\n        override\n        nonReentrant\n        returns (uint256)\n    {\n        require(_depositsAllowed, \"Deposits not allowed\");\n        _baseToken.safeTransferFrom(msg.sender, address(this), _amount);\n        // Calculate fees and shares to mint including latent contract funds\n        uint256 _amountToDeposit = _baseToken.balanceOf(address(this));\n        // Record deposit before fee is taken\n        if (address(_depositHook) != address(0)) {\n            _depositHook.hook(msg.sender, _amount, _amountToDeposit);\n        }\n        /**\n         * Add 1 to avoid rounding to zero, only process deposit if user is\n         * depositing an amount large enough to pay a fee.\n         */\n        uint256 _fee = (_amountToDeposit * _mintingFee) / FEE_DENOMINATOR + 1;\n        require(_amountToDeposit > _fee, \"Deposit amount too small\");\n        _baseToken.safeTransfer(_treasury, _fee);\n        _amountToDeposit -= _fee;\n\n        uint256 _valueBefore = _strategyController.totalValue();\n        _baseToken.approve(address(_strategyController), _amountToDeposit);\n        _strategyController.deposit(_amountToDeposit);\n        uint256 _valueAfter = _strategyController.totalValue();\n        _amountToDeposit = _valueAfter - _valueBefore;\n\n        uint256 _shares = 0;\n        if (totalSupply() == 0) {\n            _shares = _amountToDeposit;\n        } else {\n            /**\n             * # of shares owed = amount deposited / cost per share, cost per\n             * share = total supply / total value.\n             */\n            _shares = (_amountToDeposit * totalSupply()) / (_valueBefore);\n        }\n        _mint(msg.sender, _shares);\n        return _shares;\n    }\n\n    function initiateWithdrawal(uint256 _amount) external override {\n        /**\n         * Checking the balance before initiation is necessary since a user\n         * could initiate an unlimited withdrawal amount ahead of time,\n         * negating the protection a delayed withdrawal offers.\n         */\n        require(balanceOf(msg.sender) >= _amount, \"Insufficient balance\");\n        _accountToWithdrawalRequest[msg.sender].amount = _amount;\n        _accountToWithdrawalRequest[msg.sender].blockNumber = block.number;\n    }\n\n    function uninitiateWithdrawal() external override {\n        _accountToWithdrawalRequest[msg.sender].amount = 0;\n        _accountToWithdrawalRequest[msg.sender].blockNumber = 0;\n    }\n\n    function _processDelayedWithdrawal(address _account, uint256 _amount)\n        internal\n    {\n        /**\n         * Verify that the withdrawal being processed matches what was\n         * recorded during initiation.\n         */\n        require(\n            _accountToWithdrawalRequest[_account].amount == _amount,\n            \"Initiated amount does not match\"\n        );\n        uint256 _recordedBlock = _accountToWithdrawalRequest[_account]\n            .blockNumber;\n        require(\n            _recordedBlock + _delayedWithdrawalExpiry >= block.number,\n            \"Must withdraw before expiry\"\n        );\n        require(\n            block.number > _recordedBlock,\n            \"Must withdraw in a later block\"\n        );\n        // Reset the initiation prior to withdrawal.\n        _accountToWithdrawalRequest[_account].amount = 0;\n        _accountToWithdrawalRequest[_account].blockNumber = 0;\n    }\n\n    function withdraw(uint256 _amount)\n        external\n        override\n        nonReentrant\n        returns (uint256)\n    {\n        require(_withdrawalsAllowed, \"Withdrawals not allowed\");\n        if (_delayedWithdrawalExpiry != 0) {\n            _processDelayedWithdrawal(msg.sender, _amount);\n        }\n        uint256 _owed = (_strategyController.totalValue() * _amount) /\n            totalSupply();\n        _burn(msg.sender, _amount);\n\n        uint256 _balanceBefore = _baseToken.balanceOf(address(this));\n        _strategyController.withdraw(address(this), _owed);\n        uint256 _balanceAfter = _baseToken.balanceOf(address(this));\n\n        uint256 _amountWithdrawn = _balanceAfter - _balanceBefore;\n        // Record withdrawal before fee is taken\n        if (address(_withdrawHook) != address(0)) {\n            _withdrawHook.hook(msg.sender, _amount, _amountWithdrawn);\n        }\n\n        /**\n         * Send redemption fee to the protocol treasury. Add 1 to avoid\n         * rounding to zero, only process withdrawal if user is\n         * withdrawing an amount large enough to pay a fee.\n         */\n        uint256 _fee = (_amountWithdrawn * _redemptionFee) /\n            FEE_DENOMINATOR +\n            1;\n        require(_amountWithdrawn > _fee, \"Withdrawal amount too small\");\n        _baseToken.safeTransfer(_treasury, _fee);\n        _amountWithdrawn -= _fee;\n        _baseToken.safeTransfer(msg.sender, _amountWithdrawn);\n        return _amountWithdrawn;\n    }\n\n    function setDepositsAllowed(bool _allowed) external override onlyOwner {\n        _depositsAllowed = _allowed;\n        emit DepositsAllowedChanged(_allowed);\n    }\n\n    function setWithdrawalsAllowed(bool _allowed) external override onlyOwner {\n        _withdrawalsAllowed = _allowed;\n        emit WithdrawalsAllowedChanged(_allowed);\n    }\n\n    function setStrategyController(IStrategyController _newStrategyController)\n        external\n        override\n        onlyOwner\n    {\n        _strategyController = _newStrategyController;\n        emit StrategyControllerChanged(address(_strategyController));\n    }\n\n    function setDelayedWithdrawalExpiry(uint256 _newDelayedWithdrawalExpiry)\n        external\n        override\n        onlyOwner\n    {\n        _delayedWithdrawalExpiry = _newDelayedWithdrawalExpiry;\n        emit DelayedWithdrawalExpiryChanged(_delayedWithdrawalExpiry);\n    }\n\n    function setMintingFee(uint256 _newMintingFee)\n        external\n        override\n        onlyOwner\n    {\n        require(_newMintingFee <= FEE_LIMIT, \"Exceeds fee limit\");\n        _mintingFee = _newMintingFee;\n        emit MintingFeeChanged(_mintingFee);\n    }\n\n    function setRedemptionFee(uint256 _newRedemptionFee)\n        external\n        override\n        onlyOwner\n    {\n        require(_newRedemptionFee <= FEE_LIMIT, \"Exceeds fee limit\");\n        _redemptionFee = _newRedemptionFee;\n        emit RedemptionFeeChanged(_redemptionFee);\n    }\n\n    function setDepositHook(IHook _newDepositHook)\n        external\n        override\n        onlyOwner\n    {\n        _depositHook = _newDepositHook;\n        emit DepositHookChanged(address(_depositHook));\n    }\n\n    function setWithdrawHook(IHook _newWithdrawHook)\n        external\n        override\n        onlyOwner\n    {\n        _withdrawHook = _newWithdrawHook;\n        emit WithdrawHookChanged(address(_withdrawHook));\n    }\n\n    function getDepositsAllowed() external view override returns (bool) {\n        return _depositsAllowed;\n    }\n\n    function getWithdrawalsAllowed() external view override returns (bool) {\n        return _withdrawalsAllowed;\n    }\n\n    function getTreasury() external view override returns (address) {\n        return _treasury;\n    }\n\n    function getMintingFee() external view override returns (uint256) {\n        return _mintingFee;\n    }\n\n    function getRedemptionFee() external view override returns (uint256) {\n        return _redemptionFee;\n    }\n\n    function getBaseToken()\n        external\n        view\n        override\n        returns (IERC20Upgradeable)\n    {\n        return _baseToken;\n    }\n\n    function getStrategyController()\n        external\n        view\n        override\n        returns (IStrategyController)\n    {\n        return _strategyController;\n    }\n\n    function getDelayedWithdrawalExpiry()\n        external\n        view\n        override\n        returns (uint256)\n    {\n        return _delayedWithdrawalExpiry;\n    }\n\n    function getWithdrawalRequest(address _account)\n        external\n        view\n        override\n        returns (WithdrawalRequest memory)\n    {\n        return _accountToWithdrawalRequest[_account];\n    }\n\n    function getDepositHook() external view override returns (IHook) {\n        return _depositHook;\n    }\n\n    function getWithdrawHook() external view override returns (IHook) {\n        return _withdrawHook;\n    }\n\n    function getAmountForShares(uint256 _shares)\n        external\n        view\n        override\n        returns (uint256)\n    {\n        if (totalSupply() == 0) {\n            return _shares;\n        }\n        return (_shares * totalAssets()) / totalSupply();\n    }\n\n    function getSharesForAmount(uint256 _amount)\n        external\n        view\n        override\n        returns (uint256)\n    {\n        uint256 _totalAssets = totalAssets();\n        return\n            (_totalAssets > 0)\n                ? ((_amount * totalSupply()) / _totalAssets)\n                : 0;\n    }\n\n    function getFeeDenominator() external pure override returns (uint256) {\n        return FEE_DENOMINATOR;\n    }\n\n    function getFeeLimit() external pure override returns (uint256) {\n        return FEE_LIMIT;\n    }\n\n    function totalAssets() public view override returns (uint256) {\n        return\n            _baseToken.balanceOf(address(this)) +\n            _strategyController.totalValue();\n    }\n}"
    },
    {
      "filename": "contracts/core/SingleStrategyController.sol",
      "content": "// SPDX-License-Identifier: UNLICENSED\npragma solidity =0.8.7;\n\nimport \"./interfaces/IStrategy.sol\";\nimport \"./interfaces/IStrategyController.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"@openzeppelin/contracts/security/ReentrancyGuard.sol\";\n\ncontract SingleStrategyController is\n    IStrategyController,\n    Ownable,\n    ReentrancyGuard\n{\n    using SafeERC20 for IERC20;\n\n    address private _vault;\n    IStrategy private _strategy;\n    IERC20 private immutable _baseToken;\n\n    modifier onlyVault() {\n        require(msg.sender == _vault, \"Caller is not the vault\");\n        _;\n    }\n\n    constructor(IERC20 _token) {\n        require(address(_token) != address(0), \"Zero address\");\n        _baseToken = _token;\n    }\n\n    // Assumes approval to take `_amount` has already been given by vault\n    function deposit(uint256 _amount)\n        external\n        override\n        onlyVault\n        nonReentrant\n    {\n        _baseToken.safeTransferFrom(_vault, address(this), _amount);\n        _strategy.deposit(_baseToken.balanceOf(address(this)));\n    }\n\n    function withdraw(address _recipient, uint256 _amount)\n        external\n        override\n        onlyVault\n        nonReentrant\n    {\n        _strategy.withdraw(_recipient, _amount);\n    }\n\n    function migrate(IStrategy _newStrategy)\n        external\n        override\n        onlyOwner\n        nonReentrant\n    {\n        uint256 _oldStrategyBalance;\n        IStrategy _oldStrategy = _strategy;\n        _strategy = _newStrategy;\n        _baseToken.approve(address(_newStrategy), type(uint256).max);\n        if (address(_oldStrategy) != address(0)) {\n            _baseToken.approve(address(_oldStrategy), 0);\n            _oldStrategyBalance = _oldStrategy.totalValue();\n            _oldStrategy.withdraw(address(this), _oldStrategyBalance);\n            _newStrategy.deposit(_baseToken.balanceOf(address(this)));\n        }\n        emit StrategyMigrated(\n            address(_oldStrategy),\n            address(_newStrategy),\n            _oldStrategyBalance\n        );\n    }\n\n    function setVault(address _newVault) external override onlyOwner {\n        _vault = _newVault;\n        emit VaultChanged(_newVault);\n    }\n\n    function totalValue() external view override returns (uint256) {\n        return _baseToken.balanceOf(address(this)) + _strategy.totalValue();\n    }\n\n    function getVault() external view override returns (address) {\n        return _vault;\n    }\n\n    function getStrategy() external view override returns (IStrategy) {\n        return _strategy;\n    }\n\n    function getBaseToken() external view override returns (IERC20) {\n        return _baseToken;\n    }\n}"
    },
    {
      "filename": "contracts/core/SingleStrategyController.sol",
      "content": "// SPDX-License-Identifier: UNLICENSED\npragma solidity =0.8.7;\n\nimport \"./interfaces/IStrategy.sol\";\nimport \"./interfaces/IStrategyController.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"@openzeppelin/contracts/security/ReentrancyGuard.sol\";\n\ncontract SingleStrategyController is\n    IStrategyController,\n    Ownable,\n    ReentrancyGuard\n{\n    using SafeERC20 for IERC20;\n\n    address private _vault;\n    IStrategy private _strategy;\n    IERC20 private immutable _baseToken;\n\n    modifier onlyVault() {\n        require(msg.sender == _vault, \"Caller is not the vault\");\n        _;\n    }\n\n    constructor(IERC20 _token) {\n        require(address(_token) != address(0), \"Zero address\");\n        _baseToken = _token;\n    }\n\n    // Assumes approval to take `_amount` has already been given by vault\n    function deposit(uint256 _amount)\n        external\n        override\n        onlyVault\n        nonReentrant\n    {\n        _baseToken.safeTransferFrom(_vault, address(this), _amount);\n        _strategy.deposit(_baseToken.balanceOf(address(this)));\n    }\n\n    function withdraw(address _recipient, uint256 _amount)\n        external\n        override\n        onlyVault\n        nonReentrant\n    {\n        _strategy.withdraw(_recipient, _amount);\n    }\n\n    function migrate(IStrategy _newStrategy)\n        external\n        override\n        onlyOwner\n        nonReentrant\n    {\n        uint256 _oldStrategyBalance;\n        IStrategy _oldStrategy = _strategy;\n        _strategy = _newStrategy;\n        _baseToken.approve(address(_newStrategy), type(uint256).max);\n        if (address(_oldStrategy) != address(0)) {\n            _baseToken.approve(address(_oldStrategy), 0);\n            _oldStrategyBalance = _oldStrategy.totalValue();\n            _oldStrategy.withdraw(address(this), _oldStrategyBalance);\n            _newStrategy.deposit(_baseToken.balanceOf(address(this)));\n        }\n        emit StrategyMigrated(\n            address(_oldStrategy),\n            address(_newStrategy),\n            _oldStrategyBalance\n        );\n    }\n\n    function setVault(address _newVault) external override onlyOwner {\n        _vault = _newVault;\n        emit VaultChanged(_newVault);\n    }\n\n    function totalValue() external view override returns (uint256) {\n        return _baseToken.balanceOf(address(this)) + _strategy.totalValue();\n    }\n\n    function getVault() external view override returns (address) {\n        return _vault;\n    }\n\n    function getStrategy() external view override returns (IStrategy) {\n        return _strategy;\n    }\n\n    function getBaseToken() external view override returns (IERC20) {\n        return _baseToken;\n    }\n}"
    }
  ]
}