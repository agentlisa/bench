{
  "Title": "Low Granularity of Voting Threshold",
  "Content": "The `WhitelistRegistry` contract maintains a list of addresses which can act as resolvers. These are required to maintain a certain percentage ownership of the `token` supply, defined by the `resolverPercentageThreshold`. As explained in the [associated comment](https://github.com/1inch/limit-order-settlement/blob/ff7909c4f32bee8879211607275dc30d788afee8/contracts/WhitelistRegistry.sol#L39), a `resolverPercentageThreshold` of `10_000` translates to 100% supply ownership, while `1000` corresponds to 10% and `100` to 1%. The smallest possible non-zero value for the threshold being `1` means that resolvers will always need to maintain at least a 0.01% ownership of the supply of `token`. This also means that 0.01% is the smallest amount by which the required percentage ownership for resolvers can be incremented/decremented.\n\n\nHowever, assuming that `token` is the 1inch token with a total supply of 1,500,000,000, a 0.01% ownership at current prices would cost roughly $51,000. While the resolver role is likely to be restricted to a small set, $51,000 as the minimum increment seems high and the protocol may prefer a higher granularity when adjusting `resolverPercentageThreshold`.\n\n\nConsider increasing the number of decimals of the `BASIS_POINTS` variable to increase the granularity with which the threshold can be changed and be more future-proof.\n\n\n***Update:** Acknowledged, will resolve. The 1inch team stated:*\n\n\n\n> *Good find, we will increase granularity with the next whitelist release. However, the token that the whitelist is tracking is actually [Delegated-Staked-1INCH](https://etherscan.io/token/0xAccfAc2339e16DC80c50d2fa81b5c2B049B4f947) and its total supply is unlikely to reach 1INCH total supply.*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/WhitelistRegistry.sol",
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity 0.8.19;\n\nimport \"@1inch/solidity-utils/contracts/libraries/UniERC20.sol\";\nimport \"@1inch/solidity-utils/contracts/libraries/AddressSet.sol\";\nimport \"@1inch/st1inch/contracts/interfaces/IVotable.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\n\n/**\n * @title WhitelistRegistry\n * @notice The contract manages a whitelist for trading resolvers, providing functions to register,\n * promote and remove addresses, as well as setting various thresholds and limits. It also includes an\n * emergency rescue function for tokens sent to the contract accidentally.\n */\ncontract WhitelistRegistry is Ownable {\n    using UniERC20 for IERC20;\n    using AddressSet for AddressSet.Data;\n    using AddressArray for AddressArray.Data;\n\n    error BalanceLessThanThreshold();\n    error AlreadyRegistered();\n    error NotWhitelisted();\n    error SamePromotee();\n\n    /// @notice Emitted after a new resolver is registered.\n    event Registered(address addr);\n    /// @notice Emitted when a resolver is pushed out of whitelist.\n    event Unregistered(address addr);\n    /// @notice Emitted when the new minimum total supply percentage to get into the whitelist is set.\n    event ResolverPercentageThresholdSet(uint256 resolverPercentageThreshold);\n    /// @notice Emitted when a new worker for a resolver is set.\n    event Promotion(address promoter, uint256 chainId, address promotee);\n\n    uint256 public constant BASIS_POINTS = 10000;\n    IVotable public immutable token;\n\n    mapping(address => mapping(uint256 => address)) public promotions;\n    // 100% = 10000, 10% = 1000, 1% = 100\n    uint256 public resolverPercentageThreshold;\n\n    AddressSet.Data private _whitelist;\n\n    constructor(\n        IVotable token_,\n        uint256 resolverPercentageThreshold_\n    ) {\n        token = token_;\n        _setResolverPercentageThreshold(resolverPercentageThreshold_);\n    }\n\n    /**\n     * @notice Allows the contract owner to recover any tokens accidentally sent to the contract.\n     * @param token_ The token to recover.\n     * @param amount The amount of tokens to recover.\n     */\n    function rescueFunds(IERC20 token_, uint256 amount) external onlyOwner {\n        token_.uniTransfer(payable(msg.sender), amount);\n    }\n\n    /**\n     * @notice Allows the contract owner to set a new resolver threshold.\n     * The resolver threshold is the minimum total supply percentage required to get into the whitelist.\n     * @param resolverPercentageThreshold_ The new resolver threshold.\n     */\n    function setResolverPercentageThreshold(uint256 resolverPercentageThreshold_) external onlyOwner {\n        _setResolverPercentageThreshold(resolverPercentageThreshold_);\n    }\n\n\n    /**\n     * @notice Attempts to register the caller in the whitelist.\n     * @dev Reverts if the caller's total supply percentage is below the resolver threshold.\n     */\n    function register() external {\n        uint256 percentageThreshold = resolverPercentageThreshold;\n        uint256 totalSupply = token.totalSupply();\n        if (!_isValidBalance(percentageThreshold, token.balanceOf(msg.sender), totalSupply)) revert BalanceLessThanThreshold();\n        if (!_whitelist.add(msg.sender)) revert AlreadyRegistered();\n        emit Registered(msg.sender);\n        _clean(percentageThreshold, totalSupply);\n    }\n\n    /**\n     * @notice Registers a worker for the resolver to settle orders.\n     * @param chainId The chain ID where the worker will assigned.\n     * @param promotee The worker's address.\n     */\n    function promote(uint256 chainId, address promotee) external {\n        if (promotions[msg.sender][chainId] == promotee) revert SamePromotee();\n        promotions[msg.sender][chainId] = promotee;\n        emit Promotion(msg.sender, chainId, promotee);\n    }\n\n    /**\n     * @notice Cleans the whitelist by removing addresses that fall below the resolver threshold.\n     */\n    function clean() external {\n        _clean(resolverPercentageThreshold, token.totalSupply());\n    }\n\n    /**\n     * @notice Returns the addresses in the whitelist.\n     * @return whitelist A list of whitelisted addresses.\n     */\n    function getWhitelist() external view returns (address[] memory /* whitelist */) {\n        return _whitelist.items.get();\n    }\n\n    /**\n     * @notice Returns the worker list for a particular chain ID.\n     * @param chainId The chain ID to get the promoted addresses for.\n     * @return promotees A list of worker addresses.\n     */\n    function getPromotees(uint256 chainId) external view returns (address[] memory promotees) {\n        promotees = _whitelist.items.get();\n        unchecked {\n            uint256 len = promotees.length;\n            for (uint256 i = 0; i < len; ++i) {\n                promotees[i] = promotions[promotees[i]][chainId];\n            }\n        }\n    }\n\n    function _setResolverPercentageThreshold(uint256 resolverPercentageThreshold_) private {\n        resolverPercentageThreshold = resolverPercentageThreshold_;\n        emit ResolverPercentageThresholdSet(resolverPercentageThreshold_);\n    }\n\n    function _removeFromWhitelist(address account) private {\n        _whitelist.remove(account);\n        emit Unregistered(account);\n    }\n\n    function _isValidBalance(uint256 percentageThreshold, uint256 balance, uint256 totalSupply) private pure returns (bool) {\n        return (\n            balance > 0 &&\n            balance * BASIS_POINTS >= totalSupply * percentageThreshold)\n        ;\n    }\n\n    function _clean(uint256 percentageThreshold, uint256 totalSupply) private {\n        uint256 whitelistLength = _whitelist.length();\n        unchecked {\n            for (uint256 i = 0; i < whitelistLength; ) {\n                address curWhitelisted = _whitelist.at(i);\n                uint256 balance = token.balanceOf(curWhitelisted);\n                if (!_isValidBalance(percentageThreshold, balance, totalSupply)) {\n                    _removeFromWhitelist(curWhitelisted);\n                    whitelistLength--;\n                } else {\n                    i++;\n                }\n            }\n        }\n    }\n}"
    }
  ]
}