{
  "Title": "[L-05] Inconsistent documentation to actual function logic",
  "Content": "\nIt is mentioned in documentation of the function `validateActionCreation` that the param `actionInfo` is used. \n\n```solidity\n  /// @notice Reverts if action creation is not allowed.\n  /// @param actionInfo Data required to create an action.\n  function validateActionCreation(ActionInfo calldata actionInfo) external;\n```\n\nhttps://github.com/code-423n4/2023-06-llama/blob/main/src/interfaces/ILlamaStrategy.sol#L33-L35\n\nHowever, in `LlamaAbsoluteQuorum.sol` the param is commented out and is not used in the function.\n\n```solidity\n  function validateActionCreation(ActionInfo calldata /* actionInfo */ ) external view override {\n```\n\nhttps://github.com/code-423n4/2023-06-llama/blob/main/src/strategies/LlamaAbsoluteQuorum.sol#L27\n\nThe same applies to `isApprovalEnabled` and `isDisapprovalEnabled`.\n\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2023-06-llama",
  "Code": [
    {
      "filename": "src/interfaces/ILlamaStrategy.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.19;\n\nimport {ActionInfo} from \"src/lib/Structs.sol\";\nimport {LlamaCore} from \"src/LlamaCore.sol\";\nimport {LlamaPolicy} from \"src/LlamaPolicy.sol\";\n\n/// @title Llama Strategy Interface\n/// @author Llama (devsdosomething@llama.xyz)\n/// @notice This is the interface for Llama strategies which determine the rules of an action's process.\n/// @dev The interface is sorted by the stage of the action's lifecycle in which the method's are used.\n/// @dev Validation methods are not `view` because (1) the strategy may want to save off some data\n/// during the call, and (2) having `view` methods that can revert isn't great UX.\ninterface ILlamaStrategy {\n  // -------- For Inspection --------\n  // These are not strictly required by the core, but are useful for inspecting a strategy contract.\n\n  /// @notice Returns the address of the Llama core that this strategy is registered to.\n  function llamaCore() external view returns (LlamaCore);\n\n  /// @notice Returns the name of the Llama policy that this strategy is registered to.\n  function policy() external view returns (LlamaPolicy);\n\n  // -------- At Strategy Creation --------\n\n  /// @notice Initializes a new clone of the strategy.\n  /// @param config The strategy configuration, encoded as bytes to support differing constructor arguments in\n  /// different strategies.\n  function initialize(bytes memory config) external;\n\n  // -------- At Action Creation --------\n\n  /// @notice Reverts if action creation is not allowed.\n  /// @param actionInfo Data required to create an action.\n  function validateActionCreation(ActionInfo calldata actionInfo) external;\n\n  // -------- When Casting Approval --------\n\n  /// @notice Reverts if approvals are not allowed with this strategy for the given policyholder when approving with\n  /// role.\n  /// @param actionInfo Data required to create an action.\n  /// @param policyholder Address of the policyholder.\n  /// @param role The role of the policyholder being used to cast approval.\n  function isApprovalEnabled(ActionInfo calldata actionInfo, address policyholder, uint8 role) external;\n\n  /// @notice Get the quantity of an approval of a policyholder at a specific timestamp.\n  /// @param policyholder Address of the policyholder.\n  /// @param role The role to check quantity for.\n  /// @param timestamp The timestamp at which to get the approval quantity.\n  /// @return The quantity of the policyholder's approval.\n  function getApprovalQuantityAt(address policyholder, uint8 role, uint256 timestamp) external view returns (uint128);\n\n  // -------- When Casting Disapproval --------\n\n  /// @notice Reverts if disapprovals are not allowed with this strategy for the given policyholder when disapproving\n  /// with role.\n  /// @param actionInfo Data required to create an action.\n  /// @param policyholder Address of the policyholder.\n  /// @param role The role of the policyholder being used to cast disapproval.\n  function isDisapprovalEnabled(ActionInfo calldata actionInfo, address policyholder, uint8 role) external;\n\n  /// @notice Get the quantity of a disapproval of a policyholder at a specific timestamp.\n  /// @param policyholder Address of the policyholder.\n  /// @param role The role to check quantity for.\n  /// @param timestamp The timestamp at which to get the disapproval quantity.\n  /// @return The quantity of the policyholder's disapproval.\n  function getDisapprovalQuantityAt(address policyholder, uint8 role, uint256 timestamp)\n    external\n    view\n    returns (uint128);\n\n  // -------- When Queueing --------\n\n  /// @notice Returns the earliest timestamp, in seconds, at which an action can be executed.\n  /// @param actionInfo Data required to create an action.\n  /// @return The earliest timestamp at which an action can be executed.\n  function minExecutionTime(ActionInfo calldata actionInfo) external view returns (uint64);\n\n  // -------- When Canceling --------\n\n  /// @notice Reverts if the action cannot be canceled.\n  /// @param actionInfo Data required to create an action.\n  /// @param caller Policyholder initiating the cancelation.\n  function validateActionCancelation(ActionInfo calldata actionInfo, address caller) external;\n\n  // -------- When Determining Action State --------\n  // These are used during casting of approvals and disapprovals, when queueing, and when executing.\n\n  /// @notice Get whether an action is currently active.\n  /// @param actionInfo Data required to create an action.\n  /// @return Boolean value that is `true` if the action is currently active, `false` otherwise.\n  function isActive(ActionInfo calldata actionInfo) external view returns (bool);\n\n  /// @notice Get whether an action has passed the approval process.\n  /// @param actionInfo Data required to create an action.\n  /// @return Boolean value that is `true` if the action has passed the approval process.\n  function isActionApproved(ActionInfo calldata actionInfo) external view returns (bool);\n\n  /// @notice Get whether an action has been vetoed during the disapproval process.\n  /// @param actionInfo Data required to create an action.\n  /// @return Boolean value that is `true` if the action has been vetoed during the disapproval process.\n  function isActionDisapproved(ActionInfo calldata actionInfo) external view returns (bool);\n\n  /// @notice Returns `true` if the action is expired, `false` otherwise.\n  /// @param actionInfo Data required to create an action.\n  /// @return Boolean value that is `true` if the action is expired.\n  function isActionExpired(ActionInfo calldata actionInfo) external view returns (bool);\n}"
    },
    {
      "filename": "src/strategies/LlamaAbsoluteQuorum.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.19;\n\nimport {Initializable} from \"@openzeppelin/proxy/utils/Initializable.sol\";\n\nimport {FixedPointMathLib} from \"@solmate/utils/FixedPointMathLib.sol\";\n\nimport {ILlamaStrategy} from \"src/interfaces/ILlamaStrategy.sol\";\nimport {LlamaAbsoluteStrategyBase} from \"src/strategies/LlamaAbsoluteStrategyBase.sol\";\nimport {ActionState} from \"src/lib/Enums.sol\";\nimport {LlamaUtils} from \"src/lib/LlamaUtils.sol\";\nimport {Action, ActionInfo} from \"src/lib/Structs.sol\";\nimport {LlamaCore} from \"src/LlamaCore.sol\";\nimport {LlamaPolicy} from \"src/LlamaPolicy.sol\";\n\n/// @title Llama Absolute Quorum Strategy\n/// @author Llama (devsdosomething@llama.xyz)\n/// @notice This is a Llama strategy which has the following properties:\n///   - Approval/disapproval thresholds are specified as absolute numbers.\n///   - Action creators are allowed to cast approvals or disapprovals on their own actions within this strategy.\ncontract LlamaAbsoluteQuorum is LlamaAbsoluteStrategyBase {\n  // ==========================================\n  // ======== Interface Implementation ========\n  // ==========================================\n\n  /// @inheritdoc ILlamaStrategy\n  function validateActionCreation(ActionInfo calldata /* actionInfo */ ) external view override {\n    LlamaPolicy llamaPolicy = policy; // Reduce SLOADs.\n    uint256 approvalPolicySupply = llamaPolicy.getRoleSupplyAsQuantitySum(approvalRole);\n    if (approvalPolicySupply == 0) revert RoleHasZeroSupply(approvalRole);\n\n    uint256 disapprovalPolicySupply = llamaPolicy.getRoleSupplyAsQuantitySum(disapprovalRole);\n    if (disapprovalPolicySupply == 0) revert RoleHasZeroSupply(disapprovalRole);\n\n    if (minApprovals > approvalPolicySupply) revert InsufficientApprovalQuantity();\n    if (minDisapprovals != type(uint128).max && minDisapprovals > disapprovalPolicySupply) {\n      revert InsufficientDisapprovalQuantity();\n    }\n  }\n\n  // -------- When Casting Approval --------\n\n  /// @inheritdoc ILlamaStrategy\n  function isApprovalEnabled(ActionInfo calldata, /* actionInfo */ address, /* policyholder */ uint8 role)\n    external\n    view\n    override\n  {\n    if (role != approvalRole && !forceApprovalRole[role]) revert InvalidRole(approvalRole);\n  }\n\n  // -------- When Casting Disapproval --------\n\n  /// @inheritdoc ILlamaStrategy\n  function isDisapprovalEnabled(ActionInfo calldata, /* actionInfo */ address, /* policyholder */ uint8 role)\n    external\n    view\n    override\n  {\n    if (minDisapprovals == type(uint128).max) revert DisapprovalDisabled();\n    if (role != disapprovalRole && !forceDisapprovalRole[role]) revert InvalidRole(disapprovalRole);\n  }\n}"
    }
  ]
}