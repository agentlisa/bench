{
  "Title": "[12]  Constant redefined elsewhere",
  "Content": "Consider defining in only one contract so that values cannot become out of sync when only one location is updated. A [cheap way](https://medium.com/coinmonks/gas-cost-of-solidity-library-functions-dbe0cedd4678) to store constants in a single location is to create an `internal constant` in a `library`. If the variable is a local cache of another contract's value, consider making the cache variable internal or private, which will require external users to query the contract with the source of truth, so that callers don't get out of sync.\n\n*There are 8 instances of this issue:*\n```solidity\nFile: src/Pages.sol\n\n/// @audit seen in src/ArtGobblers.sol \n86:       Goo public immutable goo;\n\n/// @audit seen in src/ArtGobblers.sol \n89:       address public immutable community;\n\n/// @audit seen in src/ArtGobblers.sol \n103:      uint256 public immutable mintStart;\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/src/Pages.sol#L86\n\n```solidity\nFile: src/utils/rand/ChainlinkV1RandProvider.sol\n\n/// @audit seen in src/utils/token/PagesERC721.sol \n20:       ArtGobblers public immutable artGobblers;\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/src/utils/rand/ChainlinkV1RandProvider.sol#L20\n\n```solidity\nFile: script/deploy/DeployRinkeby.s.sol\n\n/// @audit seen in src/Pages.sol \n11:       uint256 public immutable mintStart = 1656369768;\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/script/deploy/DeployRinkeby.s.sol#L11\n\n```solidity\nFile: src/Goo.sol\n\n/// @audit seen in src/utils/rand/ChainlinkV1RandProvider.sol \n64:       address public immutable artGobblers;\n\n/// @audit seen in src/ArtGobblers.sol \n67:       address public immutable pages;\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/src/Goo.sol#L64\n\n```solidity\nFile: src/utils/GobblerReserve.sol\n\n/// @audit seen in src/Goo.sol \n18:       ArtGobblers public immutable artGobblers;\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/src/utils/GobblerReserve.sol#L18\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2022-09-artgobblers",
  "Code": [
    {
      "filename": "src/Pages.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {LibString} from \"solmate/utils/LibString.sol\";\nimport {toDaysWadUnsafe} from \"solmate/utils/SignedWadMath.sol\";\n\nimport {LogisticToLinearVRGDA} from \"VRGDAs/LogisticToLinearVRGDA.sol\";\n\nimport {PagesERC721} from \"./utils/token/PagesERC721.sol\";\n\nimport {Goo} from \"./Goo.sol\";\nimport {ArtGobblers} from \"./ArtGobblers.sol\";\n\n/*                                                                                   &@./(\n                                                                                    &//*&\n                                                                                   @/*.&\n                                                                                 (#/./%\n                                                             .,(#%%&@@&%#(/,    *#./#,                   .*(%@@@@&%#((//////(#&@%.\n                                                      #&@&/*./*************.///&@%&@@@@@@@@&%#(///**********./********************#&\n                                                  &@(/*****************************************************************************#/\n                                               (&**********************************************************************************@\n                                              @**********************************************************************************(&\n                                             &/*******************************************************************************%@.\n           ,(                                *&/***********************************************************************./#@@%(((#@%\n            .//                                (@/***./*****************************************************.///#&@@@&##(((#(##((##%&\n             /*.//                                 *@@&#(/////(#&@@@@@@@@@@@@@@@@@@@@@@@@@@@&&&&%%%%%####(#(((((((##(((##((#(###((((#@&\n              (****./                               @###(###(((((###(##((#(((((#(#####((((#(((((((#((((((((((((((((((((#(##(####((((#(#@\n              /******./                            &#((#########(############((#(######(########(####(##(###%&&&%########%&&%###((((####@\n              ,********./                         /&((((#(#((##(((#(((#((########(###((((((((((((((((#((%&#((#((#((((((((((#((((((((((((#@\n              .**********(                        @#(#(#####(###(#&&&#(((((((((#(((%@%##########(######&#((#&#(((##((((%&@%#((##(#(((#((#%&\n               /**********(                      /%(#(((((#((#&%(((#(###(#########(((###(((#####(###((#(%@%###(###((((##(((##&%#(##(((##(#@,\n               (**********./                     @#(#(((((###((#(#((#(((((###(((#(#%@@###((#####(###((#&%&&/.              .*#&&@#((((((((%@\n               ./**********(                    ,%((((((((##((((#&@#(##((((((##%%&&&&%%#&##((###(##((%*                          .@#(##((##@.\n                ./********./                    %#((#(((((#####@##(##%@&(.                %#((#((###%*            *(,.             %#((#(((@,\n                  /********(                    @#((#((((####@#%@#                         .&((((##(&           &@@@@@@%            &#((#&#&,\n                    /******(                   ,&(#(#((((###@&                              .&##(###&            /@@@@@@            (%((((#@%\n                      ./*#@@@@*                (%#(((((#((#&           #@@@@@&.              (%#(#((&.            /#.               ##(#(#(##@&\n                       @%(/((((&               &####(((((#@            .@@@@@@@               &((##(#@@(.                      .#&%#&(#####((##@.\n                      /#(((((((##         (#*  @##(#(((((#@             ,@@@/*               @&((####(&&#(##%%&&&%%##%&&&&&%##(((#%%(###((####((&&\n                       #((((%@@@@@.    @#(((#@*&##(((((((#@                              .%&#@#((####(((#((#(((((((((((((((((((##%((#(###((#####(#@\n                       #@/,,,@#((#%(  @((&%,,,&&%((((((#(#&,                        .%@%#((#@%(((###((((#(((##%&&&&&&&&&&&%#((((####(###(###(###((#@%\n                   #@##&(,,,(&(##(#@ &###@**#@@@@(((((((##(@@@#,            ,(@@&##(((((((((#((((#####((#%@@@@@@@@@@@@@@@@@&&%%################%%(##@&\n                %&##((((#&@@%(((((%# &##(####(#&@&(((((((#(##@#((((((((##((###(#(#(((#%&##(#####(####(##@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@&%##(((#((%@\n             *&#((#######((((###(%&@ (#(####(((#&@%(((((####(##%&##(#((((###(###%@@%###(##%%%###(#(####@@@@&%###((##((((((((##((((((((##((((((#((##(#@\n           (&((((((((((((((((##@&/((@@&####(((((((#&&%#(#((((((((#(((((#((###(#((#&@&&@@@@@@@@%((((((#((((#((((((((((((((((((((((((((((((((((((((((((#\n         .@#(##((((((((((#((&@#%@@&%%&@&##(((###(#####(#@%##(###((#######(((#(#%@#(((#((#(######(###############(###############(###############(##(##\n        (%(#((#((((((((((#&@%#(%%,,,,&#(#@######(####(((((%%(#(#(###(#(((#%@@@@@&##(####(((#(#(#(#######(#######(#######(#######(#######(#######(##((#\n       (%((####((###(#&@########@*,,,@#(#%@(((#((#######(((#&(##(###(%@@@@@%##((#@&%####&@@%(###(###############(###############(###############(###(#\n       @((##(##(((#@%#(#((##(#(#(#&@&#(##@&&@@@&##(#(###(#((&##%&@@%##(#(#(((####((((&%((###((#&####(###(###(###(###(###(###(###(###(###(###(###(####(\n       @((#(((##@%(((((((######((###(#&@%#(#(#(#(##########(%%((((#((((#(((((((((((((#&&#(###&&(((##############(###############(###############(#####\n       .%(#(#%@#((###(#((########&@@((((&#(#####((######(##(%&#((#######(#######(#####(#####((##(#######(#######(#######(#######(#######(#######(#####\n         &##@#(#((##((#####(#&&#(((#&(((@#######(##########(&%%#(#((##(#########(############((((###############(###############(###############(#####\n          *%(((#(#(#(#(#(#%@#(((((((((##(((((((#(#(#(#(#(#(#@###(((#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#\n         ,&(##(#(#####((%@#(###((((((((####((###(########((%@&##(###############(###############(###############(###############(###############(#####\n         &#((((#(##(####&%(##(((((%@%((#(#######(######(##%@&#((((######(#######(#######(#######(#######(#######(#######(#######(#######(#######(#####\n         %###((#(##########((###((##((##########(####(#((&&###(((###############(###############(###############(###############(###############(#####\n          &#(#(#(###(###(##((###(###((##(###(###(###((#&&#######(###(###(###(###(###(###(###(###(###(###(###(###(###(###(###(###(###(###(###(###(###(#\n           %%(#(###(#(#(########(#####(#########(###((#&%#(#####(###############(###############(###############(###############(#############(((#####\n              (&%%#%#%#((#(#####(#######(#######(######((#&%(###(#######(#######(#######(#######(#######(#######(#######(#######((##(##((#(#(###(((###\n                       ##(#(####(###############(######(((((####(###############(###############(###############(#############(##(#((##%%&@@@@&&&%%%%%\n                         &((((((((((((((((((((((((((((((((((((##((((((((((((((((((((((((((((((((((((((((((((((((((((((((##(((##&@@&&%%%%%%%%%%%%%%%%%%\n                          &#(((#(###############(#############%&((((############(###############(###############(#######%&@&%%%%%%%%%%%%%%%%%%%%%%%%%%\n                           @####(#######(#######(#######(##(%&(((#(#####(#######(#######(#######(#######(#######((##%@&%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                            @#(((###############(#######((#@#(##(###############(###############(##############(%@&%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                             &(#(###(###(###(###(###(##((#&###(((###(###(###(###(###(###(###(###(###(###(#####@&%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                             /%((###############(#####((#&######(###############(###############(#(########&&%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                              &#(#######(#######(######(%%##((##(#######(#######(#######(#######(####(((#@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                               @################(######(%%##((##((##############(###############(##(###@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                                &##((#(#(#(#(#(#(#(#(#(#(@##(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(#(##@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                                (%(((###########(#######(#&&##(#(###############(################(#@&%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                                 ##(####(#######(#######(#(#%&#((#######(#######(#######(#######(&@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n                                  /%(((###(#####(########(###(#&%###############(############((#@&%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/\n\n/// @title Pages NFT\n/// @author FrankieIsLost <frankie@paradigm.xyz>\n/// @author transmissions11 <t11s@paradigm.xyz>\n/// @notice Pages is an ERC721 that can hold custom art.\ncontract Pages is PagesERC721, LogisticToLinearVRGDA {\n    using LibString for uint256;\n\n    /*//////////////////////////////////////////////////////////////\n                                ADDRESSES\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice The address of the goo ERC20 token contract.\n    Goo public immutable goo;\n\n    /// @notice The address which receives pages reserved for the community.\n    address public immutable community;\n\n    /*//////////////////////////////////////////////////////////////\n                                  URIS\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Base URI for minted pages.\n    string public BASE_URI;\n\n    /*//////////////////////////////////////////////////////////////\n                            VRGDA INPUT STATE\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Timestamp for the start of the VRGDA mint.\n    uint256 public immutable mintStart;\n\n    /// @notice Id of the most recently minted page.\n    /// @dev Will be 0 if no pages have been minted yet.\n    uint128 public currentId;\n\n    /*//////////////////////////////////////////////////////////////\n                          COMMUNITY PAGES STATE\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice The number of pages minted to the community reserve.\n    uint128 public numMintedForCommunity;\n\n    /*//////////////////////////////////////////////////////////////\n                            PRICING CONSTANTS\n    //////////////////////////////////////////////////////////////*/\n\n    /// @dev The day the switch from a logistic to translated linear VRGDA is targeted to occur.\n    /// @dev Represented as an 18 decimal fixed point number.\n    int256 internal constant SWITCH_DAY_WAD = 233e18;\n\n    /// @notice The minimum amount of pages that must be sold for the VRGDA issuance\n    /// schedule to switch from logistic to the \"post switch\" translated linear formula.\n    /// @dev Computed off-chain by plugging SWITCH_DAY_WAD into the uninverted pacing formula.\n    /// @dev Represented as an 18 decimal fixed point number.\n    int256 internal constant SOLD_BY_SWITCH_WAD = 8336.760939794622713006e18;\n\n    /*//////////////////////////////////////////////////////////////\n                                 EVENTS\n    //////////////////////////////////////////////////////////////*/\n\n    event PagePurchased(address indexed user, uint256 indexed pageId, uint256 price);\n\n    event CommunityPagesMinted(address indexed user, uint256 lastMintedPageId, uint256 numPages);\n\n    /*//////////////////////////////////////////////////////////////\n                                 ERRORS\n    //////////////////////////////////////////////////////////////*/\n\n    error ReserveImbalance();\n\n    error PriceExceededMax(uint256 currentPrice);\n\n    /*//////////////////////////////////////////////////////////////\n                               CONSTRUCTOR\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Sets VRGDA parameters, mint start, relevant addresses, and base URI.\n    /// @param _mintStart Timestamp for the start of the VRGDA mint.\n    /// @param _goo Address of the Goo contract.\n    /// @param _community Address of the community reserve.\n    /// @param _artGobblers Address of the ArtGobblers contract.\n    /// @param _baseUri Base URI for token metadata.\n    constructor(\n        // Mint config:\n        uint256 _mintStart,\n        // Addresses:\n        Goo _goo,\n        address _community,\n        ArtGobblers _artGobblers,\n        // URIs:\n        string memory _baseUri\n    )\n        PagesERC721(_artGobblers, \"Pages\", \"PAGE\")\n        LogisticToLinearVRGDA(\n            4.2069e18, // Target price.\n            0.31e18, // Price decay percent.\n            9000e18, // Logistic asymptote.\n            0.014e18, // Logistic time scale.\n            SOLD_BY_SWITCH_WAD, // Sold by switch.\n            SWITCH_DAY_WAD, // Target switch day.\n            9e18 // Pages to target per day.\n        )\n    {\n        mintStart = _mintStart;\n\n        goo = _goo;\n\n        community = _community;\n\n        BASE_URI = _baseUri;\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                              MINTING LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Mint a page with goo, burning the cost.\n    /// @param maxPrice Maximum price to pay to mint the page.\n    /// @param useVirtualBalance Whether the cost is paid from the\n    /// user's virtual goo balance, or from their ERC20 goo balance.\n    /// @return pageId The id of the page that was minted.\n    function mintFromGoo(uint256 maxPrice, bool useVirtualBalance) external returns (uint256 pageId) {\n        // Will revert if prior to mint start.\n        uint256 currentPrice = pagePrice();\n\n        // If the current price is above the user's specified max, revert.\n        if (currentPrice > maxPrice) revert PriceExceededMax(currentPrice);\n\n        // Decrement the user's goo balance by the current\n        // price, either from virtual balance or ERC20 balance.\n        useVirtualBalance\n            ? artGobblers.burnGooForPages(msg.sender, currentPrice)\n            : goo.burnForPages(msg.sender, currentPrice);\n\n        unchecked {\n            emit PagePurchased(msg.sender, pageId = ++currentId, currentPrice);\n\n            _mint(msg.sender, pageId);\n        }\n    }\n\n    /// @notice Calculate the mint cost of a page.\n    /// @dev If the number of sales is below a pre-defined threshold, we use the\n    /// VRGDA pricing algorithm, otherwise we use the post-switch pricing formula.\n    /// @dev Reverts due to underflow if minting hasn't started yet. Done to save gas.\n    function pagePrice() public view returns (uint256) {\n        // We need checked math here to cause overflow\n        // before minting has begun, preventing mints.\n        uint256 timeSinceStart = block.timestamp - mintStart;\n\n        unchecked {\n            // The number of pages minted for the community reserve\n            // should never exceed 10% of the total supply of pages.\n            return getVRGDAPrice(toDaysWadUnsafe(timeSinceStart), currentId - numMintedForCommunity);\n        }\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                      COMMUNITY PAGES MINTING LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Mint a number of pages to the community reserve.\n    /// @param numPages The number of pages to mint to the reserve.\n    /// @dev Pages minted to the reserve cannot comprise more than 10% of the sum of the\n    /// supply of goo minted pages and the supply of pages minted to the community reserve.\n    function mintCommunityPages(uint256 numPages) external returns (uint256 lastMintedPageId) {\n        unchecked {\n            // Optimistically increment numMintedForCommunity, may be reverted below.\n            // Overflow in this calculation is possible but numPages would have to\n            // be so large that it would cause the loop to run out of gas quickly.\n            uint256 newNumMintedForCommunity = numMintedForCommunity += uint128(numPages);\n\n            // Ensure that after this mint pages minted to the community reserve won't comprise more than\n            // 10% of the new total page supply. currentId is equivalent to the current total supply of pages.\n            if (newNumMintedForCommunity > ((lastMintedPageId = currentId) + numPages) / 10) revert ReserveImbalance();\n\n            // Mint the pages to the community reserve while updating lastMintedPageId.\n            for (uint256 i = 0; i < numPages; i++) _mint(community, ++lastMintedPageId);\n\n            currentId = uint128(lastMintedPageId); // Update currentId with the last minted page id.\n\n            emit CommunityPagesMinted(msg.sender, lastMintedPageId, numPages);\n        }\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                             TOKEN URI LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Returns a page's URI if it has been minted.\n    /// @param pageId The id of the page to get the URI for.\n    function tokenURI(uint256 pageId) public view virtual override returns (string memory) {\n        if (pageId == 0 || pageId > currentId) revert(\"NOT_MINTED\");\n\n        return string.concat(BASE_URI, pageId.toString());\n    }\n}"
    },
    {
      "filename": "src/utils/rand/ChainlinkV1RandProvider.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {VRFConsumerBase} from \"chainlink/v0.8/VRFConsumerBase.sol\";\n\nimport {ArtGobblers} from \"../../ArtGobblers.sol\";\n\nimport {RandProvider} from \"./RandProvider.sol\";\n\n/// @title Chainlink V1 Randomness Provider.\n/// @author FrankieIsLost <frankie@paradigm.xyz>\n/// @author transmissions11 <t11s@paradigm.xyz>\n/// @notice RandProvider wrapper around Chainlink VRF v1.\ncontract ChainlinkV1RandProvider is RandProvider, VRFConsumerBase {\n    /*//////////////////////////////////////////////////////////////\n                                ADDRESSES\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice The address of the Art Gobblers contract.\n    ArtGobblers public immutable artGobblers;\n\n    /*//////////////////////////////////////////////////////////////\n                            VRF CONFIGURATION\n    //////////////////////////////////////////////////////////////*/\n\n    /// @dev Public key to generate randomness against.\n    bytes32 internal immutable chainlinkKeyHash;\n\n    /// @dev Fee required to fulfill a VRF request.\n    uint256 internal immutable chainlinkFee;\n\n    /*//////////////////////////////////////////////////////////////\n                                 ERRORS\n    //////////////////////////////////////////////////////////////*/\n\n    error NotGobblers();\n\n    /*//////////////////////////////////////////////////////////////\n                               CONSTRUCTOR\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Sets relevant addresses and VRF parameters.\n    /// @param _artGobblers Address of the ArtGobblers contract.\n    /// @param _vrfCoordinator Address of the VRF coordinator.\n    /// @param _linkToken Address of the LINK token contract.\n    /// @param _chainlinkKeyHash Public key to generate randomness against.\n    /// @param _chainlinkFee Fee required to fulfill a VRF request.\n    constructor(\n        ArtGobblers _artGobblers,\n        address _vrfCoordinator,\n        address _linkToken,\n        bytes32 _chainlinkKeyHash,\n        uint256 _chainlinkFee\n    ) VRFConsumerBase(_vrfCoordinator, _linkToken) {\n        artGobblers = _artGobblers;\n\n        chainlinkKeyHash = _chainlinkKeyHash;\n        chainlinkFee = _chainlinkFee;\n    }\n\n    /// @notice Request random bytes from Chainlink VRF. Can only by called by the ArtGobblers contract.\n    function requestRandomBytes() external returns (bytes32 requestId) {\n        // The caller must be the ArtGobblers contract, revert otherwise.\n        if (msg.sender != address(artGobblers)) revert NotGobblers();\n\n        emit RandomBytesRequested(requestId);\n\n        // Will revert if we don't have enough LINK to afford the request.\n        return requestRandomness(chainlinkKeyHash, chainlinkFee);\n    }\n\n    /// @dev Handles VRF response by calling back into the ArtGobblers contract.\n    function fulfillRandomness(bytes32 requestId, uint256 randomness) internal override {\n        emit RandomBytesReturned(requestId, randomness);\n\n        artGobblers.acceptRandomSeed(requestId, randomness);\n    }\n}"
    },
    {
      "filename": "script/deploy/DeployRinkeby.s.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {DeployBase} from \"./DeployBase.s.sol\";\n\ncontract DeployRinkeby is DeployBase {\n    address public immutable coldWallet = 0x126620598A797e6D9d2C280b5dB91b46F27A8330;\n\n    address public immutable root = 0x1D18077167c1177253555e45B4b5448B11E30b4b;\n\n    uint256 public immutable mintStart = 1656369768;\n\n    string public constant gobblerBaseUri = \"https://testnet.ag.xyz/api/nfts/gobblers/\";\n    string public constant gobblerUnrevealedUri = \"https://testnet.ag.xyz/api/nfts/unrevealed\";\n    string public constant pagesBaseUri = \"https://testnet.ag.xyz/api/nfts/pages/\";\n\n    constructor()\n        DeployBase(\n            // Team cold wallet:\n            coldWallet,\n            // Merkle root:\n            keccak256(abi.encodePacked(root)),\n            // Mint start:\n            mintStart,\n            // VRF coordinator:\n            address(0xb3dCcb4Cf7a26f6cf6B120Cf5A73875B7BBc655B),\n            // LINK token:\n            address(0x01BE23585060835E02B77ef475b0Cc51aA1e0709),\n            // Chainlink hash:\n            0x2ed0feb3e7fd2022120aa84fab1945545a9f2ffc9076fd6156fa96eaff4c1311,\n            // Chainlink fee:\n            0.1e18,\n            // Gobbler base URI:\n            gobblerBaseUri,\n            // Gobbler unrevealed URI:\n            gobblerUnrevealedUri,\n            // Pages base URI:\n            pagesBaseUri\n        )\n    {}\n}"
    },
    {
      "filename": "src/Goo.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {ERC20} from \"solmate/tokens/ERC20.sol\";\n\n/*                                                                %#/*********(&,\n                                                              .#*********************#.\n                                                            #****./*********************%\n                                                          %*******************************%\n                                                        &**********************************,((\n                                                       @(*,***********************************#&\n                                                    (*********************#***********************(\n                                                  ,%@/**************#%***%**&***%*******************,\n                                                  /********************#****#*#******,**************%\n                                                 ,************,#(*****************(#/&(*,*,*********#\n                                                 **************(%%(&************#@%(///************(\n                                                ./**************,*./##****************************#*%\n                                               #**&**************************************************&@@@@@&@&%#((./.\n                                              (*******************@&%&@@@.   /    %  &********(@/,****,,,*,,,****,,*,**********,*,\n                                             &******************#  /    *     /   /    .. %/****(******************,**&***********./\n                                  /%(*******************&***./#    #.#%%    .,    .,   ##&&@****#***********************************.\n                         *#(*,**************************(***(///.*     *     #     #   .  %*****(/*************************************&\n                 *(***********************************.//****&    #     #    (#&((%@*,*&(******(%************./@#*   *%&%(/&*************(\n               #,**************************************,&******&..*#&(*****,,,,/********************************             (/******,**,**,\n              %*****************************************.//**************#**************************************               .(***********#\n             (*************************./************************************************************************              @**************\n             ,**********&@@@&&%#        &,**********************************************************************@             ./*,%*,********./\n            ***********                .************@(*************(&#///////////////.//#&%/*****************&*,,                &************%\n           (**********.                 .%********************(&./////////////////////////////(%******************                *(**&,&##*\n          #**********(,                &,*./***************%(///////////////////////////////////*&****************\n        (************%                %,*****************&///////////////////////////////////////*(***************.\n      .(***************(             #******************&//////////////////////////////////////////****************\n     .&*************%*./            .*******************%/////////////////////////////////////////****************##\n      .*************%*%             (********************#(///////////////////////////////////(#*****************&**,***,.\n           #***./,***%              #**********************,%%*./////////////////////////*(@*******************(/****./********,((\n           @@,                    &**@*****************************./(%@&%%((((((%&&%(*********************************&,**********.\n                         .   .#,,*****./&/*****************************************************************************************\n                          %,******************************************************************************************************#\n                       %*******@*****************************************************./#%%,...((,           .,********************(\n                     ,*******************************@&(**./%&%*        .,//(//////////,                           ,************./\n                      /**************************&*                      ////*(/////////                            ***(*********%\n                       (*********************(#                         ..///////////(//(                          .***********./\n                         #******************%                       *..,,,(//////////(//(*.//,                     %***************&\n                           %*****************                   ////////&&&&&&&&%#(//(&@&#(#@@                    &*********************#\n                             #****************.                 ,//(//////(@@%%%%%///////****&                   &************************(\n                           .**&***(************./               .@.,(///(/(.//(***((*(//*****@/&                ,*************************./\n                            &********************#             .(#(@#//(****(//(*****(/(&(..&(                  ./*********************(#.\n                        #/***********************./          /,,./*((#%@(%&%(((((((#%&&&/(#(#@(\n                      #*,***********************,*&                 .%@@@&#,  ///(/*\n                     (*************************%                             ..(/,./(,.,*\n                      /#/*./(%&(.*/\n\n/// @title Goo Token (GOO)\n/// @author FrankieIsLost <frankie@paradigm.xyz>\n/// @author transmissions11 <t11s@paradigm.xyz>\n/// @notice Goo is the in-game token for ArtGobblers. It's a standard ERC20\n/// token that can be burned and minted by the gobblers and pages contract.\ncontract Goo is ERC20(\"Goo\", \"GOO\", 18) {\n    /*//////////////////////////////////////////////////////////////\n                                ADDRESSES\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice The address of the Art Gobblers contract.\n    address public immutable artGobblers;\n\n    /// @notice The address of the Pages contract.\n    address public immutable pages;\n\n    /*//////////////////////////////////////////////////////////////\n                                 ERRORS\n    //////////////////////////////////////////////////////////////*/\n\n    error Unauthorized();\n\n    /*//////////////////////////////////////////////////////////////\n                               CONSTRUCTOR\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Sets the addresses of relevant contracts.\n    /// @param _artGobblers Address of the ArtGobblers contract.\n    /// @param _pages Address of the Pages contract.\n    constructor(address _artGobblers, address _pages) {\n        artGobblers = _artGobblers;\n        pages = _pages;\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                             MINT/BURN LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Requires caller address to match user address.\n    modifier only(address user) {\n        if (msg.sender != user) revert Unauthorized();\n\n        _;\n    }\n\n    /// @notice Mint any amount of goo to a user. Can only be called by ArtGobblers.\n    /// @param to The address of the user to mint goo to.\n    /// @param amount The amount of goo to mint.\n    function mintForGobblers(address to, uint256 amount) external only(artGobblers) {\n        _mint(to, amount);\n    }\n\n    /// @notice Burn any amount of goo from a user. Can only be called by ArtGobblers.\n    /// @param from The address of the user to burn goo from.\n    /// @param amount The amount of goo to burn.\n    function burnForGobblers(address from, uint256 amount) external only(artGobblers) {\n        _burn(from, amount);\n    }\n\n    /// @notice Burn any amount of goo from a user. Can only be called by Pages.\n    /// @param from The address of the user to burn goo from.\n    /// @param amount The amount of goo to burn.\n    function burnForPages(address from, uint256 amount) external only(pages) {\n        _burn(from, amount);\n    }\n}"
    },
    {
      "filename": "src/utils/GobblerReserve.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {Owned} from \"solmate/auth/Owned.sol\";\n\nimport {ArtGobblers} from \"../ArtGobblers.sol\";\n\n/// @title Gobbler Reserve\n/// @author FrankieIsLost <frankie@paradigm.xyz>\n/// @author transmissions11 <t11s@paradigm.xyz>\n/// @notice Reserves gobblers for an owner while keeping any goo produced.\ncontract GobblerReserve is Owned {\n    /*//////////////////////////////////////////////////////////////\n                                ADDRESSES\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Art Gobblers contract address.\n    ArtGobblers public immutable artGobblers;\n\n    /// @notice Sets the addresses of relevant contracts and users.\n    /// @param _artGobblers The address of the ArtGobblers contract.\n    /// @param _owner The address of the owner of Gobbler Reserve.\n    constructor(ArtGobblers _artGobblers, address _owner) Owned(_owner) {\n        artGobblers = _artGobblers;\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                            WITHDRAWAL LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Withdraw gobblers from the reserve.\n    /// @param to The address to transfer the gobblers to.\n    /// @param ids The ids of the gobblers to transfer.\n    function withdraw(address to, uint256[] calldata ids) external onlyOwner {\n        // This is quite inefficient, but it's okay because this is not a hot path.\n        unchecked {\n            for (uint256 i = 0; i < ids.length; i++) {\n                artGobblers.transferFrom(address(this), to, ids[i]);\n            }\n        }\n    }\n}"
    }
  ]
}