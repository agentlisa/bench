{
  "Title": "[L-03] Incorrect revert string in `setEndingPrincipal()`",
  "Content": "\n```solidity\nrequire(endingPrincipal_ <= _principal, \"R:DP:ABOVE_CURRENT_PRINCIPAL\");\n```\n\n[Refinancer.sol#L43](https://github.com/maple-labs/loan/blob/4c6fe2cd91d6d16b8434c426fe7eb6d2bc77bc30/contracts/Refinancer.sol#L43)<br>\n\nIt should be `\"R:SEP:ABOVE_CURRENT_PRINCIPAL\"`.\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2022-03-maple",
  "Code": [
    {
      "filename": "contracts/Refinancer.sol",
      "content": "// SPDX-License-Identifier: AGPL-3.0-or-later\npragma solidity 0.8.7;\n\nimport { IRefinancer } from \"./interfaces/IRefinancer.sol\";\n\nimport { MapleLoanInternals } from \"./MapleLoanInternals.sol\";\n\n/// @title Refinancer uses storage from a MapleLoan defined by MapleLoanInternals.\ncontract Refinancer is IRefinancer, MapleLoanInternals {\n\n    function decreasePrincipal(uint256 amount_) external override {\n        require(_drawableFunds >= amount_, \"R:DP:OUTSTANDING_TOO_LARGE\");\n\n        _principal          -= amount_;\n        _principalRequested -= amount_;\n        _drawableFunds      -= amount_;\n\n        require(_principal >= _endingPrincipal, \"R:DP:BELOW_ENDING_PRINCIPAL\");\n\n        emit PrincipalDecreased(amount_);\n    }\n\n    function increasePrincipal(uint256 amount_) external override {\n        // Cannot under-fund the principal increase, but over-funding results in additional funds left unaccounted for.\n        require(_getUnaccountedAmount(_fundsAsset) >= amount_, \"R:IP:INSUFFICIENT_AMOUNT\");\n\n        _principal          += amount_;\n        _principalRequested += amount_;\n        _drawableFunds      += amount_;\n\n        emit PrincipalIncreased(amount_);\n    }\n\n    function setCollateralRequired(uint256 collateralRequired_) external override {\n        emit CollateralRequiredSet(_collateralRequired = collateralRequired_);\n    }\n\n    function setEarlyFeeRate(uint256 earlyFeeRate_) external override {\n        emit EarlyFeeRateSet(_earlyFeeRate = earlyFeeRate_);\n    }\n\n    function setEndingPrincipal(uint256 endingPrincipal_) external override {\n        require(endingPrincipal_ <= _principal, \"R:DP:ABOVE_CURRENT_PRINCIPAL\");\n        emit EndingPrincipalSet(_endingPrincipal = endingPrincipal_);\n    }\n\n    function setGracePeriod(uint256 gracePeriod_) external override {\n        emit GracePeriodSet(_gracePeriod = gracePeriod_);\n    }\n\n    function setInterestRate(uint256 interestRate_) external override {\n        emit InterestRateSet(_interestRate = interestRate_);\n    }\n\n    function setLateFeeRate(uint256 lateFeeRate_) external override {\n        emit LateFeeRateSet(_lateFeeRate = lateFeeRate_);\n    }\n\n    function setLateInterestPremium(uint256 lateInterestPremium_) external override {\n        emit LateInterestPremiumSet(_lateInterestPremium = lateInterestPremium_);\n    }\n\n    function setPaymentInterval(uint256 paymentInterval_) external override {\n        require(paymentInterval_ != 0, \"R:SPI:ZERO_AMOUNT\");\n\n        emit PaymentIntervalSet(_paymentInterval = paymentInterval_);\n    }\n\n    function setPaymentsRemaining(uint256 paymentsRemaining_) external override {\n        require(paymentsRemaining_ != 0, \"R:SPR:ZERO_AMOUNT\");\n\n        emit PaymentsRemainingSet(_paymentsRemaining = paymentsRemaining_);\n    }\n\n}"
    }
  ]
}