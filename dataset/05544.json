{
  "Title": "[06] Emission of identical values",
  "Content": "Under the context of the above/preceding recommendation, `transferInRewards()` should also have its `emit` refactored below. Otherwise, you are practically emitting two identical values that defeat the purpose of contrasting the old and the values.\n\nhttps://github.com/code-423n4/2023-10-ethena/blob/main/contracts/StakedUSDe.sol#L89-L99\n\n```diff\n  function transferInRewards(uint256 amount) external nonReentrant onlyRole(REWARDER_ROLE) notZero(amount) {\n    if (getUnvestedAmount() > 0) revert StillVesting();\n-    uint256 newVestingAmount = amount + getUnvestedAmount();\n\n-    vestingAmount = newVestingAmount;\n+    emit RewardsReceived(vestingAmount, amount);\n+    vestingAmount = amount;\n    lastDistributionTimestamp = block.timestamp;\n    // transfer assets from rewarder to this contract\n    IERC20(asset()).safeTransferFrom(msg.sender, address(this), amount);\n\n-    emit RewardsReceived(amount, newVestingAmount);\n  }\n```\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2023-10-ethena",
  "Code": [
    {
      "filename": "contracts/StakedUSDe.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0\npragma solidity 0.8.19;\n\n/**\n * solhint-disable private-vars-leading-underscore\n */\n\nimport \"@openzeppelin/contracts/token/ERC20/extensions/ERC4626.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol\";\nimport \"@openzeppelin/contracts/security/ReentrancyGuard.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/extensions/draft-ERC20Permit.sol\";\nimport \"./SingleAdminAccessControl.sol\";\nimport \"./interfaces/IStakedUSDe.sol\";\n\n/**\n * @title StakedUSDe\n * @notice The StakedUSDe contract allows users to stake USDe tokens and earn a portion of protocol LST and perpetual yield that is allocated\n * to stakers by the Ethena DAO governance voted yield distribution algorithm.  The algorithm seeks to balance the stability of the protocol by funding\n * the protocol's insurance fund, DAO activities, and rewarding stakers with a portion of the protocol's yield.\n */\ncontract StakedUSDe is SingleAdminAccessControl, ReentrancyGuard, ERC20Permit, ERC4626, IStakedUSDe {\n  using SafeERC20 for IERC20;\n\n  /* ------------- CONSTANTS ------------- */\n  /// @notice The role that is allowed to distribute rewards to this contract\n  bytes32 private constant REWARDER_ROLE = keccak256(\"REWARDER_ROLE\");\n  /// @notice The role that is allowed to blacklist and un-blacklist addresses\n  bytes32 private constant BLACKLIST_MANAGER_ROLE = keccak256(\"BLACKLIST_MANAGER_ROLE\");\n  /// @notice The role which prevents an address to stake\n  bytes32 private constant SOFT_RESTRICTED_STAKER_ROLE = keccak256(\"SOFT_RESTRICTED_STAKER_ROLE\");\n  /// @notice The role which prevents an address to transfer, stake, or unstake. The owner of the contract can redirect address staking balance if an address is in full restricting mode.\n  bytes32 private constant FULL_RESTRICTED_STAKER_ROLE = keccak256(\"FULL_RESTRICTED_STAKER_ROLE\");\n  /// @notice The vesting period of lastDistributionAmount over which it increasingly becomes available to stakers\n  uint256 private constant VESTING_PERIOD = 8 hours;\n  /// @notice Minimum non-zero shares amount to prevent donation attack\n  uint256 private constant MIN_SHARES = 1 ether;\n\n  /* ------------- STATE VARIABLES ------------- */\n\n  /// @notice The amount of the last asset distribution from the controller contract into this\n  /// contract + any unvested remainder at that time\n  uint256 public vestingAmount;\n\n  /// @notice The timestamp of the last asset distribution from the controller contract into this contract\n  uint256 public lastDistributionTimestamp;\n\n  /* ------------- MODIFIERS ------------- */\n\n  /// @notice ensure input amount nonzero\n  modifier notZero(uint256 amount) {\n    if (amount == 0) revert InvalidAmount();\n    _;\n  }\n\n  /// @notice ensures blacklist target is not owner\n  modifier notOwner(address target) {\n    if (target == owner()) revert CantBlacklistOwner();\n    _;\n  }\n\n  /* ------------- CONSTRUCTOR ------------- */\n\n  /**\n   * @notice Constructor for StakedUSDe contract.\n   * @param _asset The address of the USDe token.\n   * @param _initialRewarder The address of the initial rewarder.\n   * @param _owner The address of the admin role.\n   *\n   */\n  constructor(IERC20 _asset, address _initialRewarder, address _owner)\n    ERC20(\"Staked USDe\", \"stUSDe\")\n    ERC4626(_asset)\n    ERC20Permit(\"stUSDe\")\n  {\n    if (_owner == address(0) || _initialRewarder == address(0) || address(_asset) == address(0)) {\n      revert InvalidZeroAddress();\n    }\n\n    _grantRole(REWARDER_ROLE, _initialRewarder);\n    _grantRole(DEFAULT_ADMIN_ROLE, _owner);\n  }\n\n  /* ------------- EXTERNAL ------------- */\n\n  /**\n   * @notice Allows the owner to transfer rewards from the controller contract into this contract.\n   * @param amount The amount of rewards to transfer.\n   */\n  function transferInRewards(uint256 amount) external nonReentrant onlyRole(REWARDER_ROLE) notZero(amount) {\n    if (getUnvestedAmount() > 0) revert StillVesting();\n    uint256 newVestingAmount = amount + getUnvestedAmount();\n\n    vestingAmount = newVestingAmount;\n    lastDistributionTimestamp = block.timestamp;\n    // transfer assets from rewarder to this contract\n    IERC20(asset()).safeTransferFrom(msg.sender, address(this), amount);\n\n    emit RewardsReceived(amount, newVestingAmount);\n  }\n\n  /**\n   * @notice Allows the owner (DEFAULT_ADMIN_ROLE) and blacklist managers to blacklist addresses.\n   * @param target The address to blacklist.\n   * @param isFullBlacklisting Soft or full blacklisting level.\n   */\n  function addToBlacklist(address target, bool isFullBlacklisting)\n    external\n    onlyRole(BLACKLIST_MANAGER_ROLE)\n    notOwner(target)\n  {\n    bytes32 role = isFullBlacklisting ? FULL_RESTRICTED_STAKER_ROLE : SOFT_RESTRICTED_STAKER_ROLE;\n    _grantRole(role, target);\n  }\n\n  /**\n   * @notice Allows the owner (DEFAULT_ADMIN_ROLE) and blacklist managers to un-blacklist addresses.\n   * @param target The address to un-blacklist.\n   * @param isFullBlacklisting Soft or full blacklisting level.\n   */\n  function removeFromBlacklist(address target, bool isFullBlacklisting)\n    external\n    onlyRole(BLACKLIST_MANAGER_ROLE)\n    notOwner(target)\n  {\n    bytes32 role = isFullBlacklisting ? FULL_RESTRICTED_STAKER_ROLE : SOFT_RESTRICTED_STAKER_ROLE;\n    _revokeRole(role, target);\n  }\n\n  /**\n   * @notice Allows the owner to rescue tokens accidentally sent to the contract.\n   * Note that the owner cannot rescue USDe tokens because they functionally sit here\n   * and belong to stakers but can rescue staked USDe as they should never actually\n   * sit in this contract and a staker may well transfer them here by accident.\n   * @param token The token to be rescued.\n   * @param amount The amount of tokens to be rescued.\n   * @param to Where to send rescued tokens\n   */\n  function rescueTokens(address token, uint256 amount, address to) external onlyRole(DEFAULT_ADMIN_ROLE) {\n    if (address(token) == asset()) revert InvalidToken();\n    IERC20(token).safeTransfer(to, amount);\n  }\n\n  /**\n   * @dev Burns the full restricted user amount and mints to the desired owner address.\n   * @param from The address to burn the entire balance, with the FULL_RESTRICTED_STAKER_ROLE\n   * @param to The address to mint the entire balance of \"from\" parameter.\n   */\n  function redistributeLockedAmount(address from, address to) external onlyRole(DEFAULT_ADMIN_ROLE) {\n    if (hasRole(FULL_RESTRICTED_STAKER_ROLE, from) && !hasRole(FULL_RESTRICTED_STAKER_ROLE, to)) {\n      uint256 amountToDistribute = balanceOf(from);\n      _burn(from, amountToDistribute);\n      // to address of address(0) enables burning\n      if (to != address(0)) _mint(to, amountToDistribute);\n\n      emit LockedAmountRedistributed(from, to, amountToDistribute);\n    } else {\n      revert OperationNotAllowed();\n    }\n  }\n\n  /* ------------- PUBLIC ------------- */\n\n  /**\n   * @notice Returns the amount of USDe tokens that are vested in the contract.\n   */\n  function totalAssets() public view override returns (uint256) {\n    return IERC20(asset()).balanceOf(address(this)) - getUnvestedAmount();\n  }\n\n  /**\n   * @notice Returns the amount of USDe tokens that are unvested in the contract.\n   */\n  function getUnvestedAmount() public view returns (uint256) {\n    uint256 timeSinceLastDistribution = block.timestamp - lastDistributionTimestamp;\n\n    if (timeSinceLastDistribution >= VESTING_PERIOD) {\n      return 0;\n    }\n\n    return ((VESTING_PERIOD - timeSinceLastDistribution) * vestingAmount) / VESTING_PERIOD;\n  }\n\n  /// @dev Necessary because both ERC20 (from ERC20Permit) and ERC4626 declare decimals()\n  function decimals() public pure override(ERC4626, ERC20) returns (uint8) {\n    return 18;\n  }\n\n  /* ------------- INTERNAL ------------- */\n\n  /// @notice ensures a small non-zero amount of shares does not remain, exposing to donation attack\n  function _checkMinShares() internal view {\n    uint256 _totalSupply = totalSupply();\n    if (_totalSupply > 0 && _totalSupply < MIN_SHARES) revert MinSharesViolation();\n  }\n\n  /**\n   * @dev Deposit/mint common workflow.\n   * @param caller sender of assets\n   * @param receiver where to send shares\n   * @param assets assets to deposit\n   * @param shares shares to mint\n   */\n  function _deposit(address caller, address receiver, uint256 assets, uint256 shares)\n    internal\n    override\n    nonReentrant\n    notZero(assets)\n    notZero(shares)\n  {\n    if (hasRole(SOFT_RESTRICTED_STAKER_ROLE, caller) || hasRole(SOFT_RESTRICTED_STAKER_ROLE, receiver)) {\n      revert OperationNotAllowed();\n    }\n    super._deposit(caller, receiver, assets, shares);\n    _checkMinShares();\n  }\n\n  /**\n   * @dev Withdraw/redeem common workflow.\n   * @param caller tx sender\n   * @param receiver where to send assets\n   * @param _owner where to burn shares from\n   * @param assets asset amount to transfer out\n   * @param shares shares to burn\n   */\n  function _withdraw(address caller, address receiver, address _owner, uint256 assets, uint256 shares)\n    internal\n    override\n    nonReentrant\n    notZero(assets)\n    notZero(shares)\n  {\n    if (hasRole(FULL_RESTRICTED_STAKER_ROLE, caller) || hasRole(FULL_RESTRICTED_STAKER_ROLE, receiver)) {\n      revert OperationNotAllowed();\n    }\n\n    super._withdraw(caller, receiver, _owner, assets, shares);\n    _checkMinShares();\n  }\n\n  /**\n   * @dev Hook that is called before any transfer of tokens. This includes\n   * minting and burning. Disables transfers from or to of addresses with the FULL_RESTRICTED_STAKER_ROLE role.\n   */\n\n  function _beforeTokenTransfer(address from, address to, uint256) internal virtual override {\n    if (hasRole(FULL_RESTRICTED_STAKER_ROLE, from) && to != address(0)) {\n      revert OperationNotAllowed();\n    }\n    if (hasRole(FULL_RESTRICTED_STAKER_ROLE, to)) {\n      revert OperationNotAllowed();\n    }\n  }\n\n  /**\n   * @dev Remove renounce role access from AccessControl, to prevent users to resign roles.\n   */\n  function renounceRole(bytes32, address) public virtual override {\n    revert OperationNotAllowed();\n  }\n}"
    }
  ]
}