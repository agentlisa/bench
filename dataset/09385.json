{
  "Title": "[M-05] Chainlink's `latestRoundData` might return stale or incorrect results",
  "Content": "_Submitted by cccz, also found by 0x1f8b, 0xDjango, 0xkatana, berndartmueller, defsec, Dravee, horsefacts, hyh, IllIllI, kenta, rayn, reassor, sorrynotsorry, and WatchPug_\n\nOn ChainlinkOracleProvider.sol and ChainlinkUsdWrapper.sol , we are using latestRoundData, but there is no check if the return value indicates stale data.\n```solidity\n    function _ethPrice() private view returns (int256) {\n        (, int256 answer, , , ) = _ethOracle.latestRoundData();\n        return answer;\n    }\n...\n    function getPriceUSD(address asset) public view override returns (uint256) {\n        address feed = feeds[asset];\n        require(feed != address(0), Error.ASSET_NOT_SUPPORTED);\n\n        (, int256 answer, , uint256 updatedAt, ) = AggregatorV2V3Interface(feed).latestRoundData();\n\n        require(block.timestamp <= updatedAt + stalePriceDelay, Error.STALE_PRICE);\n        require(answer >= 0, Error.NEGATIVE_PRICE);\n\n        uint256 price = uint256(answer);\n        uint8 decimals = AggregatorV2V3Interface(feed).decimals();\n        return price.scaleFrom(decimals);\n    }\n```\n\nThis could lead to stale prices according to the Chainlink documentation:\n\n<https://docs.chain.link/docs/historical-price-data/#historical-rounds><br>\n<https://docs.chain.link/docs/faq/#how-can-i-check-if-the-answer-to-a-round-is-being-carried-over-from-a-previous-round>\n\n### Proof of Concept\n\n[ChainlinkOracleProvider.sol#L55](https://github.com/code-423n4/2022-04-backd/blob/main/backd/contracts/oracles/ChainlinkOracleProvider.sol#L55)<br>\n[ChainlinkUsdWrapper.sol#L64](https://github.com/code-423n4/2022-04-backd/blob/main/backd/contracts/oracles/ChainlinkUsdWrapper.sol#L64)\n\n### Recommended Mitigation Steps\n```solidity\n    function _ethPrice() private view returns (int256) {\n        (uint80 roundID, int256 answer, , uint256 timestamp, uint80 answeredInRound) = _ethOracle.latestRoundData();\n        require(answeredInRound >= roundID, \"Stale price\");\n        require(timestamp != 0,\"Round not complete\");\n        require(answer > 0,\"Chainlink answer reporting 0\");\n        return answer;\n    }\n...\n    function getPriceUSD(address asset) public view override returns (uint256) {\n        address feed = feeds[asset];\n        require(feed != address(0), Error.ASSET_NOT_SUPPORTED);\n        (uint80 roundID, int256 answer, , uint256 updatedAt, uint80 answeredInRound) = AggregatorV2V3Interface(feed).latestRoundData();\n        require(answeredInRound >= roundID, \"Stale price\");\n        require(answer > 0,\" Error.NEGATIVE_PRICE\");\n        require(block.timestamp <= updatedAt + stalePriceDelay, Error.STALE_PRICE);\n\n        uint256 price = uint256(answer);\n        uint8 decimals = AggregatorV2V3Interface(feed).decimals();\n        return price.scaleFrom(decimals);\n    }\n```\n\n**[chase-manning (Backd) confirmed and resolved](https://github.com/code-423n4/2022-04-backd-findings/issues/17)**\n\n\n\n***\n\n",
  "Impact": "MEDIUM",
  "Source": "https://code4rena.com/contests/2022-04-backd-contest",
  "Code": [
    {
      "filename": "backd/contracts/oracles/ChainlinkOracleProvider.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\npragma solidity 0.8.9;\n\nimport \"../access/Authorization.sol\";\n\nimport \"../../interfaces/oracles/IChainlinkOracleProvider.sol\";\nimport \"../../interfaces/vendor/ChainlinkAggregator.sol\";\n\nimport \"../../libraries/Errors.sol\";\nimport \"../../libraries/DecimalScale.sol\";\n\ncontract ChainlinkOracleProvider is IChainlinkOracleProvider, Authorization {\n    using DecimalScale for uint256;\n\n    uint256 public stalePriceDelay;\n\n    mapping(address => address) public feeds;\n\n    event FeedUpdated(address indexed asset, address indexed previousFeed, address indexed newFeed);\n\n    constructor(IRoleManager roleManager, address ethFeed) Authorization(roleManager) {\n        feeds[address(0)] = ethFeed;\n        stalePriceDelay = 2 hours;\n    }\n\n    /// @notice Allows to set Chainlink feeds\n    /// @dev All feeds should be set relative to USD.\n    /// This can only be called by governance\n    function setFeed(address asset, address feed) external override onlyGovernance {\n        address previousFeed = feeds[asset];\n        require(feed != previousFeed, Error.INVALID_ARGUMENT);\n        feeds[asset] = feed;\n        emit FeedUpdated(asset, previousFeed, feed);\n    }\n\n    /**\n     * @notice Sets the stake price delay value.\n     * @param stalePriceDelay_ The new stale price delay to set.\n     */\n    function setStalePriceDelay(uint256 stalePriceDelay_) external override onlyGovernance {\n        require(stalePriceDelay_ >= 1 hours, Error.INVALID_ARGUMENT);\n        stalePriceDelay = stalePriceDelay_;\n    }\n\n    /// @inheritdoc IOracleProvider\n    function getPriceETH(address asset) external view override returns (uint256) {\n        return (getPriceUSD(asset) * 1e18) / getPriceUSD(address(0));\n    }\n\n    /// @inheritdoc IOracleProvider\n    function getPriceUSD(address asset) public view override returns (uint256) {\n        address feed = feeds[asset];\n        require(feed != address(0), Error.ASSET_NOT_SUPPORTED);\n\n        (, int256 answer, , uint256 updatedAt, ) = AggregatorV2V3Interface(feed).latestRoundData();\n\n        require(block.timestamp <= updatedAt + stalePriceDelay, Error.STALE_PRICE);\n        require(answer >= 0, Error.NEGATIVE_PRICE);\n\n        uint256 price = uint256(answer);\n        uint8 decimals = AggregatorV2V3Interface(feed).decimals();\n        return price.scaleFrom(decimals);\n    }\n}"
    },
    {
      "filename": "backd/contracts/oracles/ChainlinkUsdWrapper.sol",
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\npragma solidity 0.8.9;\n\nimport \"../../libraries/DecimalScale.sol\";\n\ninterface IChainlinkOracle {\n    function latestRoundData()\n        external\n        view\n        returns (\n            uint80 roundId,\n            int256 answer,\n            uint256 startedAt,\n            uint256 updatedAt,\n            uint80 answeredInRound\n        );\n\n    function decimals() external view returns (uint8);\n}\n\n/**\n * Wrapper used for converting a Chainlink ETH Oracle to a USD Oracle.\n */\ncontract ChainlinkUsdWrapper is IChainlinkOracle {\n    using DecimalScale for uint256;\n\n    IChainlinkOracle private immutable _ethOracle =\n        IChainlinkOracle(0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419);\n    IChainlinkOracle private immutable _oracle;\n    uint8 private immutable _decimals;\n\n    constructor(address oracle_) {\n        _oracle = IChainlinkOracle(oracle_);\n        _decimals = IChainlinkOracle(oracle_).decimals();\n    }\n\n    function latestRoundData()\n        external\n        view\n        override\n        returns (\n            uint80 roundId,\n            int256 answer,\n            uint256 startedAt,\n            uint256 updatedAt,\n            uint80 answeredInRound\n        )\n    {\n        (\n            uint80 roundId_,\n            int256 answer_,\n            uint256 startedAt_,\n            uint256 updatedAt_,\n            uint80 answeredInRound_\n        ) = _oracle.latestRoundData();\n        return (roundId_, (answer_ * _ethPrice()) / 1e8, startedAt_, updatedAt_, answeredInRound_);\n    }\n\n    function decimals() external view override returns (uint8) {\n        return _decimals;\n    }\n\n    function _ethPrice() private view returns (int256) {\n        (, int256 answer, , , ) = _ethOracle.latestRoundData();\n        return answer;\n    }\n}"
    }
  ]
}