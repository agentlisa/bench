{
  "Title": "[N-02] Wrong bytes in comment",
  "Content": "\nWhile declaring [`SafeHelper._FALLBACK_REMOVAL_CALLDATA_HASH`](https://github.com/code-423n4/2023-10-brahma/blob/main/contracts/src/libraries/SafeHelper.sol#L44):\n\n```solidity\nFile: contracts/src/libraries/SafeHelper.sol\n\n44:     * abi.encodeCall(IGnosisSafe.setFallbackHandler, (address(0))) = 0xf08a0323000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000\n```\n\nThis is not correct, `abi.encodeCall(IGnosisSafe.setFallbackHandler, (address(0)))` is in fact:\n\n```\n0xf08a03230000000000000000000000000000000000000000000000000000000000000000\n```\nThe hash is, however, calculated on the correct bytes.\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2023-10-brahma",
  "Code": [
    {
      "filename": "contracts/src/libraries/SafeHelper.sol",
      "content": "/// SPDX-License-Identifier: BUSL-1.1\n\n/// Copyright (C) 2023 Brahma.fi\n\npragma solidity 0.8.19;\n\nimport {Enum, IGnosisSafe} from \"interfaces/external/IGnosisSafe.sol\";\nimport {Types} from \"interfaces/Types.sol\";\n\n/**\n * @title SafeHelper\n * @author Brahma.fi\n * @notice Helper library containing functions to interact with gnosis safe\n */\nlibrary SafeHelper {\n    error InvalidMultiSendCall(uint256);\n    error InvalidMultiSendInput();\n    error SafeExecTransactionFailed();\n    error UnableToParseOperation();\n\n    /// @notice uint256(keccak256(\"guard_manager.guard.address\"))\n    /// @dev This refers to the storage slot where guard is stored in Safe's layout: https://github.com/safe-global/safe-contracts/blob/ff4c6761fbfae8ab8a94f36fd26bcfb2b5414eb1/contracts/base/GuardManager.sol#L77\n    uint256 internal constant _GUARD_STORAGE_SLOT =\n        33528237782592280163068556224972516439282563014722366175641814928123294921928;\n    /// @notice uint256(keccak256(\"fallback_manager.handler.address\"))\n    /// @dev This refers to the storage slot where fallback handler is stored in Safe's layout: https://github.com/safe-global/safe-contracts/blob/ff4c6761fbfae8ab8a94f36fd26bcfb2b5414eb1/contracts/base/FallbackManager.sol#L14\n    uint256 internal constant _FALLBACK_HANDLER_STORAGE_SLOT =\n        49122629484629529244014240937346711770925847994644146912111677022347558721749;\n\n    /**\n     * @notice Contains hash for expected overridable guard removal calldata\n     * @dev This is the hash of the calldata for the following function call\n     *\n     * abi.encodeCall(IGnosisSafe.setGuard, (address(0))) = 0xe19a9dd90000000000000000000000000000000000000000000000000000000000000000\n     * keccak256(abi.encodeCall(IGnosisSafe.setGuard, (address(0)))) = 0xc0e2c16ecb99419a40dd8b9c0b339b27acebd27c481a28cd606927aeb86f5079\n     */\n    bytes32 internal constant _GUARD_REMOVAL_CALLDATA_HASH =\n        0xc0e2c16ecb99419a40dd8b9c0b339b27acebd27c481a28cd606927aeb86f5079;\n\n    /**\n     * @notice Contains hash for expected overridable fallback handler removal calldata\n     * @dev This is the hash of the calldata for the following function call\n     *\n     * abi.encodeCall(IGnosisSafe.setFallbackHandler, (address(0))) = 0xf08a0323000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000\n     * keccak256(abi.encodeCall(IGnosisSafe.setFallbackHandler, (address(0)))) = 0x5bdf8c44c012c1347b2b15694dc5cc39b899eb99e32614676b7661001be925b7\n     */\n    bytes32 internal constant _FALLBACK_REMOVAL_CALLDATA_HASH =\n        0x5bdf8c44c012c1347b2b15694dc5cc39b899eb99e32614676b7661001be925b7;\n\n    /**\n     * @notice Executes a transaction on a safe\n     *\n     * @dev Allows any contract using this library to execute a transaction on a safe\n     *  Assumes the contract using this method is the owner of the safe\n     *  Also assumes the safe is a single threshold safe\n     *  This uses pre-validated signature scheme used by gnosis\n     *\n     * @param safe Safe address\n     * @param target Target contract address\n     * @param op Safe Operation type\n     * @param data Transaction data\n     */\n    function _executeOnSafe(address safe, address target, Enum.Operation op, bytes memory data) internal {\n        bool success = IGnosisSafe(safe).execTransaction(\n            address(target), // to\n            0, // value\n            data, // data\n            op, // operation\n            0, // safeTxGas\n            0, // baseGas\n            0, // gasPrice\n            address(0), // gasToken\n            payable(address(0)), // refundReceiver\n            _generateSingleThresholdSignature(address(this)) // signatures\n        );\n\n        if (!success) revert SafeExecTransactionFailed();\n    }\n\n    /**\n     * @notice Generates a pre-validated signature for a safe transaction\n     * @dev Refer to https://docs.safe.global/learn/safe-core/safe-core-protocol/signatures#pre-validated-signatures\n     * This calldata assumes that owner is the actual address that will be sending the execTransaction call to safe\n     * @param owner Owner of the safe\n     * @return signatures bytes array containing single pre validated owner signature\n     */\n    function _generateSingleThresholdSignature(address owner) internal pure returns (bytes memory) {\n        bytes memory signatures = abi.encodePacked(\n            bytes12(0), // Padding for signature verifier address\n            bytes20(owner), // Signature Verifier\n            bytes32(0), // Position of extra data bytes (last set of data)\n            bytes1(hex\"01\") // Signature Type - 1 (presigned transaction)\n        );\n        return signatures;\n    }\n\n    /**\n     * @notice Packs multiple executables into a single bytes array compatible with Safe's MultiSend contract which can be used as argument for multicall method\n     * @dev Reference contract at https://github.com/safe-global/safe-contracts/blob/main/contracts/libraries/MultiSend.sol\n     * @param _txns Array of executables to pack\n     * @return packedTxns bytes array containing packed transactions\n     */\n    function _packMultisendTxns(Types.Executable[] memory _txns) internal pure returns (bytes memory packedTxns) {\n        uint256 len = _txns.length;\n        if (len == 0) revert InvalidMultiSendInput();\n\n        uint256 i = 0;\n        do {\n            // Enum.Operation.Call is 0\n            uint8 call = uint8(Enum.Operation.Call);\n            if (_txns[i].callType == Types.CallType.DELEGATECALL) {\n                call = uint8(Enum.Operation.DelegateCall);\n            } else if (_txns[i].callType == Types.CallType.STATICCALL) {\n                revert InvalidMultiSendCall(i);\n            }\n\n            uint256 calldataLength = _txns[i].data.length;\n\n            bytes memory encodedTxn = abi.encodePacked(\n                bytes1(call), bytes20(_txns[i].target), bytes32(_txns[i].value), bytes32(calldataLength), _txns[i].data\n            );\n\n            if (i != 0) {\n                // If not first transaction, append to packedTxns\n                packedTxns = abi.encodePacked(packedTxns, encodedTxn);\n            } else {\n                // If first transaction, set packedTxns to encodedTxn\n                packedTxns = encodedTxn;\n            }\n\n            unchecked {\n                ++i;\n            }\n        } while (i < len);\n    }\n\n    /**\n     * @notice Gets the guard for a safe\n     * @param safe address of safe\n     * @return address of guard, address(0) if no guard exists\n     */\n    function _getGuard(address safe) internal view returns (address) {\n        bytes memory guardAddress = IGnosisSafe(safe).getStorageAt(_GUARD_STORAGE_SLOT, 1);\n        return address(uint160(uint256(bytes32(guardAddress))));\n    }\n\n    /**\n     * @notice Gets the fallback handler for a safe\n     * @param safe address of safe\n     * @return address of fallback handler, address(0) if no fallback handler exists\n     */\n    function _getFallbackHandler(address safe) internal view returns (address) {\n        bytes memory fallbackHandlerAddress = IGnosisSafe(safe).getStorageAt(_FALLBACK_HANDLER_STORAGE_SLOT, 1);\n        return address(uint160(uint256(bytes32(fallbackHandlerAddress))));\n    }\n\n    /**\n     * @notice Converts a CallType enum to an Operation enum.\n     * @dev Reverts with UnableToParseOperation error if the CallType is not supported.\n     * @param callType The CallType enum to be converted.\n     * @return operation The converted Operation enum.\n     */\n    function _parseOperationEnum(Types.CallType callType) internal pure returns (Enum.Operation operation) {\n        if (callType == Types.CallType.DELEGATECALL) {\n            operation = Enum.Operation.DelegateCall;\n        } else if (callType == Types.CallType.CALL) {\n            operation = Enum.Operation.Call;\n        } else {\n            revert UnableToParseOperation();\n        }\n    }\n}"
    }
  ]
}