{
  "Title": "[17]  Not using the named return variables anywhere in the function is confusing",
  "Content": "Consider changing the variable to be an unnamed one\n\n*There are 3 instances of this issue:*\n```solidity\nFile: src/utils/token/GobblersERC1155B.sol\n\n/// @audit owner\n55:       function ownerOf(uint256 id) public view virtual returns (address owner) {\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/src/utils/token/GobblersERC1155B.sol#L55\n\n```solidity\nFile: src/utils/token/PagesERC721.sol\n\n/// @audit isApproved\n72:       function isApprovedForAll(address owner, address operator) public view returns (bool isApproved) {\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/src/utils/token/PagesERC721.sol#L72\n\n```solidity\nFile: src/utils/rand/ChainlinkV1RandProvider.sol\n\n/// @audit requestId\n62:       function requestRandomBytes() external returns (bytes32 requestId) {\n\n```\nhttps://github.com/code-423n4/2022-09-artgobblers/blob/d2087c5a8a6a4f1b9784520e7fe75afa3a9cbdbe/src/utils/rand/ChainlinkV1RandProvider.sol#L62\n\n",
  "Impact": "LOW",
  "Source": "https://code4rena.com/reports/2022-09-artgobblers",
  "Code": [
    {
      "filename": "src/utils/token/GobblersERC1155B.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {ERC1155TokenReceiver} from \"solmate/tokens/ERC1155.sol\";\n\n/// @notice ERC1155B implementation optimized for ArtGobblers by using the ownerOf storage slot to store attribute data.\n/// @author Modified from Solmate (https://github.com/Rari-Capital/solmate/blob/v7/src/tokens/ERC1155B.sol)\nabstract contract GobblersERC1155B {\n    /*//////////////////////////////////////////////////////////////\n                                 EVENTS\n    //////////////////////////////////////////////////////////////*/\n\n    event TransferSingle(\n        address indexed operator,\n        address indexed from,\n        address indexed to,\n        uint256 id,\n        uint256 amount\n    );\n\n    event TransferBatch(\n        address indexed operator,\n        address indexed from,\n        address indexed to,\n        uint256[] ids,\n        uint256[] amounts\n    );\n\n    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);\n\n    event URI(string value, uint256 indexed id);\n\n    /*//////////////////////////////////////////////////////////////\n                             ERC1155 STORAGE\n    //////////////////////////////////////////////////////////////*/\n\n    mapping(address => mapping(address => bool)) public isApprovedForAll;\n\n    /*//////////////////////////////////////////////////////////////\n                        GOBBLERS/ERC1155B STORAGE\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Struct holding gobbler data.\n    struct GobblerData {\n        // The current owner of the gobbler.\n        address owner;\n        // Index of token after shuffle.\n        uint64 idx;\n        // Multiple on goo issuance.\n        uint32 emissionMultiple;\n    }\n\n    mapping(uint256 => GobblerData) public getGobblerData;\n\n    function ownerOf(uint256 id) public view virtual returns (address owner) {\n        return getGobblerData[id].owner;\n    }\n\n    function balanceOf(address owner, uint256 id) public view virtual returns (uint256 bal) {\n        address idOwner = getGobblerData[id].owner;\n\n        assembly {\n            // We avoid branching by using assembly to take\n            // the bool output of eq() and use it as a uint.\n            bal := eq(idOwner, owner)\n        }\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                             METADATA LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function uri(uint256 id) public view virtual returns (string memory);\n\n    /*//////////////////////////////////////////////////////////////\n                              ERC1155 LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function setApprovalForAll(address operator, bool approved) public virtual {\n        isApprovedForAll[msg.sender][operator] = approved;\n\n        emit ApprovalForAll(msg.sender, operator, approved);\n    }\n\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 id,\n        uint256 amount,\n        bytes calldata data\n    ) public virtual;\n\n    function safeBatchTransferFrom(\n        address from,\n        address to,\n        uint256[] calldata ids,\n        uint256[] calldata amounts,\n        bytes calldata data\n    ) public virtual;\n\n    function balanceOfBatch(address[] calldata owners, uint256[] calldata ids)\n        public\n        view\n        virtual\n        returns (uint256[] memory balances)\n    {\n        require(owners.length == ids.length, \"LENGTH_MISMATCH\");\n\n        balances = new uint256[](owners.length);\n\n        // Unchecked because the only math done is incrementing\n        // the array index counter which cannot possibly overflow.\n        unchecked {\n            for (uint256 i = 0; i < owners.length; ++i) {\n                balances[i] = balanceOf(owners[i], ids[i]);\n            }\n        }\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                              ERC165 LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function supportsInterface(bytes4 interfaceId) public view virtual returns (bool) {\n        return\n            interfaceId == 0x01ffc9a7 || // ERC165 Interface ID for ERC165\n            interfaceId == 0xd9b67a26 || // ERC165 Interface ID for ERC1155\n            interfaceId == 0x0e89341c; // ERC165 Interface ID for ERC1155MetadataURI\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                           INTERNAL MINT LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function _mint(\n        address to,\n        uint256 id,\n        bytes memory data\n    ) internal virtual {\n        getGobblerData[id].owner = to;\n\n        // Does not check if the token was already minted or the recipient is address(0)\n        // because ArtGobblers.sol manages its ids in such a way that it ensures it won't\n        // double mint and will only mint to safe addresses or msg.sender who cannot be zero.\n\n        emit TransferSingle(msg.sender, address(0), to, id, 1);\n\n        if (to.code.length != 0) {\n            require(\n                ERC1155TokenReceiver(to).onERC1155Received(msg.sender, address(0), id, 1, data) ==\n                    ERC1155TokenReceiver.onERC1155Received.selector,\n                \"UNSAFE_RECIPIENT\"\n            );\n        }\n    }\n\n    function _batchMint(\n        address to,\n        uint256 amount,\n        uint256 lastMintedId,\n        bytes memory data\n    ) internal returns (uint256) {\n        // Doesn't check if the tokens were already minted or the recipient is address(0)\n        // because ArtGobblers.sol manages its ids in such a way that it ensures it won't\n        // double mint and will only mint to safe addresses or msg.sender who cannot be zero.\n\n        // Allocate arrays before entering the loop.\n        uint256[] memory ids = new uint256[](amount);\n        uint256[] memory amounts = new uint256[](amount);\n\n        // Counter overflow is unrealistic on human timescales.\n        unchecked {\n            for (uint256 i = 0; i < amount; ++i) {\n                ids[i] = ++lastMintedId; // Increment id while setting.\n\n                amounts[i] = 1; // ERC1155B amounts are always 1.\n\n                getGobblerData[lastMintedId].owner = to;\n            }\n        }\n\n        emit TransferBatch(msg.sender, address(0), to, ids, amounts);\n\n        if (to.code.length != 0) {\n            require(\n                ERC1155TokenReceiver(to).onERC1155BatchReceived(msg.sender, address(0), ids, amounts, data) ==\n                    ERC1155TokenReceiver.onERC1155BatchReceived.selector,\n                \"UNSAFE_RECIPIENT\"\n            );\n        }\n\n        return lastMintedId; // Return the new last minted id.\n    }\n}"
    },
    {
      "filename": "src/utils/token/PagesERC721.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {ERC721TokenReceiver} from \"solmate/tokens/ERC721.sol\";\nimport {ArtGobblers} from \"../../ArtGobblers.sol\";\n\n/// @notice ERC721 implementation optimized for Pages by pre-approving them to the ArtGobblers contract.\n/// @author Modified from Solmate (https://github.com/transmissions11/solmate/blob/main/src/tokens/ERC721.sol)\nabstract contract PagesERC721 {\n    /*//////////////////////////////////////////////////////////////\n                                 EVENTS\n    //////////////////////////////////////////////////////////////*/\n\n    event Transfer(address indexed from, address indexed to, uint256 indexed id);\n\n    event Approval(address indexed owner, address indexed spender, uint256 indexed id);\n\n    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);\n\n    /*//////////////////////////////////////////////////////////////\n                         METADATA STORAGE/LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    string public name;\n\n    string public symbol;\n\n    function tokenURI(uint256 id) external view virtual returns (string memory);\n\n    /*//////////////////////////////////////////////////////////////\n                               CONSTRUCTOR\n    //////////////////////////////////////////////////////////////*/\n\n    ArtGobblers public immutable artGobblers;\n\n    constructor(\n        ArtGobblers _artGobblers,\n        string memory _name,\n        string memory _symbol\n    ) {\n        name = _name;\n        symbol = _symbol;\n        artGobblers = _artGobblers;\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                      ERC721 BALANCE/OWNER STORAGE\n    //////////////////////////////////////////////////////////////*/\n\n    mapping(uint256 => address) internal _ownerOf;\n\n    mapping(address => uint256) internal _balanceOf;\n\n    function ownerOf(uint256 id) external view returns (address owner) {\n        require((owner = _ownerOf[id]) != address(0), \"NOT_MINTED\");\n    }\n\n    function balanceOf(address owner) external view returns (uint256) {\n        require(owner != address(0), \"ZERO_ADDRESS\");\n\n        return _balanceOf[owner];\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                         ERC721 APPROVAL STORAGE\n    //////////////////////////////////////////////////////////////*/\n\n    mapping(uint256 => address) public getApproved;\n\n    mapping(address => mapping(address => bool)) internal _isApprovedForAll;\n\n    function isApprovedForAll(address owner, address operator) public view returns (bool isApproved) {\n        if (operator == address(artGobblers)) return true; // Skip approvals for the ArtGobblers contract.\n\n        return _isApprovedForAll[owner][operator];\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                              ERC721 LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function approve(address spender, uint256 id) external {\n        address owner = _ownerOf[id];\n\n        require(msg.sender == owner || isApprovedForAll(owner, msg.sender), \"NOT_AUTHORIZED\");\n\n        getApproved[id] = spender;\n\n        emit Approval(owner, spender, id);\n    }\n\n    function setApprovalForAll(address operator, bool approved) external {\n        _isApprovedForAll[msg.sender][operator] = approved;\n\n        emit ApprovalForAll(msg.sender, operator, approved);\n    }\n\n    function transferFrom(\n        address from,\n        address to,\n        uint256 id\n    ) public {\n        require(from == _ownerOf[id], \"WRONG_FROM\");\n\n        require(to != address(0), \"INVALID_RECIPIENT\");\n\n        require(\n            msg.sender == from || isApprovedForAll(from, msg.sender) || msg.sender == getApproved[id],\n            \"NOT_AUTHORIZED\"\n        );\n\n        // Underflow of the sender's balance is impossible because we check for\n        // ownership above and the recipient's balance can't realistically overflow.\n        unchecked {\n            _balanceOf[from]--;\n\n            _balanceOf[to]++;\n        }\n\n        _ownerOf[id] = to;\n\n        delete getApproved[id];\n\n        emit Transfer(from, to, id);\n    }\n\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 id\n    ) external {\n        transferFrom(from, to, id);\n\n        if (to.code.length != 0)\n            require(\n                ERC721TokenReceiver(to).onERC721Received(msg.sender, from, id, \"\") ==\n                    ERC721TokenReceiver.onERC721Received.selector,\n                \"UNSAFE_RECIPIENT\"\n            );\n    }\n\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 id,\n        bytes calldata data\n    ) external {\n        transferFrom(from, to, id);\n\n        if (to.code.length != 0)\n            require(\n                ERC721TokenReceiver(to).onERC721Received(msg.sender, from, id, data) ==\n                    ERC721TokenReceiver.onERC721Received.selector,\n                \"UNSAFE_RECIPIENT\"\n            );\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                              ERC165 LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function supportsInterface(bytes4 interfaceId) external pure returns (bool) {\n        return\n            interfaceId == 0x01ffc9a7 || // ERC165 Interface ID for ERC165\n            interfaceId == 0x80ac58cd || // ERC165 Interface ID for ERC721\n            interfaceId == 0x5b5e139f; // ERC165 Interface ID for ERC721Metadata\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                           INTERNAL MINT LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function _mint(address to, uint256 id) internal {\n        // Does not check the token has not been already minted\n        // or is being minted to address(0) because ids in Pages.sol\n        // are set using a monotonically increasing counter and only\n        // minted to safe addresses or msg.sender who cannot be zero.\n\n        // Counter overflow is incredibly unrealistic.\n        unchecked {\n            _balanceOf[to]++;\n        }\n\n        _ownerOf[id] = to;\n\n        emit Transfer(address(0), to, id);\n    }\n}"
    },
    {
      "filename": "src/utils/rand/ChainlinkV1RandProvider.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.0;\n\nimport {VRFConsumerBase} from \"chainlink/v0.8/VRFConsumerBase.sol\";\n\nimport {ArtGobblers} from \"../../ArtGobblers.sol\";\n\nimport {RandProvider} from \"./RandProvider.sol\";\n\n/// @title Chainlink V1 Randomness Provider.\n/// @author FrankieIsLost <frankie@paradigm.xyz>\n/// @author transmissions11 <t11s@paradigm.xyz>\n/// @notice RandProvider wrapper around Chainlink VRF v1.\ncontract ChainlinkV1RandProvider is RandProvider, VRFConsumerBase {\n    /*//////////////////////////////////////////////////////////////\n                                ADDRESSES\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice The address of the Art Gobblers contract.\n    ArtGobblers public immutable artGobblers;\n\n    /*//////////////////////////////////////////////////////////////\n                            VRF CONFIGURATION\n    //////////////////////////////////////////////////////////////*/\n\n    /// @dev Public key to generate randomness against.\n    bytes32 internal immutable chainlinkKeyHash;\n\n    /// @dev Fee required to fulfill a VRF request.\n    uint256 internal immutable chainlinkFee;\n\n    /*//////////////////////////////////////////////////////////////\n                                 ERRORS\n    //////////////////////////////////////////////////////////////*/\n\n    error NotGobblers();\n\n    /*//////////////////////////////////////////////////////////////\n                               CONSTRUCTOR\n    //////////////////////////////////////////////////////////////*/\n\n    /// @notice Sets relevant addresses and VRF parameters.\n    /// @param _artGobblers Address of the ArtGobblers contract.\n    /// @param _vrfCoordinator Address of the VRF coordinator.\n    /// @param _linkToken Address of the LINK token contract.\n    /// @param _chainlinkKeyHash Public key to generate randomness against.\n    /// @param _chainlinkFee Fee required to fulfill a VRF request.\n    constructor(\n        ArtGobblers _artGobblers,\n        address _vrfCoordinator,\n        address _linkToken,\n        bytes32 _chainlinkKeyHash,\n        uint256 _chainlinkFee\n    ) VRFConsumerBase(_vrfCoordinator, _linkToken) {\n        artGobblers = _artGobblers;\n\n        chainlinkKeyHash = _chainlinkKeyHash;\n        chainlinkFee = _chainlinkFee;\n    }\n\n    /// @notice Request random bytes from Chainlink VRF. Can only by called by the ArtGobblers contract.\n    function requestRandomBytes() external returns (bytes32 requestId) {\n        // The caller must be the ArtGobblers contract, revert otherwise.\n        if (msg.sender != address(artGobblers)) revert NotGobblers();\n\n        emit RandomBytesRequested(requestId);\n\n        // Will revert if we don't have enough LINK to afford the request.\n        return requestRandomness(chainlinkKeyHash, chainlinkFee);\n    }\n\n    /// @dev Handles VRF response by calling back into the ArtGobblers contract.\n    function fulfillRandomness(bytes32 requestId, uint256 randomness) internal override {\n        emit RandomBytesReturned(requestId, randomness);\n\n        artGobblers.acceptRandomSeed(requestId, randomness);\n    }\n}"
    }
  ]
}