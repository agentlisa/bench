{
  "Title": "[L08] Lack of input validation",
  "Content": "In the `Store` contract:\n\n\n* The [`setWeeklyDelayFee` function](https://github.com/UMAprotocol/protocol/blob/9d403ddb5f2f07194daefe7da51e0e0a6306f2c4/core/contracts/oracle/implementation/Store.sol#L134) does not validate whether the `newWeeklyDelayFee` parameter is less than 1.\n\n\nIn the `VoteTiming` library:\n\n\n* The [`init` function](https://github.com/UMAprotocol/protocol/blob/9d403ddb5f2f07194daefe7da51e0e0a6306f2c4/core/contracts/oracle/implementation/VoteTiming.sol#L20) does not validate that the `phaseLength` parameter is greater than zero. This is important to avoid unexpected divisions by zero in other functions of the library.\n\n\nIn the `Shared` library:\n\n\n* The [`addMember` function](https://github.com/UMAprotocol/protocol/blob/9d403ddb5f2f07194daefe7da51e0e0a6306f2c4/core/contracts/common/implementation/MultiRole.sol#L37) does not do a null address check like the [`resetMember` function](https://github.com/UMAprotocol/protocol/blob/9d403ddb5f2f07194daefe7da51e0e0a6306f2c4/core/contracts/common/implementation/MultiRole.sol#L14) of the `Exclusive` library.\n\n\nConsider implementing the necessary logic where appropriate to validate all relevant user-controlled input.\n\n\n**Update:** *Fixed in [PR#1212](https://github.com/UMAprotocol/protocol/pull/1212). The suggested validations are implemented.*\n\n\n",
  "Impact": "LOW",
  "Source": "",
  "Code": [
    {
      "filename": "core/contracts/oracle/implementation/Store.sol",
      "content": "pragma solidity ^0.6.0;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/math/SafeMath.sol\";\nimport \"../../common/implementation/FixedPoint.sol\";\nimport \"../../common/implementation/MultiRole.sol\";\nimport \"../../common/implementation/Withdrawable.sol\";\nimport \"../interfaces/StoreInterface.sol\";\n\n\n/**\n * @title An implementation of Store that can accept Oracle fees in ETH or any arbitrary ERC20 token.\n */\ncontract Store is StoreInterface, MultiRole, Withdrawable {\n    using SafeMath for uint;\n    using FixedPoint for FixedPoint.Unsigned;\n    using FixedPoint for uint;\n\n    /****************************************\n     *    INTERNAL VARIABLES AND STORAGE    *\n     ****************************************/\n\n    enum Roles { Owner, Withdrawer }\n\n    FixedPoint.Unsigned public fixedOracleFeePerSecond; // Percentage of 1 E.g., .1 is 10% Oracle fee.\n    FixedPoint.Unsigned public weeklyDelayFee; // Percentage of 1 E.g., .1 is 10% weekly delay fee.\n\n    mapping(address => FixedPoint.Unsigned) public finalFees;\n    uint public constant SECONDS_PER_WEEK = 604800;\n\n    /****************************************\n     *                EVENTS                *\n     ****************************************/\n\n    event NewFixedOracleFeePerSecond(FixedPoint.Unsigned newOracleFee);\n\n    /**\n     * @notice Construct the Store contract.\n     */\n    constructor() public {\n        _createExclusiveRole(uint(Roles.Owner), uint(Roles.Owner), msg.sender);\n        createWithdrawRole(uint(Roles.Withdrawer), uint(Roles.Owner), msg.sender);\n    }\n\n    /****************************************\n     *  ORACLE FEE CALCULATION AND PAYMENT  *\n     ****************************************/\n\n    /**\n     * @notice Pays Oracle fees in ETH to the store.\n     * @dev To be used by contracts whose margin currency is ETH.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function payOracleFees() external override payable {\n        require(msg.value > 0);\n    }\n\n    /**\n     * @notice Pays oracle fees in the margin currency, erc20Address, to the store.\n     * @dev To be used if the margin currency is an ERC20 token rather than ETH.\n     * All approved tokens are transferred.\n     * @param erc20Address address of the ERC20 token used to pay the fee.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function payOracleFeesErc20(address erc20Address) external override {\n        IERC20 erc20 = IERC20(erc20Address);\n        uint authorizedAmount = erc20.allowance(msg.sender, address(this));\n        require(authorizedAmount > 0);\n        require(erc20.transferFrom(msg.sender, address(this), authorizedAmount));\n    }\n\n    /**\n     * @notice Computes the regular oracle fees that a contract should pay for a period.\n     * @param startTime defines the beginning time from which the fee is paid.\n     * @param endTime end time until which the fee is paid.\n     * @param pfc \"profit from corruption\", or the maximum amount of margin currency that a\n     * token sponsor could extract from the contract through corrupting the price feed in their favor.\n     * @return regularFee amount owed for the duration from start to end time for the given pfc.\n     * @return latePenalty penalty percentage, if any, for paying the fee after the deadline.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function computeRegularFee(uint startTime, uint endTime, FixedPoint.Unsigned calldata pfc)\n        external\n        override\n        view\n        returns (FixedPoint.Unsigned memory regularFee, FixedPoint.Unsigned memory latePenalty)\n    {\n        uint timeDiff = endTime.sub(startTime);\n\n        // Multiply by the unscaled `timeDiff` first, to get more accurate results.\n        regularFee = pfc.mul(timeDiff).mul(fixedOracleFeePerSecond);\n        // `weeklyDelayFee` is already scaled up.\n        latePenalty = pfc.mul(weeklyDelayFee.mul(timeDiff.div(SECONDS_PER_WEEK)));\n\n        return (regularFee, latePenalty);\n    }\n\n    /**\n     * @notice Computes the final oracle fees that a contract should pay at settlement.\n     * @param currency token used to pay the final fee.\n     * @return finalFee amount due denominated in units of `currency`.\n     */\n    // TODO(#969) Remove once prettier-plugin-solidity can handle the \"override\" keyword\n    // prettier-ignore\n    function computeFinalFee(address currency) external override view returns (FixedPoint.Unsigned memory finalFee) {\n        finalFee = finalFees[currency];\n    }\n\n    /****************************************\n     *   ADMIN STATE MODIFYING FUNCTIONS    *\n     ****************************************/\n\n    /**\n     * @notice Sets a new oracle fee per second.\n     * @param newOracleFee new fee per second charged to use the oracle.\n     */\n    function setFixedOracleFeePerSecond(FixedPoint.Unsigned memory newOracleFee)\n        public\n        onlyRoleHolder(uint(Roles.Owner))\n    {\n        // Oracle fees at or over 100% don't make sense.\n        require(newOracleFee.isLessThan(1));\n        fixedOracleFeePerSecond = newOracleFee;\n        emit NewFixedOracleFeePerSecond(newOracleFee);\n    }\n\n    /**\n     * @notice Sets a new weekly delay fee.\n     * @param newWeeklyDelayFee fee escalation per week of late fee payment.\n     */\n    function setWeeklyDelayFee(FixedPoint.Unsigned memory newWeeklyDelayFee) public onlyRoleHolder(uint(Roles.Owner)) {\n        weeklyDelayFee = newWeeklyDelayFee;\n    }\n\n    /**\n     * @notice Sets a new final fee for a particular currency.\n     * @param currency defines the token currency used to pay the final fee.\n     * @param finalFee final fee amount.\n     */\n    function setFinalFee(address currency, FixedPoint.Unsigned memory finalFee)\n        public\n        onlyRoleHolder(uint(Roles.Owner))\n    {\n        finalFees[currency] = finalFee;\n    }\n}"
    },
    {
      "filename": "core/contracts/oracle/implementation/VoteTiming.sol",
      "content": "pragma solidity ^0.6.0;\n\nimport \"@openzeppelin/contracts/math/SafeMath.sol\";\nimport \"../interfaces/VotingInterface.sol\";\n\n\n/**\n * @title Library to compute rounds and phases for an equal length commit-reveal voting cycle.\n */\nlibrary VoteTiming {\n    using SafeMath for uint;\n\n    struct Data {\n        uint phaseLength;\n    }\n\n    /**\n     * @notice Initializes the data object. Sets the phase length based on the input.\n     */\n    function init(Data storage data, uint phaseLength) internal {\n        data.phaseLength = phaseLength;\n    }\n\n    /**\n     * @notice Computes the roundID based off the current time as floor(timestamp/phaseLength).\n     * @param data input data object.\n     * @param currentTime input unix timeStamp used to compute the current roundId.\n     * @return roundId defined as a function of the currentTime and `phaseLength` from `data`.\n     */\n    function computeCurrentRoundId(Data storage data, uint currentTime) internal view returns (uint roundId) {\n        uint roundLength = data.phaseLength.mul(uint(VotingInterface.Phase.NUM_PHASES_PLACEHOLDER));\n        return currentTime.div(roundLength);\n    }\n\n    /**\n     * @notice compute the round end time as a function of the round Id.\n     * @param data input data object.\n     * @param roundId uniquely identifies the current round.\n     * @return timestamp unix time of when the current round will end.\n     */\n    function computeRoundEndTime(Data storage data, uint roundId) internal view returns (uint timestamp) {\n        uint roundLength = data.phaseLength.mul(uint(VotingInterface.Phase.NUM_PHASES_PLACEHOLDER));\n        return roundLength.mul(roundId.add(1));\n    }\n\n    /**\n     * @notice Computes the current phase based only on the current time.\n     */\n    function computeCurrentPhase(Data storage data, uint currentTime) internal view returns (VotingInterface.Phase) {\n        // This employs some hacky casting. We could make this an if-statement if we're worried about type safety.\n        return\n            VotingInterface.Phase(\n                currentTime.div(data.phaseLength).mod(uint(VotingInterface.Phase.NUM_PHASES_PLACEHOLDER))\n            );\n    }\n}"
    },
    {
      "filename": "core/contracts/common/implementation/MultiRole.sol",
      "content": "pragma solidity ^0.6.0;\n\n\nlibrary Exclusive {\n    struct RoleMembership {\n        address member;\n    }\n\n    function isMember(RoleMembership storage roleMembership, address memberToCheck) internal view returns (bool) {\n        return roleMembership.member == memberToCheck;\n    }\n\n    function resetMember(RoleMembership storage roleMembership, address newMember) internal {\n        require(newMember != address(0x0), \"Cannot set an exclusive role to 0x0\");\n        roleMembership.member = newMember;\n    }\n\n    function getMember(RoleMembership storage roleMembership) internal view returns (address) {\n        return roleMembership.member;\n    }\n\n    function init(RoleMembership storage roleMembership, address initialMember) internal {\n        resetMember(roleMembership, initialMember);\n    }\n}\n\n\nlibrary Shared {\n    struct RoleMembership {\n        mapping(address => bool) members;\n    }\n\n    function isMember(RoleMembership storage roleMembership, address memberToCheck) internal view returns (bool) {\n        return roleMembership.members[memberToCheck];\n    }\n\n    function addMember(RoleMembership storage roleMembership, address memberToAdd) internal {\n        roleMembership.members[memberToAdd] = true;\n    }\n\n    function removeMember(RoleMembership storage roleMembership, address memberToRemove) internal {\n        roleMembership.members[memberToRemove] = false;\n    }\n\n    function init(RoleMembership storage roleMembership, address[] memory initialMembers) internal {\n        for (uint i = 0; i < initialMembers.length; i++) {\n            addMember(roleMembership, initialMembers[i]);\n        }\n    }\n}\n\n\n/**\n * @title Base class to manage permissions for the derived class.\n */\ncontract MultiRole {\n    using Exclusive for Exclusive.RoleMembership;\n    using Shared for Shared.RoleMembership;\n\n    enum RoleType { Invalid, Exclusive, Shared }\n\n    struct Role {\n        uint managingRole;\n        RoleType roleType;\n        Exclusive.RoleMembership exclusiveRoleMembership;\n        Shared.RoleMembership sharedRoleMembership;\n    }\n\n    mapping(uint => Role) private roles;\n\n    /**\n     * @notice Reverts unless the caller is a member of the specified roleId.\n     */\n    modifier onlyRoleHolder(uint roleId) {\n        require(holdsRole(roleId, msg.sender), \"Sender does not hold required role\");\n        _;\n    }\n\n    /**\n     * @notice Reverts unless the caller is a member of the manager role for the specified roleId.\n     */\n    modifier onlyRoleManager(uint roleId) {\n        require(holdsRole(roles[roleId].managingRole, msg.sender), \"Can only be called by a role manager\");\n        _;\n    }\n\n    /**\n     * @notice Reverts unless the roleId represents an initialized, exclusive roleId.\n     */\n    modifier onlyExclusive(uint roleId) {\n        require(roles[roleId].roleType == RoleType.Exclusive, \"Must be called on an initialized Exclusive role\");\n        _;\n    }\n\n    /**\n     * @notice Reverts unless the roleId represents an initialized, shared roleId.\n     */\n    modifier onlyShared(uint roleId) {\n        require(roles[roleId].roleType == RoleType.Shared, \"Must be called on an initialized Shared role\");\n        _;\n    }\n\n    /**\n     * @notice Whether `memberToCheck` is a member of roleId.\n     * @dev Reverts if roleId does not correspond to an initialized role.\n     */\n    function holdsRole(uint roleId, address memberToCheck) public view returns (bool) {\n        Role storage role = roles[roleId];\n        if (role.roleType == RoleType.Exclusive) {\n            return role.exclusiveRoleMembership.isMember(memberToCheck);\n        } else if (role.roleType == RoleType.Shared) {\n            return role.sharedRoleMembership.isMember(memberToCheck);\n        }\n        require(false, \"Invalid roleId\");\n    }\n\n    /**\n     * @notice Changes the exclusive role holder of `roleId` to `newMember`.\n     * @dev Reverts if the caller is not a member of the managing role for `roleId` or if `roleId` is not an\n     * initialized, exclusive role.\n     */\n    function resetMember(uint roleId, address newMember) public onlyExclusive(roleId) onlyRoleManager(roleId) {\n        roles[roleId].exclusiveRoleMembership.resetMember(newMember);\n    }\n\n    /**\n     * @notice Gets the current holder of the exclusive role, `roleId`.\n     * @dev Reverts if `roleId` does not represent an initialized, exclusive role.\n     */\n    function getMember(uint roleId) public view onlyExclusive(roleId) returns (address) {\n        return roles[roleId].exclusiveRoleMembership.getMember();\n    }\n\n    /**\n     * @notice Adds `newMember` to the shared role, `roleId`.\n     * @dev Reverts if `roleId` does not represent an initialized, shared role or if the caller is not a member of the\n     * managing role for `roleId`.\n     */\n    function addMember(uint roleId, address newMember) public onlyShared(roleId) onlyRoleManager(roleId) {\n        roles[roleId].sharedRoleMembership.addMember(newMember);\n    }\n\n    /**\n     * @notice Removes `memberToRemove` from the shared role, `roleId`.\n     * @dev Reverts if `roleId` does not represent an initialized, shared role or if the caller is not a member of the\n     * managing role for `roleId`.\n     */\n    function removeMember(uint roleId, address memberToRemove) public onlyShared(roleId) onlyRoleManager(roleId) {\n        roles[roleId].sharedRoleMembership.removeMember(memberToRemove);\n    }\n\n    /**\n     * @notice Reverts if `roleId` is not initialized.\n     */\n    modifier onlyValidRole(uint roleId) {\n        require(roles[roleId].roleType != RoleType.Invalid, \"Attempted to use an invalid roleId\");\n        _;\n    }\n\n    /**\n     * @notice Reverts if `roleId` is initialized.\n     */\n    modifier onlyInvalidRole(uint roleId) {\n        require(roles[roleId].roleType == RoleType.Invalid, \"Cannot use a pre-existing role\");\n        _;\n    }\n\n    /**\n     * @notice Internal method to initialize a shared role, `roleId`, which will be managed by `managingRoleId`.\n     * `initialMembers` will be immediately added to the role.\n     * @dev Should be called by derived contracts, usually at construction time. Will revert if the role is already\n     * initialized.\n     */\n    function _createSharedRole(uint roleId, uint managingRoleId, address[] memory initialMembers)\n        internal\n        onlyInvalidRole(roleId)\n    {\n        Role storage role = roles[roleId];\n        role.roleType = RoleType.Shared;\n        role.managingRole = managingRoleId;\n        role.sharedRoleMembership.init(initialMembers);\n        require(\n            roles[managingRoleId].roleType != RoleType.Invalid,\n            \"Attempted to use an invalid role to manage a shared role\"\n        );\n    }\n\n    /**\n     * @notice Internal method to initialize a exclusive role, `roleId`, which will be managed by `managingRoleId`.\n     * `initialMembers` will be immediately added to the role.\n     * @dev Should be called by derived contracts, usually at construction time. Will revert if the role is already\n     * initialized.\n     */\n    function _createExclusiveRole(uint roleId, uint managingRoleId, address initialMember)\n        internal\n        onlyInvalidRole(roleId)\n    {\n        Role storage role = roles[roleId];\n        role.roleType = RoleType.Exclusive;\n        role.managingRole = managingRoleId;\n        role.exclusiveRoleMembership.init(initialMember);\n        require(\n            roles[managingRoleId].roleType != RoleType.Invalid,\n            \"Attempted to use an invalid role to manage an exclusive role\"\n        );\n    }\n}"
    }
  ]
}