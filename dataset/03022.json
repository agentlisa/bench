{
  "Title": "H-4: `ERC4626Oracle` Price will be wrong when the ERC4626's `decimals` is different from the underlying token’s decimals",
  "Content": "# Issue H-4: `ERC4626Oracle` Price will be wrong when the ERC4626's `decimals` is different from the underlying token’s decimals  \nSource: https://github.com/sherlock-audit/2022-08-sentiment-judging/tree/main/025-H \n## Found by \nLambda, JohnSmith, WATCHPUG, 0x52, berndartmueller, Bahurum\n\n## Summary\n\nEIP-4626 does not require the decimals must be the same as the underlying tokens' decimals, and when it's not, `ERC4626Oracle` will malfunction.\n\n## Vulnerability Detail\n\nIn the current implementation, `IERC4626(token).decimals()` is used as the `IERC4626(token).asset()`'s decimals to calculate the ERC4626's price.\n\nHowever, while most ERC4626s are using the underlying token’s decimals as `decimals`, there are some ERC4626s use a different decimals from underlying token’s decimals since EIP-4626 does not require the decimals must be the same as the underlying token’s decimals:\n\n> Although the convertTo functions should eliminate the need for any use of an EIP-4626 Vault’s decimals variable, it is still strongly recommended to mirror the underlying token’s decimals if at all possible, to eliminate possible sources of confusion and simplify integration across front-ends and for other off-chain users.\n\nRef: https://eips.ethereum.org/EIPS/eip-4626\n\n## Impact\n\nThe price of ERC4626 will be significantly underestimated when the underlying token's decimals > ERC4626's decimals, and be significantly overestimated when the underlying token's decimals < ERC4626's decimals.\n\n## Code Snippet\n\nhttps://github.com/sentimentxyz/oracle/blob/59b26a3d8c295208437aad36c470386c9729a4bc/src/erc4626/ERC4626Oracle.sol#L35-L43\n\n```solidity\n    function getPrice(address token) external view returns (uint) {\n        uint decimals = IERC4626(token).decimals();\n        return IERC4626(token).previewRedeem(\n            10 ** decimals\n        ).mulDivDown(\n            oracleFacade.getPrice(IERC4626(token).asset()),\n            10 ** decimals\n        );\n    }\n```\n\n## Tool used\n\nManual Review\n\n## Recommendation\n\n`getPrice()` can be changed to:\n\n```solidity\n    function getPrice(address token) external view returns (uint) {\n        uint decimals = IERC4626(token).decimals();\n        address underlyingToken = IERC4626(token).asset();\n        return IERC4626(token).previewRedeem(\n            10 ** decimals\n        ).mulDivDown(\n            oracleFacade.getPrice(underlyingToken),\n            10 ** IERC20Metadata(underlyingToken).decimals()\n        );\n    }\n```\n## Sentiment Team\nFixed as recommended. PR [here](https://github.com/sentimentxyz/oracle/pull/34).\n\n## Lead Senior Watson\nConfirmed fix. \n\n",
  "Impact": "HIGH",
  "Source": "https://app.sherlock.xyz/audits/contests/1",
  "Code": [
    {
      "filename": "src/erc4626/ERC4626Oracle.sol",
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.15;\n\nimport \"../core/IOracle.sol\";\nimport \"./IERC4626.sol\";\nimport \"solmate/utils/FixedPointMathLib.sol\";\n\ncontract ERC4626Oracle is IOracle {\n    using FixedPointMathLib for uint256;\n\n    /* -------------------------------------------------------------------------- */\n    /*                               STATE VARIABLES                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @notice address of oracle facade\n    IOracle immutable oracleFacade;\n\n    /* -------------------------------------------------------------------------- */\n    /*                                 CONSTRUCTOR                                */\n    /* -------------------------------------------------------------------------- */\n\n    /**\n        @notice Contract constructor\n        @param _oracleFacade address oracle facade\n    */\n    constructor(IOracle _oracleFacade) {\n        oracleFacade = _oracleFacade;\n    }\n\n    /* -------------------------------------------------------------------------- */\n    /*                              PUBLIC FUNCTIONS                              */\n    /* -------------------------------------------------------------------------- */\n\n    /// @inheritdoc IOracle\n    function getPrice(address token) external view returns (uint) {\n        uint decimals = IERC4626(token).decimals();\n        return IERC4626(token).previewRedeem(\n            10 ** decimals\n        ).mulDivDown(\n            oracleFacade.getPrice(IERC4626(token).asset()),\n            10 ** decimals\n        );\n    }\n}"
    }
  ]
}