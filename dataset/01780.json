{
  "Title": "3S-LENFT-N21 protocol/Lending/InterestRate.sol: cheaper to store the OptimalBorrowRate",
  "Content": "#### Description\nCalling function [`getOptimalBorrowRate()`](https://github.com/leNFT/contracts/blob/9b1ae95f97ec5d2423c022179642714cee470a68/contracts/protocol/Lending/InterestRate.sol#L116) is expensive, since it requires 3 storage loads and some mathematical computations.\n\n#### Recommendation\n- It would be cheaper, gas-wise, to store the `OptimalBorrowRate` in the `ConfigTypes.InterestRateConfig` struct, in the `_interestRateConfigs` mapping and just load it from storage whenever needed, since the value of this variable is only dependant on other config values.\n- To make sure the formula is correct, the `OptimalBorrowRate` could be calculated inside function `setInterestRateConfig()`, this way it would only be executed once per token",
  "Impact": "GAS",
  "Source": "",
  "Code": [
    {
      "filename": "contracts/protocol/Lending/InterestRate.sol",
      "content": "// SPDX-License-Identifier: agpl-3.0\npragma solidity 0.8.19;\n\nimport {IInterestRate} from \"../../interfaces/IInterestRate.sol\";\nimport {ConfigTypes} from \"../../libraries/types/ConfigTypes.sol\";\nimport {PercentageMath} from \"../../libraries/utils/PercentageMath.sol\";\nimport {Ownable} from \"@openzeppelin/contracts/access/Ownable.sol\";\n\n/// @title InterestRate\n/// @author leNFT\n/// @notice A contract for calculating the borrow rate based on the utilization rate\n/// @dev This contract implements the IInterestRate interface\n/// @dev The borrow rate is calculated based on a utilization rate between 0 and 100%\n/// @dev The optimal utilization rate is the target utilization rate where the borrow rate is equal to the base rate plus the low slope\n/// @dev Above the optimal utilization rate, the borrow rate is linearly increased based on the high slope\n/// @dev Below the optimal utilization rate, the borrow rate is linearly increased based on the low slope\n/// @dev Utilization rate is defined as the ratio of total debt to total liquidity in the system\n/// @dev The calculation of the utilization rate is done by the internal _calculateUtilizationRate function\ncontract InterestRate is IInterestRate, Ownable {\n    mapping(address => bool) private _isSupported;\n    mapping(address => ConfigTypes.InterestRateConfig)\n        private _interestRateConfigs;\n\n    modifier onlySupported(address token) {\n        _requireOnlySupported(token);\n        _;\n    }\n\n    /// @notice Sets the interest rate parameters for a token\n    /// @param token The address of the token\n    /// @param interestRateConfig The interest rate parameters\n    function addToken(\n        address token,\n        ConfigTypes.InterestRateConfig memory interestRateConfig\n    ) external onlyOwner {\n        require(_isSupported[token] == false, \"IR:AT:TOKEN_ALREADY_SUPPORTED\");\n        _isSupported[token] = true;\n        _interestRateConfigs[token] = interestRateConfig;\n    }\n\n    /// @notice Removes support for a token\n    /// @param token The address of the token\n    function removeToken(\n        address token\n    ) external onlySupported(token) onlyOwner {\n        delete _isSupported[token];\n        delete _interestRateConfigs[token];\n    }\n\n    /// @notice Gets whether a token is supported\n    /// @param token The address of the token\n    /// @return Whether the token is supported\n    function isTokenSupported(address token) external view returns (bool) {\n        return _isSupported[token];\n    }\n\n    /// @notice Gets the interest rate parameters for a token\n    /// @param token The address of the token\n    /// @return The interest rate parameters\n    function getInterestRateConfig(\n        address token\n    )\n        external\n        view\n        onlySupported(token)\n        returns (ConfigTypes.InterestRateConfig memory)\n    {\n        return _interestRateConfigs[token];\n    }\n\n    /// @notice Sets the interest rate parameters for a token\n    /// @param token The address of the token\n    /// @param interestRateConfig The interest rate parameters\n    function setInterestRateConfig(\n        address token,\n        ConfigTypes.InterestRateConfig memory interestRateConfig\n    ) external onlySupported(token) onlyOwner {\n        _interestRateConfigs[token] = interestRateConfig;\n    }\n\n    /// @notice Calculates the borrow rate based on the utilization rate\n    /// @param token The address of the token\n    /// @param assets The total assets\n    /// @param debt The total debt\n    /// @return The borrow rate\n    function calculateBorrowRate(\n        address token,\n        uint256 assets,\n        uint256 debt\n    ) external view override onlySupported(token) returns (uint256) {\n        uint256 utilizationRate = _calculateUtilizationRate(assets, debt);\n\n        if (\n            utilizationRate < _interestRateConfigs[token].optimalUtilizationRate\n        ) {\n            return\n                _interestRateConfigs[token].baseBorrowRate +\n                PercentageMath.percentMul(\n                    utilizationRate,\n                    _interestRateConfigs[token].lowSlope\n                );\n        } else {\n            return\n                getOptimalBorrowRate(token) +\n                PercentageMath.percentMul(\n                    utilizationRate -\n                        _interestRateConfigs[token].optimalUtilizationRate,\n                    _interestRateConfigs[token].highSlope\n                );\n        }\n    }\n\n    /// @notice Gets the optimal borrow rate\n    /// @param token The address of the token\n    /// @return The optimal borrow rate\n    function getOptimalBorrowRate(\n        address token\n    ) public view onlySupported(token) returns (uint256) {\n        return\n            PercentageMath.percentMul(\n                _interestRateConfigs[token].optimalUtilizationRate,\n                _interestRateConfigs[token].lowSlope\n            ) + _interestRateConfigs[token].baseBorrowRate;\n    }\n\n    /// @notice Calculates the utilization rate based on the assets and debt\n    /// @param assets The total assets\n    /// @param debt The total debt\n    /// @return The utilization rate\n    function calculateUtilizationRate(\n        address token,\n        uint256 assets,\n        uint256 debt\n    ) external view override onlySupported(token) returns (uint256) {\n        return _calculateUtilizationRate(assets, debt);\n    }\n\n    /// @notice Internal function to calculate the utilization rate based on the assets and debt\n    /// @param assets The total assets\n    /// @param debt The total debt\n    function _calculateUtilizationRate(\n        uint256 assets,\n        uint256 debt\n    ) internal pure returns (uint256) {\n        if ((assets + debt) == 0) {\n            return 0;\n        } else {\n            return (PercentageMath.PERCENTAGE_FACTOR * debt) / (assets + debt);\n        }\n    }\n\n    function _requireOnlySupported(address token) internal view {\n        require(_isSupported[token], \"IR:TOKEN_NOT_SUPPORTED\");\n    }\n}"
    }
  ]
}